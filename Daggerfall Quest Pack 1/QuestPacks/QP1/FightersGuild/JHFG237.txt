Quest: JHFG237
DisplayName: Возмездие Темному Братству
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Итак, вот контракт. Он на убийство человека, но
<ce> сначала позвольте мне рассказать вам предысторию.
<ce> Старушка, которая нам платит, - вдова
<ce> с единственным сыном... ну, у нее был сын. Темное
<ce> Братство убрало его, и теперь, когда она осталась
<ce> без средств к существованию, она использует свои
<ce> сбережения для мести. Это вас мотивирует?

RefuseQuest:  [1001]
<ce> Думаю, для этого нужен правильный человек.

AcceptQuest:  [1002]
<ce> Ладно, но будьте начеку. Темное Братство не
<ce> славится продажей цветов или выполнением домашней
<ce> работы. Вы найдете преступника в ___res_.
<ce> Поспрашивайте, пока не найдете _res_, и займитесь
<ce> делом. Когда закончите, возвращайтесь в ___tav_
<ce> и найдите _woman_ в _tav_. Там она живет,
<ce>  пока незакончатся ее сбережения. Получите у нее свои
<ce> =reward_ золотых до истечения =timer_ дней, это
<ce> предел по этому контракту. Будьте осторожны.

QuestFail:  [1003]
<ce> Ваш контракт с вдовой из _tav_,
<ce> _woman_, истек.

QuestComplete:  [1004]
<ce> _woman_ счастлива, что справедливость восторжествовала.

Message:  1010
%qdt:
 Гильдия Бойцов ___tav_ отправила меня
 в ___res_, чтобы убить члена Темного
 Братства в _res_. После этого
 я должен получить =reward_ золотых
 у _woman_, которая остановилась
 в _tav_, ___tav_. Контракт
 недействителен по истечении =timer_ дней.

Message:  1011
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Ловкость)
<ce>
<ce> В Братстве паранойя и осторожность идут
<ce> рука об руку. Вход в этот дом охраняет
<ce> ловушка, и вы слишком неуклюжи, чтобы
<ce> избежать ее. Колючки, покрытые темной
<ce> жидкостью, болезненно впиваются в кожу.

Message:  1012
<ce> Вдова была отомщена.

Message:  1013
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Ловкость)
<ce>
<ce> В Братстве паранойя и осторожность идут
<ce> рука об руку. Вход в этот дом охраняет
<ce> ловушка, но вы ловко уворачиваетесь от нее.
<ce> И это хорошо: шипы могли бы сильно
<ce> повредить шею.

Message:  1014
<ce> Вы подходите к _woman_ и рассказываете,
<ce> как вы расправились с убийцей ее сына.
<ce> Ее охватывает облегчение.
<ce>
<ce> "О, спасибо, спасибо, %ra. Подумать только, я смогу
<ce> спокойно уйти из жизни без этого тяжкого бремени!
<ce> Гильдия Бойцов защищает нас, простых людей. Вот,
<ce> позвольте мне отдать вам это золото".
<ce>
<ce> Она начинает рыться в карманах, чтобы найти ваши
<ce> =reward_ золотых. Зная, что она нищая и, скорее всего,
<ce> это ее последние деньги, вы можете сказать ей, чтобы
<ce> она оставила деньги себе. Примете ли вы ее плату?

Message:  1015
<ce> Вы берете деньги, которые она вам предлагает. Гильдия -
<ce> не благотворительная организация, и снижение цены может
<ce> негативно сказаться на возможности других членов зарабатывать
<ce> на жизнь. Кроме того, вы заработали эти деньги.

Message:  1016
<ce> Вы говорите _woman_, что вы не возьмете деньги. Она
<ce> растерянно моргает, а затем пытается скрыть слезы
<ce> благодарности. Без дальнейших объяснений она просто
<ce> улыбается вам, кладет монеты обратно в карман и говорит,
<ce>
<ce> "У меня еще есть связи с купцами здесь, в %crn.
<ce> Они услышат о вас и вашей доброте".

QBN:
Item _reward_ gold range 300 to 400

Person _woman_ faction The_Sisters_of_Kykos female
Person _merch_ faction The_Merchants
Person _db_ faction The_Dark_Brotherhood

Place _res_ remote house1
Place _tav_ local tavern

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Nightblade

--	Quest start-up:
	start timer _timer_
	log 1010 step 0
	place foe _enemy_ at _res_
	change foe _enemy_ infighting true
    pc at _res_ do _inside_

_will_ task:
	when attribute Agility is at least 35

variable _inside_

_passwill_ task:
	when _inside_ and _will_
    say 1013

_failwill_ task:
	when _inside_ and not _will_
	say 1011
    reduce player health by 25
	make pc ill with Witches'_Pox

_slain_ task:
	killed _enemy_
	say 1012
    place npc _woman_ at _tav_
    change repute with _db_ by -20

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
    prompt 1014 yes _accept_ no _refuse_
    start quest JHFGC06

_qgclicked_ task:
	clicked npc _woman_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_accept_ task:
    say 1015
    give pc _reward_
    end quest

_refuse_ task:
    say 1016
    change repute with _merch_ by +10
    give pc nothing
    end quest

_timer_ task:
	say 1003
	end quest