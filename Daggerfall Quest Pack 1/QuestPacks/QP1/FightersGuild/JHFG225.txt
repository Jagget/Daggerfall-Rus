Quest: JHFG225
DisplayName: Найти мага
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Гильдия Магов ищет кого-нибудь, кто мог бы
<ce> спасти одного из них, заблудившегося в какой-то
<ce> пещере. Заплатят, как за любую другую
<ce> работу, и вы будете в хороших отношениях и с ними,
<ce> и с нами. Хотите взять заказ?

RefuseQuest:  [1001]
<ce> Да, они довольно напыщенные люди.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___dung_. Я отмечу это
<ce> на вашей карте. Если вы сможете привести
<ce> сюда _scholar_ в течение
<ce> =timer_ дней, то получите =reward_ золотых.
<ce> Не беспокойтесь о том, что делал маг.
<ce> Ваша единственная задача -
<ce> доставить сюда вас обоих живыми.

QuestFail:  [1003]
<ce> Я вижу вас, но где же _scholar_?

QuestComplete:  [1004]
<ce> Привет, _scholar_, рад видеть
<ce> вас на поверхности! Я _qgiver_.
<ce> Мы с %pcf из Гильдии Бойцов.
<ce> Возвращайтесь домой и помните,
<ce> кто оберегал вас, поняли?
<ce>
<ce> Отличная работа. У этих людей,
<ce> знаете ли, есть семьи. Жаль,
<ce> что я не могу платить вам больше.

Message:  1010
<ce> Кто там?! %oth. Приятно
<ce> познакомиться, %pct. Я уже несколько
<ce> недель безрезультатно ищу пропавшего
<ce> атронаха. Отвратительное создание,
<ce> вероятно, уже давно покинуло эту
<ce> дыру. Да, нам пора возвращаться
<ce> в цивилизованный мир. Ведите.
   <--->
<ce> Кто там?! %oth. Приятно
<ce> познакомиться, %ra. Я пробыл здесь
<ce> слишком долго. Я убедил себя,
<ce> что _dummy_, который меня послали найти,
<ce> вообще не существует. Пожалуйста,
<ce> отведите меня в цивилизованный мир.
   <--->
<ce> Ага! О, приятно познакомиться, %ra.
<ce> Гильдия Магов тоже отправила вас
<ce> на поиски потерянного свитка? О, приказ
<ce> об отзыве. Ну, это я тоже могу
<ce> сделать. Покажите мне дорогу
<ce> к выходу, добрый друг.
   <--->
<ce> Хо! Добрый день, друг. Я уже
<ce> несколько недель ищу мумию
<ce> в свежайшиих обертках и никак
<ce> не могу найти. Гильдия Магов,
<ce> скорее всего, больше
<ce> не нуждается в них. Согласен,
<ce> нам следует найти выход
<ce> из этого места.

Message:  1011
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Удача)
<ce>
<ce> По счастливой случайности ученый оказался возле входа
<ce> в ___dung_. _scholar_ говорит:
<ce>
<ce> "Вы из Гильдии Бойцов? О боже! Я только сейчас
<ce> нашел выход и думаю, хватит ли у меня смелости
<ce> добраться до ___qgiver_ самостоятельно.
<ce> Пожалуйста, укажите мне путь!".

Message:  1030
%qdt:
 __qgiver_ из ___qgiver_
 платит мне =reward_ золотом, если я смогу
 найти _scholar_, члена
 Гильдии Магов, который заблудился
 в ___dung_. Мне нужно
 вернуться через =timer_ дней.

QBN:
Item _reward_ gold
Item _dummy_ organs

Person _qgiver_ group Questor male
Person _scholar_ faction The_Mages_Guild

Place _dung_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place npc _scholar_ at _dung_
	reveal _dung_
	pc at _dung_ do _inside_

_luck_ task:
	when attribute Luck is at least 85
	pick one of _lucky_ _unlucky_ _unlucky_

variable _unlucky_
variable _lucky_

variable _inside_

_luckywin_ task:
	when _inside_ and _lucky_
	start task _easywin_

_easywin_ task:
	say 1011
	hide npc _scholar_
	add _scholar_ face
	change repute with _scholar_ by +2

_success_ task:
	clicked npc _scholar_
	say 1010
	hide npc _scholar_
	add _scholar_ face
	change repute with _scholar_ by +2

_pcgetsgold_ task:
	when _qgclicked_ and _success_
	give pc _reward_
	end quest

_pcgetsgold2_ task:
	when _qgclicked_ and _easywin_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _success_ and not _easywin_
	say 1003
	clear _qgclicked_ _clearclick_

_timer_ task:
	end quest