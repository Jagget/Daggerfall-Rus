Quest: JHFG105
DisplayName: Мыслить порталами
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вы когда-нибудь сражались с даэдротом, %pcf? 
<ce> Они сильно бьют, но если вы сможете их ранить, то падают
<ce> довольно быстро. Я надеюсь, что в данном случае вы сможете 
<ce> это сделать. Готовы ли вы отправиться в путешествие 
<ce> за пределы города, чтобы разобраться с последствиями 
<ce> неудачного вызова?

RefuseQuest:  [1001]
<ce> Это несколько огорчает.

AcceptQuest:  [1002]
<ce> Отлично. Дело в том, что некий новоиспеченный маг
<ce> в ___house_ решил, что ему хотелось бы иметь
<ce> домашнего беса, и открыл портал неизвестно
<ce> куда. По всей видимости, бесы и даэдра встречаются
<ce> во внешних мирах примерно одинаково часто, как мне
<ce> сказали. Даэдрот проскочил прямо через портал
<ce> и начал сеять хаос по всему дому. Вы должны
<ce> добраться до _house_ в ___house_. 
<ce> Убейте монстра и расскажите мне об этом 
<ce> через =timer_ дней. В награду вы получите _reward_.

QuestFail:  [1003]
<ce> _qgiver_ сказал вам вернуться
<ce> в ___qgiver_ сегодня, чтобы
<ce> рассказать %g2 об убийстве даэдрота.

QuestComplete:  [1004]
<ce> Хорошо, тогда это ваше.

Message:  1010
<ce> Это здание наполнено магическим загрязнением.
<ce> Даэдрот должен быть внутри.

Message:  1011
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Ловкость)
<ce>
<ce> Этот даэдрот определенно знает, как обращаться с топором.
<ce> Он смеется над вашими медленными, неуклюжими движениями
<ce> и делает свободный выпад в вашу сторону.

Message:  1012
<ce> Этого даэдрота больше нет.

Message:  1013
<ce> Хорошая работа. Вот что я вам скажу: даэдрот, вышедший
<ce> на улицы, - очень плохое дело. Иногда даже у городской
<ce> стражи нет подходящего оружия для таких случаев.
<ce> Я сказал вам, что у меня есть для вас _reward_, и это
<ce> так, но я хотел бы узнать, не предпочтете ли вы _reward2_
<ce> взамен. Возьмете ли вы первоначальную награду?
<ce>
<ce> (ДА: _reward_. НЕТ: _reward2_.)

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_ поручил
 мне уничтожить даэдрота,
 который был призван в _house_,
 ___house_. Я должен вернуться
 в течение =timer_ дней, чтобы
 получить _reward_.

QBN:
Item _reward_ trinket
Item _reward2_ Holy_relic

Person _qgiver_ group Questor male

Place _house_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _giant_ is Daedroth

--	Quest start-up:
    log 1030 step 0
    start timer _timer_
    pc at _house_ set _inside_
    place foe _giant_ at _house_

_inside_ task:
    say 1010

_click_ task:
    clicked foe _giant_
    injured _giant_

_lucky_ task:
    when attribute Agility is at least 40

_notlucky_ task:
    when _click_ and not _lucky_
    say 1011
    reduce player health by 20

variable _check_
_slain_ task:
    killed _giant_
    say 1012

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_victory1_ task:
    when _qgclicked_ and _slain_
    prompt 1013 yes _original_ no _alternate_

_original_ task:
    give pc _reward_
    end quest

_alternate_ task:
    give pc _reward2_
    end quest

_timer_ task:
    say 1003
    end quest