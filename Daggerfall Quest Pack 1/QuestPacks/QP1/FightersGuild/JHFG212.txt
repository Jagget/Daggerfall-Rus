Quest: JHFG212
DisplayName: Волшебник в бешенстве
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> К нам поступил запрос из ___res_ о том,
<ce> чтобы убрать какого-то парня, сеющего хаос
<ce> по всему городу. По всей видимости, это какой-то
<ce> член Гильдии Магов, который сидит на _drug_,
<ce> по крайней мере, так они говорят. Оплата - броня,
<ce> гномский правый наплечник. Хотите взять контракт?

RefuseQuest:  [1001]
<ce> Странно, я думал, это будет легко.
<ce> Никто не хочет брать.

AcceptQuest:  [1002]
<ce> Хорошо, я верю, что вы сможете справиться
<ce> с последствиями. В контракте говорится,
<ce> что этот так называемый "_dummy_" должен быть
<ce> мертв в течение =2res_ дней. После этого
<ce> просто приходите за своими деньгами,
<ce> когда захотите. Найдите %g4 в ___res_.
<ce> Поспрашивайте окружающих, если не сможете
<ce> сами найти %g4 дом. Берегите себя.

QuestFail:  [1003]
<ce> Ваш контракт на убийство _dummy_
<ce> в ___res_ заканчивается сегодня.

QuestComplete:  [1004]
<ce> Привет, с возвращением. Приятно видеть вас
<ce> в целости и сохранности. Вот ваш гномский наплечник.

RumorsDuringQuest:  [1005]
Гильдия Бойцов не смогла спасти какого-то ребенка из заброшенной тюрьмы. Позор им.

Message:  1010
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Ловкость)
<ce>
<ce> Ваш одурманенный наркотиками противник находится
<ce> в ярости и готов на все. Он наносит вам быстрый
<ce> удар с разворота в лицо, от которого более ловкий
<ce> человек мог бы уклониться. Это очень больно.

Message:  1022
<ce> "Дурак! Как ты смеешь нападать
<ce> на члена Гильдии Магов!"

Message:  1023
<ce> Ваше задание выполнено, вы можете вернуться
<ce> к _questgiver_ с гордо поднятой головой.

Message:  1032
%qdt:
 _questgiver_ из Гильдии Бойцов заключил
 со мной контракт на убийство какого-то
 мага в ___res_ от имени
 города. У меня есть =2res_ дней,
 чтобы сделать это, после чего я могу
 получить свою награду в ___questgiver_
 в любое время.

QBN:
Person _questgiver_ group Questor male
Person _dummy_ faction The_Mages_Guild

Place _res_ remote house

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00

Item _reward_ item class 2 subclass 4
Item _drug_ drug

Foe _mage_ is Spellsword

--	Quest start-up:
	start timer _2res_
	start timer _failsafe_
	place foe _mage_ at _res_
	place npc _dummy_ at _res_
	hide npc _dummy_
	log 1032 step 0
	change foe _mage_ infighting true
	update-quest-item _reward_ set-material Dwarven

_ipr_ task:
	injured _mage_
	say 1022

_agi_ task:
	when attribute Agility is at least 35

_badagi_ task:
	when _ipr_ and not _agi_
	say 1010
	reduce player health by 18

_slain_ task:
	killed 1 _mage_
	change repute with _dummy_ by -10
	stop timer _2res_
	say 1023

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	start quest JHFGC02
	end quest

_2res_ task:
	say 1003
	end quest

_failsafe_ task:
	end quest