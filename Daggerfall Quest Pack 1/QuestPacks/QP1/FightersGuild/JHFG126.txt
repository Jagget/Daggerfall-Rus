Quest: JHFG126
DisplayName: Прятки
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> В одном городе есть группа преступников,
<ce> скрывшаяся с некоторыми довольно важными
<ce> уликами. Нужно быстро найти и уничтожить
<ce> их, пока они не нанесли непоправимый ущерб.
<ce> Не хотите сегодня побегать?

RefuseQuest:  [1001]
<ce> Хотите, наоборот, отдохнуть?
<ce> Мы что-нибудь придумаем без вас.

AcceptQuest:  [1002]
<ce> Эти бандиты окружены в одном из зданий города
<ce> и пытаются выторговать себе свободу за тот маленький
<ce> клочок бумаги, который они украли. Они сказали,
<ce> что сожгут его, если кто-нибудь попытается проникнуть
<ce> внутрь. Я не думаю, что они предпримут что-то
<ce> рискованное, если просто увидят переговорщика,
<ce> но если вы начнете драку, то лучше закончить ее
<ce> быстро. Бумага должна быть у одного из них.
<ce> Принесите ее сюда, и я назначу вам награду.
<ce>
<ce> О, чуть не забыл. _random_.

QuestComplete:  [1004]
<ce> Приемлемо. Поверьте мне, если бы они
<ce> послали для этого стражников, то нашли
<ce> бы только кучку теплого пепла. %t были
<ce> правы, запрашивая нашу помощь, даже
<ce> если мне неприятно это говорить.

Message:  1010
<ce> Этот бандит выкрикивает какие-то
<ce> кодовые слова, и другие выходят
<ce> из укрытия. Вы чувствуете запах гари,
<ce> но не знаете, откуда он исходит.

Message:  1011
<ce> Быстро осмотрев вещи, вы видите
<ce> горящий пергамент. Потушив огонь,
<ce> вы убираете пергамент
<ce> в безопасное место.

Message:  1012
<ce> Горение прекратилось. Один
<ce> из воров торжествующе ухмыляется.
<ce> Вам не удалось остановить их.

Message:  1013
<ce> Этот пергамент, хотя и обгоревший, описывает
<ce> серию прошлых и будущих преступлений
<ce> по всей территории %crn.
<ce> Его все еще можно прочитать.

Message:  1030
%qdt:
 __qgiver_ поручил мне
 уничтожить нескольких преступников, скрывающихся
 в _random_, ___random_.
 Их нужно убить быстро, пока они не успели
 уничтожить улики. Мне необходимо добраться
 туда сегодня, пока они не сбежали из здания.

QBN:
Item _reward_ gold range 400 to 600
Item _letter_ letter used 1013

Person _qgiver_ group Questor male

Place _random_ local tavern

Clock _timer_ 1.00:00
Clock _fight_ 0:04

Foe _nme_ is Thief
Foe _nme2_ is Thief
Foe _nme3_ is Thief

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place foe _nme_ at _random_
    restrain foe _nme_

_inj_ task:
    injured _nme_
    clicked foe _nme_
    say 1010
    create foe _nme2_ every 0 minutes 1 times with 100% success
    create foe _nme3_ every 0 minutes 1 times with 100% success
    start timer _fight_
    change foe _nme_ infighting true
    change foe _nme2_ infighting true
    change foe _nme3_ infighting true

_slain1_ task:
    killed _nme_

_slain2_ task:
    killed _nme2_

_slain3_ task:
    killed _nme3_

_slain_ task:
    when _slain1_ and _slain2_ and _slain3_
    stop timer _fight_
    say 1011
    get item _letter_

_fight_ task:
    say 1012
    end quest

_pcgetsgold_ task:
    toting _letter_ and _qgiver_ clicked
    give pc _reward_
    end quest

_timer_ task:
    end quest