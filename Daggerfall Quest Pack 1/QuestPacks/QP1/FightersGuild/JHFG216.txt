Quest: JHFG216
DisplayName: Удаление слабого звена
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Эй, %pcf, позвольте мне рассказать вам один секрет.
<ce> Я бы хотел, чтобы до этого не доходило, но это происходит
<ce> время от времени. У нас в гильдии есть член, который
<ce> занимается очень сомнительными делами, такими, которые
<ce> не должны затрагивать наше доброе имя. Проблема в том,
<ce> что этот дурак не нарушил никаких реальных правил,
<ce> и нам приходится мириться с его плохим поведением.
<ce> Но если бы кто-то из местных отправился туда и разобрался
<ce> с проблемой, все были бы намного счастливее, верно?
<ce> Могу ли я рассчитывать на вас в подобном случае?

RefuseQuest:  [1001]
<ce> Только сделайте мне одолжение и никому не говорите, хорошо?

AcceptQuest:  [1002]
<ce> Хорошо, тогда действуйте быстро и тихо. Отправляйтесь
<ce> в ___fg_. Если повезет, вы найдете нашего бывшего
<ce> друга в зале гильдии. Просто быстро покончите с этим
<ce> и возвращайтесь сюда. Это официальное задание
<ce> гильдии, поэтому вам заплатят =reward_ золотых,
<ce> но мы немного усложним текст контракта, чтобы убедиться,
<ce> что на вас не повесят вину. Возвращайтесь
<ce> через =timer_ дней, когда закончите.

QuestFail:  [1003]
<ce> _qgiver_ просил вас уже устранить участника-изгоя.

QuestComplete:  [1004]
<ce> Кто-нибудь там поднял шум? Нет?
<ce> Концы, как говорится, в воду. Вот ваше золото.
<ce> Ваш секрет в безопасности.

RumorsDuringQuest:  [1005]
Купцы собираются бастовать, если %t снова повысят налоги.

Message:  1020
<ce> Вы расправились с непорядочным членом гильдии.

Message:  1030
%qdt:
 _qgiver_ поручил мне отправиться
 в ___fg_, чтобы избавиться от
 нерадивого члена Гильдии Бойцов.
 Я должен вернуться в ___qgiver_
 в течение =timer_ дней.

QBN:
Item _reward_ gold
Item _drug_ drug

Person _qgiver_ group Questor male

Place _fg_ remote fighters

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Warrior

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _enemy_ at _fg_
	give item _drug_ to _enemy_
	make _drug_ permanent
	change foe _enemy_ infighting true

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_slain_ task:
	killed 1 _enemy_ saying 1020

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest