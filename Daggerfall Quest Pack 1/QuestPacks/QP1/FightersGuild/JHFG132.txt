Quest: JHFG132
DisplayName: Элемент заземления
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Знаете, как убивать атронахов? У нас
<ce> есть задание убить одного в другом
<ce> городе. Соседи боятся, что он
<ce> затаился в одном из домов. Железный
<ce> атронах, судя по всему. Чувствуете
<ce> себя готовым к этому?

RefuseQuest:  [1001]
<ce> Плохо. Контракт довольно простой.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___house_ на %di и найдите
<ce> место под названием _house_. По сообщениям,
<ce> атронах много воет, но мало двигается,
<ce> так что найти его будет несложно. Вытащите его
<ce> и возвращайтесь сюда. Я приготовлю
<ce> для вас =reward_ золотых, если вы
<ce> вернетесь через =timer_ дней.

QuestFail:  [1003]
<ce> Вы должны были уже доложить _qgiver_
<ce> о выполнении контракта на атронаха.

QuestComplete:  [1004]
<ce> Атронах мертв, не так ли? Хорошая работа.
<ce> В этих тварей трудно попасть, не так ли?
<ce> Для таких вещей нужно купить оружие получше,
<ce> я считаю. Рад выплатить вам вознаграждение.

RumorsDuringQuest:  [1005]
Какому-то воину заплатили за то, чтобы он убил кучу бесов в склепе. Что за жизнь.

Message:  1010
<ce> Этот дом заряжен электрической энергией.
<ce> Найти атронаха не составит труда.

Message:  1011
<ce> Вы осматриваете атронаха и находите
<ce> прикрепленную к нему записку.
<ce>
<ce> "ПРОВОДИТСЯ ЭКСПЕРИМЕНТ. Просьба не беспокоить!
<ce> Жизненно важные климатические эксперименты
<ce> в действии, объект инертен и не причинит никому
<ce> вреда! Пожалуйста, НЕ вызывайте Гильдию Бойцов,
<ce> Темное Братство или кого-либо еще!"
<ce>
<ce> Возможно, Гильдия Бойцов не располагала всей необходимой
<ce> информацией по этому делу. А может быть, соседям
<ce> наплевать на эксперимент и они все равно хотят его
<ce> уничтожить. Вы можете завершить контракт здесь и сейчас
<ce> или попытаться собрать больше информации.

Message:  1012
<ce> Почувствовав угрозу, атронах
<ce> оживает и движется на вас.

Message:  1013
<ce> Теперь, когда атронах уничтожен, пришло время вернуться
<ce> в ___qgiver_ и сообщить об успехе.

Message:  1014
<ce> Когда вы выходите из дома, к вам приближается испуганный маг.
<ce>
<ce> "О ужас, о ужас! Наемник? Вы были в доме?
<ce> Я вижу, вы не уничтожили атронаха! Спасибо,
<ce> спасибо! О, месяцы исследований пропали бы
<ce> даром за считанные мгновения, пока меня не было
<ce> дома! Голем - часть эксперимента по изменению
<ce> погоды, чтобы вызвать дождь для урожая и так
<ce> далее. Должен вам сказать, что у меня есть
<ce> друзья в высших кругах, и они сразу же узнают
<ce> об этом! Как хорошо, что оружием в этом мире
<ce> владеют мудрые люди, скажу я вам!"
<ce>
<ce> Заклинатель спешит в дом, чтобы скрыть все
<ce> основания для вашего контракта. Вы, несомненно,
<ce> извлечете из этого пользу, хотя и не уверены,
<ce> что расскажете об этом _qgiver_.

Message:  1015
<ce> Маги? Эксперименты? Управление погодой? Звучит
<ce> интересно и все такое, %pcf, но у вас была
<ce> работа. Контракт был довольно четким. Теперь,
<ce> когда маг предупрежден, эта штука возможно, никогда 
<ce> не будет уничтожена. Гильдии придется полностью
<ce> отказаться от контракта, а это плохо для всех
<ce> нас. В следующий раз мне нужно, чтобы вы
<ce> следовали условиям контракта вплоть до буквы.

Message:  1030
%qdt:
 Гильдия Бойцов поручила мне
 уничтожить атронаха внутри
 _house_,  ___house_.
 =reward_ золотых станут моими,
 если я вернусь 
 в ___qgiver_ через =timer_ дней.

Message:  1031
%qdt:
 Я решил не уничтожать
 атронаха в _house_, и маг
 отблагодарил меня, пообещав
 славить мое доброе имя по всему
 ____house_. Осталось только
 вернуться в ___qgiver_.

QBN:
Item _reward_ gold range 250 to 450

Person _qgiver_ group Questor male
Person _mg_ faction The_Mages_Guild

Place _house_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _nme_ is Iron_atronach

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place foe _nme_ at _house_
    restrain foe _nme_
    pc at _house_ do _inside_
    hide npc _mg_

_inside_ task:
    say 1010

_click_ task:
    clicked foe _nme_
    say 1011

_inj_ task:
    injured _nme_
    clicked foe _nme_
    say 1012
    change foe _nme_ infighting true

_slain_ task:
    killed _nme_
    say 1013

_exit_ task:
    when _click_ and not _inside_ and not _inj_
    say 1014
    remove foe _nme_
    legal repute +10
    change repute with _mg_ by +10
    log 1031 step 1

_pcgetsgold_ task:
    when _clickqg_ and _slain_
    give pc _reward_
    end quest

_pcgetsbad_ task:
    when _clickqg_ and _exit_
    say 1015
    end quest

_clickqg_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _clickqg_ and not _exit_ and not _slain_
    clear _clearclick_ _clickqg_

_timer_ task:
    say 1003
    end quest