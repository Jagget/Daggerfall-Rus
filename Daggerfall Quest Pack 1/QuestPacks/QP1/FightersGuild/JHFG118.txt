Quest: JHFG118
DisplayName: Гремящие кости
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Типичная проблема кладбища: кости не остаются под
<ce> землей. Это может быть из-за проделок некромантов,
<ce> остаточной магии, древних проклятийи т.д. Нам
<ce> просто нужен кто-то, кто пойдет туда и будет
<ce> убивать скелетов, пока земля не перестанет
<ce> вибрировать. Хотите попробовать?

RefuseQuest:  [1001]
<ce> Я знаю, скелеты могут быть довольно крепкими,
<ce> если вы неправильно экипированы.

AcceptQuest:  [1002]
<ce> Это называется ___cemetery_, довольно далеко отсюда.
<ce> Люди не могут должным образом навестить своих умерших
<ce> из-за бродящих вокруг скелетов, поэтому убедитесь,
<ce> что их не осталось. По контракту вы должны обернуться
<ce> в срок =timer_ дней, и вам выплатят =reward_ золотых.
<ce> Счастливого пути.

QuestFail:  [1003]
<ce> Ваше время сообщить Гильдии Бойцов об успехе
<ce> со скелетами истекло.

QuestComplete:  [1004]
<ce> Отличная работа! Какое оружие вы использовали? Я говорю людям, что
<ce> у вас должно быть с собой какое-нибудь тупое оружие. Мечи и кинжалы
<ce> бесполезны против цельной кости. В любом случае, я просто рад, что
<ce> вы вернулись. Возьмите свое вознаграждение и посмотрите, какие еще
<ce> заказы у нас есть в наличии.

RumorsDuringQuest:  [1005]
Хороший урожай в этом году означает, что цены на продукты питания становятся действительно низкими.

Message:  1010
%qdt:
 Гильдия Бойцов заплатит мне _reward_ золотых
 за выполнение контракта. Мне нужно уничтожать
 бродячих скелетов в окрестностях ___cemetery_,
 пока они не перестанут появляться. У меня есть
 =timer_ дней, чтобы вернуться в ___qgiver_
 за своим вознаграждением.

Message:  1011
<ce> Земля здесь действительно вибрирует, хотя и слегка. Магия,
<ce> оживляющая эти скелеты, активно работает. Вы можете подождать,
<ce>
<ce> пока скелеты не вылезут из земли, и затем разбить их на части,
<ce> или вы можете попытаться остановить процесс оживления с помощью
<ce> собственной магии. Попытаетесь ли вы остановить волшебство (ДА)
<ce> или сразитесь со скелетами после того, как они вылезут (НЕТ)?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Мистицизм, высокая сложность)

Message:  1012
<ce> Темная магия, дающая силу этим телам, иссякает.
<ce> Земля перестает двигаться. Вас ждет награда.

Message:  1013
<ce> Вы же знаете, что с такой силой лучше не связываться.
<ce> Вам просто нужно подождать, пока скелеты не восстанут.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы концентрируетесь и пытаетесь распознать действующие силы.
<ce> Вы достаточно проницательны, чтобы увидеть, что поддерживает
<ce> действие чар, и произнести заклинание, чтобы разрушить их.
<ce> Как только цикл перестает возобновляться, магия прекратится.

Message:  1015
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы концентрируетесь и пытаетесь прочитать действующие
<ce> силы. Вы приходите к выводу и пытаетесь разрушить
<ce> заклинание. Слишком поздно вы понимаете, что это далеко
<ce> не в ваших силах. Некоторые кости начинают подниматься
<ce> из земли, но гораздо более плавно, чем в случае с обычным
<ce> скелетом. Этот смотрит на вас со сверхъестественным
<ce> интеллектом. Затем он произносит одно единственное слово.
<ce>
<ce> "ИДИОТ".
<ce>
<ce> Ваша задача по уничтожению скелетов усложнилась.

QBN:
Item _reward_ gold range 300 to 500

Place _cemetery_ remote graveyard

Clock _timer_ 00:00 0 flag 17 range 0 2

Person _qgiver_ group Questor

Foe _skel_ is Skeletal_warrior
Foe _lich_ is Lich

--	Quest start-up:
    start timer _timer_
    pc at _cemetery_ set _inside_
    log 1010 step 0
    reveal _cemetery_

variable _inside_
_spawn_ task:
    when _inside_ and not _victory_
    prompt 1011 yes _attempt_ no _flee_
    create foe _skel_ every 30 minutes indefinitely with 10% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _skel_ infighting true
    clear _infight_

_skilled_ task:
    when skill Mysticism is at least 70

variable _attempt_

_fastwin_ task:
    when _skilled_ and _attempt_
    say 1014
    start task _victory_

_bigfail_ task:
    when not _skilled_ and _attempt_
    say 1015
    create foe _lich_ every 0 minutes 1 times with 100% success
    change foe _lich_ infighting true

_flee_ task:
    say 1013

_slain_ task:
    killed 4 _skel_
    start task _victory_

_victory_ task:
    say 1012

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    start quest JHFGC04
    end quest

_timer_ task:
    end quest