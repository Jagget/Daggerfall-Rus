Quest: JHFG122
DisplayName: Вышибала
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Привет, есть время, %pcf? У меня здесь работа
<ce> для таверны в городе, но я не должен ее отдавать
<ce> после 21:00. У них будет какое-то мероприятие, и они беспокоятся о пьяницах, драках,
<ce> такие вещи. Оплата стандартная, если вы захотите.

RefuseQuest:  [1001]
<ce> Похоже на довольно безобидные вещи. Вы уверены, что не хотите этого?

AcceptQuest:  [1002]
<ce> Хорошо, но если сейчас после 21:00, то это ваша вина,
<ce> что вы не поймали меня на этом. Вечеринка будет в
<ce> _tav_ здесь, в городе. Вы останетесь там
<ce> с 21:00 до рассвета или пока они вас не отпустят,
<ce> в зависимости от того, что наступит раньше. Обеспечьте безопасность людей и
<ce> вы получите свои =reward_ золотых. Сообщите
<ce> _prop_, когда прибудете туда.

QuestFail:  [1003]
<ce> Вечеринка в _tav_ началась,
<ce> но вас нигде не видно.

QuestComplete:  [1004]
<ce> _prop_ идет искать вас.
<ce>
<ce> "Репутация Гильдии Бойцов хорошо заслужена.
<ce> Мы позовем вас в следующий раз, когда будем делать одно из этих".

QuestorPostFailure:  [1009]
_prop_ продолжает рассказывать всем, насколько плохо вы были для _tav_.

Message:  1010
<ce> _prop_ ждет вас.
<ce>
<ce> "Гильдия бойцов? Хорошо. Оставайтесь здесь и
<ce> убедитесь, что все находится под контролем.
<ce> Я приду за вами, когда ваша смена закончится".

Message:  1011
<ce> Обильный алкоголь в таверне снижает некоторые из
<ce> сдержанности посетителей. Один решает бежать домой, чтобы принести
<ce> паука, чтобы впечатлить других, но острая
<ce> арахнофобия охватывает их, и громкое кричание
<ce> заставляет существо паниковать. Вскоре он начинает атаковать все
<ce> ближайшие живые существа, включая вас.

Message:  1012
<ce> Владелец паука горько рыдает,
<ce> но _prop_ кивает вам в знак одобрения.

Message:  1013
<ce> "Я слышал, что здесь проходит вечеринка!
<ce> У кого мешок с деньгами, а?!"
<ce>
<ce> Группа негодяев входит в таверну и
<ce> начинает трясти людей. _prop_
<ce> кричит, что у них есть телохранитель и указывает
<ce> на вас, направляя преступников к вам.

Message:  1014
<ce> Пьяное помутнение на вечеринке заставляет некоторых присутствующих
<ce> верить, что все это было разыграно для их развлечения.
<ce> Как только запах крови станет заметным, они,
<ce> вероятно, передумают.

Message:  1015
<ce> Среди посетителей начинается импровизированная лотерея.
<ce> Победитель получит сумму в =smallg_ золотых монет,
<ce> основанную на пожертвованиях других. Вам сообщают, что
<ce> в качестве ночного сторожа на вечеринке, вы можете участвовать без
<ce> оплаты вступительного взноса. Вы присоединяетесь к лотерее?

Message:  1016
<ce> Имена кладут в шляпу, и выбирают одно.
<ce>
<ce> "_dummy_, приди забери свое золото!"
<ce>
<ce> Вы возвращаетесь к своим обязанностям, когда развлечение утихает.

Message:  1017
<ce> Имена кладут в шляпу, и выбирают одно.
<ce>
<ce> "%pcn, приди забери свое золото!"
<ce>
<ce> Кто-то делает вам одолжение, доставляя деньги, но
<ce> один недовольный начинает подозревать вашу победу.
<ce> Смесь алкоголя и храбрости приводит к конфронтации.
<ce> _prop_ дает понять, что здесь оправдана самооборона.

Message:  1018
<ce> К счастью, ваш нападавший был неизвестен ни одному из посетителей.
<ce> Вероятно, это был просто гражданин ___tav_, который забрел сюда.

Message:  1019
<ce> Посетитель опускается, и таверна зажигается тревогой.
<ce> _prop_ бежит к вам.
<ce>
<ce> "Алкогольное отравление. Вы самый трезвый человек здесь.
<ce> Можете ли вы отвести его в _temp_?
<ce> У него там есть семья, которая может вылечить его бесплатно.
<ce> Убедитесь, что это правильный храм. Пожалуйста, поторопитесь".
<ce>
<ce> Бездействующее тело приводят к вам.
<ce> Теперь жизнь этого человека зависит от вас.

Message:  1020
<ce> Как только вы входите в храм, ближайший служитель
<ce> бледнеет и бежит за помощью. Вскоре группа помощников забирает
<ce> тело у вас для лечения, которое происходит без задержек.
<ce> Первый служитель немедленно сообщает вам об успехе.

Message:  1021
<ce> Странно, вы замечаете рядом мешок золота,
<ce> который, похоже, находится вне поля зрения всех остальных.
<ce> Адреналиновый всплеск сегодняшних
<ce> событий, возможно, слишком их измотал,
<ce> что привело к моментальному отвлечению. Вы
<ce> попытаетесь взять его для себя?

Message:  1022
<ce> (ПРОВЕРКА СКРЫТНОСТИ/КАРМАННИКА ПРОЙДЕНА)
<ce>
<ce> Это слишком легко для кого-то с вашими навыками.
<ce> Мешок с деньгами теперь ваш, добавляя
<ce> дополнительные =stealg_ золотых монет к вашему счету.

Message:  1023
<ce> (ПРОВЕРКА СКРЫТНОСТИ/КАРМАННИКА ПРОВАЛЕНА)
<ce>
<ce> Ваше движение при захвате мешка с монетами слишком
<ce> заметно. Кто-то, выглядывающий, видит, что у вас
<ce> в руке имущество, которое явно не ваше.
<ce> Ночные часы разбиваются пронзительным криком.
<ce>
<ce> "ВОР! Гильдия бойцов прислала нам вора!
<ce> Кто-нибудь, помогите!"
<ce>
<ce> Что-то подсказывает вам, что они не будут платить
<ce> за вашу охранную службу. Возможно, вам следует
<ce> поработать над своими воровскими навыками, прежде чем пытаться
<ce> обмануть своих клиентов.
   <--->
<ce> Ваше движение при захвате мешка с монетами слишком
<ce> заметно. Кто-то, выглядывающий, видит, что у вас
<ce> в руке имущество, которое явно не ваше.
<ce> Ночные часы разбиваются пронзительным криком.
<ce>
<ce> "Помощь! У нас в таверне вор! Помогите нам!"
<ce>
<ce> Что-то подсказывает вам, что они не будут платить
<ce> за вашу охранную службу. Возможно, вам следует
<ce> поработать над своими воровскими навыками, прежде чем пытаться
<ce> обмануть своих клиентов.

Message:  1026
<ce> Толпа внезапно начинает петь песню, совершенно не в тональности.

Message:  1027
<ce> "О да? Хочешь поссориться об этом? Подойди сюда, я научу тебя!"
<ce>
<ce> Бой не начинается.

Message:  1029
<ce> "Как насчет того, чтобы посмотреть, если мы будем бросать бутылки в того %ra,
<ce> который патрулирует таверну, посмотрим, кто первым попадет в %pg3?
<ce> Ха-ха-ха!"
<ce>
<ce> _prop_ катает %g3 глазами и шлепает голову нарушителя.

Message:  1030
%qdt:
 Гильдия бойцов в ___questgiver_ поручила
 мне охранять _tav_ во время вечеринки сегодня вечером.
 Я должен быть там к 21:00 и стоять на страже до тех пор,
 пока меня не отпустят.

Message:  1031
<ce> "Эй, у меня есть идея! Как насчет того, чтобы взять этот свиток,
<ce> который ты взял из Гильдии магов, и прочитать его вместе?"
<ce>
<ce> Прежде чем кто-то успевает остановить их, два пьяницы
<ce> читают то, что, как они предполагают, является словами на свитке.
<ce> Существо без привязанности появляется из ниоткуда.

Message:  1032
<ce> Люди гоготают и кричат от ваших боевых способностей.

Message:  1033
<ce> "Видите этот трюк? У меня есть домашний дракон! Смотрите все!"
<ce>
<ce> Вспышка света внезапно вспыхивает, и маленькое чешуйчатое
<ce> существо начинает порхать. _prop_,
<ce> знакомый с этим трюком, настаивает, чтобы вы его не убивали.

Message:  1034
<ce> "Этот грязный %ra убил моего дракона!
<ce> Что с тобой не так, неблагодарный?!"
<ce>
<ce> _prop_ пытается успокоить гребную толпу,
<ce> но это немного слишком много для %g2.
<ce> Похоже, вы за это не получите оплаты.

Message:  1035
 %qdt:
_prop_ требует от меня отвести пьяницу
в _temp_ в городе.

Message:  1036
<ce> Пьяница вообще перестал дышать.
<ce> Вам потребовалось слишком много времени, чтобы добраться до _temp_.
<ce> _prop_, слишком разъяренный для слов, отказывается платить.

Message:  1037
<ce> Дракон затягивается обратно в небольшую коробку с рунами,
<ce> которую держит один из посетителей, и все они громко смеются.

QBN:
Item _reward_ gold
Item _stealg_ gold
Item _smallg_ gold range 150 to 350

Person _questgiver_ group Questor male
Person _prop_ group Local_3.0
Person _dummy_ group Local_3.1

Place _tav_ local tavern
Place _temp_ local temple

Clock _t1_ 0:45
Clock _t2_ 0:45
Clock _t3_ 0:45
Clock _t4_ 0:45
Clock _t5_ 0:45
Clock _t6_ 0:45
Clock _t7_ 0:45
Clock _t8_ 0:45
Clock _victory_ 0:45
Clock _drunk_ 2:00
Clock _draglife_ 0:05
Clock _delay_ 00:00

Foe _rob_ is 3 Thief
Foe _bard_ is Bard
Foe _spid_ is Spider
Foe _firat_ is Fire_atronach
Foe _iceat_ is Ice_atronach
Foe _drag_ is Dragonling_souless

--	Quest start-up:
    log 1030 step 0
    pc at _tav_ set _inside_

_arriv_ task:
    daily from 21:00 to 21:59

_duty_ task:
    daily from 6:00 to 22:00

_fail_ task:
    when not _duty_ and not _inside_ and not _e4_
    say 1003
    end quest

variable _dud_
_inside_ task:
    play sound talking_loud every 0 minutes 100 times

_start_ task:
    when _inside_ and _arriv_
    say 1010
    start timer _t1_

_t1_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t2_

_t2_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t3_

_t3_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t4_

_t4_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t5_

_t5_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t6_

_t6_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t7_

_t7_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t8_

_t8_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _victory_

_e1_ task:
    say 1011
    create foe _spid_ every 0 minutes 1 times with 100% success
    change foe _spid_ infighting true

_e1a_ task:
    killed _spid_
    say 1012

_e2_ task:
    say 1013
    create foe _rob_ every 0 minutes 1 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _rob_ infighting true
    clear _infight_

_e2a_ task:
    killed 3 _rob_
    say 1014

_e3_ task:
    prompt 1015 yes _entry_ no _refuse_

variable _refuse_
_entry_ task:
    pick one of _loser_ _loser_ _winner_

_loser_ task:
    say 1016

_winner_ task:
    say 1017
    create foe _bard_ every 0 minutes 1 times with 100% success

_e3a_ task:
    killed _bard_
    say 1018

_e4_ task:
    say 1019
    start timer _drunk_
    pc at _temp_ set _cure_
    log 1035 step 1
    stop timer _t1_
    stop timer _t2_
    stop timer _t3_
    stop timer _t4_
    stop timer _t5_
    stop timer _t6_
    stop timer _t7_
    stop timer _t8_

_drunk_ task:
    say 1036
    end quest

_cure_ task:
    say 1020
    start timer _delay_

_delay_ task:
    start task _victory_

_e5_ task:
    prompt 1021 yes _st1_ no _refuse_

variable _st1_
_st2_ task:
    when _st1_ and _skill_
    say 1022
    get item _stealg_

_stfail_ task:
    when _st1_ and not _skill_
    say 1023
    get item _stealg_
    end quest

_stealth_ task:
    when skill Stealth is at least 75
    start task _skill_

_pickpocket_ task:
    when skill Pickpocket is at least 45
    start task _skill_

variable _skill_

_e8_ task:
    say 1026

_e9_ task:
    say 1027

_e11_ task:
    say 1029

_e12_ task:
    say 1031
    pick one of _sum1_ _sum2_

_sum1_ task:
    create foe _firat_ every 0 minutes 1 times with 100% success
    change foe _firat_ infighting true

_sum2_ task:
    create foe _iceat_ every 0 minutes 1 times with 100% success
    change foe _iceat_ infighting true

_killsum_ task:
    killed _firat_
    killed _iceat_
    say 1032

_e13_ task:
    say 1033
    create foe _drag_ every 0 minutes 1 times with 100% success
    restrain foe _drag_
    start timer _draglife_

_e13a_ task:
    killed _drag_
    say 1034
    end quest

_draglife_ task:
    remove foe _drag_
    say 1037

_victory_ task:
    give pc _reward_
    end quest