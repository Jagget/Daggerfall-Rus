Quest: JHFG122
DisplayName: Вышибала
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Эй, у вас есть время, %pcf? У меня есть работа в таверне
<ce> в городе, но я не должен давать ее после 9 вечера. У них
<ce> собирается какая-то тусовка, и они беспокоятся о пьяницах,
<ce> драках и тому подобном. Это оплачивается по стандартному
<ce> тарифу, если вы этого хотите.

RefuseQuest:  [1001]
<ce> Звучит довольно банально. Вы уверены, что не хотите
<ce> этот заказ?

AcceptQuest:  [1002]
<ce> Ладно, но если сейчас уже после 9, то это ваша вина,
<ce> что вы меня не предупредили. Вечеринка состоится
<ce> в _tav_ здесь, в городе. Вы будете
<ce> находиться там с 9 вечера до рассвета или пока вас
<ce> не отпустят, смотря что наступит раньше. Обеспечьте
<ce> безопасность людей, и вам заплатят =reward_ золотых.
<ce> Доложите _prop_, когда доберетесь туда.

QuestFail:  [1003]
<ce> Вечеринка в _tav_ началась,
<ce> но вас нигде нет.

QuestComplete:  [1004]
<ce> К вам подходит _prop_.
<ce>
<ce> "Репутация Гильдии Бойцов вполне заслужена. Мы позовем вас
<ce> в следующий раз, когда будем проводить подобное мероприятие".

QuestorPostFailure:  [1009]
_prop_ постоянно рассказывает всем о том, как плохо
вы себя вели в _tav_.

Message:  1010
<ce> _prop_ уже ждет вас.
<ce>
<ce> "Гильдия Бойцов? Хорошо. Оставайтесь здесь и следите
<ce> за тем, чтобы все было под контролем.
<ce> Я приду за вами, когда закончится ваша смена".

Message:  1011
<ce> Обилие алкоголя в таверне ослабляет запреты у некоторых
<ce> посетителей. Один из них решает сбегать домой
<ce> и принести домашнего паука, чтобы поразить остальных,
<ce> но всеми овладевает острая арахнофобия, и от громких криков
<ce> существо впадает в панику. Вскоре оно начинает нападать
<ce> на всех находящихся поблизости, в том числе и на вас.

Message:  1012
<ce> Владелец паука горько рыдает,
<ce> но _prop_ одобрительно кивает вам.

Message:  1013
<ce> "Я слышал, что здесь проходит вечеринка!
<ce> У кого мешок с деньгами, а?!"
<ce>
<ce> В таверну входит группа негодяев
<ce> и начинает трясти людей. _prop_ кричит,
<ce> что у них есть телохранитель, и указывает
<ce> на вас, направляя преступников к вам.

Message:  1014
<ce> В пьяном угаре вечеринки некоторые присутствующие
<ce> считают, что все это было подстроено для их
<ce> развлечения. Но как только запах крови станет
<ce> очевидным, они, вероятно, изменят свое мнение.

Message:  1015
<ce> Среди посетителей начинается импровизированная лотерея.
<ce> Победитель получит сумму в размере =smallg_ золотых,
<ce> собранную из пожертвований других участников.
<ce>
<ce> "Сообщаем вам, что в качестве ночного охранника
<ce> вечеринки вы можете участвовать без уплаты
<ce> вступительного взноса. Участвуете ли вы в лотерее?"

Message:  1016
<ce> Имена бросают в шляпу и выбирают одно из них.
<ce>
<ce> "_dummy_, идите за своим золотом!"
<ce>
<ce> Вы возвращаетесь к своим обязанностям, когда развлечение
<ce> стихает.

Message:  1017
<ce> Имена бросают в шляпу и выбирают одно из них.
<ce>
<ce> "%pcn, подойдите за своим золотом!"
<ce>
<ce> Кто-то оказывает вам услугу, передавая деньги, но один
<ce> недовольный посетитель начинает сомневаться в вашей победе.
<ce> Смесь алкоголя и бравады приводит к конфронтации.
<ce> _prop_ подает сигнал, что в данном случае
<ce> самооборона оправдана.

Message:  1018
<ce> К счастью, нападавший не был известен никому из посетителей.
<ce> Скорее всего, это просто случайно забредший сюда житель.

Message:  1019
<ce> Один из посетителей падает, и в таверне поднимается
<ce> тревога. К вам бежит _prop_.
<ce>
<ce> "Алкогольное отравление. Вы здесь самый трезвый человек.
<ce> Можете доставить этого человека в _temp_?
<ce> У него там есть родственники, которые могут вылечить
<ce> его бесплатно. Убедитесь, что это правильный храм.
<ce> Пожалуйста, поторопитесь".
<ce>
<ce> Бесчувственное тело приносят к вам.
<ce> Теперь жизнь этого человека зависит от вас.

Message:  1020
<ce> Как только вы входите в храм, ближайший служитель бледнеет
<ce> и бежит за помощью. Вскоре толпа помощников забирает у вас
<ce> тело для исцеления, которое начинается без промедления.
<ce> Один из служителей незамедлительно сообщает вам об успехе.

Message:  1021
<ce> Любопытно! Вы замечаете неподалеку мешок
<ce> с золотом, который, как вам кажется, находится
<ce> вне поля зрения остальных. Возможно, выброс
<ce> адреналина от сегодняшних событий слишком всех
<ce> измотал и притупил их внимание. Попытаетесь
<ce> ли вы забрать мешок себе?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Карманные кражи, средняя сложность)
<ce> (ПРОВЕРКА НАВЫКА: ДА: Скрытность, высокая сложность)

Message:  1022
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Скрытность/Карманные кражи)
<ce>
<ce> Слишком просто для человека вашей квалификации.
<ce> Мешок с деньгами теперь ваш, что добавляет
<ce> на ваш счет еще =stealg_ золотых.

Message:  1023
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Скрытность/Карманные кражи)
<ce>
<ce> Ваше движение, когда вы хватаете мешочек с монетами,
<ce> слишком очевидно. Кто-то, обернувшись, видит, что вы
<ce> держите в руках собственность, которая явно вам
<ce> не принадлежит. Ночные часы нарушаются
<ce> пронзительным криком.
<ce>
<ce> "ВОР! Гильдия Бойцов прислала нам вора!
<ce> Кто-нибудь, помогите!"
<ce>
<ce> Что-то подсказывает вам, что они не будут платить
<ce> вам за охрану. Возможно, вам следует поработать
<ce> над своими воровскими навыками, прежде чем
<ce> пытаться красть у своих клиентов.
   <--->
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Скрытность/Карманные кражи)
<ce>
<ce> Ваше движение, когда вы хватаете мешочек с монетами,
<ce> слишком очевидно. Кто-то, обернувшись, видит, что вы
<ce> держите в руках собственность, которая явно вам
<ce> не принадлежит. Ночные часы нарушаются
<ce> пронзительным криком.
<ce>
<ce> "Помогите! У нас в таверне вор! Помогите нам!"
<ce>
<ce> Что-то подсказывает вам, что они не будут платить
<ce> вам за охрану. Возможно, вам следует поработать
<ce> над своими воровскими навыками, прежде чем
<ce> пытаться красть у своих клиентов.

Message:  1026
<ce> Толпа заводит импровизированную песню, совершенно не в тон.

Message:  1027
<ce> "О да? Хочешь поспорить? Иди сюда, я тебя научу!"
<ce>
<ce> Драка не начинается.

Message:  1029
<ce> "Как насчет того, чтобы побросать бутылки в %ra,
<ce> что патрулирует таверну? Посмотрим, кто первым попадет
<ce> в н%pg3? Ха-ха-ха!"
<ce>
<ce> _prop_ закатывает глаза и бьет зачинщика по голове.

Message:  1030
%qdt:
 Гильдия Бойцов в ___questgiver_
 поручила мне охранять _tav_ во время
 сегодняшней вечеринки. Мне нужно быть
 там к 21:00 и стоять на страже до отбоя.

Message:  1031
<ce> "Эй, у меня есть идея! Как насчет того, чтобы взять
<ce> тот свиток, который вы взяли в Гильдии Магов,
<ce> и прочитать его вместе?"
<ce>
<ce> Прежде чем у кого-либо хватило ума остановить их, двое пьяниц
<ce> зачитали то, что было написано в свитке. Из воздуха
<ce> появляется существо, которое никто не контролирует.

Message:  1032
<ce> Народ улюлюкает и кричит, глядя на ваше боевое мастерство.

Message:  1033
<ce> "Хотите фокус? У меня есть домашний дракон! Смотрите все!"
<ce>
<ce> Затем возникает вспышка света, и маленькое чешуйчатое
<ce> существо начинает хлопать крыльями. _prop_,
<ce> зная этот трюк, настаивает, чтобы вы его не убивали.

Message:  1034
<ce> "Мерзкий %ra убил моего дракона!
<ce> Что с тобой не так, бестолочь?!"
<ce>
<ce> _prop_ пытается успокоить ревущую толпу,
<ce> но для н%g4 это уже слишком. Очевидно,
<ce> вам сегодня не заплатят.

Message:  1035
%qdt:
 Мне нужно отвезти пьяницу
 в _temp_ в этом городе.

Message:  1036
<ce> Пьяница совсем перестал дышать. Вам потребовалось
<ce> слишком много времени, чтобы доставить его в _temp_.
<ce> _prop_ очень гневается и отказывается платить.

Message:  1037
<ce> Дракона затягивает обратно в маленькую шкатулку с рунами,
<ce> которую держит один из завсегдатаев, и все они
<ce> покатываются со смеху.

QBN:
Item _reward_ gold
Item _stealg_ gold
Item _smallg_ gold range 150 to 350

Person _questgiver_ group Questor male
Person _prop_ group Local_3.0
Person _dummy_ group Local_3.1

Place _tav_ local tavern
Place _temp_ local temple

Clock _t1_ 0:45
Clock _t2_ 0:45
Clock _t3_ 0:45
Clock _t4_ 0:45
Clock _t5_ 0:45
Clock _t6_ 0:45
Clock _t7_ 0:45
Clock _t8_ 0:45
Clock _victory_ 0:45
Clock _drunk_ 2:00
Clock _draglife_ 0:05
Clock _delay_ 00:00

Foe _rob_ is 3 Thief
Foe _bard_ is Bard
Foe _spid_ is Spider
Foe _firat_ is Fire_atronach
Foe _iceat_ is Ice_atronach
Foe _drag_ is Dragonling_souless

--	Quest start-up:
    log 1030 step 0
    pc at _tav_ set _inside_

_arriv_ task:
    daily from 21:00 to 21:59

_duty_ task:
    daily from 6:00 to 22:00

_fail_ task:
    when not _duty_ and not _inside_ and not _e4_
    say 1003
    end quest

variable _dud_
_inside_ task:
    play sound talking_loud every 0 minutes 100 times

_start_ task:
    when _inside_ and _arriv_
    say 1010
    start timer _t1_

_t1_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t2_

_t2_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t3_

_t3_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t4_

_t4_ task:
    pick one of _e1_ _e2_ _e3_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t5_

_t5_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t6_

_t6_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t7_

_t7_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _t8_

_t8_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e8_ _e9_ _e11_ _e12_ _e13_ _dud_ _dud_ _dud_ _dud_ _dud_ _dud_
    start timer _victory_

_e1_ task:
    say 1011
    create foe _spid_ every 0 minutes 1 times with 100% success
    change foe _spid_ infighting true

_e1a_ task:
    killed _spid_
    say 1012

_e2_ task:
    say 1013
    create foe _rob_ every 0 minutes 1 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _rob_ infighting true
    clear _infight_

_e2a_ task:
    killed 3 _rob_
    say 1014

_e3_ task:
    prompt 1015 yes _entry_ no _refuse_

variable _refuse_
_entry_ task:
    pick one of _loser_ _loser_ _winner_

_loser_ task:
    say 1016

_winner_ task:
    say 1017
    create foe _bard_ every 0 minutes 1 times with 100% success

_e3a_ task:
    killed _bard_
    say 1018

_e4_ task:
    say 1019
    start timer _drunk_
    pc at _temp_ set _cure_
    log 1035 step 1
    stop timer _t1_
    stop timer _t2_
    stop timer _t3_
    stop timer _t4_
    stop timer _t5_
    stop timer _t6_
    stop timer _t7_
    stop timer _t8_

_drunk_ task:
    say 1036
    end quest

_cure_ task:
    say 1020
    start timer _delay_

_delay_ task:
    start task _victory_

_e5_ task:
    prompt 1021 yes _st1_ no _refuse_

variable _st1_
_st2_ task:
    when _st1_ and _skill_
    say 1022
    get item _stealg_

_stfail_ task:
    when _st1_ and not _skill_
    say 1023
    get item _stealg_
    end quest

_stealth_ task:
    when skill Stealth is at least 75
    start task _skill_

_pickpocket_ task:
    when skill Pickpocket is at least 45
    start task _skill_

variable _skill_

_e8_ task:
    say 1026

_e9_ task:
    say 1027

_e11_ task:
    say 1029

_e12_ task:
    say 1031
    pick one of _sum1_ _sum2_

_sum1_ task:
    create foe _firat_ every 0 minutes 1 times with 100% success
    change foe _firat_ infighting true

_sum2_ task:
    create foe _iceat_ every 0 minutes 1 times with 100% success
    change foe _iceat_ infighting true

_killsum_ task:
    killed _firat_
    killed _iceat_
    say 1032

_e13_ task:
    say 1033
    create foe _drag_ every 0 minutes 1 times with 100% success
    restrain foe _drag_
    start timer _draglife_

_e13a_ task:
    killed _drag_
    say 1034
    end quest

_draglife_ task:
    remove foe _drag_
    say 1037

_victory_ task:
    give pc _reward_
    end quest