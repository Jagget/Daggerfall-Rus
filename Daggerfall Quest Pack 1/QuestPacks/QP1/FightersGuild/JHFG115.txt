Quest: JHFG115
DisplayName: Пятое колесо
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас есть человек, который ищет боевого напарника, хочет
<ce> спуститься в какую-нибудь пещеру, разбить парочку бандитов
<ce> и поделить добычу. Говорит, что предлагает за это какой-то
<ce> зачарованный предмет. Хотите взять заказ?

RefuseQuest:  [1001]
<ce> Предпочитаете работать в одиночку?

AcceptQuest:  [1002]
<ce> "Что ж, тогда я вас познакомлю. Подождите".
<ce>
<ce> _questgiver_ кричит в дверь.
<ce> Вскоре входит ваш клиент, чтобы познакомиться с вами.
<ce>
<ce> "Здравствуйте! %pcn, говорите?
<ce> Меня зовут _friend_.
<ce> Очень приятно. Давайте отправимся в ___dung_.
<ce> Я знаю, что там укрывается банда преступников, и мы
<ce> с пользой проведем время, очищая его. Я подарю вам
<ce> этот волшебный _reward_, как только мы все закончим.
<ce> Тогда вперед!"

QuestFail:  [1003]
<ce> Задание провалено.

QuestComplete:  [1004]
<ce> Вы считаете свою работу выполненной.

Message:  1019
<ce> _friend_ говорит:
<ce>
<ce> "Итак, мой _reward_ за вашу помощь в бою, верно?
<ce> Полагаю, это место должно быть достаточно
<ce> прибыльным, чтобы окупить это. Мы поделим
<ce> предметы, снятые с тел преступников".
<ce>
<ce> Эти слова оставляют некоторые сомнения в %g4 искренности.

Message:  1020
<ce> "Что ж, я доволен. Я думаю, мы оба заработалигораздо 
<ce> больше, чем ожидали. Забирайте _reward_. Может быть,
<ce> я снова увижу вас в ___questgiver_".
<ce>
<ce> _friend_ бросает вам предмет
<ce> и исчезает из виду.

Message:  1021
<ce> _friend_ говорит:
<ce>
<ce> "Послушайте, я знаю, что мы договорились, но вы
<ce> берете слишком много! Я даже не уверен, что смогу
<ce> получить прибыль сегодня. По-моему, будет
<ce> справедливо, если мы оба просто уйдем с тем,
<ce> что у нас есть в руках, и забудем обо всей этой
<ce> сделке. Спасибо за помощь".
<ce>
<ce> Похоже, %g1 собирается уходить. Если бы вы
<ce> захотели, то могли бы убить %g4, чтобы получить
<ce> %g4 вещи. Отпустите ли вы %g4 без оплаты?

Message:  1022
<ce> Когда %g1 исчезает, вы понимаете, что, по крайней
<ce> мере, сохранили свою честь, не говоря уже
<ce> о репутации Гильдии Бойцов. Кому-то другому
<ce> придется научить %g4 не нарушать свое слово.

Message:  1023
<ce> "Подождите, что?"
<ce>
<ce> Ваше нападение стало полной неожиданностью.

Message:  1025
<ce> Эй, %pcf, вам не встречался _friend_?
<ce> Потому что %g1 направлялся прямо сюда после
<ce> того, как вы копошились в ___dung_.
<ce>
<ce> Мертв? О, да ладно, это неправильно. Гильдия
<ce> работает не для этого. Послушайте, сделайте
<ce> одолжение и сохраните это в тайне. Гильдия
<ce> Бойцов нужна в ___questgiver_, 
<ce> и если начнут распространяться слухи о том,
<ce> что какой-то идиот уводит людей на смерть 
<ce> в случайные пещеры, %t прогонят нас отсюда.
<ce> Давайте сделаем вид, что этого никогда
<ce> не было, хорошо?

Message:  1030
%qdt:
 Гильдия Бойцов ___questgiver_
 наняла меня и _friend_, еще одного
 воина, сражаться с бандитами
 за сокровища внутри ___dung_. Мне
 заплатят зачарованный _reward_,
 когда мы закончим.

QBN:
Item _reward_ magic_item

Person _questgiver_ group Questor male
Person _friend_ faction Generic_Knightly_Order
Person _fg_ faction The_Fighters_Guild

Place _dung_ remote dungeon3

Clock _timer_ 75.00:00
Clock _dungtime_ 20:00 2.00:00

Foe _enemy1_ is Burglar
Foe _enemy2_ is Thief
Foe _frenemy_ is Warrior

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    reveal _dung_
    add _friend_ face
    pc at _dung_ do _inside_
    pick one of _e1_ _e2_

_infight_ task:
    daily from 0:00 to 23:59
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true
    clear _infight_

_inside_ task:
    say 1019
    create foe _enemy1_ every 46 minutes indefinitely with 23% success
    create foe _enemy2_ every 35 minutes indefinitely with 11% success
    start timer _dungtime_

_notinside_ task:
    when not _inside_
    stop timer _dungtime_

_dungtime_ task:
    start task _victory_

_slain_ task:
    killed 3 _enemy1_
    killed 3 _enemy2_
    start task _victory_

variable _e1_
variable _victory_
_true_ task:
    when _victory_ and _e1_
    say 1020
    drop _friend_ face
    give pc _reward_
    end quest

variable _e2_
_trick_ task:
    when _victory_ and _e2_
    prompt 1021 yes _mercy_ no _kill_

_mercy_ task:
    say 1022
    give pc nothing
    end quest

_kill_ task:
    drop _friend_ face
    say 1023
    create foe _frenemy_ every 0 minutes 1 times with 100% success
    give item _reward_ to _frenemy_
    change foe _frenemy_ infighting true
    make _reward_ permanent

_kill2_ task:
    killed _frenemy_

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _kill2_
    clear _qgclicked_ _clearclick_

_question_ task:
    when _qgclicked_ and _kill2_
    say 1025
    change repute with _fg_ by +3
    end quest
-+3 is intentional, not +2.

_timer_ task:
    end quest