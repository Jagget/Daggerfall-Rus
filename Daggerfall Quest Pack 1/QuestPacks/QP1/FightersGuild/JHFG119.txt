Quest: JHFG119
DisplayName: Неожиданное вторжение
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Привет, %pcf, рад, что вы здесь. Хочу быть
<ce> с тобой откровенным, в городе происходит
<ce> что-то странное. Я не знаю, в чем причина.
<ce> У нас в магазинах появилось множество
<ce> призванных монстров, сеющих хаос. Вы,
<ce> вероятно, услышите об этом, если
<ce> задержитесь здесь на несколько минут. Не
<ce> хотите помочь нам обойти несколько
<ce> магазинов, чтобы уничтожить монстров?
   <--->
<ce> %pcf, не могли бы вы задержаться здесь ненадолго?
<ce> ___questgiver_ сейчас выглядит не лучшим образом.
<ce> Какая-то проблема со случайно появляющимися
<ce> монстрами всех напугала, и это лишь вопрос
<ce> времени, когда мы услышим очередной призыв
<ce> о помощи. Хотите сыграть роль героя?

RefuseQuest:  [1001]
<ce> Тогда ладно. Идите и найдите себе другое занятие.
   <--->
<ce> Ничего страшного. В Гильдии Бойцов много героев.

AcceptQuest:  [1002]
<ce> Вы слышите крик, доносящийся снаружи.
<ce>
<ce> "Они в _place1_! Кто-нибудь, помогите!"
<ce>
<ce> _questgiver_ говорит: "Что ж, ваш выход. Мы поговорим
<ce> о вашей награде, когда они будут мертвы. Просто не
<ce> сдавайтесь, пока все не закончится, хорошо? В этом
<ce> городе есть невинные люди, которым нужна помощь."
   <--->
<ce> Вы слышите крик, доносящийся снаружи.
<ce>
<ce> "Монстр проник в _place1_!
<ce> Там невинные люди! Кто-нибудь, пожалуйста, помогите им!"
<ce>
<ce> _questgiver_ говорит: "Что ж, приступайте. Я соберу
<ce> какую-нибудь награду, когда все закончится. И не
<ce> тяните время. Люди могут погибнуть, если вы будете
<ce> действовать слишком медленно."

QuestFail:  [1003]
<ce> Городская стража не собирается обеспечивать безопасность
<ce> этого города, и вы, по-видимому, тоже.

QuestComplete:  [1004]
<ce> Вам весело, %pcf? Что за суета. Хорошо,
<ce> что у нас здесь есть такие люди, как вы.
<ce> Надеюсь, мы выясним, кто за этим стоит,
<ce> и всадим ему длинный меч между ребер.
<ce> Но не здесь и не сейчас. Возьмите это,
<ce> наслаждайтесь. И не удивляйтесь, если %t
<ce> узнают о вашем добром поступке.
   <--->
<ce> Ого! Да вы в одиночку затащили битву сегодня,
<ce> а %pcf? Отличная работа. Я уверен, что %t захотят
<ce> узнать, кто сегодня обеспечивал безопасность
<ce> города. Конечно, это была не городская стража.
<ce> Пока вы были заняты, мы послали несколько других
<ce> наших людей отбиваться от бесов, но они просто
<ce> получат немного золота. Настоящая награда - для
<ce> вас. Все ваше, дружище.

QuestorPostSuccess:  [1008]
___questgiver_ в большом долгу перед вами, приятель.

QuestorPostFailure:  [1009]
К счастью, в ваше отсутствие город охраняли другие люди.

Message:  1020
<ce> Снаружи доносится крик.
<ce>
<ce> "Они в _place2_! Бегите!"
   <--->
<ce> Снаружи доносится крик.
<ce>
<ce> "Монстр в _place2_!
<ce> Кто-нибудь, быстрее!"

Message:  1021
<ce> Слышен крик снаружи.
<ce>
<ce> "Что-то в _place3_!
<ce> Помогите! Кто-нибудь!"
   <--->
<ce> Вы слышите крик снаружи.
<ce>
<ce> "Убейте его! Оно в _place3_!
<ce> Кто-нибудь, помогите!"

Message:  1022
<ce> Вы слышите, как кто-то кричит в отчаянии.
<ce>
<ce> "В _place4_ завелся монстр!
<ce> Кто-нибудь, убейте его!"
   <--->
<ce> Вы слышите, как кто-то кричит в отчаянии.
<ce>
<ce> "Это ужасно! Оно в _place4_!
<ce> Зовите магов!"

Message:  1023
<ce> Слышен вопль отчаяния.
<ce>
<ce> "Уберите это! Убейте это! Оно
<ce> в _place5_!"
   <--->
<ce> Вы слышите вопль отчаяния.
<ce>
<ce> "Это в _place5_!
<ce> Кто-нибудь, кто угодно, помогите!"

Message:  1024
<ce> В городе воцарилась тишина.

Message:  1030
%qdt:
 Гильдия Бойцов
 в ___questgiver_ просит
 разогнать монстров, которые
 появляются в зданиях по всему
 городу. Мне нужно поторопиться.
 Первое из них - _place1_.

Message:  1031
%qdt:
 Мне нужно поспешить в _place2_.

Message:  1032
%qdt:
 Монстр находится в _place3_.

Message:  1033
%qdt:
 Враг находится в _place4_.

Message:  1034
%qdt:
 Помощь требуется в _place5_.

Message:  1035
%qdt:
 Все стихло.

QBN:
Item _reward_ magic_item

Person _questgiver_ group Questor male

Place _place1_ local random
Place _place2_ local random
Place _place3_ local random
Place _place4_ local random
Place _place5_ local random

Clock _timer1_ 00:20
Clock _delay1_ 00:00 00:01
Clock _timer2_ 00:20
Clock _delay2_ 00:00 00:01
Clock _timer3_ 00:20
Clock _delay3_ 00:00 00:01
Clock _timer4_ 00:20
Clock _delay4_ 00:00 00:01
Clock _timer5_ 00:20
Clock _delay5_ 00:00 00:01
Clock _failsafe_ 6:00
Clock _failsafe2_ 3.00:00

Foe _mon1a_ is Gargoyle
Foe _mon1b_ is Dragonling
Foe _mon2a_ is Frost_daedra
Foe _mon2b_ is Fire_daedra
Foe _mon3a_ is Mummy
Foe _mon3b_ is Wraith
Foe _mon4a_ is Flesh_atronach
Foe _mon4b_ is Daedroth
Foe _mon5a_ is Daedra_seducer
Foe _mon5b_ is Lich
Foe _im_ is Imp
Foe _ha_ is Harpy
Foe _sp_ is Spriggan

--	Quest start-up:
  start timer _timer1_
  start timer _failsafe_
  log 1030 step 0
  pick one of _spawn1a_ _spawn1b_

_infight_ task:
  daily from 0:00 to 23:59
  change foe _im_ infighting true
  change foe _ha_ infighting true
  change foe _sp_ infighting true
  clear _infight_

_misc_ task:
  when not _killed5_
  create foe _im_ every 4 minutes indefinitely with 80% success
  create foe _ha_ every 11 minutes indefinitely with 60% success
  create foe _sp_ every 5 minutes indefinitely with 40% success

_spawn1a_ task:
  pc at _place1_ do _mon11_

_mon11_ task:
  create foe _mon1a_ every 0 minutes 1 times with 100% success
  change foe _mon1a_ infighting true
  stop timer _timer1_

_spawn1b_ task:
  pc at _place1_ do _mon12_

_mon12_ task:
  create foe _mon1b_ every 0 minutes 1 times with 100% success
  change foe _mon1b_ infighting true
  stop timer _timer1_

_killed1_ task:
  killed _mon1a_
  killed _mon1b_
  start timer _delay1_

_delay1_ task:
  say 1020
  log 1031 step 1
  pick one of _spawn2a_ _spawn2b_
  start timer _timer2_

_spawn2a_ task:
  pc at _place2_ do _mon21_

_mon21_ task:
  create foe _mon2a_ every 0 minutes 1 times with 100% success
  change foe _mon2a_ infighting true
  stop timer _timer2_

_spawn2b_ task:
  pc at _place2_ do _mon22_

_mon22_ task:
  create foe _mon2b_ every 0 minutes 1 times with 100% success
  change foe _mon2b_ infighting true
  stop timer _timer2_

_killed2_ task:
  killed _mon2a_
  killed _mon2b_
  start timer _delay2_

_delay2_ task:
  say 1021
  log 1032 step 2
  pick one of _spawn3a_ _spawn3b_
  start timer _timer3_

_spawn3a_ task:
  pc at _place3_ do _mon31_

_mon31_ task:
  create foe _mon3a_ every 0 minutes 1 times with 100% success
  change foe _mon3a_ infighting true
  stop timer _timer3_

_spawn3b_ task:
  pc at _place3_ do _mon32_

_mon32_ task:
  create foe _mon3b_ every 0 minutes 1 times with 100% success
  change foe _mon3b_ infighting true
  stop timer _timer3_

_killed3_ task:
  killed _mon3a_
  killed _mon3b_
  start timer _delay3_

_delay3_ task:
  say 1022
  log 1033 step 3
  pick one of _spawn4a_ _spawn4b_
  start timer _timer4_

_spawn4a_ task:
  pc at _place4_ do _mon41_

_mon41_ task:
  create foe _mon4a_ every 0 minutes 1 times with 100% success
  change foe _mon4a_ infighting true
  stop timer _timer4_

_spawn4b_ task:
  pc at _place4_ do _mon42_

_mon42_ task:
  create foe _mon4b_ every 0 minutes 1 times with 100% success
  change foe _mon4b_ infighting true
  stop timer _timer4_

_killed4_ task:
  killed _mon4a_
  killed _mon4b_
  start timer _delay4_

_delay4_ task:
  say 1023
  log 1034 step 4
  pick one of _spawn5a_ _spawn5b_
  start timer _timer5_

_spawn5a_ task:
  pc at _place5_ do _mon51_

_mon51_ task:
  create foe _mon5a_ every 0 minutes 1 times with 100% success
  change foe _mon5a_ infighting true
  stop timer _timer5_

_spawn5b_ task:
  pc at _place5_ do _mon52_

_mon52_ task:
  create foe _mon5b_ every 0 minutes 1 times with 100% success
  change foe _mon5b_ infighting true
  stop timer _timer5_

_killed5_ task:
  killed _mon5a_
  killed _mon5b_
  start timer _delay5_
  stop timer _failsafe_
  start timer _failsafe2_

_delay5_ task:
  log 1035 step 5
  say 1024
  remove foe _im_
  remove foe _ha_
  remove foe _sp_

_pcgetsgold_ task:
  when _qgclicked_ and _killed5_
  give pc _reward_
  legal repute +5
  end quest

_qgclicked_ task:
  clicked npc _questgiver_

_timer1_ task:
  start task _fail_

_timer2_ task:
  start task _fail_

_timer3_ task:
  start task _fail_

_timer4_ task:
  start task _fail_

_timer5_ task:
  start task _fail_

_failsafe_ task:
  start task _fail_

_fail_ task:
  say 1003
  end quest

_clearclick_ task:
  when _qgclicked_ and not _killed5_
  clear _qgclicked_ _clearclick_

_failsafe2_ task:
  end quest