Quest: JHFG236
DisplayName: Своенравный волшебник
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Некий призыватель уединился в пещере и посылает
<ce> своих големов нападать на прохожих. Когда они
<ce> умирают или сдаются, големы доставляют ему добычу.
<ce> %t спонсируют этот контракт на убийство волшебника,
<ce> и предлагают за это заколдованный предмет.

RefuseQuest:  [1001]
<ce> Иногда магические предметы действительно хороши, вот увидите.

AcceptQuest:  [1002]
<ce> Что ж, тогда позвольте мне отметить ___dung_
<ce> на вашей карте. Не знаю, насколько сложно убить
<ce> такого волшебника, но эти атронахи наносят большой
<ce> урон. Постарайтесь не проводить в пещере слишком
<ce> много времени. Если нужно, спите снаружи, чтобы
<ce> они не пришли на вас охотиться. Вернитесь сюда
<ce> через =timer_ дней с известием о смерти
<ce> волшебника, и я подготовлю для вас _item_.

QuestFail:  [1003]
<ce> Гильдии Бойцов нужно было, чтобы к этому времени вы уже сообщили
<ce> об убийстве призывателя из ___dung_.

QuestComplete:  [1004]
<ce> Ха, я знал, что вы это сделаете! Да, хорошая работа.
<ce> Гильдия Бойцов не потерпит поражения от какого-то
<ce> придурка-мага. А теперь - ваша награда.

QuestorPostfailure:  [1009]

Message:  1010
%qdt:
 _qgiver_ из ___qgiver_ поручил
 мне контракт от властей региона
 ____qgiver_ на убийство колдуна
 в подземелье под названием
 ___dung_. Если я вернусь
 в течение =timer_ дней, то получу
 зачарованный _item_.

Message:  1011
<ce> Вы чувствуете сильную магию
<ce> в воздухе. Колдун где-то здесь.

Message:  1012
<ce> После смерти мага атронахи перестанут охотиться на вас.

QBN:
Person _qgiver_ group Questor male

Place _dung_ remote dungeon9

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _anc_ is Sorcerer
Foe _e1_ is Flesh_atronach
Foe _e2_ is Ice_atronach
Foe _e3_ is Fire_atronach
Foe _e4_ is Iron_atronach

Item _item_ magic_item

--	Quest start-up:
    start timer _timer_
    log 1010 step 0
    place foe _anc_ at _dung_
    reveal _dung_
    pc at _dung_ set _inside_

_inside_ task:
    say 1011

_infight_ task:
    daily from 0:00 to 23:59
    change foe _e1_ infighting true
    change foe _e2_ infighting true
    change foe _e3_ infighting true
    change foe _e4_ infighting true
    clear _infight_

_spawn_ task:
    when _inside_ and not _slain_
    create foe _e1_ every 5 minutes indefinitely with 2% success
    create foe _e2_ every 8 minutes indefinitely with 3% success
    create foe _e3_ every 11 minutes indefinitely with 4% success
    create foe _e4_ every 13 minutes indefinitely with 5% success

_slain_ task:
    killed _anc_
    say 1012

_pcgetsgold_ task:
    when _qgclicked_ and _slain_
    give pc _item_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest