Quest: JHFG130
DisplayName: Как змея
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Попался странный контракт, в котором, надеюсь, кто-нибудь
<ce> сможет разобраться. Говорят, что по какому-то кладбищу
<ce> бродит оборотень, притворяясь вампиром. Просят помочь
<ce> убить эту тварь, так как она слишком сильна для одного
<ce> человека. Не хотите помочь?

RefuseQuest:  [1001]
<ce> Ладно, все равно спасибо.

AcceptQuest:  [1002]
<ce> "Что ж, тогда давайте отправим вас в путь".
<ce>
<ce> _questgiver_ кричит, и член гильдии
<ce> открывает дверь. Входит вооруженный до зубов воин и кланяется.
<ce>
<ce> "Приветствую вас, боец! Я _fighter_. Благодарю
<ce> вас за то, что вступились за меня. Нам нужно
<ce> убить оборотня, эту мерзкую тварь! Мы отправляемся
<ce> в ___cemetery_, и у нас есть =timer_ дней!"

QuestFail:  [1003]
<ce> Задание провалено.

QuestComplete:  [1004]
<ce> Вы забираете свою награду.

RumorsDuringQuest:  [1005]
Множество оборотней бродило по ночам.

Message:  1010
<ce> "Сейчас еще день, и чудовище хорошо маскируется", -
<ce> говорит _fighter_. "Давайте разобьем лагерь
<ce> до ночи и тогда мы найдем нашего врага".

Message:  1011
<ce> Вы и _fighter_ замечаете какое-то
<ce> движение поблизости и достаете оружие.
<ce> Некто сохраняет дистанцию и окликает.
<ce>
<ce> "Стойте, искатели приключений! Это кладбище находится под
<ce> моей защитой. Я не позволю больше совершать набеги на него!
<ce> Пусть мертвые покоятся, а их вещи остаются нетронутыми!"
<ce>
<ce> _fighter_ кричит: "Ты убил моих друзей, которые пришли
<ce> до меня! Ты не заслуживаешь пощады, кровосос! Ты умрешь!"
<ce>
<ce> Вы спрашиваете вампира, почему он принял такую форму,
<ce> и получаете растерянный ответ. "Я не понимаю, что вы имеете
<ce> в виду. Я вампир, который защищает останки своей семьи".

Message:  1012
<ce> Оказавшись в меньшинстве, _fighter_
<ce> сражается еще отчаяннее.

Message:  1013
<ce> Вампир шипит, готовясь к битве.

Message:  1014
<ce> _fighter_ вздыхает. "Я рад, что вы приняли мою точку зрения.
<ce> Таких монстров не должно существовать. Мне придется
<ce> вернуться сюда с телегой, чтобы мы могли начать разграбление
<ce> этого места. Вот, возьмите вот это".

Message:  1015
<ce> Вампир кланяется. "Ваше уважение к мертвым достойно
<ce> похвалы. Я скоро уйду и сообщу своим сородичам о ваших
<ce> деяниях. Спасибо, %ra, что проявили ко мне милосердие".
<ce>
<ce> На теле _fighter_ вы замечаете мешочек
<ce> с деньгами - должно быть, это ваша награда.
<ce> И поскольку %g1 уже не будет возражать...

Message:  1016
<ce> Оба противника теперь мертвы. На теле _fighter_
<ce> вы замечаете мешочек с деньгами - должно быть, это ваша
<ce> награда. И поскольку %g1 уже не будет возражать...

Message:  1017
<ce> Вы должны были уже быть в ___cemetery_.
<ce> _fighter_ сердито фыркает.

Message:  1018
<ce> _fighter_ качает головой и поворачивается
<ce> к вам. "Я сказал это, чтобы вы пришли
<ce> сюда. Я не хотел говорить гильдии
<ce> правду, вот и придумал про оборотня.
<ce> А теперь хватит болтать".
<ce>
<ce> Затем %g1 кричит вампиру: "Ты сейчас
<ce> умрешь, как умерли мои друзья!"
<ce>
<ce> Вампир смотрит на вас со страхом, но набирается храбрости.
<ce> "Я должен защитить свою семью.  Они - все,
<ce> что у меня осталось".
<ce>
<ce> Эти двое начинают сражаться.
<ce> Поможете ли вы бойцу (да) или вампиру (нет)?

Message:  1030
%qdt:
 _fighter_ завербовал меня через Гильдию
 Бойцов ___questgiver_. Нужно помочь
 убить оборотня, который бродит
 по кладбищу ___cemetery_. Мы должны
 добраться туда за =timer_ дней.

QBN:
Item _reward_ gold range 500 to 600
Item _dagger_ item class 3 subclass 1
Item _sword_ item class 3 subclass 8

Place _cemetery_ remote graveyard

Clock _timer_ 00:00 0 flag 17 range 0 2

Person _fighter_ group Resident1
Person _questgiver_ group Questor
Person _vamp1_ faction The_Anthotis
Person _vamp2_ faction The_Garlythi
Person _vamp3_ faction The_Haarvenu
Person _vamp4_ faction The_Khulari
Person _vamp5_ faction The_Lyrezi
Person _vamp6_ faction The_Montalion
Person _vamp7_ faction The_Selenu
Person _vamp8_ faction The_Thrafey
Person _vamp9_ faction The_Vraseth

Foe _fgt_ is Warrior
Foe _vmp_ is Vampire

--	Quest start-up:
    start timer _timer_
    pc at _cemetery_ set _inside_
    log 1030 step 0
    add _fighter_ face
    reveal _cemetery_
    update-quest-item _dagger_ set-material Ebony
    update-quest-item _sword_ set-material Silver

_daytime_ task:
    daily from 6:00 to 18:00

variable _inside_

_notyet_ task:
    when _inside_ and _daytime_
    say 1010

_nowspawn_ task:
    when _inside_ and not _daytime_ and not _endfight_
    say 1011
    prompt 1018 yes _helpfight_ no _helpvamp_
    create foe _fgt_ every 0 minutes 1 times with 100% success
    create foe _vmp_ every 0 minutes 1 times with 100% success
    change foe _fgt_ infighting true
    change foe _vmp_ infighting true
    drop _fighter_ face
    give item _dagger_ to _vmp_
    give item _sword_ to _fgt_
    make _dagger_ permanent
    make _sword_ permanent

_helpfight_ task:
    say 1013
    cast Shield spell on _vmp_
    cast Orc_Strength spell on _vmp_
    cast Feet_of_Notorgo spell on _vmp_
    cast Free_Action spell on _vmp_
    change foe _fgt_ team 1

_helpvamp_ task:
    say 1012
    cast Shield spell on _fgt_
    cast Orc_Strength spell on _fgt_
    cast Feet_of_Notorgo spell on _fgt_
    cast Free_Action spell on _fgt_
    change foe _vmp_ team 1

_killfight_ task:
    killed _fgt_

_killvamp_ task:
    killed _vmp_

_vampvict_ task:
    when _helpvamp_ and _killfight_ and not _killvamp_
    say 1015
    give pc _reward_
    start task _endfight_
    stop timer _timer_
    change repute with _vamp1_ by +5
    change repute with _vamp2_ by +5
    change repute with _vamp3_ by +5
    change repute with _vamp4_ by +5
    change repute with _vamp5_ by +5
    change repute with _vamp6_ by +5
    change repute with _vamp7_ by +5
    change repute with _vamp8_ by +5
    change repute with _vamp9_ by +5
    remove foe _vmp_

_fightvict_ task:
    when _helpfight_ and _killvamp_ and not _killfight_
    say 1014
    give pc _reward_
    remove foe _fgt_
    start task _endfight_

_nend1_ task:
    when _killvamp_ and _killfight_
    say 1016
    give pc _reward_
    start task _endfight_

_endfight_ task:
    remove log step 0

_delay_ task:
    when _endfight_ and not _inside_
    end quest

_timertext_ task:
    when _timer_ and not _helpfight_ and not _helpvamp_
    say 1017

_timer_ task:
    end quest