Quest: JHFG103c
DisplayName: Даэдра на свободе

QRC:

QuestComplete:  [1004]
<ce> Ужас ___farm_ был уничтожен,
<ce> и %t больше не будут осуждать вас.

Message:  1010
<ce> Скучающий курьер без особого энтузиазма вручает вам письмо.

Message:  1011
%pcn
<ce>
 У нас возникла проблема. Некоторое время назад вы заключили
контракт с Гильдией Бойцов на уничтожение одного
преступника в ___dung_. Где-то в промежутке между тем, как
%t отправили приказ и вашим прибытием, в негодяя вселился
некий даэдра. Теперь, благодаря вам, даэдра освободился от
своего тела и продолжает нападать на ___farm_. %t считают
что ответственность за это лежит на вас. Отправляйтесь туда
и уничтожьте даэдра в течение 30 дней, иначе ваше доброе
имя потеряет свой блеск в провинции ____farm_.
<ce>
                    _fg_
                    Гильдия Бойцов
                    ____farm_

QBN:

Clock _delay_ 5.00:00 7.00:00
Clock _timer_ 30.00:00

Place _farm_ remote poorhome
Place _dung_ local dungeon

Person _fg_ faction The_Fighters_Guild

Foe _daed_ is Daedroth

Item _letter_ letter used 1011

---- Quest startup
    start timer _delay_

_delay_ task:
    give pc _letter_ notify 1010
    have _letter_ set _begin_
    pc at _farm_ set _arrive_

_begin_ task:
    start timer _timer_
    pc at _farm_ set _arrive_

_arrive_ task:
    start task _spawn_

_spawn_ task:
    create foe _daed_ every 0 minutes 1 times with 100% success
    change foe _daed_ infighting true

_slain_ task:
    killed _daed_
    give pc nothing
    end quest

_timer_ task:
    legal repute -10
    end quest
