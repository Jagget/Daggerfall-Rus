Quest: JHFG238
DisplayName: Нежеланный вор
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %rt отдали приказ избавиться от одного члена
<ce> Гильдии Воров в этом городе. Околачивается
<ce> в магазине, я думаю, для прикрытия. Платят
<ce> не очень много, но это довольно простой контракт.
<ce> Хотите?

RefuseQuest:  [1001]
<ce> Вы ведь не боитесь Гильдии Воров, не так ли, %pct?

AcceptQuest:  [1002]
<ce> Отлично! Я беспокоился об этом. Рад, что кто-то
<ce> этим занялся. Я подписываю контракт, а это значит,
<ce> что у вас есть 24 часа, чтобы добраться
<ce> до _res_ и убить этого вора,
<ce> кем бы он ни был. Вероятно, его будет довольно
<ce> легко обнаружить. Только вернитесь целым
<ce> и невредимым, у меня будут для вас
<ce> _gold_ золотых, прямо из казны.

QuestFail:  [1003]
<ce> Ваш контракт на убийство вора
<ce> в _res_ просрочен.

QuestComplete:  [1004]
<ce> Да, Гильдия Воров не очень-то любит воевать.
<ce> Если бы они просто пришли к нам, мы бы научили
<ce> их драться, но вы же знаете, как это бывает.
<ce> Соблазны преступного мира и все такое.
<ce> Ну, хватит болтать. Вот ваше золото.

Message:  1010
<ce> Вы слышите шорохи и какие-то нервные разговоры.
<ce> Ваше присутствие здесь определенно замечено.

Message:  1011
<ce> Вор убит, а вы еще живы.

Message:  1030
%qdt:
 _questgiver_ из Гильдии Бойцов
 заплатит мне _gold_ золотых, если я доберусь
 до _res_ в ___res_
 и убью враждебно настроенного вора.
 Мне нужно вернуться в зал гильдии через 24 часа.

QBN:
Person _questgiver_ group Questor male
Person _dummy_ faction The_Thieves_Guild

Place _res_ local shop

Clock _timer_ 1.00:00

Item _gold_ gold range 150 to 200

Foe _mage_ is Thief

--	Quest start-up:
	place foe _mage_ at _res_
	log 1030 step 0
    pc at _res_ set _inside_
    start timer _timer_

_inside_ task:
    say 1010
	change foe _mage_ infighting true

_slain_ task:
    killed _mage_
    say 1011
    change repute with _dummy_ by -10

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc _gold_
    start quest JHFGC01
	end quest

_qgclicked_ task:
    clicked npc _questgiver_

_timer_ task:
	say 1003
	end quest