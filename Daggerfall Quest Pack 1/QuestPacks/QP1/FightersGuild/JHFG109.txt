Quest: JHFG109
DisplayName: Дикая охота
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Привет, %pcf, у меня есть для вас что-то вроде
<ce> испытания без всякого риска. Что-то вроде вендетты,
<ce> которую нужно разрешить. У нас есть клиент, которого
<ce> достал оборотень, пожирающий животных на его ферме.
<ce> Он знает, что тот живет в ___dungeon_, 
<ce> но не может потрудиться пойти туда и сделать это 
<ce> самостоятельно. Для этого вам нужно быть хорошим 
<ce> охотником, по-настоящему хорошим. Хотите попробовать? 
<ce> Награда за выполнение - _reward_ с чарами.

RefuseQuest:  [1001]
<ce> Да, рано или поздно он появится в нужное время
<ce> и обнаружит, что его ждут серебряные вилы. Позвольте
<ce> мне посмотреть, что еще у меня есть.

AcceptQuest:  [1002]
<ce> Вот это дух! Как я уже сказал, я не буду держать на вас
<ce> зла, если вы не доведете это до конца. На кону не стоят
<ce> деньги, человеческие жизни не подвергаются опасности.
<ce> Пока, по крайней мере. Фокус в том, что эта тварь слишком 
<ce> хорошо знает свое логово. Отличает незнакомцев и друзей.
<ce> Как только вы ступите на его территорию, у вас будет всего
<ce> несколько часов, чтобы найти его, прежде чем он исчезнет,
<ce> около шести, как мне сказали. Так что не блуждайте
<ce> там без цели. Доберитесь до ___dungeon_
<ce> и убейте тварь, пока она не исчезла. Я придержу
<ce> _reward_, пока вы не предъявите мне доказательства.

QuestFail:  [1003]
<ce> Вы понимаете, что прошло уже много времени.
<ce> Оборотень, должно быть, давно ушел.

QuestComplete:  [1004]
<ce> Вы сделали это? Потрясающе. Говорю вам, старый
<ce> фермер будет в восторге. Думаю, я расскажу всем,
<ce> что в ___qgiver_ есть эксперт по ликантропам.
<ce> В любом случае, забирайте свою награду.

Message:  1015
<ce> Теперь, когда вы ступили внутрь, пути назад нет.
<ce> Пришло время найти оборотня, пока он не сбежал.

Message:  1020
<ce> Ваша охота была успешной. Вы берете образец
<ce> крови оборотня и кладете в рюкзак.

Message:  1030
%qdt:
 Гильдия Бойцов ___qgiver_ поставила
 передо мной задачу найти и убить оборотня
 в ___dungeon_, который склонен убегать,
 если на него охотятся. Как только я войду
 в подземелье, у меня будет всего около
 шести часов, чтобы найти и убить его.
 Моей наградой будет магический _reward_.

QBN:
Item _reward_ magic_item
Item _blood_ werewolf_blood

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon11

Clock _failsafe_ 90.00:00
Clock _hunt_ 6:00

Foe _enemy1_ is Werewolf

--	Quest start-up:
	start timer _failsafe_
	log 1030 step 0
	reveal _dungeon_
	pc at _dungeon_ set _starthunt_
	place foe _enemy1_ at _dungeon_

_starthunt_ task:
	say 1015
	start timer _hunt_

_inj_ task:
	injured _enemy1_
	clicked foe _enemy1_
	change foe _enemy1_ infighting true

_slain_ task:
	killed 1 _enemy1_ saying 1020
	stop timer _hunt_
	get item _blood_

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_hunt_ task:
	say 1003
	change repute with _qgiver_ by +3
	end quest

_failsafe_ task:
	end quest