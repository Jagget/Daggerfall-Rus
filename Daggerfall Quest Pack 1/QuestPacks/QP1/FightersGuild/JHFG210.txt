Quest: JHFG210
DisplayName: Восьминогие уроды
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> В ___captive_ возникли некоторые проблемы
<ce> с ядовитыми тварями. Хотите пойти?
<ce> За это платят =reward_ золотых.

RefuseQuest:  [1001]
<ce> Вы один из тех, у кого аллергия на паралич?
<ce> Я думаю, это было бы проблемой.

AcceptQuest:  [1002]
<ce> Вам нужно убить достаточно, чтобы помешать им
<ce> распространиться дальше, как бы много их ни было.
<ce> Не позволяйте им обездвижить вас ядом и следите
<ce> за тем, чтобы не задеть никого из людей.
<ce> Возможно, вы захотите работать по ночам. Сделайте
<ce> это за =2captive_ дней, но после этого можете
<ce> не торопиться. Это в ___captive_, запомните.

QuestFail:  [1003]
<ce> ___captive_ уже должен был очиститься от заразы.

QuestComplete:  [1004]
<ce> Было довольно просто, %pcf? Или как вы думаете? Многие люди
<ce> ненавидят сражаться с этими тварями. Гильдия не могла найти
<ce> никого, кто мог бы это сделать, так что хорошо, что вы
<ce> вмешались. Вот ваши деньги.

Message:  1010
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Удача)
<ce>
<ce> Вы добираетесь до ___captive_ и с удивлением
<ce> видите повсюду небольшие останки членистоногих.
<ce> Вы спрашиваете члена городской стражи, что произошло.
<ce>
<ce> "Пестицид, вот что произошло! Гильдия Магов в кои-то веки
<ce> доказала свою состоятельность. Создали зачарованную пищу
<ce> для тварей, и она начала разъедать их изнутри.
<ce> Мы избавились от них еще несколько дней назад".
<ce>
<ce> Хотя вы не выполняли работу, ___captive_
<ce> технически свободен от вредителей.

Message:  1019
<ce> Этот контракт на уничтожение заключается просто
<ce> в хождении по улицам. Вы можете подождать до ночи,
<ce> чтобы не травмировать случайных прохожих.

Message:  1020
<ce> Вы выполнили контракт с Гильдией Бойцов. _qgiver_
<ce> отдаст вам ваши деньги в ___qgiver_.

Message:  1030
%qdt:
 _qgiver_ отправил меня
 в ___captive_, чтобы избавиться
 от ядовитых существ. Мне
 нужно сделать это в течение
 =2captive_ дней, после этого
 я могу вернуться в ___qgiver_
 за деньгами, когда захочу.

QBN:
Item _reward_ gold

Person _qgiver_ group Questor male

Place _captive_ remote templehome

Clock _timer_ 90.00:00
Clock _2captive_ 00:00 0 flag 1 range 0 2

Foe _enemy1_ is Spider
Foe _enemy2_ is Giant_scorpion

--	Quest start-up:
	start timer _timer_
	start timer _2captive_
	log 1030 step 0
	pick one of _spid_ _scorp_
	pc at _captive_ set _templehome_

_luck_ task:
	when attribute Luck is at least 85
	pick one of _lucky_ _unlucky_ _unlucky_

variable _unlucky_
variable _lucky_

variable _templehome_

_spawner_ task:
	when _templehome_ and not _lucky_
	say 1019

_freewin_ task:
	when _lucky_ and _templehome_
	say 1010
	start task _slain_

_infight_ task:
	daily from 0:00 to 23:59
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	clear _infight_

variable _spid_
variable _scorp_
_spawn_ task:
	when _templehome_ and _spid_ and not _slain_
	create foe _enemy1_ every 12 minutes indefinitely with 50% success

_spawn2_ task:
	when _templehome_ and _scorp_ and not _slain_
	create foe _enemy2_ every 12 minutes indefinitely with 50% success

_slain_ task:
	killed 5 _enemy1_
	killed 5 _enemy2_
	say 1020
	stop timer _2captive_

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_timer_ task:
	end quest

_2captive_ task:
	say 1003
	end quest