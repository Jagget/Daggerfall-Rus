Quest: JHFG234
DisplayName: Огни Обливиона
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Оказывается, существует два вида атронахов.
<ce> Один - это те, что приходят из потустороннего
<ce> мира, марионетки даэдра и тому подобное. Потом
<ce> Гильдия Магов изучила их и стала создавать их
<ce> копии в качестве слуг. Нам нужен кто-то, кто
<ce> выследит настоящего огненного атронаха из
<ce> самого Обливиона. Это вас заинтересовало?

RefuseQuest:  [1001]
<ce> Стоило попробовать.

AcceptQuest:  [1002]
<ce> Он не представляет большой угрозы, но настоящая
<ce> проблема в том, что он находится во дворце.
<ce> Отправляйтесь в ___place_ и найдите его внутри.
<ce> Убейте его и вернитесь сюда за =reward_ золотых.
<ce> Если вы убъете его в течение =timer_ дней,
<ce> то можете не торопиться с возвращением.

QuestFail:  [1003]
<ce> К этому времени вам нужно было убить атронаха в ___place_.

QuestComplete:  [1004]
<ce> Не слишком сложно, правда, %pcf? Может быть,
<ce> для некоторых, но не для Гильдии Бойцов.

Message:  1010
%qdt:
 Гильдия Бойцов ___qgiver_ направила
 меня в ___place_ для уничтожения
 огненного атронаха в _place_
 в течение =timer_ дней. Затем я могу
 вернуться за наградой в любое время.

Message:  1011
<ce> Невыносимая жара в комнате начинает спадать, когда атронах
<ce> перестает двигаться. Ваша работа выполнена.

Message:  1012
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Выносливость)
<ce>
<ce> Этот атронах явно пришел прямо из Обливиона.
<ce> Огненные щупальца тянутся к вам и терзают
<ce> кожу, причиняя сильную боль.

QBN:
Item _reward_ gold range 200 to 300

Person _qgiver_ group Questor male

Place _place_ remote palace

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _failsafe_ 180.00:00

Foe _enemy_ is Fire_atronach

--	Quest start-up:
	log 1010 step 0
	start timer _timer_
	place foe _enemy_ at _place_
	change foe _enemy_ infighting true

_will_ task:
	when attribute Endurance is at least 35

_inj_ task:
	injured _enemy_

_badwill_ task:
	when _inj_ and not _will_
	say 1012
	reduce player health by 25

_slain_ task:
	killed _enemy_
	say 1011
	stop timer _timer_
	start timer _failsafe_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest

_failsafe_ task:
	end quest