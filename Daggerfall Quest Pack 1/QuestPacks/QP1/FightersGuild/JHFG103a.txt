Quest: JHFG103a
DisplayName: Вы наняты на работу

QRC:

QuestComplete:  [1004]
<ce> Неплохо. Можно сказать, у вас есть потенциал. Возможно,
<ce> когда-нибудь мы найдем вам применение. Вот ваше золото.
<ce> А пока забудьте, что мы встречались.

Message:  1010
<ce> Вы находите письмо среди своих вещей.

Message:  1011
%ra
<ce>
  Это довольно забавно. Мы наблюдали
за якобы "очень искусным убийцей",
которого мы надеялись нанять для работы.
И тут появляетесь вы и с ходу убиваете этого
негодяя. Мы оценили вашу квалификацию.
Встретимся через =firsttimer_ дней, если
хотите заработать немного денег.
<ce>
                    _res_
                    ____res_

Message:  1012
<ce> Хорошо. Мы беспокоились, что вы струсили. Или, может быть,
<ce> вы просто один из тех сумасшедших, которые убивают случайных
<ce> людей без раздумий, и не ради выгоды. Так вот, у нас есть
<ce> человек, который должен умереть там, где-то далеко. Сделайте
<ce> это быстро, иначе кто-нибудь заметит и поднимет тревогу.
<ce> Вы согласны на =reward_ золотых?

Message:  1013
<ce> А, должно быть, вы просто глупы. До свидания.

Message:  1014
<ce> Отправляйтесь на ___farm_. Там есть волшебник,
<ce> который ухаживает за территорией. Он слишком много
<ce> раз действовал мне на нервы. Убейте его и вернитесь
<ce> сюда через =timer_ дней. Но если вы будете находиться
<ce> на территории слишком долго, то вас поймают, как
<ce> идиота, и вы уже не найдете меня здесь.

Message:  1015
%qdt:
 Я взял заказ на убийство волшебника,
 живущего на ___farm_. Если я справлюсь
 с этим заданием в течение =timer_ дней
 и не попадусь, то смогу вернуться
 в ___res_ за своими =reward_ золотых.

Message:  1016
<ce> Если это ___farm_, то вы наверняка
<ce> где-то здесь найдете волшебника.

Message:  1017
<ce> Ваша цель убита. Вам следует поспешить уйти отсюда.

Message:  1018
<ce> Из маленького дома доносятся крики, и в воздухе
<ce> витает ощущение магической связи. Теперь о вашем
<ce> поступке знает вся провинция ____farm_.

QBN:

Clock _delay_ 2.00:00 3.00:00
Clock _firsttimer_ 12.00:00 14.00:00
Clock _timer_ 30.00:00
Clock _alert_ 00:05

Place _farm_ remote poorhome
-Place _dung_ local dungeon
Place _res_ remote house

Person _tg_ faction The_Thieves_Guild

Foe _wiz_ is Mage

Item _letter_ letter used 1011
Item _reward_ gold range 2001 to 2999

---- Quest startup
    start timer _delay_

_delay_ task:
    give pc _letter_ notify 1010
    have _letter_ set _receive_
    place npc _tg_ at _res_

_receive_ task:
    start timer _firsttimer_

_talk_ task:
    clicked npc _tg_

_talk2_ task:
    when _talk_ and not _accept_
    stop timer _firsttimer_
    prompt 1012 yes _accept_ no _quit_

_quit_ task:
    say 1013
    destroy _tg_
    end quest

_accept_ task:
    say 1014
    log 1015 step 0
    start timer _timer_
    pc at _farm_ set _arrive_

_firsttimer_ task:
    end quest

_begin_ task:
    start timer _timer_
    pc at _farm_ set _arrive_

_arrive_ task:
    say 1016
    start timer _alert_
    start task _spawn_

_noalert_ task:
    when not _arrive_
    stop timer _alert_

_alert_ task:
    say 1018
    end quest

variable _spawn_
_spawnthere_ task:
    when _spawn_ and _arrive_
    create foe _wiz_ every 0 minutes 1 times with 100% success
    change foe _wiz_ infighting true

_slain_ task:
    killed _wiz_
    say 1017

_talk3_ task:
    when _talk_ and _accept_ and not _slain_
    clear _talk_ _talk3_

_victory_ task:
    when _talk_ and _slain_
    give pc _reward_
    end quest

_timer_ task:
    end quest