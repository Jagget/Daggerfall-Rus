Quest: JHFG127a
DisplayName: Темные высказывания
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHFG127a

RefuseQuest:  [1001]
<ce> ERROR 1001 JHFG127a

AcceptQuest:  [1002]
<ce> ERROR 1002 JHFG127a

QuestFail:  [1003]
<ce> ERROR 1003 JHFG127a

QuestComplete:  [1004]
<ce> _helpdb_ обнимает _tele_,
<ce> волшебника, способного произносить заклинание перемещения.
<ce> Они проводят всего несколько минут, обсуждая, что
<ce> произошло с тех пор, как _helpdb_ просил
<ce> вашей помощи, оба понимают насущную неотложность
<ce> ситуации. Наконец, ваш покровитель обращается к вам,
<ce> глубоко кланяясь, берет обратно _tri_.
<ce>
<ce> "_tele_, прежде чем отправить меня,
<ce> не могли бы вы телепортировать этот амулет
<ce> в какую-нибудь яму в Горах Ротгарии?
<ce> Он больше никогда не должен увидеть дневной свет."
<ce>
<ce> Старик с улыбкой выполняет просьбу, и
<ce> объект исчезает с небольшой вспышкой.
<ce>
<ce> "%pcf, как я мог бы достаточно поблагодарить вас?
<ce> Боюсь, это последний раз, когда я говорю
<ce> с вами, ведь все элементы моей прошлой жизни должны быть навсегда
<ce> забыты. Но, пожалуйста, за ваши старания, возьмите эту кучку
<ce> денег. Я могу по крайней мере уступить это в знак моей безграничной
<ce> благодарности. Возьмите это и знайте, что я буду помнить вас навсегда."
<ce> _helpdb_ бросает вам мешок с деньгами.
<ce>
<ce> Заклинание начинается и быстро заканчивается, и прежде чем вы
<ce> понимаете, молодой беглец исчез. Волшебник
<ce> типает свою шляпу вам дружелюбно и уходит из
<ce> здания, оставляя вас одного.

RumorsDuringQuest:  [1005]
Я видел отряд новобранцев Гильдии Магов, направляющихся к ферме ранее.

Message:  1010
К %ra по имени %pcn,

  Добрый день. Я пишу, чтобы попросить вашей помощи в личном
вопросе большой важности. Я ребенок Темного
Братства, принятый с рождения, который увидел
достоинства _temp_. Я хочу искупить
грехи моего прошлого. Поможете ли вы мне освободиться?
Ради моей безопасности я не могу дать свое имя в письменной форме, но
я буду оставаться в _res_ в ___res_
в течение короткого периода времени. Пожалуйста, приходите ко мне, если вы
готовы помочь спасти душу, которая чувствует себя потерянной.

Message:  1011
<ce> Этот человек выглядит ошеломленным и быстро
<ce> достает оружие. Увидев, что вы не
<ce> ответили тем же, %g1 колеблется и затем успокаивается.
<ce>
<ce> "О, это должны быть вы! Тот %ra! Ваше имя...
<ce> %pcf, что-то в этом роде? Да, я слышал
<ce> многое о вас от нескольких товарищей из Гильдии Бойцов.
<ce> Надежный, по крайней мере. И если вы пришли сюда,
<ce> чтобы помочь, это говорит о том, что вы достаточно моральны, чтобы доверять. Меня
<ce> зовут _helpdb_, и я должен сказать,
<ce> что уйти из Темного Братства - это непростое
<ce> дело. Хотелось бы просто идти к заброшенному
<ce> замку и провести остаток своих дней в одиночестве, но если бы я был
<ce> готов к бою, я бы не просил о вашей помощи! Мне
<ce> дана новая жизнь в _temp_,
<ce> которая поставила меня в конфликт с моими связями в Темном Братстве,
<ce> и я принимаю решение отказаться от связей с моим отцом
<ce> и всеми остальными. Я хочу новую жизнь, новое начало и
<ce> новую историю. Но теперь позвольте мне спросить вас прямо.
<ce> Вы пришли помочь мне?

Message:  1012
<ce> "Да будет благословен %god! И каждое другое божественное существо в
<ce> нашем пантеоне. Вы бы подумали, что человек моего ранга должен
<ce> быть более знаком с ними, но я снова
<ce> подчеркиваю темное прошлое, от которого я пытаюсь уйти.
<ce> Теперь позвольте рассказать вам немного о _db_.
<ce> Он один из высших чинов Братства в %crn, и мой отец. Сказать, что он был бы
<ce> разочарован моим выбором, было бы занижением.
<ce> Пожалуйста, будьте настороже, пока мы не расстанемся. Если вы
<ce> согласны, пожалуйста, отведите меня к _thief_,
<ce> члену Гильдии Воров в ___th_.
<ce> Похоже, %g1 отлично справляется с принятием новых
<ce> личностей. Ну что ж, пойдем."

Message:  1013
<ce> "Кстати, позвольте дать вам этот _tri_.
<ce> Это семейная реликвия. Верните мне его,
<ce> если когда-нибудь решите, что нам следует расстаться.
<ce> Если меня найдут мертвым где-то, вы не захотите
<ce> иметь к этому никакого отношения, я обещаю
<ce> вам. Мой отец никогда не прекратит охоту."

Message:  1015
<ce> _helpdb_ передает _thief_
<ce> тяжелый мешок монет, и двое садятся
<ce> на довольно долгое время, чтобы обсудить планы на будущее.
<ce> Опытный вор объясняет необходимость покинуть
<ce> %crn навсегда, важность
<ce> освоения нового акцента и новой лексики,
<ce> а также тщательного изучения новой профессии.
<ce> _thief_ объясняет некоторые ежедневные
<ce> упражнения, которые помогут изменить
<ce> манеру речи жреца. _helpdb_
<ce> внимательно принимает всю информацию
<ce> и прощается с этим человеком,
<ce> который выглядит как старый друг.
<ce>
<ce> "Спасибо за ожидание, %pcf.
<ce> Мои дни в %crn сочтены, но я знал это,
<ce> когда серьезно относился к своей верности _temp_.
<ce> Теперь я оставил несколько личных
<ce> вещей у друга в храме
<ce> в ___temp_. Это будет наша следующая остановка,
<ce> когда мы дойдем до этого. Я пойду за вами."

Message:  1016
<ce> "Мой друг _pri_ должен быть здесь.
<ce> Пойдем найдем %g2, чтобы забрать мои вещи."

Message:  1017
<ce> _helpdb_ ужаснулся при
<ce> виде тела _pri_.
<ce> Раны очень свежие. Окружающие
<ce> посетители молчат в ужасе.
<ce>
<ce> "И %g1 носит мои вещи, они
<ce> прямо здесь! В чем смысл всего этого?"
<ce>
<ce> Дверь в храм открывается, и слышны шаги.
<ce> Ответ на вопрос _helpdb_
<ce> может появиться из этой двери.

Message:  1018
(Верхняя часть этого письма была намеренно обрезана,
удаляя имена получателей.)

  ...прошу вас вернуть моего ребенка, _helpdb_.
  Похоже, %g2 одержим _temp_.
  Какой-то %ra присматривает за %g2. Убейте телохранителя, но
  НЕ трогайте мою семью. Приведите %g2 ко мне в течение =dummytimer_ дней,
  иначе вы лишитесь награды. Мефала с вами.

              _db_
              _dbtown_
              ___dbtown_

Message:  1019
<ce> _helpdb_ выглядит немного потрясенным
<ce> после пережитого в храме, хотя, безусловно,
<ce> меньше, чем обычный священник.
<ce>
<ce> "Спасибо, что вытащили нас оттуда. В схватке
<ce> мне удалось взять мои вещи с тела моего бедного друга. Я
<ce> думаю, что все это была ловушка, чтобы убить нас, пока я
<ce> осматривал тело. Теперь давайте идем. Мне просто
<ce> надо пойти в последнее место. Пожалуйста, отведите меня к дому в
<ce> ___leave_, _leave_. Там у меня есть кто-то,
<ce> кто может переправить меня в другую часть Илиакского
<ce> залива без следа. Я наконец смогу начать свою новую
<ce> жизнь в мгновение ока. Я не терплюсь уйти,
<ce> но мы будем следовать вашему плану.

Message:  1022
<ce> _helpdb_ резко напрягается, когда %g1
<ce> понимает, куда вы привели %g2. %g1 шепчет:
<ce>
<ce> "Что мы здесь делаем? Почему здесь, из всех мест?"
<ce>
<ce> Вы берете %g4 за руку и крепко тянете %g2 за собой.

Message:  1023
<ce> _db_ ухмыляется, глядя на эту необычную картину перед ним.
<ce>
<ce> "Интересно. Знаменитый %ra,
<ce> который сопровождал моего временно запутанного
<ce> ребенка, теперь передо мной. Ночная Мать
<ce> была ко мне добра, вижу я.
<ce>
<ce> "Вот самый простой способ, чтобы каждый остался в выигрыше.
<ce> У меня нет материальной награды, но в обмен на возврат
<ce> одного из моих блудных наследников, я буду
<ce> држать вас в высоком уважении среди Братства и
<ce> хорошо отзываться о вас перед подходящими людьми. Покровительство
<ce> детей Ночной Матери - это то, чем не стоит пренебрегать
<ce> мудрому человеку. Все, что вам нужно сделать,
<ce> это назвать свое имя и вернуть мне моего ребенка."
<ce>
<ce> _helpdb_ потеет и дрожит.
<ce> Этот момент может определить остаток %g4 жизни.
<ce> Вы примете предложение _db_?

Message:  1024
<ce> _db_ громко смеется, когда
<ce> вы толкаете _helpdb_ к нему.
<ce> Следующие несколько секунд проходят мгновенно.
<ce> _db_ опрокидывает _helpdb_
<ce> на землю. Прежде чем %g1 успевает
<ce> шевельнуться, отец %g4 использует ближайший
<ce> посох и быстро ломает голени %g4.
<ce> _helpdb_ издает жалобный крик от
<ce> огромной боли и
<ce> вашего жестокого предательства. Удары
<ce> продолжаются, разбивая руку, несколько ребер
<ce> и другие части тела. Всего через несколько мгновений ваш бывший заказчик
<ce> задыхается, находясь на грани
<ce> смерти. _db_
<ce> наклоняется и говорит очень медленно:
<ce>
<ce> "Ты никогда не будешь свободен. Никогда."
<ce>
<ce> Удовлетворенный, он достает из-под
<ce> своего плаща маленький камень. Он
<ce> и _helpdb_ исчезают в
<ce> вспышке тьмы, и вы остаетесь один.

Message:  1025
<ce> Руки _db_ безжизненно свисают вниз.
<ce> Очевидно, он не впечатлен вашим задором.
<ce>
<ce> "Какая трата моего времени. Чего вы
<ce> добивались, придя сюда? Надеялись убить
<ce> одного из избранных Мефалы? Продолжайте мечтать, %ra."
<ce>
<ce> У вас нет времени на реакцию, прежде чем он
<ce> достает камень из своего плаща. Он
<ce> активирует его, и вспышка тьмы
<ce> переносит его в какое-то далекое место.
<ce>
<ce> "Ты что, сошел с ума?!" кричит _helpdb_.
<ce> "Что это была за неудачная попытка нападения?
<ce> Я пытаюсь отказаться от жизни убийцы,
<ce> а не усугублять ее! Пошли! Давайте убираться отсюда!"

Message:  1026
<ce> _db_ кричит в ужасе: "НЕТ!"
<ce> Ваша попытка убить члена его
<ce> семьи явно не входила в его планы.
<ce> Тем не менее, ваше внезапное нападение
<ce> застало его безоружным и
<ce> неспособным защитить своего ребенка. Он активирует
<ce> механизм телепортации, который
<ce> переносит его далеко, хотя вы
<ce> слышите его рыдания, когда он срабатывает.

Message:  1030
%qdt:
 Член Темного Братства,
 желающий выйти из дела убийств,
 попросил меня о помощи в выборе
 совершенно нового пути в жизни.
 Я могу найти его или ее в ___res_
 по адресу _res_.

Message:  1031
%qdt:
 _helpdb_, который попросил меня
 о помощи в уходе из Темного Братства, хочет, чтобы я
 сопровождал %g2 в ___th_,
 чтобы встретить члена Гильдии Воров,
 _thief_. Мне также был
 дан _tri_, который я должен вернуть
 _helpdb_, если я когда-либо решу расстаться с %g2.

Message:  1032
%qdt:
 _helpdb_ также должен
 забрать %g4 оставшиеся вещи
 в _temp_ по адресу ___temp_.

Message:  1033
%qdt:
 Друг _helpdb_ был убит,
 и на нас напали, пока мы были в _temp_.
 Последний пункт назначения - _leave_,
 дом в ___leave_.
 %g1 сможет бесследно уйти из %crn,
 если %g1 доберется туда.

Message:  1075
Будьте очень тихи на этот счет. _db_ останавливается в ___dbtown_.
   <--->
Держитесь подальше от ___dbtown_, и вы будете подальше от _db_. Это секрет.

Message:  1080
<ce> Вы убьете _helpdb_ взамен?

Message:  1081
<ce> Ваша хитрость застала %g2 врасплох.
<ce> Первый удар за вами.

Message:  1082
<ce> "Ну что ж, по крайней мере, я могу перейти к
<ce> другим предложениям о помощи. Спасибо за то,
<ce> что дали мне знать. Желаю вам удачи
<ce> в Гильдии Бойцов."
<ce>
<ce> _helpdb_ вежливо жестом указывает
<ce> и выходит из двери.

Message:  1085
<ce> Вы даете _helpdb_ _tri_.
<ce> Хотя %g1 выглядит разочарованным, %g1 быстро
<ce> приходит в себя и принимает ту часть
<ce> помощи, которую вы смогли предложить.
<ce>
<ce> Пока %g4 находится вне строя, вы можете
<ce> нанести первый удар. Попробуете ли вы
<ce> убить _helpdb_?

Message:  1086
<ce> Кланяясь вежливо, %g1 благодарит вас
<ce> за то, что вы держали %g2 в безопасности до сих пор,
<ce> и продолжает свое путешествие.

Message:  1087
<ce> "Что? Что ты делаешь?!"
<ce> _helpdb_ визжит в
<ce> ужасе, когда вы готовитесь к бою.

Message:  1088
<ce> _helpdb_ мертв.
<ce> Ваша задача здесь выполнена.

Message:  1089
<ce> Хотя _helpdb_ вероятно
<ce> будет потрясен вашим уходом, вы
<ce> избавили себя от значительных неприятностей,
<ce> оставив %g2 в покое. Надеемся, вы
<ce> никогда больше не услышите об этом деле.

QBN:

Person _helpdb_ group Local_4.2
Person _db_ faction Dark_Slayers male anyInfo 1075
Person _thief_ faction The_Thieves_Guild
Person _pri_ faction Generic_Temple
Person _tele_ faction The_Travelers_League male
Person _gendb_ faction The_Dark_Brotherhood

Place _res_ remote library
Place _temp_ remote temple
Place _th_ remote random
Place _dbtown_ remote house
Place _leave_ remote house

Clock _initial_ 60.00:00
Clock _killhelp_ 01:00

Item _tri_ trinket
Item _reward_ gold range 1001 to 1499
Item _body_ dead_body
Item _warning_ letter used 1018
Item _dummytimer_ gold range 6 to 15

Foe _helpdb1_ is Rogue
Foe _ambush_ is 3 Thief
Foe _spawn_ is 2 Acrobat

--	Quest start-up:
    start timer _initial_
    say 1010
    place npc _helpdb_ at _res_
    log 1030 step 0
    pc at _dbtown_ set _atdb_
    pc at _temp_ set _attemple_
    _tri_ used do _giveup_
    place npc _db_ at _dbtown_

_daily_ task:
    daily from 0:00 to 23:59
    change foe _ambush_ infighting true
    change foe _spawn_ infighting true
    clear _daily_

_initial_ task:
    end quest

_begin_ task:
    clicked npc _helpdb_
    prompt 1011 yes _join_ no _refuse_
    stop timer _initial_

_join_ task:
    say 1012
    say 1013
    log 1031 step 1
    hide npc _helpdb_
    add _helpdb_ face
    create foe _spawn_ every 60 minutes indefinitely with 10% success
    place npc _thief_ at _th_
    get item _tri_
    add dialog for person _db_

_refuse_ task:
    prompt 1080 yes _murder_ no _fullrefuse_

_murder_ task:
    say 1081
    hide npc _helpdb_
    place foe _helpdb1_ at _res_
    start timer _killhelp_
    change foe _helpdb1_ infighting true

_fullrefuse_ task:
    say 1082
    hide npc _helpdb_
    end quest

_killhelp_ task:
    say 1089
    remove foe _helpdb1_
    end quest

_murderend_ task:
    killed _helpdb1_
    change repute with _gendb_ by -25
    say 1088
    end quest

_giveup_ task:
    prompt 1085 yes _strike_ no _peaceful_
    take _tri_ from pc
    drop _helpdb_ face

_peaceful_ task:
    say 1086
    end quest

_strike_ task:
    say 1087
    create foe _helpdb1_ every 0 minutes 1 times with 100% success
    change foe _helpdb1_ infighting true
    give item _tri_ to _helpdb1_
    make _tri_ permanent

_thieftalk_ task:
    clicked npc _thief_
    say 1015
    log 1032 step 2
    place item _body_ at _temp_

_attemple_ task:
    say 1016

_bodytalk_ task:
    clicked item _body_
    say 1017
    create foe _ambush_ every 0 minutes 1 times with 100% success
    give item _warning_ to _ambush_
    take _body_ from pc

_nextturn_ task:
    when _bodytalk_ and not _attemple_
    say 1019
    log 1033 step 3
    place npc _tele_ at _leave_

_victory_ task:
    clicked npc _tele_
    take _tri_ from pc
    give pc _reward_
    change repute with _gendb_ by -15
    hide npc _tele_
    end quest

_atdb_ task:
    say 1022

_strike2_ task:
    when _strike_ and _atdb_ and not _poorly_
    say 1026
    hide npc _db_
    change repute with _gendb_ by -25

_dbconv_ task:
    clicked npc _db_
    prompt 1023 yes _allin_ no _poorly_

_allin_ task:
    say 1024
    change repute with _gendb_ by +10
    hide npc _db_
    make _tri_ permanent
    end quest

_poorly_ task:
    say 1025
    hide npc _db_
    change repute with _gendb_ by -25