Quest: JHFG204
DisplayName: Пока смерть не разлучит их
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вампир-лорд захватил частное кладбище
<ce> вместе с двумя своими женами. У него там,
<ce> не знаю, какой-то гарем, что ли. Я не вампир,
<ce> не спрашивайте. Хозяин кладбища платит больше
<ce> тысячи золотых тому, кто от них избавится.

RefuseQuest:  [1001]
<ce> Я вас понимаю. Вампиры - очень неприятный народ.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___cem_, это на %di. Очевидно,
<ce> они не собираются выходить на дневной свет,
<ce> так что подождите снаружи, пока они не появятся.
<ce> Убедитесь, что они абсолютно мертвы или что
<ce> там у вампиров считается эквивалентом смерти,
<ce> и ваши деньги будут ждать вас здесь.
<ce> Вы получите =reward_ золотых, если вернетесь
<ce> в течение =timer_ дней.

QuestFail:  [1003]
<ce> Гильдия Бойцов послала вас зачистить
<ce> ___cem_ к этому времени.

QuestComplete:  [1004]
<ce> Эй, у вас хорошо получается. Если вы
<ce> даже вампиров крошите, вас уже мало что
<ce> может напугать. Вот, пожалуйста, с моими
<ce> комплиментами.

Message:  1010
<ce> Это правильное место, но солнце еще слишком высоко.
<ce> Вам придется задержаться здесь, пока не наступит ночь.

Message:  1011
<ce> Вас сразу же замечают. В воздухе
<ce> раздается гортанное шипение, когда лорд
<ce> и его жены приближаются, чтобы убить вас.

Message:  1012
<ce> Все три вампира повержены.

Message:  1030
%qdt:
 __qgiver_ из ___qgiver_ заплатит
 мне =reward_ золотых, если
 я уничтожу вампира-лорда и двух
 женщин-вампиров, которые захватили
 ___cem_, ____cem_. Для получения
 вознаграждения мне нужно вернуться
 через =timer_ дней.

QBN:
Item _reward_ gold range 1100 to 1300

Place _cem_ remote graveyard

Person _qgiver_ group Questor

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _vampman_ is Ancient_vampire
Foe _vamw1_ is Vampire
Foe _vamw2_ is Vampire

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _cem_ set _outside_
    reveal _cem_

_day_ task:
    daily from 5:00 to 19:00

variable _outside_

_dayarrive_ task:
    when _outside_ and _day_
    say 1010

_nightarrive_ task:
    when _outside_ and not _day_
    start task _spawn_

_spawn_ task:
    say 1011
    create foe _vampman_ every 0 minutes 1 times with 100% success
    create foe _vamw1_ every 0 minutes 1 times with 100% success
    create foe _vamw2_ every 0 minutes 1 times with 100% success
    change foe _vampman_ infighting true
    change foe _vamw1_ infighting true
    change foe _vamw2_ infighting true
    change foe _vampman_ team 0
    change foe _vamw1_ team 0
    change foe _vamw2_ team 0

_killed1_ task:
    killed _vampman_

_killed2_ task:
    killed _vamw1_

_killed3_ task:
    killed _vamw2_

_victory_ task:
    when _killed1_ and _killed2_ and _killed3_
    say 1012

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _victory_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _victory_
	give pc _reward_
	end quest

_timer_ task:
	say 1003
	end quest