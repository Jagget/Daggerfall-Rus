Quest: JHFG124
DisplayName: Коробка с летучими мышами
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Ах, прекрасно. Бьюсь об заклад, вы бы подошли
<ce> для этого задания. Мы заключили пари, что никто
<ce> не сможет выжить после нападения роя из пятидесяти
<ce> летучих мышей. Хотите доказать, что они ошибаются?

RefuseQuest:  [1001]
<ce> Чувак, я поставил на это деньги.
<ce> Кто этим займется?

AcceptQuest:  [1002]
<ce> Потрясающе! Я с вами согласен. Люди здесь абсолютно
<ce> уверены, что пятьдесят - это слишком много, но мы им
<ce> докажем, что они ошибаются. Вам понадобится этот сундук.
<ce> Отправляйтесь к одному из святилищ в глуши, которые
<ce> называются "Святилище Этого" или "Монумент Того". Откройте
<ce> его там. Сделайте это в любом другом месте, и ставка
<ce> отменяется. Как только летучие мыши начнут появляться, они
<ce> нападут только на вас, так что не беспокойтесь о служителях
<ce> святилища. Вам нужно просто выжить и вернуться сюда,
<ce> хорошо? И не убегайте. Мы будем наблюдать.

QuestFail:  [1003]
<ce> "Эй, нет! Не здесь!"
<ce>
<ce> Сундук исчезает в клубах дыма.

QuestComplete:  [1004]
<ce> Вот как мы это делаем, приятель. Отличная работа.
<ce> Я потратил время, чтобы превратить вашу
<ce> долю в нечто более полезное, чем деньги.

RumorsDuringQuest:  [1005]
Некий заклинатель разбогател на защите Гильдии Магов от незваных гостей.

Message:  1010
<ce> Вы слышите голос _qgiver_,
<ce> доносящийся из-под деревянной обшивки сундука:
<ce>
<ce> "Ладно, это хорошее место. Не отходите слишком далеко
<ce> от святилища. Откройте сундук, когда будете готовы".

Message:  1011
<ce> Умирает последняя летучая мышь, и вы слышите,
<ce> как _qgiver_ разражается хохотом.
<ce>
<ce> "Ну все, сосунки, вы сами все видели!
<ce> Давайте это сюда! Эй, %pcf, ваша доля у меня,
<ce> отдам вам когда вы вернетесь. Безопасной дороги".
<ce>
<ce> Наконец, сундук исчезает в клубах дыма.

Message:  1013
<ce> Вы ставите сундук на землю и открываете крышку.
<ce> Оттуда вылетают крылатые существа,
<ce> науськанные нападать только на вас.

Message:  1030
%qdt:
 Я пытаюсь выполнить пари,
 заключенное _qgiver_ из __qgiver_
 на убийство пятидесяти летучих
 мышей. Он дал мне сундук, который
 мне нужно открыть возле одного из
 отдаленных святилищ в глуши. Если
 я убью летучих мышей и выживу,
 я смогу вернуться в ___qgiver_
 за своей долей пари.

QBN:
Person _qgiver_ group Questor male

Item _chest_ item class 1 subclass 8
Item _reward_ magic_item

Foe _bat_ is Giant_bat

--	Quest start-up:
	log 1030 step 0
	get item _chest_
	_chest_ used do _open_

_there_ task:
	when pc enters cult
	say 1010
	clear _notthere_

_notthere_ task:
	when pc exits cult
	clear _there_

variable _open_
_begin_ task:
	when _open_ and _there_ and not _slain_
	say 1013
	create foe _bat_ every 0 minutes 50 times with 100% success
	take _chest_ from pc

_infight_ task:
	daily from 0:00 to 23:59
	change foe _bat_ infighting true
	clear _infight_

_fail_ task:
	when _open_ and not _there_ and not _slain_
	remove foe _bat_
	say 1003
	end quest

_slain_ task:
	killed 45 _bat_ saying 1011

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest