Quest: JHFG235
DisplayName: Грохочущие кости
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Какой-то идиот в городе решил попробовать
<ce> воскресить из мертвых своих бабушку и дедушку.
<ce> Я, конечно, понимаю, мы все скучаем по своим родным,
<ce> верно? Но теперь они - пара злобных скелетов,
<ce> разгуливающих по дому, и он платит деньги за то,
<ce> чтобы они умерли... снова. Заинтересовались, %pcf?

RefuseQuest:  [1001]
<ce> Я вас не виню, со скелетами
<ce> не все так просто, как кажется.

AcceptQuest:  [1002]
<ce> Вам нужно в _place_, это... гм, на %di.
<ce> Да, теперь я вспомнил. Внутри вы найдете оба
<ce> скелета, и вам заплатят, когда они будут
<ce> мертвы. Обычно мы берем меньше
<ce> за местную работу, но он очень хотел, чтобы все
<ce> было сделано за 24 часа, так что это ваше
<ce> ограничение по времени. Вы получите =reward_
<ce> золотых. Также я надеюсь, что у вас есть тупое
<ce> оружие или что-то в этом роде, потому что
<ce> лезвия не пробивают твердую кость. Возможно,
<ce> вам стоит заглянуть в магазин. В любом случае,
<ce> я все сказал.

QuestFail:  [1003]
<ce> Вам уже нужно было вернуться после
<ce> убийства скелетов в _place_.

QuestComplete:  [1004]
<ce> Отличная работа, %pcf. Вот почему мы
<ce> рассчитывали на вас. Получайте свое золото.

Message:  1010
%qdt:
 Гильдия Бойцов ___qgiver_
 отправила меня в _place_
 для убийства двух скелетов.
 У меня есть 24 часа, чтобы
 вернуться за =reward_ золотых.

Message:  1011
<ce> Вы уничтожили оба скелета. Клиент,
<ce> вероятно, больше не будет связываться
<ce> с некромантией после этого инцидента.

Message:  1012
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Ловкость)
<ce>
<ce> Одна из опасностей борьбы с несколькими противниками 
<ce> заключается в том, что вы рискуете получить удар в спину.
<ce> Обладая некоторым интеллектом, один из скелетов находит
<ce> возможность сделать именно это.

QBN:
Item _reward_ gold range 200 to 250

Person _qgiver_ group Questor male

Place _place_ local house

Clock _timer_ 1.00:00

Foe _enemy1_ is Skeletal_warrior
Foe _enemy2_ is Skeletal_warrior

--	Quest start-up:
    start timer _timer_
    log 1010 step 0
    place foe _enemy1_ at _place_
    change foe _enemy1_ infighting true
    pc at _place_ do _inside_

_will_ task:
    when attribute Agility is at least 35

_inj_ task:
    injured _enemy1_
    injured _enemy2_
    clicked foe _enemy1_
    clicked foe _enemy2_

_badwill_ task:
    when _inj_ and not _will_
    say 1012
    reduce player health by 30

_inside_ task:
    start task _spawner_

_spawner_ task:
    create foe _enemy2_ every 2 minutes 1 times with 100% success
    change foe _enemy2_ infighting true

_slain1_ task:
    killed _enemy1_

_slain2_ task:
    killed _enemy2_

_slain_ task:
    when _slain1_ and _slain2_
    say 1011

_pcgetsgold_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest