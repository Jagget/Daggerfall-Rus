Quest: JHFG217
DisplayName: Злодейство в высших кругах
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %t поручили нам расправиться с некромантом, который
<ce> также является высокопоставленным членом Гильдии
<ce> Магов. Хочешь избавить нас от этого контракта?

RefuseQuest:  [1001]
<ce> %oth. Ну кто-то же должен это сделать.

AcceptQuest:  [1002]
<ce> Круто, круто. Ну, больше сказать нечего. Некроманты
<ce> обычно находятся в ведении Гильдии Магов, но этого
<ce> они категорически отрицают. Зная их, это вопрос
<ce> полезности, а не лояльности. Но я молчу.
<ce> Отправляйтесь в ___poorhome_. Приготовьтесь
<ce> сразиться с нежитью, когда будете там. Нам сказали,
<ce> что все должно быть готово в течение =timer_ дней,
<ce> затем возвращайтесь сюда за своими деньгами.

QuestFail:  [1003]
<ce> К настоящему времени %t должны были уже узнать,
<ce> что некромант из ___poorhome_ убит.

QuestComplete:  [1004]
<ce> Привет, рад, что вы вернулись. Некроманты могут
<ce> быть суровыми, но Гильдия Бойцов еще суровее.
<ce> Спасибо за то, что вы являетесь таким прекрасным
<ce> примером того, что мы делаем. Возьмите это.

Message:  1010
<ce> Ваше вторжение на частную территорию принято к
<ce> сведению. Жилец, медитирующий на улице, встает
<ce> и начинает что-то начитывать на непонятном языке.

Message:  1011
<ce> Некромант убит. %t должны
<ce> получить отчет от _qgiver_ об этом.

Message:  1012
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Удача)
<ce>
<ce> У некроманта не работают заклинания вызова.
<ce> "Почему?! Что происходит?!"
<ce>
<ce> Иногда удача на вашей стороне.

Message:  1030
%qdt:
 Гильдия Бойцов ___qgiver_
 поручила мне уничтожить некроманта,
 члена Гильдии Магов, живущего
 в ___poorhome_. У меня есть
 =timer_ дней, чтобы доложить об этом.

QBN:
Foe _q1_ is Skeletal_warrior
Foe _q2_ is Zombie
Foe _mage_ is Mage

Item _reward_ gold

Person _qgiver_ group Questor male
Person _dummymg_ faction The_Mages_Guild

Place _poorhome_ remote poorhome

Clock _timer_ 00:00 0 flag 1 range 0 2

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  pc at _poorhome_ do _inside_

_luck_ task:
  when attribute Luck is at least 85
  pick one of _lucky_ _unlucky_ _unlucky_

variable _unlucky_
variable _lucky_

variable _inside_
_spawn_ task:
  when _inside_ and not _slain_ and not _lucky_
  say 1010
  create foe _mage_ every 0 minutes 1 times with 100% success
  change foe _mage_ team 13
  create foe _q1_ every 1 minutes indefinitely with 75% success
  create foe _q2_ every 1 minutes indefinitely with 50% success

_luckyspawn_ task:
  when _inside_ and _lucky_
  say 1010
  create foe _mage_ every 0 minutes 1 times with 100% success
  say 1012

_infight_ task:
  daily from 0:00 to 23:59
  change foe _mage_ infighting true
  change foe _q1_ infighting true
  change foe _q2_ infighting true
  clear _infight_

_slain_ task:
  killed _mage_
  say 1011
  change repute with _dummymg_ by -10

_victory_ task:
  when _qgclicked_ and _slain_
  give pc _reward_
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_timer_ task:
  say 1003
  end quest