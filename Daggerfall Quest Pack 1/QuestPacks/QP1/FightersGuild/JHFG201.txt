Quest: JHFG201
DisplayName: Местный вампир
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас есть работа для такого опытного человека, как вы,
<ce> %pcf, но я не буду вас винить, если откажетесь.
<ce> Это связано с одним вампиром и отсутствием солнечного света.
<ce> Вы думаете, что сможете решить такого рода проблему?
<ce> Вы получите гномский наплечник.
   <--->
<ce> Вы когда-нибудь убивали вампира, %pcf? Речь
<ce> идет не об этих древних. У нас тут заявка - завалить
<ce> одного обычного вампира в доме в ___place_. Берите
<ce> задание, если чувствуете, что готовы к этому.

RefuseQuest:  [1001]
<ce> Я поищу кого-нибудь другого.

AcceptQuest:  [1002]
<ce> Значит, так. Вы направляетесь в _place_.
<ce> Это в ___place_. Убейте чудовище и вернитесь сюда,
<ce> чтобы я мог вручить вам гномский наплечник. Даю вам
<ce> =timer_ дней, прежде чем пошлю за подкреплением
<ce> в храм или еще куда-нибудь. Удачи, %pcf.

QuestFail:  [1003]
<ce> Вам нужно было уже вернуться после
<ce> убийства вампира в ___place_.

QuestComplete:  [1004]
<ce> Отличная работа, %pcf. Вот почему
<ce> мы рассчитывали на вас. Возьмите свое золото.

Message:  1010
%qdt:
 Гильдия Бойцов ___qgiver_
 отправила меня в ___place_,
 чтобы уничтожить вампира
 в _place_.  Мне нужно вернуться
 в течение =timer_ дней, иначе
 они передадут контракт в храм.

Message:  1011
<ce> "Нет!" - вампир вскрикнул
<ce> и рухнул на пол.

Message:  1012
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Сила воли)
<ce>
<ce> Вампир поднимает руки и кричит, произнося заклинание
<ce> в целях самозащиты. Это оглушает вас всего на мгновение,
<ce> но этого достаточно, чтобы он применил несколько усиливающих
<ce> заклинаний. Этот бой только что стал сложнее.

QBN:
Item _reward_ item class 2 subclass 3

Person _qgiver_ group Questor male

Place _place_ remote house1

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Vampire

--	Quest start-up:
	start timer _timer_
	log 1010 step 0
	place foe _enemy_ at _place_
	change foe _enemy_ infighting true
	update-quest-item _reward_ set-material Dwarven

_will_ task:
	when attribute Willpower is at least 35

_inj_ task:
	injured _enemy_

_failwill_ task:
	when _inj_ and not _will_
	say 1012
	cast Heal spell on _enemy_
	cast Feet_of_Notorgo spell on _enemy_
	cast Orc_Strength spell on _enemy_
	cast Shield spell on _enemy_
	cast Shalidor's_Mirror spell on _enemy_

_slain_ task:
	killed _enemy_
	say 1011

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest