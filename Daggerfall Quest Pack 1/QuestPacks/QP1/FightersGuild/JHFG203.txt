Quest: JHFG203
DisplayName: И лесничие и тигры и медведи, ну и дела
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Земельный спор. Один аристократ
<ce> в %crn хочет расширить свой особняк,
<ce> но лесничие из близлежащих лесов
<ce> очень недовольны этим. Они
<ce> угрожают устроить погром, если
<ce> он не откажется от своих планов.
<ce> Я не думаю, что здесь можно
<ce> обойтись дипломатическим решением,
<ce> вам придется драться. Ваш тип работы?
   <--->
<ce> Земельный спор. Одна аристократка
<ce> в %crn хочет расширить свой особняк,
<ce> но лесничие из близлежащих лесов
<ce> очень недовольны этим. Они
<ce> угрожают устроить погром, если
<ce> она не откажется от своих планов.
<ce> Я не думаю, что здесь можно
<ce> обойтись дипломатическим решением,
<ce> вам придется драться. Ваш тип работы?

RefuseQuest:  [1001]
<ce> Ну, хорошо, я отложу этот контракт в сторону.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___manor_. Я знаю, что место
<ce> будет выглядеть крошечным, и именно таким
<ce> лесничие хотят его сохранить. У них есть
<ce> способность очаровывать зверей, так что ожидайте
<ce> драки с людьми и животными. Как только все злодеи
<ce> будут мертвы, вам заплатят на месте. Просто
<ce> убедитесь, что вы сделаете это до истечения
<ce> =timer_ дней. Это место долго не продержится.

QuestFail:  [1003]
<ce> По контракту с Гильдей Бойцов, вы должны были
<ce> уже зачистить ___manor_.

QuestComplete:  [1004]
<ce> Слуга грациозно кланяется вам и уходит.
<ce>
<ce> "Да будет так".

Message:  1010
<ce> К ___manor_ нельзя подобраться скрытно.
<ce> Разъяренные лесничие видят вас издалека.
<ce>
<ce> "Слуги природы должны исполнять ее волю!"
<ce>
<ce> Они разделяются и пытаются окружить вас,
<ce> призывая на помощь диких животных.

Message:  1011
<ce> Из дома выходит слуга и осматривает поле боя.
<ce> Убедившись, что опасность миновала, он
<ce> подходит и обращается к вам.
<ce>
<ce> "Приветствую вас, великий воин. Мой господин благодарит
<ce> вас за то, что вы избавили нас от наших преследователей.
<ce> Я уполномочен вручить вам =reward_ золотых, но если вы
<ce> пожелаете, мы могли бы вместо этого распространить славу
<ce> о вас за границами этого региона, чтобы вы выглядели
<ce> лучше в глазах закона".
<ce>
<ce> Возьмете ли вы золото в качестве награды?
   <--->
<ce> Из дома выходит слуга и осматривает поле боя.
<ce> Убедившись, что опасность миновала, она
<ce> подходит и обращается к вам.
<ce>
<ce> "Приветствую вас, великий воин. Мой господин благодарит
<ce> вас за то, что вы избавили нас от наших преследователей.
<ce> Я уполномочен вручить вам =reward_ золотых, но если вы
<ce> пожелаете, мы могли бы вместо этого распространить славу
<ce> о вас за границами этого региона, чтобы вы выглядели
<ce> лучше в глазах закона".
<ce>
<ce> Возьмете ли вы золото в качестве награды?

Message:  1030
%qdt:
 От имени Гильдии Бойцов
 я направляюсь в ___manor_,
 ____manor_, чтобы избавиться
 от разъяренных лесничих, угрожающих
 владениям. Мне заплатят золотом,
 как только они будут мертвы, если
 я закончу в течение =timer_ дней.

QBN:
Item _reward_ gold range 600 to 800

Place _manor_ remote poorhome

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 0:01

Foe _bear_ is Grizzley_bear
Foe _tiger_ is Sabretooth_tiger
Foe _ranger1_ is Ranger
Foe _ranger2_ is Ranger

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _manor_ set _outside_

_outside_ task:
    start task _spawn_

_spawn_ task:
    say 1010
    create foe _bear_ every 0 minutes 1 times with 100% success
    create foe _tiger_ every 0 minutes 1 times with 100% success
    create foe _ranger1_ every 0 minutes 1 times with 100% success
    create foe _ranger2_ every 0 minutes 1 times with 100% success
    change foe _bear_ infighting true
    change foe _tiger_ infighting true
    change foe _ranger1_ infighting true
    change foe _ranger2_ infighting true
    change foe _bear_ team 0
    change foe _tiger_ team 0
    change foe _ranger1_ team 0
    change foe _ranger2_ team 0

_killed1_ task:
    killed _tiger_

_killed2_ task:
    killed _bear_

_killed3_ task:
    killed _ranger1_

_killed4_ task:
    killed _ranger2_

_countdown_ task:
    when _killed1_ and _killed2_ and _killed3_ and _killed4_
    start timer _delay_

_delay_ task:
    prompt 1011 yes _givegold_ no _giverep_

_givegold_ task:
    give pc _reward_
    end quest

_giverep_ task:
    legal repute +10
    give pc nothing
    end quest

_timer_ task:
	say 1003
	end quest