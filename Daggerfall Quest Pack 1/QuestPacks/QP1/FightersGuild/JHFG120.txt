Quest: JHFG120
DisplayName: Низвергнут, но не уничтожен
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Один член гильдии чувствует себя по-настоящему
<ce> подавленным. Этот парень здесь, в городе.
<ce> Он просто немного хандрит, не в силах вернуться
<ce> в привычное русло. Держу пари, у вас бы
<ce> неплохо получилось это изменить, верно, %pcf?

RefuseQuest:  [1001]
<ce> Я по-прежнему так считаю, даже если вы так не думаете.

AcceptQuest:  [1002]
<ce> Хорошо, тогда идите и найдите его
<ce> в _res_. Бедняга очень потрясен,
<ce> но я не знаю чем. Его зовут _guy_.
<ce> Посмотрим, сможете ли вы вправить ему
<ce> мозги. Вы получите =reward_ золотых,
<ce> когда закончите. Не затягивайте дольше,
<ce> чем на =timer_ дней.

QuestFail:  [1003]
<ce> _guy_ чувствует себя лучше?
<ce> Нет? Я буду ждать здесь.

QuestComplete:  [1004]
<ce> Ха-а, я знал, что вы сможете это
<ce> сделать! Это определенно стоит
<ce> золота. Мне нужен кто-то вроде него,
<ce> чтобы убивать крыс и тигров, пока вы
<ce> заняты на крупных контрактах.

Message:  1010
<ce> "О, что? Зачем вы здесь?
<ce> _qgiver_... А, %g1. Нет, нет,
<ce> мне это неинтересно. Я завязал
<ce> с гильдией. Такая жизнь не для меня".
<ce>
<ce> Вы тратите некоторое время, пытаясь выдавить
<ce> из него причину, и наталкиваетесь на глухое
<ce> сопротивление. Наконец, с некоторым
<ce> раздражением он выпаливает правду.
<ce>
<ce> "Это из-за подзмемелья ___dung_,
<ce> ясно?! Грязное логово гадюк чуть
<ce> не убило меня! Я провел там не больше
<ce> часа, а уже был почти мертв! Если вы
<ce> не собираетесь отвести меня туда
<ce> и показать, как ведут себя настоящие
<ce> бойцы, я не хочу об этом слышать!"
<ce>
<ce> Похоже, что убедить его можно только
<ce> одним способом. Немного неохотно он
<ce> соглашается последовать за вами
<ce> в ___dung_. Если что-то и может
<ce> помочь ему преодолеть травму, то только это.

Message:  1011
<ce> _guy_ начинает непроизвольно
<ce> дрожать, как только вы входите.
<ce>
<ce> "Знаете что? Бейтесь сколько хотите.
<ce> Я ничего не буду делать, пока не увижу, что вы выжили".

Message:  1012
<ce> _guy_ начинает заражаться
<ce> вашей уверенностью. Почувствовав тягу
<ce> к приключениям, он улыбается, возможно,
<ce> впервые за несколько недель.
<ce>
<ce> "Знаете что? Я лучше этого. %oth,
<ce> зачем я вообще это говорю?
<ce> Я сделаю это! Я могу!"
<ce>
<ce> Затем он покидает вас, чтобы снова начать
<ce> поиски приключений. _qgiver_
<ce> будет рад услышать об этом.

Message:  1013
<ce> "Мы слишком долго занимались этим,
<ce> и у меня есть дела поважнее".
<ce>
<ce> _guy_ уходит куда-то еще.

Message:  1014
<ce> (ПРОВЕРКА ПРОЙДЕНА: Этикет/Жаргон/Характер)
<ce>
<ce> _guy_ явно не хочет разговаривать
<ce> ни с вами, ни с кем-либо еще из Гильдии Бойцов.
<ce> Но вы с вашими ораторскими способностями
<ce> преодолевали гораздо большие барьеры.
<ce> Вы обсуждаете его трудности на недавнем
<ce> задании гильдии и говорите о том,
<ce> что делать в следующий раз, когда это случится.

Message:  1030
%qdt:
 _qgiver_ из __qgiver_,
 ___qgiver_, попросил меня
 помочь другому члену гильдии
 по имени _guy_ преодолеть
 своего рода депрессию. Я могу найти
 его в _res_.
 Мне нужно это сделать и вернуться
 через =timer_ дней.

Message:  1031
%qdt:
 Я и _guy_  договорились вместе
 спуститься в ___dung_, где начались
 его проблемы. Он хочет посмотреть,
 как я сражаюсь там с врагами
 и убедиться, что это возможно.

QBN:

Person _qgiver_ group Questor
Person _guy_ group Local_3.1 male

Place _res_ local house
Place _dung_ remote dungeon2

Clock _timer_ 00:00 0 flag 1 range 0 2
Clock _indung_ 4:00 6:00

Item _reward_ gold

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _guy_ at _res_
  pick one of _easy_ _easy_ _hard_

variable _easy_
variable _hard_
_etiq_ task:
  when skill Etiquette is at least 40
  start task _skilled_

_stre_ task:
  when skill Streetwise is at least 40
  start task _skilled_

_pers_ task:
  when attribute Personality is at least 85
  start task _skilled_

variable _skilled_
_begin_ task:
  clicked npc _guy_

_persuade_ task:
  when _begin_ and _easy_ and _skilled_
  say 1014
  hide npc _guy_
  start task _victory_

_victory_ task:
  say 1012

_normalpath_ task:
  when _begin_ and not _easy_ or _begin_ and not _skilled_
  say 1010
  hide npc _guy_
  add _guy_ face
  reveal _dung_
  pc at _dung_ do _inside_
  log 1031 step 1

_inside_ task:
  start timer _indung_
  say 1011

_notinside_ task:
  when not _inside_
  stop timer _indung_

variable _indung_
_hardwin_ task:
  when _normalpath_ and _indung_
  drop _guy_ face
  start task _victory_

_goodbye_ task:
  when _timer_ and _normalpath_ and not _victory_
  drop _guy_ face
  say 1013

_ending_ task:
  when _qgclicked_ and _victory_
  give pc _reward_
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _victory_
  say 1003
  clear _qgclicked_ _clearclick_

_timer_ task:
  end quest