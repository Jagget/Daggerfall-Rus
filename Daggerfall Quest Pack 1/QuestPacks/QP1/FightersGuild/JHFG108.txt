Quest: JHFG108
DisplayName: Спасти вампира
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Мы получили заказ - помочь сохранить жизнь вампиру, э-э,
<ce> не позволить сделать его еще более мертвым. Черт, даже
<ce> не знаю, как и сказать. Можете ли вы держать вампира и его
<ce> врагов на расстоянии друг от друга? И не говорить
<ce> никому, кто он такой, пока вы этим занимаетесь?

RefuseQuest:  [1001]
<ce> Хорошо. Тогда лучше ничего вам больше не говорить.

AcceptQuest:  [1002]
<ce> Итак, вот в чем секрет. Вампир - представитель
<ce> знати здесь, в ___pal_.
<ce> Имя - _noble_. Очень тихий.
<ce> Живет во дворце. На самом деле неплохой парень.
<ce> Вы могли бы увидеть его в каком-нибудь пабе,
<ce> покупающим выпивку для посетителей. Оказывается,
<ce> какие-то негодяи хотят посеять некоторую
<ce> нестабильность, и он беспокоится за свою безопасность
<ce> сегодня вечером. Говорит, что нападение, скорее всего,
<ce> произойдет после полуночи. Вы будете там и поможете
<ce> ему справиться с тем, что с ним случится, за это
<ce> получите хорошую репутацию героя в округе, я в этом
<ce> уверен. Просто будьте готовы ко всему.

QuestFail:  [1003]
<ce> _noble_ был убит.

QuestComplete:  [1004]
<ce> _noble_ вздыхает. Без пота или затрудненного дыхания
<ce> трудно сказать, действительно ли битва стоила ему
<ce> каких-либо усилий. Вскоре он говорит:
<ce>
<ce> "Вы умеете хранить секреты, не так ли, %ra?
<ce> Боюсь, у меня не так много денег, чтобы заплатить вам,
<ce> но в обмен на ваше благоразумие я сделаю так, что ваша
<ce> слава в %crn взлетит еще выше. Я думаю, что теперь
<ce> ваша репутация среди народа будет намного лучше".
<ce>
<ce> Позвав слугу, он удаляется в другую комнату.
<ce> Вскоре приносят вашу награду.

RumorsDuringQuest:  [1005]
Какой-то аргонианин вымогает деньги у людей по всему Вэйресту. Странно, не правда ли?

RumorsPostFailure:  [1006]
Какой позор, что случилось с _noble_.
   <--->
Даже дворец в ___pal_ больше не в безопасности после того, что случилось с _noble_.
   <--->
_noble_ был единственным в своем роде. Нам будет его не хватать.
   <--->
Говорят, _noble_ был вампиром. Страшная мысль!

RumorsPostSuccess:  [1007]
Мне всегда нравилось слышать о _noble_. Очаровательный парень.
   <--->
_noble_ - единственный в своем роде.
   <--->
Я слышал, _noble_ отправился покупать медовуху для горожан.

Message:  1010
<ce> "Гильдия Бойцов? Хорошо. Рад, что вы здесь. Я просто..."
<ce>
<ce> Магическое искажение поблизости прерывает _noble_.
<ce> Открывается портал, достаточно долгий, чтобы через него
<ce> могли пройти, скрежеща зубами от голода, несколько
<ce> зверей. Затем портал закрывается, исчерпав свою силу.
<ce>
<ce> "Нет времени! Помогите мне сейчас же!"

Message:  1012
<ce> Вы должны были поговорить с _noble_
<ce> после полуночи, чтобы защитить его.

Message:  1030
%qdt:
 Гильдии Бойцов нужно защитить
 местного аристократа, скрытного
 вампира по имени _noble_,
 во дворце в ___pal_. Мне нужно найти
 его внутри после полуночи, чтобы
 узнать, чем я могу помочь.

QBN:

Place _pal_ local palace

Clock _timer_ 1.06:00 0

Foe _nym_ is Nymph
Foe _garg_ is Gargoyle
Foe _ally_ is Ancient_vampire

Person _noble_ factiontype Vampire_Clan male

Item _reward_ gold range 400 to 600

--	Quest start-up:
    start timer _timer_
    log 1030 step 0

_appear_ task:
    daily from 0:01 to 3:59
    place npc _noble_ at _pal_

_failtime_ task:
    daily from 4:00 to 4:30

_talk_ task:
    clicked npc _noble_
    create foe _nym_ every 0 minutes 3 times with 100% success
    create foe _garg_ every 0 minutes 3 times with 100% success
    say 1010
    stop timer _timer_
    hide npc _noble_
    place foe _ally_ at _pal_

_infight_ task:
    daily from 00:00 to 23:59
    change foe _nym_ infighting true
    change foe _garg_ infighting true
    change foe _ally_ infighting true
    change foe _ally_ team 1
    change foe _nym_ team 0
    change foe _garg_ team 0
    clear _infight_

_kill1_ task:
    killed 3 _nym_

_kill2_ task:
    killed 3 _garg_

_victory_ task:
    when _kill1_ and _kill2_
    give pc _reward_
    remove foe _ally_
    legal repute +20
    end quest

_casualty_ task:
    killed 1 _ally_ saying 1003
    end quest

_bigfail_ task:
    when _failtime_ and not _talk_
    say 1012
    end quest

_timer_ task:
    say 1012
    end quest