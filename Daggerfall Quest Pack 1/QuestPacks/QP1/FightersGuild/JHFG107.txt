Quest: JHFG107
DisplayName: Работа курьером
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас есть работа, которая не совсем
<ce> соответствует тому, для чего создавалась
<ce> Гильдия Бойцов, но она все же оплачивается.
<ce> Не хотите ли вы сходить в ___res_
<ce> и принести _pick_, который кто-то там
<ce> оставил? Звучит как легкая добыча.
   <--->
<ce> Мы получили просьбу зайти
<ce> в ___res_ и забрать кое-что,
<ce> случайно там оставшееся. Звучит
<ce> достаточно просто. Не хотите
<ce> взять это на себя, %pcf?

RefuseQuest:  [1001]
<ce> Чувак, ты здесь только крушить черепа собираешься или что?

AcceptQuest:  [1002]
<ce> Хорошо, тогда приступайте. ___res_,
<ce> _res_. Вы ищете _pick_. По их словам,
<ce> это должно лежать на полу. Просто
<ce> возьмите, и там же должны быть
<ce> кое-какие инструкции о том, куда
<ce> вы должны это отнести. По крайней мере, мне
<ce> так сказали. После этого _culp_ должен
<ce> выдать вам вашу награду.
<ce> Я даю на это =timer_ дней.

QuestFail:  [1003]
<ce> _questgiver_ ожидал, что вы вернетесь
<ce> в ___questgiver_ с _pick_ к этому времени.

QuestComplete:  [1004]
<ce> Тогда держи.
<ce> Дай этим стервятникам что-нибудь на память,
<ce> если увидишь их снова.

RumorsDuringQuest:  [1005]
Говорят, на этой неделе какой-то дебил-бард и %t сошлись в суде.

Message:  1011
<ce> Опять это? %oth. Эй, мне очень жаль, что так
<ce> получилось. %t получат полный отчет, и все делают
<ce> все возможное, чтобы найти того, кто за этим
<ce> стоит. Но послушайте, я не позволю вам уйти
<ce> с пустыми руками. Оставьте себе _pick_, что вы
<ce> получили от них, и продайте, если хотите, а я дам
<ce> вам снаряжние из своих запасов.
<ce> Это мелочь, но это буквально все, что у меня
<ce> осталось с прошлой недели. Просто знайте: мы
<ce> ценим, что у нас в гильдии есть такие, как вы,
<ce> которые заботятся обо всех нас.
<ce>
<ce> Итак, вам нужен двуручный топор или клеймор?
<ce>
<ce> (ДА: топор, НЕТ: клеймор)

Message:  1020
<ce> Вы слышите слабый смешок.

Message:  1023
<ce> "Э-это... не-возззм..."
<ce> Главарь валится с ног.

Message:  1025
<ce> В этом месяце мы платим =bigsum_ за каждого недоумка
<ce> из Гильдии Бойцов. Помните, приоритеты - это глаза,
<ce> сердца и легкие. Частичные выплаты означают меньшую
<ce> оплату для вас.
<ce>
                             К.

Message:  1030
%qdt:
 Гильдия Бойцов дала мне
 задание, которое не связано
 с борьбой. Меня направили
 в _res_, ___res_, чтобы
 забрать оттуда _pick_. После
 этого мне скажут, куда это
 отнести. Мне нужно вернуться
 в ___questgiver_
 через =timer_ дней.

Message:  1031
%qdt:
 Меня подставили и устроили жестокую засаду.
 Мне следует вернуться в ___questgiver_
 и рассказать об этом _questgiver_.

QBN:

Person _questgiver_ group Questor male
Person _culp_ group Local_3.0

Item _pick_ mineral
Item _reward_ gold range 60 to 140
Item _bigsum_ gold range 1235 to 1399
Item _letter_ letter used 1025

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is 5 Thief
Foe _perp_ is Nightblade

--   Quest start-up:
   start timer _timer_
   log 1030 step 0
   place item _pick_ at _res_
   give item _letter_ to _perp_

_got_ task:
   clicked item _pick_
   say 1020
   create foe _enemy_ every 0 minutes 1 times with 100% success
   create foe _perp_ every 0 minutes 1 times with 20% success

_infight_ task:
   daily from 0:00 to 23:59
   change foe _enemy_ infighting true
   change foe _perp_ infighting true
   clear _infight_

_slain_ task:
   killed _perp_
   say 1023
   log 1031 step 1

_victory_ task:
   when _qgclicked_ and _got_
   make _pick_ permanent
   give pc _reward_
   end quest

_qgclicked_ task:
   clicked npc _questgiver_

_clearclick_ task:
   when _qgclicked_ and not _got_
   clear _qgclicked_ _clearclick_

_timer_ task:
   say 1003
   end quest