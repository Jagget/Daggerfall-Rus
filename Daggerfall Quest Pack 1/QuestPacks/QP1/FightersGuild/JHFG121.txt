Quest: JHFG121
DisplayName: Непомерный долг
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Кое-кто здесь, в ___qgiver_, влез в такие
<ce> долги, что сбежать - единственный выход. Очевидно, на подходе
<ce> охотники за головами, так что это дело может обернуться
<ce> неприятностями. Хотите поохранять заказчика?

RefuseQuest:  [1001]
<ce> Если подумать, я вас не виню.
   <--->
<ce> Знаете, это, наверное, очень разумное решение.

AcceptQuest:  [1002]
<ce> Заказчик - _friend_. Вы найдете %g4 в _res_
<ce> в этом городе. Сначала вы отвезете %g4 в %g4
<ce> дом в другом городе, чтобы забрать вещи,
<ce> а затем - в убежище, где бы оно
<ce> ни находилось. Там вам заплатят, и вы будете
<ce> свободны. Так что отправляйтесь туда
<ce> и проследите, чтобы все сработало должным образом.

QuestFail:  [1003]
<ce> "Ну, вы знаете, мы потратили довольно много времени
<ce> и не добрались до того места, где мне нужно быть. Я думаю,
<ce> будет справедливо отменить этот заказ прямо сейчас,
<ce> не так ли? Доброго дня".
<ce>
<ce> Вы на мгновение отвлеклись, и _friend_ сбежал.
<ce> Вы теряете %g4 след.

QuestComplete:  [1004]
<ce> Вы отказываетесь от мести
<ce> и отпускаете %g4 с миром.
<ce> На ваших руках не будет %g4 крови.

QuestorPostFailure:  [1009]
Я же не могу доверить вам безопасность должников, не так ли?

Message:  1010
<ce> Привет! Вы, значит, из Гильдии Бойцов? Хорошо.
<ce> _qgiver_ обещал мне прислать кого-нибудь
<ce> способного. Думаю, это комплимент, а, друг?
<ce> Ха-ха-ха! Ну, эм, мне пришлось прятаться здесь
<ce> некоторое время. Мне нужно вернуться в ___farhome_
<ce> и забрать свою одежду и то немногое, что у меня есть.
<ce> Банки вывезли большую часть моих вещей, но до этого
<ce> последнего дома они, кажется, еще не добрались.
<ce> Он называется _farhome_. Там я скажу вам, куда вы
<ce> меня поведете дальше. Итак, показывайте дорогу,
<ce> чемпион! Моя жизнь в ваших руках!

Message:  1011
<ce> А, хорошо, мои вещи здесь. Дайте-ка взглянуть.
<ce>
<ce> Ну, по крайней мере, некоторые из них. Мои деловые
<ce> партнеры, видимо, уже не раз совершали набеги
<ce> на дом. Вредные ребята, или, лучше сказать,
<ce> неудачники. Я вынужден работать с такими
<ce> негодяями. Не то что вы, а, дружище? Ха-ха-ха!
<ce> А теперь давайте отправимся в ___farmhome_.
<ce> Там я выработал для себя новую личность
<ce> и буду вполне доволен, если начну все с чистого листа.
<ce> Отправляемся, как только будете готовы, %pcf.

Message:  1012
<ce> "Ах, ___farmhome_! Какое блаженство!
<ce> Не могу дождаться, когда начну его развивать. У других
<ce> совладельцев были некоторые возражения, но они знают,
<ce> что я подхожу для этого. Я объясняю вам, мой дорогой
<ce> друг, эту маленькую деталь в надежде, что вы поймете,
<ce> почему я не могу заплатить вам за это маленькое
<ce> путешествие. Все мои активы находятся в неликвидных
<ce> товарах, и я не могу достать наличную сумму денег,
<ce> чтобы заплатить вам, по крайней мере, в течение
<ce> нескольких лет. Но я благодарю вас за хорошую работу.
<ce> Практика, которую вы получили по дороге, стоила вашего
<ce> времени, не так ли? Давайте оставим все как есть".
<ce>
<ce> Позволите ли вы
<ce> так явно обмануть себя?

Message:  1013
<ce> Ваше настойчивое требование остановиться
<ce> ничего не дает. Вы решаете, что есть только
<ce> один способ получить то, что вы заслужили.
<ce>
<ce> "Друг? Что вы делаете?"

Message:  1014
<ce> Вы убили _friend_.

Message:  1020
<ce> Приближается группа вооруженных стражников.
<ce>
<ce> "Извините, %ra, но вы, кажется, путешествуете
<ce> с одним подонком по имени _friend_. Вам лучше
<ce> отойти в сторону. У нас есть ордер на %g4
<ce> арест от имени _bank_. Мы уполномочены выдать
<ce> вам =reward_ золотых за ваше сотрудничество.
<ce> Пожалуйста, отойдите".
<ce>
<ce> Сдадите ли вы _friend_ властям?

Message:  1021
<ce> "Хороший выбор. Вперед".
<ce>
<ce> Несмотря на недоуменные протесты, вы
<ce> передаете _friend_ в руки правосудия.
<ce> Один из наемников вручает вам кошель,
<ce> набитый деньгами, и группа уходит.
<ce> В кошле вы находите =reward_ золотых.

Message:  1022
<ce> Наемники кивают друг другу
<ce> и начинают атаку.

Message:  1030
%qdt:
 Гильдия Бойцов в ___qgiver_
 поручила мне охранять некоего
 должника по имени _friend_
 от наемников, которые охотятся на н%g4.
 Я могу найти %g4 в _res_,
 в ___res_.

Message:  1031
%qdt:
 _friend_ хочет сначала заехать
 в ___farhome_, чтобы забрать свои
 вещи в _farhome_.

Message:  1032
%qdt:
 _friend_ спрячется
 в ___farmhome_ от своих кредиторов.

QBN:

Item _reward_ gold range 300 to 500
Item _loot_ gold range 2000 to 3000

Foe _merc_ is 2 Knight
Foe _frenemy_ is Bard

Person _qgiver_ group Questor male
Person _friend_ faction The_Merchants

Place _res_ local house
Place _bank_ remote bank
Place _farhome_ remote house
Place _farmhome_ remote farmhome

Clock _timer_ 00:00 0 flag 1 range 0 2
Clock _found_ 1.00:00 4.00:00
Clock _battle_ 08:00

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _friend_ at _res_

_talk_ task:
    clicked npc _friend_
    say 1010
    hide npc _friend_
    add _friend_ face
    start timer _found_
    pc at _farhome_ do _explain_
    log 1031 step 1

_explain_ task:
    say 1011
    start task _click_

_click_ task:
    log 1032 step 2
    pc at _farmhome_ do _final_

_ambush_ task:
    when _click_ and not _explain_
    start task _found_

_found_ task:
    prompt 1020 yes _obey_ no _resist_

_obey_ task:
    say 1021
    get item _reward_
    make _reward_ permanent
    drop _friend_ face
    end quest

_resist_ task:
    say 1022
    create foe _merc_ every 0 minutes 1 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _merc_ infighting true
    clear _infight_

_final_ task:
    drop _friend_ face
    stop timer _timer_
    prompt 1012 yes _escape_ no _confront_

_escape_ task:
    give pc nothing
    end quest

_confront_ task:
    start timer _battle_
    say 1013
    create foe _frenemy_ every 0 minutes 1 times with 100% success
    give item _loot_ to _frenemy_
    make _loot_ permanent
    restrain foe _frenemy_

_battle_ task:
    remove foe _frenemy_
    start task _escape_

_murder_ task:
    killed _frenemy_
    say 1014
    end quest

_weasel_ task:
    when _timer_ and _talk_
    say 1003
    drop _friend_ face

_timer_ task:
    end quest