Quest: JHIM105
DisplayName: Бравые ребята
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Меня зовут _qgiver_. Мой дурацкий
<ce> сводный брат вчера вечером устроил драку в баре и теперь
<ce> должен вернуться и поддержать нашу "семейную честь".
<ce> Лично меня совсем не заботит то, сколько людей
<ce> получит по морде, но я обещал своему покойному отцу на
<ce> его смертном одре, что позабочусь о нашей семье. Я дам вам
<ce> =reward_ золотых монет, если отправитесь
<ce> в _tavern_ и поможете с дракой.

RefuseQuest:  [1001]
<ce> Возможно, он и сам справится.
<ce> Я надеюсь, что так и будет.

AcceptQuest:  [1002]
<ce> Хорошо. Вот, это _family_. Вы
<ce> прекрасно поладите. Я думаю, что сейчас
<ce> самое время отправиться в
<ce> _tavern_. Подонки там все равно
<ce> пьяны в хлам и днем и ночью. Помогите нам, и вы
<ce> получите свою оплату, а я буду хорошо
<ce> отзываться о вас всякий раз,
<ce> когда кто-то будет спрашивать.

QuestFail:  [1003]
<ce> "Забудьте. Я возвращаюсь в
<ce> _tavern_ и сделаю это сам".

QuestComplete:  [1004]
<ce> Вы берете деньги, которые по праву принадлежат вам.

QuestorPostSuccess:   [1008]
Сводные родственники всегда создают проблемы, поверьте мне.

Message:  1010
<ce> "Привет, %pcf, спасибо, что пошли со мной. Мне
<ce> не нужна большая помощь или что-то в этом роде, но
<ce> немного поддержки не помешает, не так ли?"
<ce>
<ce> _family_ берет клинок
<ce> для боя и следует за вами.

Message:  1011
<ce> Как только вы входите в таверну, две
<ce> фигуры встают и начинают кричать на вас.
<ce> Среди шума вы вычленяете, что
<ce> они злы на вашего партнера. Бой
<ce> казался неизбежным на этом этапе.

Message:  1020
<ce> "Вы довольно хороший боец, %pcf.
<ce> Я признаю это, но вы были своего рода обузой для меня
<ce> во время боя, и я думаю, что почувствовал бы себя
<ce>  обманутым, если бы мне пришлось платить вам.
<ce> Больше походило на то, что я защищал вас, согласны? Давайте
<ce> просто пойдем каждый своей дорогой и закончим на этом".
<ce>
<ce> Позволите ли вы _family_
<ce> уйти не заплатив?

Message:  1021
<ce> "%oth! Что вы делаете?!"

Message:  1023
<ce> "Вот именно! Я знал, что вы со мной согласитесь.
<ce> Может быть, вы сможете уговорить бармена
<ce> дать немного бесплатного пива за то, что вы избавились
<ce> от этих придурков. Пока, %ra".
<ce>
<ce> _family_ машет рукой и выскальзывает за дверь.
<ce> По крайней мере, %g может прожить еще один день.

Message:  1025
<ce> "Вау, это было невероятно! Вы сражаетесь, как
<ce> демон на сурсуме! Берите деньги.
<ce> Просто наблюдать, как ты их избиваешь,
<ce> стоило всего этого разгрома!"
<ce>
<ce> _family_ передает вам холщовый мешок,
<ce> в котором лежат _reward_ золотых,
<ce> затем направляется выпить.

Message:  1030
%qdt:
 _qgiver_ попросил
 меня сопровождать своего сводного брата
 _family_ в _tavern_,
 чтобы уладить потасовку в баре.
 Я получу оплату от н%g4,
 когда все закончится.

Message:  1099
<ce> Если повезет, то после этого у меня будет более разумный
<ce> родственник. Некоторым людям нужно,
<ce> чтобы им вбили разум в голову.

QBN:
Item _reward_ gold range 101 to 149

Person _qgiver_ group Questor male
Person _family_ group Chemist

Place _tavern_ local tavern

Clock _timer_ 1.00:00
Clock _delay_ 00:01

Foe _enemy1_ is Monk
Foe _enemy2_ is Monk
Foe _fam_ is Archer

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _tavern_ set _inside_
    say 1010
    add _family_ face
    pick one of _honor_ _betray_

_inside_ task:
    start task _begin_

_begin_ task:
    say 1011
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    create foe _enemy2_ every 0 minutes 1 times with 100% success
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true

_kill1_ task:
    killed _enemy1_

_kill2_ task:
    killed _enemy2_

_success_ task:
    when _kill1_ and _kill2_
    start timer _delay_

_delay_ task:
    drop _family_ face

variable _honor_
_happy_ task:
    when _delay_ and not _betray_
    say 1025
    give pc _reward_
    end quest

variable _betray_
_sad_ task:
    when _delay_ and _betray_
    prompt 1020 yes _escape_ no _murder_

_escape_ task:
    say 1023
    end quest

_murder_ task:
    say 1021
    create foe _fam_ every 0 minutes 1 times with 100% success
    change foe _fam_ infighting true
    change foe _fam_ team 0

_deadfam_ task:
    killed _fam_
    give pc _reward_
    change repute with _qgiver_ by -10
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_timer_ task:
    say 1003
    drop _family_ face
    end quest

_clearclick_ task:
    when _qgclicked_
    say 1099
    clear _qgclicked_ _clearclick_