Quest: JHIM114
DisplayName: Мелкое ограбление во всей красе
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Спокойно. У меня есть информация
<ce> о доме в городе, где ночью будут
<ce> хранить большие деньги. Я могу
<ce> сказать вам, где это, но это будет
<ce> стоить дорого. Хотите узнать?

RefuseQuest:  [1001]
<ce> Хорошо, оставим это так.

AcceptQuest:  [1002]
<ce> Это будет стоить 200 золотых. Но оно того стоит.
<ce> Одну секунду, мне нужно достать свой кошель с деньгами.

QuestComplete:  [1004]
<ce> Похоже, это и есть тайник.

QuestorPostSuccess:   [1008]
Не приведите ко мне власти.

Message:  1010
<ce> 200 золотых, дыа-а-с-сэ-эр. Я их возьму, большое
<ce> вам спасибо.
<ce>
<ce> Итак, вот в чем дело. _res_ здесь, в городе,
<ce> будет хранить деньги от продажи
<ce> недвижимости. Я не слышал, сколько именно,
<ce> но, по-моему, это больше, чем тысяча. Приходите после
<ce> полуночи. Если попробуете взяться за это дело
<ce> днем, они взбесятся. Их курьер приедет
<ce> забрать деньги после полуночи, так что,
<ce> если хотите обхитрить его, вот ваше прикрытие.
<ce> Что бы ни случилось, это ваше шоу.
<ce> Приятно было иметь с вами дело.

Message:  1011
<ce> Вы входите в дом и тихо закрываете за
<ce> собой дверь.

Message:  1012
<ce> "Извините, кто вы?"
<ce>
<ce> Юное создание испуганно смотрит на вас из-за угла.
<ce> Вы говорите, что пришли забрать деньги?

Message:  1013
<ce> "О, вы курьер? Хорошо".
<ce>
<ce> Она уходит, оставляя вас с вашей задачей.
   <--->
<ce> "О, вы курьер? Хорошо".
<ce>
<ce> Он уходит, оставляя вас с вашей задачей.

Message:  1014
<ce> "Тогда зачем ты здесь?! А?!"
<ce>
<ce> Ее пронзительный крик разрывает воздух,
<ce> и люди начинают искать вас.
   <--->
<ce> "Тогда зачем ты здесь?! А?!"
<ce>
<ce> Его громкий крик пронзает воздух,
<ce> и люди начинают искать вас.

Message:  1015
<ce> Купленная вами информация была верной.
<ce> Они действительно охраняют дом до полуночи.
<ce> Вы пришли в неподходящее время и были
<ce> определены как угроза.

Message:  1016
<ce> 200, ну же. Я не могу ждать вечно.

Message:  1030
%qdt:
 Я получил информацию о доме в
 ___res_, где хранится крупная
 сумма денег. _res_ будет
 почти без охраны с полуночи до
 пяти утра. Если мне удастся
 попасть туда, я смогу забрать
 золото.

QBN:

Person _qg_ group Questor

Item _sack_ large_sack
Item _reward_ gold range 835 to 1399

Place _res_ local house

Clock _timer_ 1.00:00
Clock _sneak_ 0:00 0:01

Foe _warr_ is Warrior

--   Quest start-up:
  start timer _timer_

_start_ task:
  clicked _qg_ and at least 200 gold otherwise do _fail_
  say 1010
  place item _sack_ at _res_
  pc at _res_ do _inside_
  log 1030 step 0

_fail_ task:
  say 1016

variable _inside_
_nosneak_ task:
  when not _inside_
  stop timer _sneak_

_entry_ task:
  when _inside_ and _start_ and not _day_
  say 1011
  start timer _sneak_

_day_ task:
  daily from 5:00 to 23:59

_caught1_ task:
  when _inside_ and _start_ and _day_
  say 1015
  setplayercrime Theft
  spawncityguards immediate

_sneak_ task:
  prompt 1012 yes _agree_ no _caught2_

_agree_ task:
  say 1013

_caught2_ task:
  say 1014
  setplayercrime Theft
  spawncityguards immediate

_found_ task:
  clicked item _sack_
  take _sack_ from pc
  give pc _reward_
  remove log step 0

_finish_ task:
  when _found_ and not _inside_
  end quest

_timer_ task:
  end quest
