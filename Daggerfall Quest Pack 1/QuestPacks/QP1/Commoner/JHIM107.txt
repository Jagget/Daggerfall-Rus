Quest: JHIM107
DisplayName: Остроумный ответ.
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Прости мою невнимательность, %ra. Я просто не смог...
<ce> спать прошлой ночью. Какой-то идиот начал спор
<ce> со мной о моих предках, и я не мог это оставить. Наш
<ce> громкий разговор перерос в крик, и я до сих пор чувствую 
<ce> в себе эту тяжесть. Может быть, вы могли бы немного
<ce> отомстить за меня? Готовы ли вы научить этого дурака
<ce> небольшому смирению с помощью ударов?

RefuseQuest:  [1001]
<ce> Тогда оставьте меня с моей головной болью.

AcceptQuest:  [1002]
<ce> Что ж, благодарю вас за согласие. Думаю, у меня есть книга,
<ce> которую я мог бы вам дать за это. Найдите этого дурака
<ce> в _res_, наверное, он мечтает о том, с кем бы еще 
<ce> подраться. Все, о чем я прошу, - это принести мне хоть слово
<ce> раскаяния с его стороны. И скажу я это сейчас: 
<ce> не перестарайтесь. Сегодня на моих руках не будет крови. 
<ce> Пожалуйста, возвращайтесь завтра с хорошими новостями.

QuestFail:  [1003]
<ce> У-убит?! Ты что, сошел с ума? Стража, здесь убийца!
<ce> %ra, убийца, %pcf! Я здесь ни при чем!
<ce> Кто-нибудь, помогите, пожалуйста!

QuestComplete:  [1004]
<ce> Ах, какое это приносит мне удовольствие! Подумать только -
<ce> простое избиение решило мою проблему! Спасибо, %ra. Вот книга,
<ce> которую я обещал. Пусть она послужит вам верой и правдой.

QuestorPostSuccess:   [1008]
Если мне понадобится еще кого-то избить, я знаю, к кому обратиться!

Message:  1020
<ce> Так _questgiver_ нанимает головорезов, чтобы терзать меня!
<ce> Ладно, я отменяю свои слова! Оставьте меня в покое! Исчезни сейчас же!

Message:  1021
<ce> Тело беспомощно падает на пол.

Message:  1022
<ce> Расскажете ли вы _questgiver_ об убийстве?

Message:  1030
%qdt:
 _questgiver_ попросил меня
 научить небольшому смирению врага,
 который живет в _res_.
 Мне следует сделать это за один день.

Message:  1099
<ce> Покончите с этим делом и расскажите мне о нем.

QBN:
Item _reward_ book2

Person _questgiver_ group Questor male

Place _res_ local house

Clock _timer_ 1.00:00
Clock _delay_ 0:05

Foe _enemy_ is Bard

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _enemy_ at _res_
	restrain foe _enemy_

_inj_ task:
	injured _enemy_
	say 1020
	restrain foe _enemy_

_slain_ task:
	killed 1 _enemy_ saying 1021

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_ and not _inj_
	say 1099
	clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
	when _qgclicked_ and _inj_ and not _slain_
	give pc _reward_
	end quest

_decision_ task:
	when _qgclicked_ and _slain_
	prompt 1022 yes _bigfail_ no _lie_

_lie_ task:
	give pc _reward_
	end quest

_bigfail_ task:
	say 1003
	change repute with _questgiver_ by -10
	setplayercrime Murder
	spawncityguards immediate
	start timer _delay_
	remove log step 0

_delay_ task:
	end quest

_timer_ task:
	end quest