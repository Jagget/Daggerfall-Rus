Quest: JHDB114
DisplayName: Мрачные произведения искусства
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Есть один любопытный запрос на убийство определенного
<ce> человека и доставку тела нашему клиенту.
<ce> По-моему, попахивает некромантией. Можно ли поручить
<ce> вам такую работу?

RefuseQuest:  [1001]
<ce> А, потому что у вас есть стандарты, понимаю.

AcceptQuest:  [1002]
<ce> Я должен заметить, что точно не знаю, является ли
<ce> клиент на самом деле служителем темных искусств. Это всего
<ce> лишь ощущение, которое вызывает у меня эта просьба.
<ce> Труп должен быть доставлен со всеми жизненно важными 
<ce> органами в целости и сохранности в ___nhome_. 
<ce> Тело должно быть доставлено не позднее =2nhome_ дней. 
<ce> В заявке упоминается _mark_, ___mhome_.
<ce> Однако я предупреждаю вас, что стража %crn не потерпит
<ce> вида %ra, несущего на себе труп. Избегайте любых 
<ce> населенных пунктов до тех пор, пока не доставите труп.
<ce> Ночная Мать присматривает за вами, %pct.

QuestFail:  [1003]
<ce> Тело явно разложилось больше, чем просил клиент.
<ce> Вы выбрасываете его.

QuestComplete:  [1004]
<ce> Изнутри коттеджа вас заметила фигура в мантии.
<ce> Вскоре %g1 выходит поприветствовать вас.
<ce>
<ce> "Да? О, великолепно. Отличная работа, %ra. Конечно, 
<ce> немного оплаты не помешает. Примите это как причину 
<ce> хранить молчание, ладно? Я имею дело с людьми, которые
<ce> просто не потерпят, чтобы об этой работе шептались
<ce> по углам. Вот, теперь берите".

Message:  1010
<ce> "Привет, да?"
<ce>
<ce> Вы застали _mark_ врасплох.
<ce> Убийственный удар следует незамедлительно.

Message:  1011
<ce> Вы начинаете нести труп. Вам следует
<ce> избегать цивилизованных мест, пока он при вас.

Message:  1012
<ce> Вас заметили, когда вы несли труп _mark_.
<ce> Поднята тревога. Несколько стражников
<ce> приближаются, чтобы свершить правосудие.

Message:  1030
%qdt:
 Темное Братство ___qgiver_
 заключило со мной контракт на убийство
 _mark_ из ___mhome_ 
 и доставку тела в место,
 называемое ___nhome_. Я должен
 донести %g4 не позднее =2nhome_ дней.

QBN:

Person _qgiver_ group Questor male
Person _mark_ faction Generic_Temple
Person _dummynec_ faction The_Necromancers

Place _mhome_ remote house
Place _nhome_ remote poorhome

Item _reward_ gold

Clock _count_ 1:00
Clock _2nhome_ 6.00:00 8.00:00
Clock _failsafe_ 60.00:00

--	Quest start-up:
    start timer _failsafe_
    log 1030 step 0
    place npc _mark_ at _mhome_
    pc at _nhome_ do _arrive_

_enter_ task:
    when pc enters city
    clear _exit_

_exit_ task:
    when pc enters city
    clear _enter_

_slain_ task:
    clicked npc _mark_
    say 1010
    hide npc _mark_
    say 1011
    start timer _count_
    start timer _2nhome_

variable _count_
_alert_ task:
    when _count_ and _enter_
    say 1012
    setplayercrime Murder
    spawncityguards immediate

variable _arrive_
_reward_ task:
    when _slain_ and _arrive_
    give pc _reward_
    change repute with _dummynec_ by +5
    end quest

_2nhome_ task:
    say 1003
    end quest

_failsafe_ task:
    end quest