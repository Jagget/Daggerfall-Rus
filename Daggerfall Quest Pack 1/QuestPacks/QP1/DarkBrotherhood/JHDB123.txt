Quest: JHDB123
DisplayName: Спасение Темного Братства
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Один из наших высокопоставленных сотрудников стал
<ce> жертвой заговора с целью получения выкупа, и нам
<ce> нужен кто-то для проведения спасательной
<ce> операции. Покажи свою преданность Братству.

RefuseQuest:  [1001]
<ce> О, конечно. Какая преданность?

AcceptQuest:  [1002]
<ce> В пределах ___dung_ находится в плену родственник
<ce> одного из наших местных Темных Братьев. Поскольку
<ce> указанный член семьи в данный момент слишком
<ce> вовлечен в заговор более высокого уровня, мы
<ce> пообещали прислать вместо него кого-нибудь,
<ce> способного решить этот вопрос. Найдите _captive_
<ce> и вернитесь сюда в течение =timer_ дней.

QuestFail:  [1003]
<ce> На твоем месте я бы не тратил время впустую.
<ce> _captive_ находится в ___dung_.

QuestComplete:  [1004]
<ce> Почему _captive_ не с вами?
<ce> Предоставьте свой отчет.
<ce>
<ce> Понимаю. Я верю вам на слово.
<ce> Я передам эту новость дальше.
<ce> Гнев Мефалы удвоится за такое
<ce> циничное преступление.

Message:  1010
<ce> Вы находите _captive_,
<ce> но она сопротивляется вашему приближению.
<ce>
<ce> "Нет! Нет! Оставь меня здесь, внизу!
<ce> Я не хочу уходить, пожалуйста!"
<ce>
<ce> Похоже, некоторых людей нужно
<ce> силой вытаскивать из роскоши этой
<ce> убогой дыры. Ты тянешь ее за собой.

Message:  1011
<ce> _captive_ визжит, когда вы выходите из
<ce> ___dung_.
<ce>
<ce> "Я ЖЕ ГОВОРИЛА ТЕБЕ! Я ЖЕ СКАЗАЛА ТЕБЕ
<ce> ОСТАВИТЬ МЕНЯ ТАМ! ЭТО БОЛЬНО! НЕТ!"
<ce>
<ce> За этим следует ужасная трансформация,
<ce> оставляющая ее в совершенно ином состоянии.

Message:  1030
%qdt:
 Я предпринял спасательную
 операцию от имени Темного
 Братства ___qgiver_. Мне нужно
 найти _captive_ в ___dung_ и
 вернуться в течение =timer_ дней.

Message:  1031
%qdt:
 _captive_ была проклята на
 ужасную смерть, если она покинет
 ___dung_. У меня нет другого
 выбора, кроме как сообщить о
 случившемся _qgiver_.

QBN:

Person _qgiver_ group Questor
Person _captive_ faction Dancers female

Foe _zomb_ is Zombie

Place _dung_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _captive_ at _dung_
    pc at _dung_ do _inside_
    reveal _dung_

variable _inside_
_begin_ task:
    clicked npc _captive_
    say 1010
    hide npc _captive_
    add _captive_ face

_transform_ task:
    when _begin_ and not _inside_
    say 1011
    drop _captive_ face
    create foe _zomb_ every 0 minutes 1 times with 100% success
    log 1031 step 1
    change foe _zomb_ infighting true

_pcgetsgold_ task:
    when _qgclicked_ and _transform_
    give pc nothing
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _transform_
    say 1003
    clear _qgclicked_ _clearclick_

_timer_ task:
    end quest