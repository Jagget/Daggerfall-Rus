Quest: JHDB116
DisplayName: Яд
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Время от времени мы получаем запрос забрать жизнь
<ce> и отвести от себя подозрения. Величайшие убийцы во всем 
<ce> Тамриэле знают, как сделать так, чтобы люди умирали быстро
<ce> или медленно, и именно поэтому наше дело живет.
<ce> Согласитесь ли вы на простую работу по отравлению цели?

RefuseQuest:  [1001]
<ce> Почему? Вам нужно пить кровь после каждого убийства?

AcceptQuest:  [1002]
<ce> Это задание приведет вас в ___tavern_.
<ce> Цель, _mark_, ожидает доставки предоплаченной партии 
<ce> _plant_ своим обычным курьером в течение =2tavern_ дней.
<ce> Вы доставите _plant_ вместо него, так что трудностей 
<ce> не будет. Ни при каких обстоятельствах
<ce> не касайтесь растения. Яд, который мы поместили
<ce> на него, будет активным в течение нескольких часов.
<ce> Чтобы избежать подозрений, вы должны покинуть здание
<ce> сразу же после доставки. В противном случае ваше лицо
<ce> будет легко запомнить. Возвращайтесь, когда задание
<ce> будет выполнено. Прощайте.

QuestFail:  [1003]
<ce> _mark_ ожидал, что _plant_ уже
<ce> должно быть доставлено в ___tavern_.

QuestComplete:  [1004]
<ce> Я полагаю, что вы не задержались в таверне
<ce> надолго? Было бы позором для Братства быть связанным 
<ce> с неуклюжим передвижением одного из его членов,
<ce> независимо от %pg3 заслуг. Ваша преданность
<ce> была замечена, %pct.

Message:  1015
<ce> Время, проведенное вами в этой таверне, дало ее 
<ce> завсегдатаям возможность запомнить вас.
<ce> Ваша работа провалилась.

Message:  1020
<ce> Да, курьер? Спасибо! Да, это отлично подойдет. Прекрасный
<ce> образец, я считаю. Прекрасная работа. Передавайте привет
<ce> моим друзьям. Не скоро еще я смогу навестить их.

Message:  1021
<ce> Да? Что это? Простите меня за эти слова, но я вас 
<ce> не узнаю и не доверяю вам. Насколько я знаю, вы могли
<ce> передать это не тому человеку, и скоро какой-нибудь
<ce> разъяренный мужлан выломает мою дверь ради своего
<ce> _plant_. Есть ли какая-то причина, по которой я должен
<ce> принять это?
   <--->
<ce> Здравствуйте. Простите, что? Я вас не узнаю. 
<ce> Я получаю свои посылки от определенного курьера,
<ce> которому доверяю, и это не вы. Есть ли какая-нибудь
<ce> причина, по которой я должен принять _plant_?

Message:  1022
<ce> Мой обычный курьер сообщил бы мне, если бы что-то 
<ce> изменилось. Конечно, если он послал вас, вы должны знать,
<ce> каково наше соглашение. Собираюсь ли я заплатить вам
<ce> за эту доставку?
   <--->
<ce> Я был бы дураком, если бы согласился на это. Но наша дружба
<ce> не стоит того, чтобы выбрасывать ее на свалку только 
<ce> из-за сбоев в процессе доставки. Скажите мне, прежде чем я
<ce> приму свое решение. Соглашение о том, что я должен получить
<ce> это, требует ли оно, чтобы я заплатил своему курьеру?

Message:  1023
<ce> Хорошо сказано. Я признаю, что становлюсь слишком
<ce> подозрительным. Это всего лишь образец _plant_. 
<ce> Что вообще может пойти не так? Спасибо вам, друг. 
<ce> Я возьму это сейчас. Передавайте мой привет ребятам 
<ce> из аптеки. Я надеюсь скоро их увидеть.
   <--->
<ce> Действительно. Я думаю, что это была долгая неделя,
<ce> и на этот раз мои подозрения взяли надо мной верх.
<ce> Я возьму это. Благодарю вас. В конце концов, какой
<ce> вред может быть от этого маленького образца
<ce> _plant_? Прощайте, друг. Счастливого пути.

Message:  1024
<ce> Нет, нет, так не пойдет. Здесь явно что-то не так,
<ce> и я не стану выставлять себя вором, принимая этот
<ce> странный груз. Уходите сейчас же.
   <--->
<ce> Нет, я отказываюсь. Все это слишком странно. Уходите сейчас
<ce> же. Я не буду иметь к этому никакого отношения. Если я
<ce> совершаю ошибку, то разберусь с ней позже. Убирайтесь.

Message:  1030
%qdt:
 Я должен доставить отравленное растение
 к _mark_ в ___tavern_
 до того, как пройдет =2tavern_ дней,
 а затем вернуться в Темное Братство
 __qgiver_, чтобы сообщить
 о своем успехе.

QBN:
Item _plant_ large_plant

Person _qgiver_ group Questor male
Person _mark_ faction Generic_Temple

Place _tavern_ remote tavern

Clock _2tavern_ 00:00 0 flag 17 range 0 2
Clock _surprise_ 00:08 00:12
Clock _failsafe_ 180.00:00

--	Quest start-up:
  start timer _2tavern_
  log 1030 step 0
  place npc _mark_ at _tavern_
  get item _plant_
  pc at _tavern_ set _inside_
  pick one of _e1_ _e2_

variable _e1_
variable _e2_
_inside_ task:
  start timer _surprise_

variable _surprise_
_deliver_ task:
  toting _plant_ and _mark_ clicked
  stop timer _2tavern_
  start timer _failsafe_

_easy_ task:
  when _deliver_ and _e1_
  say 1020

_hard_ task:
  when _deliver_ and _e2_
  prompt 1021 yes _step2_ no _fail_

_step2_ task:
  prompt 1022 yes _fail_ no _step3_

_step3_ task:
  say 1023

_fail_ task:
  say 1024
  end quest

_failure_ task:
  when _surprise_ and _inside_
  say 1015
  end quest

_victory_ task:
  when _qgclicked_ and _deliver_
  give pc nothing
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _deliver_
  clear _qgclicked_ _clearclick_

_2tavern_ task:
  say 1003
  end quest

_failsafe_ task:
  end quest