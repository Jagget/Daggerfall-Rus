Quest: JHDBY00
DisplayName: Услуги Братства по восстановлению репутации
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Одно из преимуществ хороших отношений с Братством —
<ce> это влияние во всех слоях общества. Мы заслужили
<ce> расположение не слишком верного писца при нашем
<ce> правителе, который готов за определенную плату
<ce> вычеркнуть преступления, совершенные нашими членами.
<ce> Если понадобится, мы можем узнать, во сколько 
<ce> это вам обойдется.

RefuseQuest:  [1001]
<ce> «Тогда не тратьте мое время попусту».

AcceptQuest:  [1002]
<ce> «Тогда я сделаю запрос».

QuestFail:  [1003]
<ce> ERROR JHDBY00

Message:  1010
<ce> «У вас нет никаких проблем с законом
<ce> в регионе %crn.
<ce> Страдаете паранойей, %pct?»

QBN:

Person _tg_ faction The_Dark_Brotherhood

Clock _timer_ 0:00

--	Quest start-up:
    start timer _timer_

variable _timer_

_legalrepute0_ task:
    when not _legalrepute1_ and not _legalrepute2_ and not _legalrepute3_ and not _legalrepute4_ and not _legalrepute5_

_legalrepute1_ task:
    player legal-repute is lower than -5

_legalrepute2_ task:
    player legal-repute is lower than -20

_legalrepute3_ task:
    player legal-repute is lower than -40

_legalrepute4_ task:
    player legal-repute is lower than -60

_legalrepute5_ task:
    player legal-repute is lower than -80

_nooutcome_ task:
    when _legalrepute0_ and _timer_
    say 1010
    start task _normalend_

_redirect1_ task:
    when _legalrepute1_ and not _legalrepute2_ and _timer_
    start quest JHrepchange1
    start task _normalend_

_redirect2_ task:
    when _legalrepute2_ and not _legalrepute3_ and _timer_
    start quest JHrepchange2
    start task _normalend_

_redirect3_ task:
    when _legalrepute3_ and not _legalrepute4_ and _timer_
    start quest JHrepchange3
    start task _normalend_

_redirect4_ task:
    when _legalrepute4_ and not _legalrepute5_ and _timer_
    start quest JHrepchange4
    start task _normalend_

_redirect5_ task:
    when _legalrepute5_
    start quest JHrepchange5
    start task _normalend_

_normalend_ task:
    change repute with _tg_ by +2
    end quest