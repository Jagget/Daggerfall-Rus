Quest: JHDB201
DisplayName: Возврат к естественному порядку
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Поймите, что у нас нет личной вендетты против этой
<ce> цели, %pct. Для нас все вольны повиноваться небу,
<ce> деревьям, Матери Ночи или вообще ничему, лишь бы это
<ce> их удовлетворяло. Тем не менее, когда кто-то
<ce> заставляет диких зверей нападать на прохожих, очень
<ce> скоро ему приходится держать ответ. Сможете ли вы вернуть
<ce> на землю помешанного на природе отшельника?
   <--->
<ce> В редких случаях мы получаем петиции об исполнении
<ce> закона, когда городская стража отказывается это
<ce> делать. Мое прочтение закона довольно небрежное, но я
<ce> уверен, что должно существовать какое-то положение,
<ce> запрещающее выводить из себя диких животных 
<ce> и отправлять их в близлежащие города. Готовы ли вы
<ce> сыграть палача против управляющего природой отшельника?

RefuseQuest:  [1001]
<ce> Этот контракт будет занят очень быстро.
<ce> Для кого-то это будет настоящей радостью.

AcceptQuest:  [1002]
<ce> Отшельник превратил ___mondung_ в свой дом,
<ce> совершая там ритуалы, которые приводят дикую природу
<ce> в неистовство. Очевидно, эти атаки ослабевают 
<ce> в ночные часы, поскольку даже жрецам грязи
<ce> требуется отдых. Восстановите естественный
<ce> порядок и вернитесь до того, как пройдет =timer_
<ce> ночей. Да пребудет с вами Мефала.

QuestFail:  [1003]
<ce> Вы уже должны были доложить _questgiver_
<ce> о смерти дикого отшельника.

QuestComplete:  [1004]
<ce> Теперь вы видите, как опасно питаться только
<ce> _alch_. Братство с удовольствием обеспечивает
<ce> возврат к естественному порядку, когда все другие
<ce> ответственные стороны отказываются это делать.

Message:  1020
<ce> Жрец природы теперь мертв. Дикие животные, скорее
<ce> всего, вернутся к своему естественному образу жизни.

Message:  1024
<ce> Странно. _questgiver_ предупреждал вас, 
<ce> что ваша цель вызывает хаос в местной дикой
<ce> природе, но в этом месте нет никаких признаков
<ce> этого. Возможно, слухи были преувеличены.

Message:  1030
%qdt:
 В ___mondung_ есть религиозный отшельник,
 который стал причиной нападений животных
 на окружающие города. Я должен выполнить
 контракт Темного Братства, 
 чтобы уничтожить священника и вернуться 
 к ___questgiver_ через =timer_ дней.

QBN:
Item _alch_ small_plant

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon6

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Healer
Foe _wild1_ is Grizzley_bear
Foe _wild2_ is Giant_bat
Foe _wild3_ is Giant_rat
Foe _wild4_ is Spriggan

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pick one of _dud_ _dud_ _dud_ _dud_ _alt_
	pc at _mondung_ set _inside_
	place foe _enemy_ at _mondung_
	reveal _mondung_

_daytime_ task:
	daily from 8:00 to 18:00

_daily_ task:
	daily from 0:00 to 23:59
	change foe _wild1_ infighting true
	change foe _wild2_ infighting true
	change foe _wild3_ infighting true
	change foe _wild4_ infighting true
	change foe _wild1_ team 0
	change foe _wild2_ team 0
	change foe _wild3_ team 0
	change foe _wild4_ team 0
	clear _daily_

variable _inside_
variable _dud_
variable _alt_
_altspawn_ task:
	when _inside_ and _alt_
	start task _altinform_

_altinform_ task:
	say 1024

_spawn_ task:
	when _inside_ and _daytime_ and not _slain_ and not _alt_
	create foe _wild1_ every 80 minutes indefinitely with 45% success
	create foe _wild2_ every 65 minutes indefinitely with 36% success
	create foe _wild3_ every 59 minutes indefinitely with 34% success
	create foe _wild4_ every 40 minutes indefinitely with 22% success

_injured_ task:
	injured _enemy_
	clicked foe _enemy_
	change foe _enemy_ infighting true
	change foe _enemy_ team 0

_slain_ task:
	killed 1 _enemy_ saying 1020

_pcgetsgold_ task:
	when _slain_ and _qgclicked_
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_timer_ task:
	say 1003
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_