Quest: JHDB121a
DisplayName: Убийство посла
-- Message panels
--Needs AnyInfo or Rumors fix
QRC:

QuestComplete:  [1004]
<ce> Приветствую вас. Ах, вы тот %ra, которого я должен поблагодарить.
<ce> Я в восторге от того, что Темное Братство
<ce> равноправно работает даже с орками.
<ce> Их цена была велика, но оно того стоило.
<ce>
<ce> Мой народ хочет жить в мире, а наших врагов
<ce> много. Вы оказали нам большую услугу. В противном случае
<ce> этого человека, Септима, можно было бы убедить
<ce> убить нас всех. Столько яда от этого злобного советника.
<ce>
<ce> Наш подарок вам обошёлся нам дорого, но
<ce> без всякого сожаления. Чтобы никто никогда не говорил, что
<ce> орки не награждают своих благодетелей.
<ce> Если ты увидишь Императора, скажи ему это. Мы
<ce> будем верны нашим договорам.

Message:  1010
<ce> "Приятно познакомиться. Пожалуйста,
<ce> давайте выйдем наружу. Здесть нас услышат".
<ce>
<ce> Она ловко бросает свою сумку с тканями
<ce> в ближайший угол и следует за вами.

Message:  1011
<ce> Волшебница вдыхает свежий воздух и начинает говорить,
<ce> освободившись от сырых залов дворца.
<ce>
<ce> "Ух, какое облегчение. Я уверена, что вы привыкли к
<ce> подобной грязной роскоши, но я - нет. Этот воздух
<ce> подходит мне гораздо больше.
<ce>
<ce> "Меня зовут Фития Спрингхоллоу. Последние несколько
<ce> недель я была занят попытками узнать побольше об этом
<ce> предполагаемом после, но я не могу даже мельком увидеть
<ce> его. Слишком важная персона, чтобы появляться на
<ce> публике. Он использует посланников для общения, но они
<ce> слишком хороши, даже пьяные. Слуги Вэйреста не
<ce> бездельники, это точно.
<ce>
<ce> "Моя единственная зацепка - это парень, которого я
<ce> подкупила, чтобы он немного раскрылся. Я предлагаю нам
<ce> поторопиться. Никто не знает, что может случиться с
<ce> болтливыми информаторами в Вэйресте. Нам нужно идти в
<ce> _res_ здесь, в городе. Показывай дорогу, %ra."

Message:  1012
<ce> Фития говорит:
<ce>
<ce> "Мы команда, но у нас разные роли. Я и близко
<ce> не так искусена в бою, как ты. Это твоя часть.
<ce> Я бегло говорю по-оркски и довольно хорошо умею
<ce> убеждать. Это моя роль. Если ты будешь
<ce> оберегать меня и позволишь мне поговорить с
<ce> важными людьми, мы сможем это осуществить".

Message:  1013
<ce> "Здесь так тихо", - шепчет Фития. "Приятно видеть, что он
<ce> принимает какие-то меры предосторожности, но это жутковато".

Message:  1014
<ce> Входная дверь распахивается, и в комнату врываются
<ce> несколько вооруженных воинов.
<ce>
<ce> "Предатели Императора! Мы знаем, что готовится заговор!
<ce> Умрите, как собаки, которыми вы и являетесь!"
<ce>
<ce> Фития плавно перебегает в другую часть дома, чтобы
<ce> избежать бедлама. Теперь твоя очередь.

Message:  1015
<ce> Когда этот последний враг повержен, Фития
<ce> выглядывает из-за угла и подает вам сигналы
<ce> рукой. Убедившись, что все в безопасности, она
<ce> выходит из укрытия и осматривает тела. У них нет
<ce> никаких доказательств того, кто их послал.
<ce>
<ce> Затем вы слышите шорох в доме.
<ce>
<ce> "Привет? Уфина, это ты?"
<ce>
<ce> Фития подмигивает тебе и отвечает: "Да, это я.
<ce> Плохие люди мертвы. Нам нужно срочно поговорить."
<ce>
<ce> Мужчина отвечает: "Да, мы должны. Поторопись."

Message:  1016
<ce> "Что тебя так задержало, Уфина? Люди, которые слишком много
<ce> знают, не могут выжить в этой политической атмосфере. Мне нужно
<ce> отправиться в Меневию, прежде чем они доберутся до меня."
<ce>
<ce> Фития гладит его по лбу и спокойно отвечает: "Видишь ли,
<ce> мне нужен был телохранитель. И этот %ra отлично
<ce> справляется со своей работой. Уезжай из города, но скажи
<ce> мне, что мне нужно знать об этом злом после, который
<ce> порабощает всех этих бедных детей в Бруме. Мы должны
<ce> отплатить этому негодяю за его преступления!"
<ce>
<ce> Мужчина, кажется, почти загипнотизирован ее
<ce> взглядом. К нему возвращается самообладание, и он
<ce> отвечает: "О-о, да. Ну, его зовут Агрейн Утан. Он
<ce> использует много вымышленных имен, когда
<ce> занимается бизнесом, но это настоящее. Мужчина
<ce> скрывался из-за нескольких покушений на его жизнь
<ce> за последние несколько месяцев. Я говорю, что он
<ce> заслужил это из-за этих бедных детей".

Message:  1017
<ce> "Об этом парне ходит много слухов. некоторые люди
<ce> начали собственное расследование, пытаясь
<ce> выяснить, есть ли в нем что-то такое, что они
<ce> могли бы использовать, чтобы отправить его
<ce> обратно к императору. поговорите с местными
<ce> жителями и используйте кодовое слово "_tri_".
<ce> если они мои друзья и они что-нибудь узнали об
<ce> этом шакале, они вам расскажут."
<ce>
<ce> Фития благодарит его и дарит необычайно страстный
<ce> поцелуй, и он покидает резиденцию. Затем она
<ce> поворачивается к вам.
<ce>
<ce> "Извини, но это как раз то, чего иногда требует
<ce> эта работа. Я не хотела, чтобы ты это видел. Что
<ce> ж, показывай дорогу. Пока что это твое шоу."

Message:  1018
_tri_? Тсс. Пойди посмотри в _restwo_ в ___restwo_.
<--->
Ты ищешь какую-нибудь "_tri_"? О, как бы я хотел быть таким же богатым!
<--->
Твой _tri_? Он потерян? Надеюсь, ты найдешь это.
<--->
Я не могу помочь тебе отправиться на поиски твоей потерянной вещи.
<--->
Если бы мне давали золотой за каждый _tri_, которую я проиграл за эти годы! Хах!

Message:  1019
<ce> Фития хихикает. "Я не могу сказать, правдивы ли эти
<ce> слухи об Агрейне Утане. Как бы то ни было, люди верят им
<ce> достаточно, чтобы вести нас прямо туда, куда нам нужно.
<ce> У меня были задания и посложнее."

Message:  1020
<ce> Вы входите в _restwo_ с Фитией, и она
<ce> прижимает палец к губам. Вглядываясь
<ce> в темноту, она бросает кинжал в
<ce> воздух. Он попадает в невидимого
<ce> часового, который со стоном падает.
<ce>
<ce> "Мы сказали, что бой - это твоя работа, но ты
<ce> бы не увидел этого. Кстати, его вещи - мои."
<ce> Она подмигивает. "Я никогда не говорила, что
<ce> не могу убивать, не так ли?

Message:  1021
<ce> Эта бумага просто содержит указания к месту
<ce> под названием ___dung_
<ce> в ____dung_. Небольшая заметка объясняет, что
<ce> раньше здесь хранились вещи посла, но их
<ce> перевезли, опасаясь незваных гостей.

Message:  1022
<ce> Фития дрожит. "Я подожду снаружи, большое вам спасибо.
<ce> У меня есть масса других задач, над которыми я могу
<ce> поработать на свежем воздухе. Я установлю связь между
<ce> нами, так что даже если ты сделаешь "Возврат" отсюда,
<ce> я смогу следить за тобой. Приходи ко мне на улицу,
<ce> если найдешь что-нибудь стоящее."

Message:  1023
<ce> В этой сумке вы находите обычные принадлежности
<ce> для посла, такие как чернила, используемые
<ce> пальцевых печатей, несколько чистых имперских
<ce> бланков, изготовленных специально для "_alias_",
<ce> и компас. Однако вы также находите несколько
<ce> флаконов со свежей кровью и немного пепла.

Message:  1024
<ce> "Ну, вы что-нибудь нашли?" Глаза Фития
<ce> загораются, когда вы позволяете ей изучить
<ce> содержимое сумки посла. "О, клянусь %god! Это
<ce> потрясающе! Эта свинья - вампир! Посмотрите на
<ce> эти флаконы, именно то, что вам нужно, чтобы
<ce> сохранять образ так долго! Мы собираемся
<ce> насадить его на вертел, вот именно!
<ce>
<ce> "Теперь это простой вопрос. Нам просто
<ce> нужно найти его, и я произнесу заклинание,
<ce> чтобы раскрыть его природу. Мы поджарим его
<ce> уже завтра!
<ce>
<ce> "Имя на этом бланке, должно быть, его
<ce> нынешний псевдоним. Здесь сказано, что он в
<ce> ___palace_! Браво! Давайте пошевеливаться!"

Message:  1025
Я не знаю, но какой-то _alias_ из Империи остановился на ночь во ___palace_.
<--->
_alias_? Это довольно распространенное имя. Не уверен, знаю ли я, кто это.
<--->
Почему? Это что, какая-то игра? Я не знаю, кто это.
<--->
О, вы ищете _alias_. Важная фигура, но я не знаю, где именно.

Message:  1026
<ce> "Добрый день. Чем я могу вам помочь, граждане?"
<ce>
<ce> Фития ухмыляется и драматично поднимает руку.
<ce> "Смотрите, люди Вэйреста! Узри истинную
<ce> природу этого зверя, которого вы называете
<ce> послом! Страдай от лучей солнца!"
<ce>
<ce> Она вызывает огненный шар в своей руке, который
<ce> излучает пронзительный блеск. Вы должны прикрыть
<ce> глаза, чтобы сохранить зрение. Агрейн Утан вскрикивает
<ce> от воздействия этого заклинания солнечного света и
<ce> яростным ударом отправляет ее на пол.
<ce>
<ce> "Идиоты! Смотрите, какие страдания постигают тех, кто
<ce> сражается против Его Величества Императора!"
<ce>
<ce> Быстрее, чем вы успеваете среагировать, он накладывает
<ce> заклинание на нескольких охранников, которые прибыли на
<ce> разведку. Их глаза остекленевают, и они направляют свое
<ce> оружие скорее на вас, а не на него. Что еще хуже, вы
<ce> узнаете слова заклинания "Возврат". Если вы собираетесь
<ce> выполнить свою миссию, вам лучше сделать это быстро.

Message:  1027
<ce> Агрейн Утан хихикает, растворяясь в
<ce> воздухе. Фитии нигде не видно. Попытка
<ce> покушения провалилась.

Message:  1028
<ce> Вампир булькает, безвольно падая на землю. Его
<ce> чары, наложенные на охранников, рассеиваются, и они
<ce> приближаются, чтобы расследовать его смерть.
<ce> Заклинание Фитии оставило на его лице глубокие
<ce> ожоги. В сочетании с доказательствами, которые вы
<ce> предъявили из украденной сумки, нет никаких
<ce> сомнений в истинной природе посла.
<ce>
<ce> Волшебница медленно поднимается, сначала на
<ce> колени, а затем медленно наноги. "Прости.
<ce> Мне не следовало делать этого с такого
<ce> близкого расстояния. Это мое тщеславие снова
<ce> выходит мне боком." Она слегка кашляет
<ce> кровью, но серьезных ранений у нее нет.
<ce>
<ce> "Они сказали поговорить с Гортвогом о нашей награде.
<ce> Сначала мне нужно восстановиться, так что я
<ce> отправлюсь туда, когда мне станет лучше. Ты иди
<ce> дальше и посмотри, что он приготовил для тебя.
<ce> Надеюсь, это что-то хорошее". Фития слабо улыбается,
<ce> потрясенная пережитым. Затем к ней возвращается
<ce> самообладание, и она крепко пожимает вам руку на
<ce> прощание, прежде чем отправиться в ближайшую таверну.

Message:  1030
%qdt:
 Я выполняю задание с магом из
 Темного Братства, чтобы
 остановить посла Императора в
 Вэйресте. Мой партнер направил
 меня в _res_ в Вэйрест, чтобы
 поговорить с информатором.

Message:  1031
%qdt:
 Информатор сказал нам, что мы
 могли бы получить информацию о
 после, расспросив местных
 жителей о "_tri_". У этого
 человека много врагов, и многие
 хотят, чтобы он потерпел неудачу.

Message:  1032
%qdt:
 Документы в _restwo_
 в ___restwo_ гласят,
 что вещи посла
 хранятся в ___dung_ в
 ___dung_.

-Message:  1033
-%qdt:
- Мой партнер предполагает, что посол -
- вампир, использующий псевдоним
- "_alias_". Если мы сможем поспрашивать
- вокруг и найти его, возможно, мы сможем
- противостоять ему и покончить с этим.

Message:  1033
%qdt:
 Мой партнер предполагает, что посол - вампир,
 использующий псевдоним "_alias_",
 и проживающий в ___palace_.
 Возможно, мы сможем противостоять
 ему и покончить с этим.

Message:  1034
%qdt:
 Вместе мы убили вампира и выполнили
 нашу миссию. Моя напарница сказала
 мне поговорить с Гортвогом в
 Орсиниуме о моем вознаграждении.

Message:  1035
<ce> Фития внезапно останавливается.
<ce> "Подожди, я получаю сообщение издалека".
<ce> Она изо всех сил пытается понять, а
<ce> затем кивает. - Мы и так слишком
<ce> задержались. Посол вернулся в Имперский
<ce> город. Мы не сможем этого сделать".
<ce> Почти не попрощавшись, она уходит, чтобы
<ce> выполнить какую-то инструкцию, которую
<ce> вам было запрещено слышать.

QBN:
Person _elf_ named Br'itsa
Person _alias_ faction Random_Noble male
- anyInfo 1025
Person _gort_ named Gortwog atHome
Person _inform_ faction The_Thieves_Guild male
Person _db_ faction The_Dark_Brotherhood

Place _palace_ remote palace
Place _dung_ remote dungeon
Place _res_ local house
Place _restwo_ remote house
Place _wayrest_ permanent WayrestCity
Place _orsin_ permanent OrsiniumCity

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _firsttalk_ 0:02 0:08
Clock _ffdelay_ 0:02
Clock _secondtalk_ 1:00 2:00
Clock _recall_ 0:03

Foe _warr_ is Warrior
Foe _guar_ is Knight
Foe _vamp_ is Ancient_vampire

Item _reward_ artifact Volendrung
Item _sack_ small_sack
Item _tri_ trinket anyInfo 1018
Item _paper_ letter

--	Quest start-up:
    say 1010
    pc at _wayrest_ do _begin_
    pc at _res_ do _firstfight_

_begin_ task:
    say 1011
    start timer _timer_
    log 1030 step 0
    start timer _firsttalk_
    start task _firstpic_

_firstpic_ task:
    add _elf_ face

_firsttalk_ task:
    say 1012

_firstfight_ task:
    say 1013
    start timer _ffdelay_

_nores_ task:
    when not _firstfight_
    stop timer _ffdelay_

_ffdelay_ task:
    create foe _warr_ every 0 minutes 4 times with 100% success
    say 1014
    drop _elf_ face

_ffdone_ task:
    killed 4 _warr_
    say 1015
    add _elf_ face
    place npc _inform_ at _res_

_fftalk_ task:
    clicked npc _inform_
    say 1016
    say 1017
    add dialog for item _tri_
    hide npc _inform_
    place item _paper_ at _restwo_
    pc at _restwo_ set _secres_
    start timer _secondtalk_
    log 1031 step 1

_secondtalk_ task:
    say 1019

_secres_ task:
    say 1020

_grabpaper_ task:
    clicked item _paper_
    say 1021
    reveal _dung_
    take _paper_ from pc
    place item _sack_ at _dung_
    pc at _dung_ do _dungintro_
    log 1032 step 1

_dungintro_ task:
    say 1022
    drop _elf_ face

_findsack_ task:
    clicked item _sack_
    say 1023
-    add dialog for person _alias_

_thirdtalk_ task:
    when _findsack_ and not _dungintro_
    add _elf_ face
    say 1024
    place npc _alias_ at _palace_
    log 1033 step 1

_confront_ task:
    clicked npc _alias_
    say 1026
    drop _elf_ face
    hide npc _alias_
    place foe _vamp_ at _palace_
    create foe _guar_ every 0 minutes 4 times with 100% success
    start timer _recall_
    change foe _vamp_ infighting true
    change foe _vamp_ team 19

_infight_ task:
    daily from 0:00 to 23:59
    change foe _guar_ infighting true
    clear _infight_

_recall_ task:
    remove foe _vamp_
    say 1027
    legal repute -70
    change repute with _db_ by -30
    end quest

_slain_ task:
    killed _vamp_
    stop timer _recall_
    remove foe _guar_
    say 1028
    reveal _orsin_
    log 1034 step 1

_qgclicked_ task:
    clicked npc _gort_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    end quest

_timer_ task:
    say 1035
    drop _elf_ face
    end quest