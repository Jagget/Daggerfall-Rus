Quest: JHDB106
DisplayName: Сбор средств
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Наше основное ремесло и средство существования -
<ce> это устранение проблем, где это необходимо. Время
<ce> от времени мы занимаемся другими способами
<ce> привлечения средств, когда появляется такая
<ce> возможность. Нам сообщили о беззащитном ребенке,
<ce> принадлежащем людям, не имеющим особого значения,
<ce> кроме размера их банковского счета. Мы хотим
<ce> получить часть этого счета. Интересует ли тебя
<ce> такое направление деятельности?
   <--->
<ce> Иногда Братству создают ошибочную
<ce> репутацию охотников за головами и частных
<ce> палачей. Это, конечно, один из аспектов
<ce> нашей работы, но мы не ограничиваемся
<ce> простым убийством. У меня есть просьба
<ce> спокойно перевезти ребенка из одного
<ce> города в другой. Возьмешь ли ты на себя
<ce> ответственность за это?

RefuseQuest:  [1001]
<ce> Не бойся, %pct. У нас есть масса
<ce> возможностей пролить кровь, что
<ce> явно тебе по сердцу.

AcceptQuest:  [1002]
<ce> Очень хорошо. Ребенка зовут _kid_. Вы
<ce> найдете %g2 в ___res_, внутри _res_.
<ce> Разберитесь с охраной по своему
<ce> усмотрению. Мы ожидаем, что они не
<ce> окажут большого сопротивления. Мы
<ce> определили _goal_ в ___goal_ как
<ce> подходящее место для содержания
<ce> ребенка до момента выкупа. Там тебя
<ce> будет ждать один из членов Братства.
<ce> Пусть Мефала оберегает тебя.

QuestFail:  [1003]
<ce> Ты потратил слишком много времени. План Братства по
<ce> выкупу, скорее всего, провалится на этом этапе.

QuestComplete:  [1004]
<ce> При входе в здание тебя останавливают.
<ce>
<ce> "Отличная работа, %pct. Мы благодарим тебя от
<ce> имени Ночной Матери за эту услугу, которую ты
<ce> нам оказываешь. Пойдем, _kid_. Мы приготовили
<ce> для тебя отдельную комнату. Ты будешь в
<ce> восторге от того, что увидишь".
<ce>
<ce> Ребенка уводят, и ты остаешься один.

Message:  1020
<ce> Это должно быть _res_.
<ce> Внутри должен находиться _kid_.

Message:  1021
<ce> Прежде чем _kid_ успеет произнести хоть слово,
<ce> ты заставляешь %g2 замолчать, накрываешь %g2
<ce> халатом и притягиваешь %g2 к себе.

Message:  1030
%qdt:
 Темному Братству нужно,
 чтобы я забрал ребенка из
 _res_, который находится
 в ___res_. Я должен
 доставить ребенка в
 _goal_ в ___goal_ до
 истечения =timer_ дней.

Message:  1099
<ce> Сначала в ___res_, потом в ___goal_.
<ce> Простая задача.

QBN:
Person _questgiver_ group Questor male
Person _kid_ faction Children

Place _res_ remote house
Place _goal_ remote house

Clock _timer_ 30.00:00

Foe _enemy_ is 2 Archer

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place npc _kid_ at _res_
	pc at _res_ set _ires_

_ires_ task:
	say 1020
	create foe _enemy_ every 1 minutes 1 times with 75% success

_daily_ task:
	daily from 0:00 to 23:59
	change foe _enemy_ infighting true
	clear _daily_

_kclicked_ task:
	clicked npc _kid_
	say 1021
	hide npc _kid_
	pc at _goal_ set _igol_

variable _igol_
_victory_ task:
	when _kclicked_ and _igol_
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _questgiver_
	say 1099
	clear _qgclicked_

_timer_ task:
	say 1003
	end quest