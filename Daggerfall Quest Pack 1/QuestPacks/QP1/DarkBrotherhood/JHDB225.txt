Quest: JHDB225
DisplayName: Боец сбился с пути
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Мефала следит за вашим клинком, %pct.
<ce> Ваша преданность Братству превыше всех
<ce> остальных забот. Докажите это, избавившись
<ce> от члена Гильдии Бойцов, который поселился
<ce> в одной из пещер %crn.

RefuseQuest:  [1001]
<ce> Тогда найдите какой-нибудь другой способ доказать свою 
<ce> преданность. Успейте доказать, пока не пришло и ваше время.

AcceptQuest:  [1002]
<ce> Вы отправитесь в ___dung_ и найдете бойца внутри.
<ce> Используйте любой метод, который вам больше всего подходит.
<ce> Чем очевиднее, что это была работа Братства, тем лучше для
<ce> всех нас. Не позволяйте никому безнаказанно портить нашу
<ce> репутацию. Дурак пробудет там всего =2res_ дней, поэтому
<ce> убедитесь, что к тому времени убийство будет совершено.
<ce> Сообщите о своем успехе в любое время после этого.

QuestFail:  [1003]
<ce> Цель из Гильдии Бойцов к настоящему времени сбежал 
<ce> из ___dung_.

QuestComplete:  [1004]
<ce> Некоторые в этом зале опасались, что у вас есть
<ce> привязанности на стороне. Я рад видеть обратное. 
<ce> Мы не всегда компенсируем наши контракты, но мне случайно
<ce> попался в руки этот _magic_, который может
<ce> парализовать врагов, и я подумал о вас. Пусть враги
<ce> Мефалы познают истинный ужас от ваших рук.

Message:  1021
<ce> Я так просто не сдамся!
<ce> Я член Гильдии Бойцов!
   <--->
<ce> Ха! Я покажу вам, чему меня научила гильдия!

Message:  1022
Журнал заданий:
  _fg_ из ___fg_
  поручил мне отправиться в ___dung_
  и убить всех пауков внутри него.
  Я должен вернуться в течение =random_ дней
  за своей оплатой в размере =gold_ золотых.

Message:  1031
%qdt:
 _questgiver_ поручил мне
 убить члена
 Гильдии Бойцов в ___dung_
 до того, как пройдет =2res_ дней.
 Я могу вернуться в Братство
 в ___questgiver_ в любое время после этого.

Message:  1041
<ce> Гильдия Бойцов теперь стала на одного
<ce> наемника меньше. Братство будет довольно.

QBN:
Person _questgiver_ group Questor male
Person _dummytg_ faction The_Fighters_Guild

Place _fg_ remote fighters
Place _dung_ remote dungeon6

Item _random_ gold range 12 to 22
Item _gold_ gold range 121 to 449
Item _magic_ item class 4 subclass 35
Item _letter_ letter

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00

Foe _thief_ is Warrior

--	Quest start-up:
    start timer _2res_
    start timer _failsafe_
    place foe _thief_ at _dung_
    log 1031 step 0
    reveal _dung_

_ith_ task:
    injured _thief_
    clicked foe _thief_
    say 1021
    change foe _thief_ infighting true
    give item _letter_ to _thief_
    _letter_ used do _readletter_
    cast Heal spell on _thief_
    cast Shield spell on _thief_
    cast Orc_Strength spell on _thief_
    cast Troll's_Blood spell on _thief_

_readletter_ task:
    say 1022
    take _letter_ from pc

_slain_ task:
    killed 1 _thief_ saying 1041
    change repute with _dummytg_ by -20
    stop timer _2res_

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _magic_
    end quest

_2res_ task:
    say 1003
    end quest

_failsafe_ task:
    end quest