Quest: JHDB122
DisplayName: Цирк Квилла
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Чулмор Квилл - дальний... родственник, я
<ce> бы сказал, Темного Братства. Легковес
<ce> никогда не демонстрировал умения
<ce> обращаться с оружием, но, тем не менее,
<ce> заставляет нужных людей умирать, когда
<ce> это необходимо. Мы получили от него
<ce> приглашение отправить опытного бойца на
<ce> арену смерти за значительную золотую
<ce> награду. Соглашайся или забудь.

RefuseQuest:  [1001]
<ce> Да, давайте вернемся к делу.

AcceptQuest:  [1002]
<ce> Квилл ждет в _place_, в
<ce> ___place_. Найдите его до того,
<ce> как истечет =timer_ дней. Нас не
<ce> волнует, каков будет результат,
<ce> хотя ваша неудача могла бы стать
<ce> занимательной историей.

QuestFail:  [1003]
<ce> Вы вышли за пределы поля боя. _chul_
<ce> тошнотворно смеётся вам вслед. По умолчанию вы
<ce> объявлены проигравшим.
<ce>
<ce> "Такая трусость заслуживает достойного
<ce> наказания, не так ли, мальчики? Возможно, доза
<ce> Чумы научит %pg3 не обольщаться!"
<ce>
<ce> Хрустальный шар исчезает. Ты чувствуешь какой-то неприятный запах.

QuestComplete:  [1004]
<ce> Враги перестают появляться. Наконец, хрустальный
<ce> шар начинает слегка дымиться.
<ce>
<ce> "Ну и шоу вы для нас устроили! Умное дитя. Вы
<ce> послужили нашим целям, и теперь мы будем служить
<ce> вашим. Забирай свою награду".
<ce>
<ce> Шар разлетается вдребезги со вспышкой света, и на его
<ce> месте вы находите золото.

Message:  1011
<ce> Хо-хо! Братство прислало мне нового
<ce> призывника в цирк! Полагаю, вы здесь
<ce> именно для этого? У нас с тобой нет
<ce> других важных дел. Но у меня есть для
<ce> тебя подарок, если ты хочешь подыграть.
<ce> Вы бы хотели заработать 10000 золотых?
<ce> Все, что вам нужно сделать, это выжить.
<ce> Разве это не звучит заманчиво?

Message:  1012
<ce> О, восхитительный ответ. Вы, должно быть, считаете, что вы
<ce> очень храбры, не так ли? Что ж, поживем - увидим. Я и мои
<ce> друзья хотим посмотреть, как вы выживете против нескольких
<ce> обычных домашних животных, которых мы выставим против вас.
<ce> Участвуйте в гонке к ___cult_ до истечения =2cult_ дней.
<ce> Если у вас получится, мы примем ваше предложение о
<ce> развлечении. Мы надеемся, что вы нас не подведете.

Message:  1013
<ce> О, тогда ты, должно быть, один из тех слабаков, которых
<ce> продолжает спасать Гильдия Бойцов. Очень плохо. Однажды я
<ce> встречу своего чемпиона, я уверен!

Message:  1014
<ce> Вы подходите к святилищу, но никого не находите. Возможно,
<ce> людей Чулмора еще нет здесь.
<ce>
<ce> "А, наша игрушка прибыла! Добро пожаловать, игрушка! Цирк
<ce> Квилла нуждается в особенном развлечении, и сегодняшняя
<ce> программа - это вы!"
<ce>
<ce> Голос ведущего раздается из хрустального шара, который вы
<ce> видите на земле неподалеку. Служитель храма ухмыляется,
<ce> явно участвуя в сделке.
<ce>
<ce> "Ты же не думал, что мы будем присутствовать, не так ли?
<ce> Но мы можем видеть все шоу целиком с помощью этого
<ce> хитроумного устройства. Теперь наше соглашение: сражайся
<ce> до победы, и не смей даже думать о бегстве! Держитесь
<ce> поближе к святилищу! Да начнутся игры!"

Message:  1015
<ce> Пронзительный смех Квилла пронзает воздух.
<ce>
<ce> "Почему бы нам не начать с чего-нибудь попроще? Мы
<ce> всегда получаем хорошее шоу от нежити".
<ce>
<ce> Шар испускает проекцию трех зомби, которые затем
<ce> полностью материализуются.

Message:  1016
<ce> Пронзительный смех Квилла пронзает воздух.
<ce>
<ce> "Почему бы нам не начать с чего-нибудь попроще? Мы
<ce> всегда получаем хорошее шоу от нежити".
<ce>
<ce> Шар испускает проекцию трех призраков, которые затем
<ce> полностью материализуются.

Message:  1017
<ce> Пронзительный смех Квилла пронзает воздух.
<ce>
<ce> "Почему бы нам не начать с чего-нибудь попроще? Мы
<ce> всегда получаем хорошее шоу от нежити".
<ce>
<ce> Шар испускает проекцию трех привидений, которые затем
<ce> полностью материализуются.

Message:  1018
<ce> "Ну, это было немного жестоко, не так ли?
<ce> Давайте сделаем передышку. Я слышал, атронахи
<ce> в моде в это время года".

Message:  1019
<ce> "Мои товарищи здесь советуют мне ускорить темп. Мы
<ce> бы с тобой не хотели их подвести, не так ли? Давай
<ce> на этот раз выберем что-нибудь побыстрее".

Message:  1020
<ce> "Мои друзья советуют мне послать еще немного
<ce> нежити. Кто я такой, чтобы говорить им "нет"?"

Message:  1021
<ce> "Это специально для моих друзей. Вы когда-нибудь задумывались,
<ce> каково это - сразиться с сорока летучими мышами?"

Message:  1022
<ce> "Большинство людей не ожидали бы, что у нас
<ce> будут вампиры наготове. Но я думаю, именно это
<ce> делает Кирка Квилла всемирно известным, не так
<ce> ли? Элемент неожиданности".

Message:  1023
<ce> "Я удивлен, что мы так долго ждали, прежде чем представить
<ce> проекцию даэдра. Но неизвестность начала меня утомлять".

Message:  1024
<ce> "Я решил, что выберу что-нибудь простое, чтобы покончить
<ce> с этим. Как насчет последней нежити? Он будет с тобой
<ce> помягче, я клянусь".

Message:  1030
%qdt:
 Темное братство дало мне шанс
 поучаствовать в каком-то
 мероприятии для Кирка Квилла. Я
 должен встретиться с _chul_ в
 _place_ в ___place_, чтобы услышать
 задание, за которое я возьмусь. У
 меня есть =timer_ дней, чтобы
 сделать это.

Message:  1031
%qdt:
 _chul_ пригласил меня пойти в ___cult_,
 чтобы выжить, в обмен на 10000 золотых. Я
 должен прибыть туда в течение =2cult_ дней.

Message:  1032
<ce> К настоящему времени срок действия приглашения _chul_ истек.
<ce> Конечно, это не могло быть настолько важно.

QBN:
Place _place_ local house
Place _cult_ remote cult

Clock _2cult_ 00:00 0 flag 2 range 0 2
Clock _timer_ 00:00 0 flag 1 range 0 2
Clock _delay_ 00:01
Clock _delay2_ 00:00

Person _chul_ named Chulmore_Quill

Item _reward_ gold range 10000 to 10000

Foe _z3_ is Zombie
Foe _g3_ is Ghost
Foe _w3_ is Wraith
Foe _2fatr_ is Fire_atronach
Foe _2iatr_ is Ice_atronach
Foe _2satr_ is Flesh_atronach
Foe _2natr_ is Iron_atronach
Foe _3ww_ is Werewolf
Foe _1dr_ is Dragonling_souless
Foe _1gi_ is Giant
Foe _3skel_ is Skeletal_warrior
Foe _3mumm_ is Mummy
Foe _1ghos_ is Ghost
Foe _40bats_ is Giant_bat
Foe _4vamp_ is Vampire
Foe _1avamp_ is Ancient_vampire
Foe _4daedroth_ is Daedroth
Foe _2seducer_ is Daedra_seducer
Foe _alich_ is Ancient_lich

--	Quest start-up:
    place npc _chul_ at _place_
    log 1030 step 0
    start timer _timer_

_infight_ task:
    daily from 0:00 to 23:59
    change foe _z3_ infighting true
    change foe _g3_ infighting true
    change foe _w3_ infighting true
    change foe _2fatr_ infighting true
    change foe _2iatr_ infighting true
    change foe _2satr_ infighting true
    change foe _2natr_ infighting true
    change foe _3ww_ infighting true
    change foe _1dr_ infighting true
    change foe _1gi_ infighting true
    change foe _3ww_ team 0
    change foe _3skel_ infighting true
    change foe _3mumm_ infighting true
    change foe _1ghos_ infighting true
    change foe _40bats_ infighting true
    change foe _4vamp_ infighting true
    change foe _1avamp_ infighting true
    change foe _4daedroth_ infighting true
    change foe _2seducer_ infighting true
    change foe _alich_ infighting true
    clear _infight_

_begin_ task:
    clicked npc _chul_
    prompt 1011 yes _accept_ no _reject_
    stop timer _timer_

_accept_ task:
    start timer _2cult_
    say 1012
    log 1031 step 1

_reject_ task:
    say 1013
    hide npc _chul_
    end quest

_there_ task:
    when pc enters cult
    start task _inside_

_flee_ task:
    when pc exits cult
    clear _inside_

_arrive_ task:
    when _inside_ and _accept_
    start timer _delay_

variable _inside_
_delay_ task:
    say 1014
    start timer _delay2_

_wimp_ task:
    when _delay_ and not _inside_
    say 1003
    make pc ill with Plague
    end quest

_delay2_ task:
    pick one of _a1_ _a2_ _a3_

_a1_ task:
    say 1015
    create foe _z3_ every 0 minutes 3 times with 100% success

_a2_ task:
    say 1016
    create foe _g3_ every 0 minutes 3 times with 100% success

_a3_ task:
    say 1017
    create foe _w3_ every 0 minutes 3 times with 100% success

_round1_ task:
    killed 3 _z3_
    killed 3 _g3_
    killed 3 _w3_
    say 1018
    create foe _2fatr_ every 0 minutes 2 times with 100% success
    create foe _2iatr_ every 0 minutes 2 times with 100% success
    create foe _2satr_ every 0 minutes 2 times with 100% success
    create foe _2natr_ every 0 minutes 2 times with 100% success

_atr1_ task:
    killed 2 _2fatr_

_atr2_ task:
    killed 2 _2iatr_

_atr3_ task:
    killed 2 _2satr_

_atr4_ task:
    killed 2 _2natr_

_round2_ task:
    when _atr1_ and _atr2_ and _atr3_ and _atr4_
    say 1019
    create foe _3ww_ every 0 minutes 3 times with 100% success
    create foe _1dr_ every 0 minutes 1 times with 100% success
    create foe _1gi_ every 0 minutes 1 times with 100% success

_were_ task:
    killed 3 _3ww_

_drag_ task:
    killed 1 _1dr_
    change foe _1dr_ team 0
    change foe _1gi_ team 0

_gian_ task:
    killed _1gi_

_round3_ task:
    when _were_ and _drag_ and _gian_
    say 1020
    create foe _3skel_ every 0 minutes 3 times with 100% success
    create foe _3mumm_ every 0 minutes 3 times with 100% success
    create foe _1ghos_ every 0 minutes 1 times with 100% success

_skels_ task:
    killed 3 _3skel_

_mummies_ task:
    killed 3 _3mumm_

_ghost_ task:
    killed _1ghos_

_round4_ task:
    when _skels_ and _mummies_ and _ghost_
    say 1021
    create foe _40bats_ every 0 minutes 40 times with 100% success

_round5_ task:
    killed 40 _40bats_
    say 1022
    create foe _4vamp_ every 0 minutes 4 times with 100% success
    create foe _1avamp_ every 0 minutes 4 times with 100% success

_vampires_ task:
    killed 4 _4vamp_

_avampire_ task:
    killed 1 _1avamp_

_round6_ task:
    when _vampires_ and _avampire_
    say 1023
    create foe _4daedroth_ every 0 minutes 4 times with 100% success
    create foe _2seducer_ every 0 minutes 2 times with 100% success

_daedroth_ task:
    killed 4 _4daedroth_

_seducer_ task:
    killed 2 _2seducer_

_round7_ task:
    when _daedroth_ and _seducer_
    say 1024
    create foe _alich_ every 0 minutes 1 times with 100% success
    cast Invisibility spell on _alich_

_victory_ task:
    killed _alich_
    give pc _reward_
    end quest

_2cult_ task:
    end quest

_timer_ task:
    say 1032
    end quest