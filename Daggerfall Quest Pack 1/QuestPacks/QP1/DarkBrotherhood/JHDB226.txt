Quest: JHDB226
DisplayName: Прополка Гильдии Бойцов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Похоже, Гильдия Бойцов в последнее время начала принимать 
<ce> контракты на убийство цивилизованных людей. Мы не испытываем 
<ce> никаких угрызений совести, когда они убивают гарпий и 
<ce> спригганов, но убийство - это область Мефалы, и мы уберем 
<ce> незваных гостей. Вашей наградой за убийство трех членов 
<ce> Гильдии Бойцов станет предмет, зачарованный силой левитации.

RefuseQuest:  [1001]
<ce> Похоже, ты любишь Гильдию Бойцов больше, 
<ce> чем Братство. Это меня глубоко беспокоит.

AcceptQuest:  [1002]
<ce> Способ казни не имеет значения, главное, чтобы 
<ce> после этого страх перед Братством только усилился. 
<ce> Найдите их в _res_, на %di. У вас есть 
<ce> два дня, чтобы предоставить мне историю об их убийстве.

QuestFail:  [1003]
<ce> Все трое еще живы, %pct.

QuestComplete:  [1004]
<ce> Хмм. После смерти этих варваров в городе стало немного 
<ce> спокойнее. Говорят, Мефала наслаждается тишиной. Что касается 
<ce> тебя, возьми свой _reward_, который позволит тебе парить, как 
<ce> ястреб. Пусть это поможет тебе в твоем служении Матери Ночи.

Message:  1010
<ce> Это тот самый дом. Теперь вам нужно найти 
<ce> членов Гильдии Бойцов внутри.

Message:  1011
<ce> "Т-Т-Темное Братство! Эй, вы все!
<ce> Проснитесь! У нас на носу драка!!"

Message:  1012
<ce> Враги Братства мертвы.

Message:  1030
%qdt:
 Темное Братство ___questgiver_
 даст мне предмет, наделенный способностью к
 левитации, если я убью трех членов
 Гильдии Бойцов, которые совершили
 несанкционированное убийство. Они остановились
 в _res_ в том же городе. Я должен
 сообщить о своем успехе в течение =timer_ дней.

QBN:
Person _questgiver_ group Questor male
Person _fgperson_ faction The_Fighters_Guild remote

Place _res_ local house

Clock _timer_ 2.00:00

Item _reward_ item class 4 subclass 10

Foe _enemy_ is Barbarian
Foe _dae1_ is Warrior
Foe _dae2_ is Archer

--	Quest start-up:
    start timer _timer_
    place npc _fgperson_ at _res_
    log 1030 step 0
    pc at _res_ do _inside_

_inside_ task:
    say 1010
    place npc _fgperson_ at _res_

_talk_ task:
    clicked npc _fgperson_
    say 1011
    place foe _enemy_ at _res_
    create foe _dae1_ every 0 minutes 1 times with 100% success
    create foe _dae2_ every 0 minutes 1 times with 100% success
    hide npc _fgperson_

_kill1_ task:
    killed _enemy_
    change repute with _fgperson_ by -10

_kill2_ task:
    killed _dae1_
    change repute with _fgperson_ by -10

_kill3_ task:
    killed _dae2_
    change repute with _fgperson_ by -10

_victory_ task:
    when _kill1_ and _kill2_ and _kill3_
    say 1012

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    say 1003
    clear _qgclicked_ _clearclick_
    
_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    start quest JHDBC01
    end quest

_timer_ task:
    end quest