Quest: JHDB120
DisplayName: Раздор в Гильдии Магов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Недавно мы получили запрос от Гильдии Магов об
<ce> устранении одного из их членов. Довольно
<ce> официально выглядящий. Я полагаю, вам не
<ce> повредит улучшить свое положение среди Магов,
<ce> согласившись на эту работу, не так ли, %pct?

RefuseQuest:  [1001]
<ce> Ты можешь быть предан другим, %ra.
<ce> Мы не будем ревновать.

AcceptQuest:  [1002]
<ce> Хорошо. Сначала отправляйтесь в зал гильдии в
<ce> ___guild_. Они объяснят, куда идти. Нас не
<ce> особенно волнует, почему дурак должен
<ce> умереть; Братство просто счастливо быть
<ce> инструментом, и вам выпала честь исполнить
<ce> задачу. Они потребовали, чтобы это дело было
<ce> завершено в течение =timer_ дней. Убедитесь,
<ce> что к этому времени цель перестала дышать,
<ce> кто бы это ни был. Имя вашего контактного
<ce> лица в гильдии - _mage_. Прощайте.

QuestFail:  [1003]
<ce> Вы заблудились по дороге в ___guild_, %pct?

QuestComplete:  [1004]
<ce> С этим делом покончено. Магам из ___guild_
<ce> больше не нужно бояться изгоя,
<ce> призывающего даэдра.

Message:  1010
<ce> Добрый день. Ах, вы будете нашим, гм, специалистом!
<ce> Для этого единственного трудного дела! Пожалуйста,
<ce> давайте говорить потише.
<ce>
<ce> Мы хотели бы рассказать вам о проблеме, которая нуждается
<ce> в решении, и это довольно срочно. Призыв даэдра - задача,
<ce> сопряженная с большими опасностями, и ее не следует
<ce> пропагандировать каждому ребенку, желающему поговорить с
<ce> потусторонними силами. Мы, члены Гильдии Магов, поступаем
<ce> правильно, предоставляя эту власть только ответственным.
<ce> Тем не менее, мы иногда позволяем тому или иному
<ce> ускользнуть от нас, как это произошло сейчас.
<ce>
<ce> Наш твердолобый ученик укрылся в норе,
<ce> ___dung_, надеясь продолжить погоню за
<ce> даэдра там, внизу. Где они этому искусству
<ce> научились, мы не знаем, ибо простого
<ce> подмастерья в наших залах никогда такому не
<ce> учат. Пожалуйста, покончите с этой угрозой,
<ce> чтобы нам всем было спокойнее.

Message:  1011
<ce> Лицо этого врага бледнеет. Раздается несколько
<ce> возгласов на непонятном языке, и одна рука лезет в
<ce> рюкзак за каким-то пергаментом. Он быстро начинает
<ce> разрушаться из-за наложенных на него чар.

Message:  1012
<ce> Этот пергамент перестал распадаться со смертью
<ce> мага-изгоя, но в своем нынешнем состоянии он,
<ce> скорее всего, продержится не больше суток. Если вы
<ce> и собираетесь извлечь из него какую-то пользу, то
<ce> только путем запоминания его содержимого.

Message:  1013
Тысяча благодарностей тебе, дитя. Полагаться на кого-то из
цивилизованного мира, как правило, приводит к беспорядкам
среди нашего рода, но ты доказал, что принадлежишь к
исключительной породе. Твое имя среди нас заслуженно.
<ce>
Мы считаем нужным предоставить тебе прямое посещение
нашего сестричества. Нам больше не нужно тратить время на
посредников. Если ты посетишь нас в нашем шабаше, ты
будешь как один из нас. Своими глазами узри Горящего
Мученика Кинарет в районе Даггерфолла. Приложи заходящее
солнце к своему правому уху и иди. Ты скоро найдешь наш
алтарь и получишь радостный прием, если не свернешь со
своего пути. Ищите нас на плато с видом на море.

Message:  1030
%qdt:
 Темное Братство ___questgiver_
 поручило мне поговорить с
 _mage_ из Гильдии Магов
 в ___guild_, который
 расскажет мне о деле, которое должено
 быть улажено в течение =timer_ дней.

Message:  1031
%qdt:
 Гильдия Магов, в свою очередь, отправила
 меня в ___dung_, чтобы избавиться
 от кого-то, кто слишком сильно заинтересован
 в вызове даэдра.

QBN:
Person _questgiver_ group Questor male
Person _mage_ faction The_Thieves_Guild

Place _guild_ remote magery
Place _dung_ remote dungeon7

Clock _timer_ 00:00 0 flag 1 range 0 2
Clock _1day_ 1.00:00

Foe _enemy_ is Battle_mage
Foe _dae1_ is Fire_daedra
Foe _dae2_ is Frost_daedra

Item _parch_ letter used 1013

--	Quest start-up:
    start timer _timer_
    place npc _mage_ at _guild_
    log 1030 step 0

_talk_ task:
    clicked npc _mage_
    say 1010
    reveal _dung_
    log 1031 step 1
    place foe _enemy_ at _dung_

_inj_ task:
    injured _enemy_
    clicked foe _enemy_
    say 1011
    create foe _dae1_ every 0 minutes 1 times with 100% success
    create foe _dae2_ every 0 minutes 1 times with 100% success
    change foe _dae1_ infighting true
    change foe _dae2_ infighting true
    give item _parch_ to _enemy_
    have _parch_ set _warn_
    change foe _enemy_ infighting true
    change foe _dae1_ team 0
    change foe _dae2_ team 0
    change foe _enemy_ team 0

_slay_ task:
    killed _enemy_
    give pc nothing
    remove log step 0
    remove log step 1
    start timer _1day_
    stop timer _timer_
    change repute with _mage_ by +5

_warn_ task:
    say 1012

_1day_ task:
    end quest

_timer_ task:
    end quest