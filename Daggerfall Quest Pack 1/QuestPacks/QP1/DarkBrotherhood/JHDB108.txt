Quest: JHDB108
DisplayName: В тонком воздухе
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Такие знатоки ремесла зачистки, как мы, время 
<ce> от времени должны принимать особые меры для
<ce> обеспечения анонимности, чтобы в начале новой недели мы 
<ce> не обнаружили, что зал нашей гильдии превратился в пепел.
<ce> У нас есть одно такое дело, которое
<ce> нельзя доверить новичку. Вам выпала честь
<ce> выполнить это деликатное задание. Примите ее.
   <--->
<ce> Каждый член Темного Братства должен испытывать глубокое
<ce> чувство гордости за ту работу, которую мы выполняем.
<ce> Все, кто состоит в Братстве, должны постоянно работать
<ce> над тем, чтобы стать лучшими убийцами во всем Тамриэле.
<ce> С этой целью мы должны провести несколько сложный
<ce> расчет, и вы были выбраны, чтобы довести его до конца.

RefuseQuest:  [1001]
<ce> Чего вы надеетесь добиться?
   <--->
<ce> Ах, верно, мне нужен кто-то,
<ce> кто не трус. Я подожду еще.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___dngn_. Мы договорились о том, что наш
<ce> дорогой аристократ будет перенесен в его глубины
<ce> через =dungeontimer_ дней. Заклинание "Разрыв волшебника"
<ce> будет сопровождать телепортацию, чтобы предотвратить
<ce> заклинание "Возврат", но оно развеется при обычном отдыхе.
<ce> На завершение работы отводите не более семи часов. У нас
<ce> есть =rwrd_ золотых, которые мы отдадим вам, когда дело
<ce> будет завершено. А теперь идите. Во имя нашего блага
<ce> вы не должны потерпеть неудачу в этом деле.

QuestFail:  [1003]
<ce> У вас закончилось время. Темное Братство будет нести
<ce> ответственность за вашу неудачу.

QuestComplete:  [1004]
<ce> Ну что, вам понравилось, %pcf? Очень надеюсь, что да.
<ce> Забирайте свою награду.

Message:  1020
<ce> Вы совершили идеальное преступление. Пройдут годы,
<ce> прежде чем тело вообще будет опознано. А теперь
<ce> вернемся к _questgiver_.

Message:  1021
<ce> Помеченный Темным Братством уже
<ce> должен был прибыть в ___dngn_.
<ce> Нельзя терять время.

Message:  1022
<ce> Зная, что конец близок, ваш клиент впадает
<ce> в истерику предсмертных переговоров.
<ce>
<ce> "Пожалуйста! Пожалуйста! Остановитесь, я знаю %t! Если вы
<ce> только оставите меня в живых, я возвеличу ваше имя в %crn!
<ce> Я помогу вам восстановить всю испорченную репутацию,
<ce> которую вы здесь имеете! Пожалуйста, просто пощадите меня!"
   <--->
<ce> Зная, что конец близок, ваш клиент впадает
<ce> в истерику предсмертных переговоров.
<ce>
<ce> "Нет! Нет, умоляю вас, остановитесь! Послушайте, я могу 
<ce> вам помочь! У наемного убийцы наверняка запятнанная 
<ce> репутация в %crn, и я могу помочь вам ее восстановить! 
<ce> Просто оставьте меня в живых, и я клянусь жизнью %t, 
<ce> что дам вам влияние, которого вы раньше не знали!"

Message:  1023
<ce> Похоже, что вы достигли соглашения. Ваш новый друг
<ce> благодарит вас за доброту и клянется неустанно
<ce> трудиться, чтобы дать вам новое имя в %crn.
<ce> Вы предполагаете, что обещание будет выполнено, 
<ce> так как повторить этот сценарий будет несложно.

Message:  1030
%qdt:
 _questgiver_ из Темного Братства,
 ___questgiver_, отправил меня 
 в ___dngn_, чтобы я ожидал прибытия
 некоего аристократа в течение
 =dungeontimer_ дней. Я должен
 закончить работу до того, как будет
 наложено заклинание "Возврат". 
 Мне заплатят =rwrd_ золотых.

QBN:
Item _rwrd_ gold range 1201 to 1399

Person _questgiver_ group Questor male

Place _dngn_ remote dungeon9

Clock _dungeontimer_ 00:00 0 flag 1 range 0 2
Clock _killtimer_ 07:15 09:00
Clock _return_ 180.00:00

Foe _enemy1_ is Ranger
Foe _enemy2_ is Bard
Foe _enemy3_ is Knight

--	Quest start-up:
	start timer _dungeontimer_
	log 1030 step 0
	pick one of _E.01_ _E.02_ _E.03_
	reveal _dngn_
	pick one of _m1_ _m2_

variable _E.01_
variable _E.02_
variable _E.03_
variable _m1_
variable _m2_

_dungeontimer_ task:
	say 1021
	start timer _killtimer_

_spawn1_ task:
	when _E.01_ and _dungeontimer_
	place foe _enemy1_ at _dngn_

_spawn2_ task:
	when _E.02_ and _dungeontimer_
	place foe _enemy2_ at _dngn_

_spawn3_ task:
	when _E.03_ and _dungeontimer_
	place foe _enemy3_ at _dngn_

_inj_ task:
	injured _enemy1_
	injured _enemy2_
	injured _enemy3_
	clicked foe _enemy1_
	clicked foe _enemy2_
	clicked foe _enemy3_
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	change foe _enemy3_ infighting true

_prom_ task:
	when _inj_ and _m2_
	prompt 1022 yes _mercy_ no _normal_

_mercy_ task:
	say 1023
	remove foe _enemy1_
	remove foe _enemy2_
	remove foe _enemy3_

variable _normal_
_slain_ task:
	killed 1 _enemy1_ saying 1020
	killed 1 _enemy2_ saying 1020
	killed 1 _enemy3_ saying 1020
	stop timer _killtimer_
	start timer _return_

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _rwrd_
	end quest

_gainrep_ task:
	when _killtimer_ and _mercy_ and not _slain_
	legal repute +20

_killtimer_ task:
	say 1003
	change repute with _questgiver_ by -13
	end quest

_return_ task:
	end quest