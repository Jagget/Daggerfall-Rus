Quest: JHDB110
DisplayName: Похищение и выкуп
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вам что-нибудь говорит имя _kid_,
<ce> %pcf? Он является одной из наиболее известных
<ce> целей похищения в %crn. У нас есть сообщение
<ce> о заговоре, проводимом какими-то любителями
<ce> и мы желаем перехватить цель.
<ce> Будешь ли ты рукой Мефалы в похищении этого ребенка?

RefuseQuest:  [1001]
<ce> Пустяковое дело. У нам много другой работы.

AcceptQuest:  [1002]
<ce> Мы знаем, что _kid_ будет доставлен в ___place_
<ce> через =2place_ дней. Загляни в _place_.
<ce> Похитители намерены провести не более
<ce> суток в здании, так что вы обязаны
<ce> успеть вовремя. Как только вы завершите передачу,
<ce> приведите ребенка в _ransom_ что в
<ce> ___ransom_. А теперь иди.

QuestFail:  [1003]
<ce> Ты опоздал. _kid_ к настоящему
<ce> времени уже покинул ___place_.

QuestComplete:  [1004]
<ce> Как только вы входите в здание, член
<ce> Братства не даёт тебе заговорить.
<ce>
<ce> "Отличная работа. Мы возьмем на себя бремя
<ce> благополучия ребенка. Пойдем, _kid_.
<ce> Нам многое предстоит сделать".
<ce>
<ce> Они выводят _kid_ из здания.
<ce> Ваша работа выполнена.

Message:  1015
<ce> _kid_ теперь будет находиться на _place_.

Message:  1019
<ce> (ПРОВЕРКА СКРЫТНОСТИ ПРОЙДЕНА)
<ce>
<ce> Ты прокрадываешься в _place_
<ce> совершенно бесшумно. Теперь внезапная атака.

Message:  1020
<ce> (ПРОВЕРКА СКРЫТНОСТИ ПРОВАЛЕНА)
<ce>
<ce> Вы входите в _place_, где
<ce> они держат _kid_.

Message:  1021
<ce> Ты затыкаешь рот _kid_ и тянешь %g4 за собой.

Message:  1030
%qdt:
 Темное Братство послало
 меня прервать заговор другой группы
 с целью получения выкупа. Я найду _kid_ в
 _place_ в ___place_
 за =2place_ дней и уведу
 %g4 в _ransom_
 в ___ransom_. Ребенок будет
 находиться в ___place_ только один день.
 Я не должен опоздать.

Message:  1040
<ce> Как это возможно?!
   <--->
<ce> Этого не может быть!

QBN:
Person _kid_ faction Children

Place _place_ remote house
Place _ransom_ remote house

Clock _2place_ 00:00 0 flag 1 range 0 2
Clock _steal_ 1.00:00
Clock _timer_ 90.00:00

Foe _enemy1_ is Acrobat
Foe _enemy2_ is 2 Rogue

--	Quest start-up:
	start timer _timer_
	start timer _2place_
	log 1030 step 0
	pc at _ransom_ set _igol_
	pc at _place_ set _ires_

_2place_ task:
	say 1015
	start timer _steal_
	place npc _kid_ at _place_
	place foe _enemy1_ at _place_
	change foe _enemy1_ infighting true

variable _ires_
_sneak_ task:
	when skill Stealth is at least 70

_safeentry_ task:
	when _ires_ and _sneak_ and _2place_
	say 1019

_nosafe_ task:
	when _ires_ and not _sneak_ and _2place_
	say 1020
	create foe _enemy2_ every 600 minutes indefinitely with 75% success

_daily_ task:
	daily from 0:00 to 23:59
	change foe _enemy2_ infighting true
	clear _daily_

_dam_ task:
	injured _enemy1_
	say 1040

_kclicked_ task:
	clicked npc _kid_
	hide npc _kid_
	say 1021
	stop timer _steal_

variable _igol_
_victory_ task:
	when _kclicked_ and _igol_
	give pc nothing
	end quest

_steal_ task:
	say 1003
	end quest

_timer_ task:
	end quest