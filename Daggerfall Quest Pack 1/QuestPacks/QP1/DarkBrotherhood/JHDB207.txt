Quest: JHDB207
DisplayName: Телохранители запрещены
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Мы редко задаем вопросы о том, почему нам
<ce> поручают те или иные задачи, тем более когда
<ce> за них хорошо платят. Я ничего не скажу вам 
<ce> об этой цели, кроме того, что этой цели пришлось
<ce> нанять телохранителя. Покажите бедному дураку 
<ce> в ___res_, что мы думаем о телохранителях.
   <--->
<ce> Бывают времена, когда мы находим удовлетворение 
<ce> в том, чтобы просто доказать свое превосходство
<ce> над всеми в ____res_. Какое имя внушает
<ce> гражданам хотя бы половину такого страха, как
<ce> Темное Братство? Однако, чтобы гарантировать,
<ce> что мы останемся чем-то большим, чем просто
<ce> именем, мы должны одолеть тех, кто хочет
<ce> помешать нам. Отправляйтесь в ___res_ и убейте
<ce> дурака, который нанял телохранителя.

RefuseQuest:  [1001]
<ce> Возможно, вам стоит поработать в Гильдии Бойцов,
<ce> если вы не любите пачкать руки.

AcceptQuest:  [1002]
<ce> Телохранитель нас мало волнует. Возможно,
<ce> сокрушение его эго принесло бы вам больше
<ce> удовлетворения, чем его убийство. Контракт
<ce> только для цели. Этот трагический дуэт
<ce> остановится в _res_ в ___res_. Вернитесь
<ce> сюда через =timer_ дней. Да пребудет 
<ce> с вами Мефала, %pct.

QuestFail:  [1003]
<ce> Темное Братство ожидало вас 
<ce> в ___questgiver_ к этому времени.

QuestComplete:  [1004]
<ce> Молодец, %pct. И что вы сделали 
<ce> с некомпетентным стражем? Подходящее
<ce> решение. Ночная Мать будет довольна.

Message:  1019
<ce> Вы входите в _res_,
<ce> высматривая этого так называемого телохранителя.

Message:  1020
<ce> Акробат терпит поражение.
<ce> Контракт выполнен.

Message:  1025
<ce> Вы входите в _res_, и к вам обращается голос,
<ce> доносящийся из ниоткуда.
<ce>
<ce> "%ra, прекрати. Я вижу, что ты делаешь.
<ce> Тот, кого ты ищешь, находится под моей защитой.
<ce> Продолжение твоей бесплодной миссии вызовет
<ce> мой гнев. Не недооценивай меня".
   <--->
<ce> Вы входите в _res_, и к вам обращается голос,
<ce> доносящийся из ниоткуда.
<ce>
<ce> "Прекрати. Тот, кого ты ищешь, - мой
<ce> подопечный, и я тридцатикратно отомщу за любой
<ce> причиненный тобой вред. Повернись и уходи
<ce> сейчас же, чтобы не столкнуться с моим гневом".

Message:  1026
<ce> Возвращается голос, который вы слышали ранее.
<ce>
<ce> "Ты не можешь понять, что ты натворил.
<ce> Твоя жизнь под угрозой. Пострадай за свой поступок".
   <--->
<ce> Возвращается голос, который вы слышали ранее.
<ce>
<ce> "Твой конец настал. Ничто не может искупить
<ce> совершенное тобой преступление. Ты умрешь, червяк".

Message:  1030
%qdt:
 Темное Братство отправило
 меня из ___questgiver_ 
 в ___res_ искать
 дурака и телохранителя, которые прячутся
 в _res_. Я должен, по крайней
 мере, убить дурака и вернуться в течение
 =timer_ дней.

QBN:

Person _questgiver_ group Questor male

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:01 00:02

Foe _acro_ is Acrobat
Foe _bguard_ is Ranger

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _acro_ at _res_
	pc at _res_ set _inside_
	pick one of _e1_ _e2_
	change foe _acro_ infighting true

variable _e1_
variable _e2_
variable _inside_
_spawn_ task:
	when _inside_ and _e1_
	say 1019
	create foe _bguard_ every 0 minutes 1 times with 100% success

_infight_ task:
	daily from 0:00 to 23:59
	change foe _bguard_ infighting true
	clear _infight_

_extend_ task:
	when _inside_ and _e2_
	say 1025

_slain_ task:
	killed 1 _acro_ saying 1020

_extend2_ task:
	when _e2_ and _slain_
	start timer _delay_

_delay_ task:
	say 1026

_qgclicked_ task:
	clicked npc _questgiver_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	end quest

_timer_ task:
	say 1003
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_