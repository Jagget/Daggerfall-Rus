Quest: JHDB113
DisplayName: К волкам
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У одного из наших постоянных клиентов есть своеобразное
<ce> желание избавиться от одного из наиболее отъявленных
<ce> негодяев %crn в довольно драматичной манере.
<ce> Нам нужен тот, кто возьмет заложника в удаленную
<ce> пещеру, чтобы его съели заживо. Способны ли вы на это?

RefuseQuest:  [1001]
<ce> Ах, вы пытаетесь избежать ужасной работы. Понимаю.

AcceptQuest:  [1002]
<ce> В ___res_ аристократ по имени _hos_
<ce> живет в _res_. Перевезите %g4 оттуда 
<ce> в ___dung_. Оказавшись внутри, позвоните в _bell_
<ce> и отойдите назад. Все остальное разрешится само собой.
<ce> Я просто прошу вас принести какие-нибудь доказательства
<ce> смерти дурака, чтобы предоставить нашему клиенту.
<ce> Сделайте это за =timer_ дней.

QuestFail:  [1003]
<ce> _qgiver_ попросил вас к настоящему времени
<ce> закончить контракт на кормление.

QuestComplete:  [1004]
<ce> Довольно эффективно, не правда ли, %pct? Я даже
<ce> почти захотел забыть, каково это - чувствовать,
<ce> как лезвие пронзает человеческую плоть все глубже,
<ce> до тех пор, пока цель не перестанет двигаться. 
<ce> Ах, эти радости нашей работы. Клиент специально оговорил,
<ce> чтобы нашему агенту был выплачен платеж. Возьмите это.

Message:  1010
<ce> _hos_ не смог произнести ни слова,
<ce> как вы уже заткнули %g2 рот. Вы начинаете
<ce> выводить своего полупослушного заложника из здания.

Message:  1011
<ce> Вы звоните в колокольчик и отступаете назад, толкая _hos_
<ce> на землю. Со связанными руками %g1 может только наблюдать
<ce> за парой приближающихся тигров. "Утилизация" тела занимает
<ce> меньше минуты. Будьте осторожны и отхватите один 
<ce> из пальцев жертвы до того, как животные все сожрут.

Message:  1012
<ce> Как назло, животные не насытились.
<ce> Возможно, их очень давно не кормили.
<ce> Похоже, они не испытывают никаких угрызений совести
<ce> и смотрят на вас как на еще одну порцию еды.

Message:  1030
%qdt:
 Темное Братство из ___qgiver_
 поручило мне отвести некоего
 _hos_ из __res_, живущего
 в _res_, в пещеру под названием
 ___dung_, где дрессированные животные
 избавятся от %g4, как только я позвоню в колокольчик.
 Я должен принести доказательства
 смерти до того, как истечет =timer_ дней.

QBN:
Person _qgiver_ group Questor male
Person _hos_ faction Dancers

Place _res_ remote house
Place _dung_ remote dungeon6

Item _bell_ bell
Item _reward_ gold
Item _finger_ finger

Foe _tig_ is Sabretooth_tiger

Clock _timer_ 00:00 0 flag 1 range 0 2

--	Quest start-up:
    start timer _timer_
    pc at _dung_ do _inside_
    log 1030 step 0
    reveal _dung_
    place npc _hos_ at _res_
    get item _bell_
    pick one of _e1_ _e2_

variable _e1_
variable _e2_
variable _inside_
_take_ task:
    clicked npc _hos_
    say 1010
    hide npc _hos_
    add _hos_ face

_enable_ task:
    when _inside_ and _take_
    _bell_ used do _slain_

_slain_ task:
    drop _hos_ face
    say 1011
    get item _finger_

_attack_ task:
    when _slain_ and _e2_
    say 1012
    create foe _tig_ every 0 minutes 2 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _tig_ infighting true
    clear _infight_

_pcreward_ task:
    toting _finger_ and _qgiver_ clicked
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest