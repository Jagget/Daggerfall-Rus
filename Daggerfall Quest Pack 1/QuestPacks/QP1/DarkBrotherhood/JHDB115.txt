Quest: JHDB115
DisplayName: Самооборона
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Небольшая группа так называемых "охотников" за Братством
<ce> поселилась в отдаленной пещере в %crn.
<ce> Нам нужен кто-то опытный, чтобы телепортироваться
<ce> в их логово и уничтожить там всех.
<ce> Вы сможете с этим справиться?

RefuseQuest:  [1001]
<ce> Возможно, вы ищете один из тех ненасильственных
<ce> контрактов, которых не существует.

AcceptQuest:  [1002]
<ce> Превосходно. Возьмите этот _gem_. Его чары
<ce> отправят вас в пещеру. Это место охраняется достаточно
<ce> хорошо, любая попытка входа или выхода, кроме телепорта,
<ce> будет замечена. Поэтому, как только вы окажетесь внутри,
<ce> не думайте уходить, если только вы не хотите сообщить
<ce> о своей неудаче. Драгоценный камень потеряет свою силу
<ce> примерно через 6 часов, что позволит вам подготовиться.
<ce> Принесите мне новости об успехе, только когда вы будете
<ce> уверены, что этой группы мстителей больше нет. Пусть 
<ce> Ночная Мать благословит вашу работу, %pct.

QuestFail:  [1003]
<ce> Магическая сущность драгоценного камня улетучилась.
<ce> Теперь у вас нет возможности найти группу мстителей.

QuestComplete:  [1004]
<ce> Молодец. Теперь %t будет думать дважды, прежде чем
<ce> попытается повторить подобную уловку. 
<ce> Отличная работа, %pcf.
   <--->
<ce> Смешно, что %t задумали этот план, 
<ce> чтобы заменить нас на что-то более подконтрольное.
<ce> В будущем у нас будет больше таких групп для охоты.

Message:  1010
<ce> Как только вы активируете _gem_, его магическая сила
<ce> раздувается и начинает интенсивно вращаться.
<ce> Ослепительный свет заставляет вас закрыть глаза.

Message:  1011
<ce> Самоцвет разлетелся на куски под действием магической силы.
<ce> Группа охотников за Братством где-то рядом.
<ce>
<ce> (Подсказка: вы можете оказаться в месте, из которого
<ce> невозможно выбраться, например, на дне желоба. Если так,
<ce> то сделайте сохранение и попробуйте выполнить консольную
<ce> команду "teleport", чтобы выбраться.)

Message:  1012
<ce> Ваша выжидательная позиция принесла свои плоды.
<ce> Прибыла группа, готовая к бою.

Message:  1013
<ce> Похоже, что охотников больше не осталось.
<ce> Теперь нужно доложить _qgiver_.

Message:  1014
<ce> Покидая ___dung_, вы сбили защиту, установленную
<ce> охотничьей группой. Невозможно будет найти их снова.

Message:  1015
<ce> Вы заметили приближающуюся громадную магическую 
<ce> конструкцию. Тяжелым металлическим голосом оно заявляет
<ce> о своих намерениях.
<ce>
<ce> "Смерть нарушителям. Мастера должны выжить".
<ce>
<ce> У этой банды, похоже, есть система защиты
<ce> ___dung_ для выслеживания незваных гостей.
   <--->
<ce> Вы замечаете, что к вам приближается громоздкая магическая
<ce> конструкция. Тяжелым металлическим голосом оно заявляет
<ce> о своих намерениях.
<ce>
<ce> "Вторженец. Умри за свое вероломство".
<ce>
<ce> У этой банды, похоже, есть система защиты
<ce> ___dung_ для выслеживания незваных гостей.

Message:  1030
%qdt:
 Темное Братство ___qgiver_
 дало мне _gem_, который отправит
 меня в подземелье, убежище для группы
 охотников за Братством.
 Я не должен покидать пещеру до тех пор, пока
 пока они не будут убиты.

QBN:
Person _qgiver_ group Questor male

Item _gem_ gem

Place _dung_ remote dungeon2

Clock _gtimer_ 6:00 7:00
Clock _spawn_ 6:00 12:00
Clock _defense_ 1:00 2:00

Foe _e1_ is Knight
Foe _e2_ is Iron_atronach
Foe _e3_ is Iron_atronach

--	Quest start-up:
    start timer _gtimer_
    log 1030 step 0
    get item _gem_
    _gem_ used do _tele_
    pc at _dung_ set _inside_
    pick one of _e1_ _e2_

variable _e1_
variable _e2_
_gtimer_ task:
    say 1003
    end quest

_tele_ task:
    stop timer _gtimer_
    say 1010
    teleport pc to _dung_
    say 1011
    take _gem_ from pc
    start timer _spawn_

variable _inside_
_dspawn_ task:
    when _inside_ and _e2_
    start timer _defense_

variable _defense_
_atr_ task:
    when _defense_ and _inside_
    say 1015
    create foe _e2_ every 0 minutes 1 times with 100% success
    create foe _e3_ every 60 minutes indefinitely with 50% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _e1_ infighting true
    change foe _e2_ infighting true
    change foe _e3_ infighting true
    change foe _e1_ team 0
    change foe _e2_ team 0
    change foe _e3_ team 0
    clear _infight_

_fail_ task:
    when _tele_ and not _inside_ and not _slain_
    say 1014
    end quest

_spawn_ task:
    create foe _e1_ every 0 minutes 3 times with 80% success msg 1012

_inj_ task:
    injured _e1_

_slain_ task:
    killed 3 _e1_
    say 1013

_pcgetsgold_ task:
    when _slain_ and _qgclicked_
    give pc nothing
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_