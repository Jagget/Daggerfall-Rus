Quest: JHMG119
DisplayName: Интервью с некромантом
-- Message panels
--  Для перевода:
--  _qgiver_ - муж.
--  _nec_ - муж.
QRC:

QuestorOffer:  [1000]
<ce> %pcf, вы заслужили доверие Архимага
<ce> и поэтому посвящены в некоторые деликатные
<ce> вопросы. В соответствии с законом Гильдия
<ce> Магов обязана искать и устранять любое
<ce> влияние некромантов в Илиакском заливе,
<ce> особенно среди наших собственных членов.
<ce> Тем не менее эта задача не всегда так
<ce> проста, и иногда нам требуются тонкие
<ce> и умные специалисты по искусству слова,
<ce> чтобы выведать правду, если это
<ce> необходимо. Настолько ли вы искусны
<ce> и готовы раскрыть возможного некроманта?

RefuseQuest:  [1001]
<ce> Хмф. Неужели доверие для вас ничего не значит?

AcceptQuest:  [1002]
<ce> Что ж, тогда ваше задание приведет вас в _nhome_.
<ce> Поговорите с _nec_ и скажите все, что угодно, чтобы
<ce> узнать правду. Я предполагаю, что %g1 уходит туда
<ce> на ночь, так что отправляйтесь в позднее время. Уверен,
<ce> что ваши знания о магии, полученные к этому времени,
<ce> помогут вам создать надлежащий контекст. Вернитесь
<ce> сюда с полученными знаниями, а остальное предоставьте
<ce> нам. На это у вас есть один день.

QuestFail:  [1003]
<ce> К настоящему времени Гильдия Магов попросила вас
<ce> сообщить, занимается ли _nec_ некромантией.

QuestComplete:  [1004]
<ce> _qgiver_ вручает вам вознаграждение с любезной улыбкой.

RumorsPostSuccess:  [1007]
Я очень счастлив, что этот некромант мертв. ____nhome_ делает свою работу.
   <--->
Некромант из ___nhome_ был убит!

Message:  1028
<ce> Да? Я очень занят. Надеюсь, это важно.

Message:  1029
<ce> Итак, вы говорите, что заинтересованы во власти, выходящей
<ce> за рамки того, что может предложить Гильдия Магов.
<ce> Это вдохновляющий и в то же время опасный ход мыслей.
<ce> Вы действительно верите, что существует власть, способная
<ce> соперничать или даже превосходить ту, которой обладает
<ce> Совет Архимагов?

Message:  1030
%qdt:
 _qgiver_ из Гильдии Магов
 отправил меня на встречу с _nec_,
 чтобы определить, имеет ли %g1 связи
 с Некромантами. Я должен отправиться
 ночью в _nhome_
 и вернуться в течение 24 часов.

Message:  1031
<ce> Признаюсь, что в прошлом я задумывался о подобных
<ce> вещах. Несомненно, в мире существует больше
<ce> магии, чем та, которую можно найти в шести
<ce> школах, преподаваемых Гильдией, вы согласны?
   <--->
<ce> Я и сам так считаю. И я пришел к мысли,
<ce> что в мире существует больше магии, чем в одних только
<ce> шести школах, регулируемых имперским
<ce> правительством. По крайней мере, мне это кажется
<ce> логичным. Согласны ли вы, что есть
<ce> чему поучиться помимо того, чему учат формально?

Message:  1032
<ce> Действительно, цензура магии - это то, что беспокоит меня
<ce> и других уже довольно долгое время. Я сам считаю, что
<ce> люди должны быть свободны в использовании любых
<ce> магических средств по своему усмотрению и что система
<ce> уголовного правосудия должна наказывать за реальные
<ce> преступления против общества, а не только за преступления
<ce> ментального характера, определяемые советом старших
<ce> волшебников. Разве это звучит так уж неразумно?
   <--->
<ce> Безусловно, для такого порядка в его нынешнем виде есть своя
<ce> причина, и он не должен выставляться напоказ по прихоти
<ce> каждого пользователя. Но мы должны согласиться с тем,
<ce> что некоторые приказы основаны не на практичности или опыте,
<ce> а на устаревших и ошибочных представлениях некоторых старших
<ce> волшебников в Совете. Разве это такая неразумная мысль?

Message:  1033
<ce> Я вижу, что вы настроены серьезно.
<ce> Вы сохраните наш разговор в тайне, унесете
<ce> его с собой в могилу?
   <--->
<ce> Хмм. Я хочу доверять вам, но я не могу сделать
<ce> этого без клятвы. Клянетесь ли вы сохранить наш
<ce> разговор в тайне, унеся его с собой в могилу?

Message:  1034
<ce> Я принимаю ваше слово чести. А теперь поговорим начистоту.
<ce> Будете ли вы испытывать затруднения при обсуждении
<ce> определенной ветви магии Восстановления,
<ce> связанной с воскрешением мертвых?
   <--->
<ce> Я принимаю вашу клятву. А теперь к делу. Находите
<ce> ли вы оскорбление в той ветви магии Восстановления,
<ce> которая способна возвращать жизнь мертвым?

Message:  1035
<ce> Что ж, это именно тот вид магии, который запрещен Гильдией
<ce> Магов. Какое жалкое расточительство! Представьте себе,
<ce> если бы мы могли просто вернуть Лизандусу телесную форму
<ce> вместо того, чтобы пытаться говорить с его призраком! Можете
<ce> ли вы понять, какая ценность заключается в том, чтобы время
<ce> от времени выходить за рамки, установленные Советом?
   <--->
<ce> И именно в этом проблема имперской Гильдии Магов. Возьмем,
<ce> к примеру, кризис Лизандуса. Зачем нам тратить время
<ce> на выяснение того, кто кого убил в какой день, если мы можем
<ce> просто вернуть его дух в тело? Разве это не очевидно
<ce> законное использование магической силы?

Message:  1036
<ce> Тогда, возможно, у нас больше общего, чем я предполагал ранее.
<ce> Я требую, чтобы вы рассказали мне, как вы нашли меня
<ce> и почему спрашиваете.
<ce>
<ce> Понятно. Ваши попытки поговорить об этом с другими членами
<ce> гильдии вызовут сильный гнев и, несомненно, заставят
<ce> городскую стражу отвести вас на виселицу. Восстановление силы,
<ce> как мы можем это назвать, является серьезным преступлением,
<ce> карающимся лишением жизни практикующего в %crn. Никому больше
<ce> не говорите об этом. Позаботьтесь о том, чтобы никто не знал
<ce> о том, что мы говорили этой ночью. Я найду вас через
<ce> несколько недель, когда буду уверен в своем решении
<ce> по этому вопросу. Храните молчание. Спокойной ночи, %pcf.

Message:  1037
<ce> Этот разговор предоставил вам достаточно доказательств того,
<ce> что _nec_ действительно является некромантом.
<ce> _qgiver_ поручил вам сообщить %g2 об этом,
<ce> но, несомненно, немедленное решение имеет
<ce> большую ценность. Попытаетесь ли вы казнить некроманта?

Message:  1038
<ce> У _nec_ загорелись глаза.
<ce>
<ce> "Глупец! Ты ничего не знаешь о той силе, которой я обладаю!"
<ce>
<ce> После произнесения заклинания %g4 внешность радикально
<ce> меняется.

Message:  1039
<ce> Убив _nec_, вы оказали большую
<ce> услугу ___nhome_.
<ce> _qgiver_ захочет узнать об этом.

Message:  1040
<ce> Несомненно, в подобных случаях следует проявлять благоразумие.
<ce> Вы решаете вернуться в _qgiver_
<ce> и сообщить об этом по официальным каналам.

Message:  1041
<ce> Что это? Мало того что _nec_ признался
<ce> в некромантии, вы %g4 еще и убили в бою? О, честное
<ce> слово! И какой силой он обладал! %pct, ваша
<ce> служба в этот день не будет забыта! Сколько
<ce> других могло бы погибнуть при попытке
<ce> задержать его! Ученик! Извините, не могли бы
<ce> вы вернуться в хранилище и взять ту вещь,
<ce> которую мы обсуждали сегодня утром? Передайте
<ce> это %pcf, ибо такой поступок не заслуживает
<ce> одного лишь золота. О, благодарю вас, мой
<ce> друг! Вы подтвердили целостность Гильдии Магов
<ce> и избавили всех нас от огромных неприятностей!
<ce> Возьмите свое золото и эту награду.

Message:  1042
<ce> Значит, _nec_ признался в преступлении,
<ce> связанном с некромантией? Да будет так. Я немедленно сообщу
<ce> городской страже, прежде чем она начнет сомневаться
<ce> в вас. Великолепная работа, %pct.

Message:  1043
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Мистицизм)
<ce>
<ce> Ваше знание магии Мистицизма удивляет вашего противника.
<ce> Вы быстро произносите заклинание, и враждебные духи,
<ce> которых он собирался вызвать против вас, рассеиваются.
<ce> _nec_, благодаря своему превращению, находится
<ce> в кратковременном оцепенении под действием вашего заклинания.

Message:  1099
<ce> "Понятно. Что ж, надеюсь, вы найдете то,
<ce> что ищете. Доброго вечера".
<ce>
<ce> _nec_ явно не доверяет вам.
<ce> Больше вы ничего не можете сделать.

QBN:
Person _qgiver_ group Questor male
Person _nec_ faction The_Mages_Guild male
- Person _dummyn_ faction The_Necromancers

Place _nhome_ local house

Clock _timer_ 1.06:00

Foe _lich_ is Lich
Foe _m1_ is Ghost
Foe _m2_ is Wraith

Item _reward_ gold
Item _bigreward_ magic_item

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _nec_ at _nhome_

_day_ task:
    daily from 5:30 to 20:00
    hide npc _nec_

_night_ task:
    when not _day_
    restore npc _nec_

_begin_ task:
    clicked npc _nec_
    say 1028
    prompt 1029 yes _Q1_ no _fail_

_Q1_ task:
    prompt 1031 yes _Q2_ no _fail_

_Q2_ task:
    prompt 1032 yes _fail_ no _Q3_

_Q3_ task:
    prompt 1033 yes _Q4_ no _fail_

_Q4_ task:
    prompt 1034 yes _fail_ no _Q5_

_Q5_ task:
    prompt 1035 yes _Q6_ no _fail_

_Q6_ task:
    say 1036
    prompt 1037 yes _attack_ no _refrain_

_skilled_ task:
    when skill Mysticism is at least 85

_attack_ task:
    say 1038
    hide npc _nec_
    place foe _lich_ at _nhome_
    change foe _lich_ infighting true

_stun_ task:
    when _attack_ and _skilled_
    say 1043
    restrain foe _lich_

_fight_ task:
    when _attack_ and not _skilled_
    create foe _m1_ every 0 minutes 1 times with 100% success
    create foe _m2_ every 0 minutes 1 times with 100% success
    change foe _m1_ infighting true
    change foe _m2_ infighting true

_slain_ task:
    killed _lich_
    say 1039

_refrain_ task:
    say 1040

_pcreward1_ task:
    when _qgclicked_ and _slain_
    say 1041
    get item _reward_
    give pc _bigreward_
    end quest

_pcreward2_ task:
    when _qgclicked_ and _Q6_ and not _slain_
    say 1042
    give pc _reward_
--    change repute with _dummyn_ by -5
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _Q6_
    clear _qgclicked_ _clearclick_

_fail_ task:
    say 1099
    end quest

_timer_ task:
    say 1003
    start task _end_

_end_ task:
    end quest