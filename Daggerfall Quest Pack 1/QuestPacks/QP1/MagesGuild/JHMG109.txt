Quest: JHMG109
DisplayName: Не подпускать
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Если вы не возражаете, нам предстоит решить одну
<ce> деликатную задачу. Великий исследователь _dummy_
<ce> поручил нам манускрипт своего бесценного труда,
<ce> который должен быть немедленно доставлен в
<ce> ___res_ в течение =2res_ дней. К сожалению, наш
<ce> коллега _client_ не сможет забрать его до
<ce> назначенного срока, так что за сохранность
<ce> документов отвечаем мы. У нас есть основания
<ce> подозревать, что Гильдия Воров положит глаз на
<ce> нашего курьера. Не согласитесь ли вы сопровождать
<ce> эти записи, полностью осознавая ту опасность,
<ce> которая подстерегает вас на пути?
   <--->
<ce> Нам требуется доставка, но с некоторой
<ce> отсрочкой. Документы от знаменитого _dummy_
<ce> должны быть перенесены в ___res_ после
<ce> истечения =2res_ дней. Пока они у нас,
<ce> существует немалая вероятность, что Гильдия
<ce> Воров попытается выйти на след курьера. В
<ce> состоянии ли вы гарантировать безопасность
<ce> этих бумаг в их путешествии?

RefuseQuest:  [1001]
<ce> Я очень надеюсь, что наша гильдия сможет
<ce> выдержать вторжение Гильдии Воров.

AcceptQuest:  [1002]
<ce> Как прекрасно! По вашему прибытию _client_ предоставит
<ce> вам назначенную сумму. Не могу предугадать, какие
<ce> именно меры вы предпримете для сохранения целостности
<ce> документов, но я уверен, вы израсходуете все свои
<ce> ресурсы для этой цели. Будьте бдительны: Гильдия Воров
<ce> известна своим искусством скрытности, а не открытого
<ce> боя, и я боюсь, что любое пренебрежение с их стороны
<ce> может иметь долгосрочные последствия. Независимо от
<ce> того, что вам предстоит, ни при каких обстоятельствах
<ce> не отступайте от задачи доставки манускрипта. Пусть
<ce> %god охраняет вас в ближайшие =2res_ дней.

QuestFail:  [1003]
<ce> Хорошо. Мы уходим отсюда.

QuestComplete:  [1004]
<ce> Доброго времени суток. Что у нас здесь? О, это
<ce> воплощение изысканного мастерства _dummy_. Дайте
<ce> мне на мгновение осмотреть, пропечаталось ли на
<ce> нем %g4 клеймо. Все в порядке, кажется, все в
<ce> полном порядке. Пожалуйста, передайте мои
<ce> наилучшие пожелания _qgiver_. Вот ваше
<ce> вознаграждение. Было приятно общаться с вами.

Message:  1010
<ce> Вы слышите голоса неподалеку.
<ce>
<ce> "Вот и %pg. Иди".

Message:  1011
<ce> Не играй с нами здесь в игрушки. Мы - Гильдия Воров.
<ce> Просто передай нам документы и исчезни.
<ce> Мы замолвим за тебя словечко, кальмар.

Message:  1012
<ce> Вы хотите сделать это по-плохому?
<ce> Что ж, сделаем это по-плохому.

Message:  1013
<ce> Если _qgiver_ говорил
<ce> правильно, то _client_ уже
<ce> должен был прибыть в _res_.

Message:  1020
<ce> Молодой человек пытается быть хитрым, вкладывая
<ce> Вам в руки лист бумаги. Некоторые, должно быть,
<ce> все еще учатся этому ремеслу.

Message:  1021
%ra по имени %pcf
<ce>
  Некоторые из нас думают, что попытаться убить вас будет легко.
Другие думают, что смогут отобрать у вас эти записки. Я говорю,
что они дураки, и у меня есть решение получше.
<ce>
  Приходите в _near_ и заплатите мне 400 золотых.
Я заставлю Гильдию Воров думать, что у вас при себе
какие-то другие бумаги. В противном случае у вас
возникнут некоторые проблемы по дороге в ___res_.

                _dummytg_

Message:  1022
<ce> Да? О, это ты.
<ce>
<ce> Что ж, я вижу ваши искренние намерения, но чего я не
<ce> вижу, так это 400 золотых. Попробуйте заплатить мне
<ce> столько, и я сделаю для вас все, что смогу.

Message:  1023
<ce> Да? У вас есть деньги?
<ce>
<ce> Да, 400 ровно. Все в порядке. Так вот, я
<ce> собираюсь сказать людям, устраивающим засады,
<ce> что на самом деле это будет следующий %ra,
<ce> который выйдет из Гильдии Магов после тебя. Я
<ce> сделаю это убедительно. Не волнуйтесь. Ваши
<ce> деньги потрачены не зря.

Message:  1030
%qdt:
 _qgiver_ из Гильдии Магов
 поручил мне охранять рукопись,
 которая должна попасть в
 ___res_ через =2res_ дней. Мне
 нужно доставить ее _client_,
 чтобы получить оплату.

QBN:
Item _reward_ gold
Item _manuscript_ letter
Item _letter_ letter used 1021

Person _qgiver_ group Questor male
Person _dummy_ group Local_3.1
Person _client_ faction The_Mages_Guild
Person _dummytg_ faction The_Thieves_Guild

Place _res_ remote house
Place _near_ local random

Clock _2res_ 5.00:00 8.00:00
Clock _failsafe_ 90.00:00
Clock _first_ 2.00:00 3.00:00
Clock _second_ 2.00:00 3.00:00

Foe _enemy_ is 3 Warrior
Foe _enemy2_ is 3 Warrior

--	Quest start-up:
    start timer _2res_
    start timer _failsafe_
    start timer _first_
    get item _manuscript_
    log 1030 step 0
    pick one of _dud_ _friend_ _friend_

variable _dud_
_friend_ task:
    give pc _letter_ notify 1020
    place npc _dummytg_ at _near_

_ftalk_ task:
    clicked _dummytg_ and at least 400 gold otherwise do _bribefail_
    say 1023
    stop timer _first_
    stop timer _second_

_bribefail_ task:
    say 1022

_first_ task:
    say 1010
    create foe _enemy_ every 0 minutes 1 times with 100% success
    start timer _second_
    take _letter_ from pc
    hide npc _dummytg_

_infight_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ team 0
    change foe _enemy2_ team 0
    change foe _enemy_ infighting true
    change foe _enemy2_ infighting true
    clear _infight_

_msg_ task:
    injured _enemy_
    clicked foe _enemy_
    prompt 1011 yes _deliver_ no _ignore_

_deliver_ task:
    take _manuscript_ from pc
    change repute with _dummytg_ by +5
    say 1003
    remove foe _enemy_
    end quest

variable _ignore_
_anger_ task:
    killed _enemy_
    change repute with _dummytg_ by -15

variable _second_
_secamb_ task:
    when _second_ and _friend_
    create foe _enemy2_ every 0 minutes 1 times with 100% success

_inj2_ task:
    injured _enemy2_
    say 1012

_2res_ task:
    place npc _client_ at _res_
    say 1013

_qgclicked_ task:
    toting _manuscript_ and _client_ clicked
    give pc _reward_
    end quest

_failsafe_ task:
    end quest