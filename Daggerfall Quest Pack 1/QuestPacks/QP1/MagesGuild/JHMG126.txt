Quest: JHMG126
DisplayName: Пропавший изумруд
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Совсем недавно я потерял изумруд, который мог бы пригодиться
<ce> в одном из предстоящих экспериментов. Весьма несвоевременно,
<ce> если честно. Что скажете о том, чтобы добыть
<ce> мне один? Неважно откуда.

RefuseQuest:  [1001]
<ce> Жаль. Тогда я попрошу учеников, чтобы пришли на помощь.

AcceptQuest:  [1002]
<ce> Вы серьезно? Как мило! Подойдет любой изумруд. Главное,
<ce> чтобы он был зеленым, полупрозрачным и подходил для алхимии.
<ce> Принесите его сюда, и я вас щедро вознагражу.
<ce> Как насчет =reward_ золотых? Советую не покупать его
<ce> в магазине, это дорого, но мне не важно, как вы его
<ce> добудете. Было бы неплохо управиться за неделю,
<ce> так что поторопитесь, пожалуйста.

QuestFail:  [1003]
<ce> _questgiver_ из __questgiver_ ожидает,
<ce> что к настоящему моменту вы уже вернетесь с изумрудом.

QuestComplete:  [1004]
<ce> О, прекрасно! Должен сказать, такой энтузиазм, как у вас,
<ce> мы приветствуем на всех уровнях гильдии. Вознаграждение,
<ce> признаться, скромное, но вы получите соответствующую запись
<ce> в реестре гильдии о том, что оказали выдающуюся услугу.
<ce> Благодарю вас, %pct.

Message:  1010
<ce> Теперь у вас есть изумруд, который вы можете доставить
<ce> в Гильдию Магов.

Message:  1011
<ce> Да? У вас есть изумруд? Пока нет? Я буду ждать вашего 
<ce> возвращения.

Message:  1012
<ce> "Привет. Ищешь изумруд?"
<ce>  
<ce> Рядом с вами стоит некто в капюшоне, небрежно
<ce> опираясь на стену и держа в руке изумруд.
<ce>
<ce> "Похоже, старый _questgiver_ из Гильдии Магов
<ce> потерял один такой. Какая жаль. Я бы с радостью отдал
<ce> тебе этот всего за 200 золотых, если у тебя есть столько".
<ce>
<ce> Купите ли вы изумруд у этого вора?

Message:  1013
<ce> Вор пожимает плечами. "Я еще немного побуду здесь, 
<ce> если передумаешь".

Message:  1014
<ce> "Вижу твое желание, но не вижу кошелька. Мне нужны 200 монет,
<ce> никакие вексели не принимаются". - Вор кладет изумруд обратно
<ce> в карман. - "Я побуду здесь еще немного".

Message:  1015
<ce> "Хорошая сделка. Спасибо, что немного помогли местной 
<ce> экономике".
<ce>   
<ce> Вор кладет ваши деньги в карман и уходит.

Message:  1016
<ce> "Нет, мне нужны монеты. Настоящие деньги. 200. Без обмана".

Message:  1017
<ce> "Что ты творишь?! Отвали!"

Message:  1030
%qdt:
 _questgiver_ из ___questgiver_ ищет изумруд.
 Нужно принести ему один до того, как пройдет =timer_ д.
 Я могу купить его в магазине, украсть, найти
 в подземелье или добыть его любым другим способом.

QBN:

Person _questgiver_ group Questor male
Person _tg_ faction The_Thieves_Guild

Foe _thief_ is Thief

Item _reward_ gold range 150 to 200
Item _emerald_ emerald

Clock _timer_ 7.00:00
Clock _findthief_ 1:00 2.00:00

--   Quest start-up:
   start timer _timer_
   start timer _findthief_
   log 1030 step 0
   pc at any magery set _inside_
   hide npc _tg_

variable _inside_
variable _findthief_
_encounterthief_ task:
    when _findthief_ and not _inside_ and not _got_
    prompt 1012 yes _paygold_ no _refuse_

_refuse_ task:
    say 1013
    create foe _thief_ every 0 minutes 1 times with 100% success
    restrain foe _thief_

_paygold_ task:
    pay 200 money do _purchase_ otherwise do _nomoney_

_nomoney_ task:
    say 1014
    create foe _thief_ every 0 minutes 1 times with 100% success
    restrain foe _thief_

_purchase_ task:
    clear _paygold_
    get item _emerald_
    make _emerald_ permanent
    say 1015
    remove foe _thief_

_click_ task:
    clicked foe _thief_

_secondtry_ task:
    when _click_ and not _inj_
    start task _paygold2_
    clear _click_ _secondtry_

_paygold2_ task:
    pay 200 money do _purchase_ otherwise do _nomoney2_

_nomoney2_ task:
    say 1016
    clear _click_ _nomoney2_ _paygold2_

_inj_ task:
    injured _thief_ saying 1017
    give item _emerald_ to _thief_
    make _emerald_ permanent
    change repute with _tg_ by -3

_slain_ task:
    killed _thief_
    change repute with _tg_ by -7

_got_ task:
   player possesses 1 items class 14 subclass 1
   say 1010
--uses templateindex. see Interkarma/daggerfall-unity/blob/master/Assets/Scripts/Game/Items/ItemEnums.cs

_qgclicked_ task:
   clicked npc _questgiver_

_clearclick_ task:
   when _qgclicked_ and not _got_ and not _victory_
   say 1011
   clear _qgclicked_ _clearclick_

_victory_ task:
    when _qgclicked_ and _got_
    player handsover 1 items class 14 subclass 1
    give pc _reward_
    end quest

_timer_ task:
   say 1003
   end quest
