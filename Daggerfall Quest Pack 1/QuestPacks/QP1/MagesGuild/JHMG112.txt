Quest: JHMG112
DisplayName: Turnabout Wizardry
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, перед нами сценарий, повторявшийся уже
<ce> не раз, что, несомненно, вам известно. Член
<ce> нашей гильдии обретает столь огромную мощь,
<ce> что начинает игнорировать нормы морали,
<ce> прибегая к кражам и убийствам силой
<ce> заклинаний. На сей раз подобное поведение
<ce> проявляет _enmage_ из ___res_, %pct,
<ce> обладатель несомненных талантов. Мы
<ce> беспокоимся за жизнь любого, кто отправится
<ce> к н%g2, но альтернативы у нас нет. Есть ли у
<ce> вас мужество встретить этого опасного
<ce> противника лицом к лицу?
   <--->
<ce> Из наших рядов вышел один %pct, чье
<ce> умение стало угрозой для %crn. Занимая
<ce> тот же ранг, что и вы, _enmage_ слишком
<ce> увлекся рискованными практиками и теперь
<ce> должен быть остановлен. Вы рискуете
<ce> многим, но мы готовы предложить
<ce> поддержку, если вы откликнетесь на наш
<ce> призыв. Готовы ли вы принять этот вызов?

RefuseQuest:  [1001]
<ce> Возможно, придется прибегнуть к вмешательству
<ce> самого Архимагистра, чтобы разрешить этот конфликт.

AcceptQuest:  [1002]
<ce> Пусть %god сопутствует вам с момента, когда вы
<ce> шагнете за порог этого зала гильдии, %pcf.
<ce> Отправляйтесь в ___res_ и разыщите %g4. Мы
<ce> надеемся, что местные жители смогут указать вам,
<ce> где %g1 находится. Горячо желаем, чтобы из
<ce> вашего предприятия не произошло никаких бед. Наш
<ce> соратник, _aide_, выразил желание, чтобы вы
<ce> обратились к н%g2 прежде, чем выдвигаться в
<ce> путь. Вы найдете %g4 прямо здесь, в этом городе.
<ce> Завершите это задание за =2res_ дней, и я лично
<ce> отблагодарю вас за ваше доблестное деяние.

QuestComplete:  [1004]
<ce> %oth, я вправду задумывался, увижу ли я вас
<ce> снова. Расскажите, пожалуйста, как прошло
<ce> сражение. Оправдала ли _thing_, которую вы
<ce> получили от _aide_, все усилия, что вы
<ce> приложили? О, как же я хотел бы иметь больше,
<ce> чем этот _reward_, чтобы достойно вознаградить
<ce> вас. Выполнено блестяще, мой верный %pct.

Message:  1010
<ce> Ах, так они прислали %pcn. Что ж, я полагаю, в
<ce> конце концов мне придется предать это огласке.
<ce> Лучше сделать из вас пример, чтобы они узнали,
<ce> что такое страх. Если вы пришли сразиться со
<ce> мной, начинайте свою ничтожную попытку!

Message:  1011
<ce> _enmage_ ухмыляется. На вас начинает
<ce> накладываться могущественное
<ce> проклятие. Вы надеетесь, что %g1 не
<ce> так силен, как намекал _qgiver_.

Message:  1012
<ce> Теперь у тебя нет никакой надежды поймать _enmage_.
<ce> Лучшее, что ты можешь сделать, - это сбежать отсюда
<ce> и надеяться, что в следующий раз этого не случится.

Message:  1013
<ce> _enmage_ убит.

Message:  1015
<ce> %pcn! Слава небесам, вы здесь. Мне бы хотелось призвать вас
<ce> к бегству и избеганию встречи с _enmage_, но ведь перед
<ce> вами стоит задача, которую необходимо исполнить. Прошу вас
<ce> лишь взять с собой это изделие, мое творение — _thing_. Не
<ce> раскрывайте никому его могущество, ибо оно заряжено силой,
<ce> затмевающей свет. Как только _enmage_ приготовится к
<ce> схватке, незамедлительно примените _thing_. Я осведомлен,
<ce> что %g1 способен на установку ловушек, тайных и
<ce> смертоносных. Как же я желаю оказать вам больше поддержки,
<ce> но гильдия не может допустить, чтобы за победу было
<ce> уплачено жизнями многих. Прошу вас, вернитесь невредимым.

Message:  1016
<ce> Вы достаете и активируете _thing_. Лицо
<ce> _enmage_ внезапно бледнеет, осознав, что вы
<ce> лишили %g4 части %g4 силы. Вы продолжаете
<ce> сражение более уверенно, так как только что
<ce> отменили один из %g4 трюков.

Message:  1020
<ce> _enmage_, скорее всего, уже в бегах.

Message:  1030
%qdt:
 Гильдия Магов ___qgiver_
 попросила меня найти и уничтожить
 члена гильдии _enmage_,
 могущественного мага. Так же я
 могу рассчитывать на помощь
 _aide_. Мне нужно отправиться в
 ___res_ и уничтожить врага в
 течение =2res_ дней.

QBN:
Clock _failsafe_ 180.00:00
Clock _trigger_ 00:01
Clock _2res_ 00:00 0 flag 1 range 0 2

Item _thing_ scarab
Item _reward_ gem

Place _res_ remote random
Place _near_ local magery
Place _dung_ remote dungeon

Person _qgiver_ group Questor male
Person _aide_ faction The_Mages_Guild
Person _enmage_ faction The_Mages_Guild

Foe _mage_ is Nightblade
Foe _daedr_ is 3 Daedroth

--	Quest start-up:
        start timer _failsafe_
        start timer _2res_
        log 1030 step 0
        place npc _aide_ at _near_
        place npc _enmage_ at _res_

_talkaide_ task:
        clicked npc _aide_
        say 1015
        get item _thing_

_talkmage_ task:
        clicked npc _enmage_
        say 1010
        hide npc _enmage_
        place foe _mage_ at _res_
        start timer _trigger_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        setvar _tele_
        _thing_ used do _cancel_
        change foe _mage_ infighting true

_null_ task:
        clear _null_

variable _dae_
variable _tele_
variable _dis_
variable _cancel_

_docancel_ task:
        when _cancel_ and not _slain_
        stop timer _trigger_
        say 1016

_trigger_ task:
        play sound magic_fizzle every 0 minutes 1 times
        say 1011

_curse1_ task:
        when _trigger_ and _dae_
        create foe _daedr_ every 0 minutes 1 times with 100% success

_daily_ task:
        daily from 0:00 to 23:59
        change foe _daedr_ infighting true
        clear _daily_

_curse2_ task:
        when _trigger_ and _tele_
        teleport pc to _dung_
        say 1012
        start task _delay_

_delay_ task:
        end quest

_curse3_ task:
        when _trigger_ and _dis_
        make pc ill with Plague

_slain_ task:
        killed _mage_
        stop timer _trigger_
        stop timer _2res_
        say 1013

_qgclicked_ task:
        clicked npc _qgiver_

_clearclick_ task:
        when _qgclicked_ and not _slain_
        clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
        when _qgclicked_ and _slain_
        give pc _reward_
        start quest JHMGC05
        end quest

_2res_ task:
        say 1020
        end quest

_failsafe_ task:
        end quest