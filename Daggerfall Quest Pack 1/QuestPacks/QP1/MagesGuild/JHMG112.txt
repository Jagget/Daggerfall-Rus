Quest: JHMG112
DisplayName: Оборотное волшебство
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, перед нами сценарий, повторявшийся уже
<ce> не раз, что, несомненно, вам известно. Член
<ce> нашей гильдии обретает столь огромную мощь,
<ce> что начинает игнорировать нормы морали,
<ce> прибегая к кражам и убийствам силой
<ce> заклинаний. На сей раз подобное поведение проявляет
<ce> _enmage_ из ___res_, %pct,
<ce> обладатель несомненных талантов.
<ce> Мы беспокоимся за жизнь любого, кто отправится
<ce> к н%g2, но альтернативы у нас нет. Достаточно
<ce> ли у вас мужества встретить этого опасного
<ce> противника лицом к лицу?
   <--->
<ce> Из наших рядов вышел один %pct, чье
<ce> умение стало угрозой для %crn. Занимая
<ce> тот же ранг, что и вы, _enmage_ слишком
<ce> увлекся рискованными практиками и теперь
<ce> должен быть остановлен. Вы столкнетесь с большой
<ce> опасностью, но мы готовы предложить
<ce> поддержку, если вы решите принять наш запрос.
<ce> Готовы ли вы?

RefuseQuest:  [1001]
<ce> Возможно, придется прибегнуть к вмешательству
<ce> самого Архимагистра, чтобы разрешить этот конфликт.

AcceptQuest:  [1002]
<ce> Пусть %god сопутствует вам с момента, когда вы
<ce> шагнете за порог этого зала гильдии, %pcf.
<ce> Отправляйтесь в ___res_
<ce> и разыщите %g4. Мы надеемся, что местные жители
<ce> смогут указать вам, где %g1 находится. Мы искренне
<ce> надеемся, что из этого не выйдет ничего плохого.
<ce> Наш соратник, _aide_, настаивал,
<ce> чтобы вы обратились к н%g2, прежде чем выдвигаться
<ce> в путь. Вы найдете %g4 прямо здесь, в этом городе.
<ce> Завершите это задание за =2res_ дней, и я лично
<ce> отблагодарю вас за ваше доблестное деяние.

QuestComplete:  [1004]
<ce> %oth, я вправду задумывался, заговорю ли я с вами
<ce> снова. Расскажите, пожалуйста, как прошло сражение.
<ce> Оправдало ли все приложенные вами усилия
<ce> применение _thing_, что вы получили
<ce> от _aide_? О, как же я хотел бы
<ce> иметь больше, чем этот _reward_,
<ce> чтобы достойно вознаградить вас.
<ce> Выполнено блестяще, мой верный %pct.

Message:  1010
<ce> Ах, так они прислали %pcn. Что ж, полагаю,
<ce> в конце концов мне придется предать это огласке.
<ce> Лучше сделать из вас пример, чтобы они узнали,
<ce> что такое страх. Если вы пришли сразиться
<ce> со мной, начинайте свою ничтожную попытку!

Message:  1011
<ce> _enmage_ ухмыляется. На вас начинает
<ce> накладываться могущественное проклятие.
<ce> Вы надеетесь, что %g1 не так силен, как намекал
<ce> _qgiver_.

Message:  1012
<ce> Теперь у вас нет никакой надежды поймать
<ce> _enmage_. Лучшее, что вы можете
<ce> сделать, - это сбежать отсюда и надеяться,
<ce> что в следующий раз этого не случится.

Message:  1013
<ce> _enmage_ убит.

Message:  1015
<ce> %pcn! Слава небесам, вы здесь. Жаль, что я не могу
<ce> убедить вас бежать и никогда не встречаться лицом
<ce> к лицу с _enmage_,
<ce> но ведь у вас есть долг, который необходимо выполнить.
<ce> Прошу вас лишь взять с собой это изделие,
<ce> мое творение - _thing_.
<ce> Не раскрывайте никому его могущество, ибо оно заряжено
<ce> силой, затмевающей свет. Как только
<ce> _enmage_ приготовится к схватке,
<ce> незамедлительно примените _thing_.
<ce> Я осведомлен, что %g1 способен на установку ловушек,
<ce> тайных и смертоносных. Как же я желаю оказать вам
<ce> больше поддержки, но гильдия не может допустить,
<ce> чтобы за победу было уплачено жизнями многих.
<ce> Прошу вас, вернитесь невредимым.

Message:  1016
<ce> Вы достаете и активируете _thing_.
<ce> Лицо _enmage_ внезапно бледнеет,
<ce> %g1 осознает, что вы только что лишили %g4 части
<ce> %g4 силы. Вы продолжаете сражение более уверенно,
<ce> так как только что отменили один из %g4 трюков.

Message:  1020
<ce> _enmage_, скорее всего, уже в бегах.

Message:  1030
%qdt:
 Гильдия Магов ___qgiver_
 попросила меня найти и уничтожить
 члена гильдии _enmage_,
 могущественного мага. Также я
 могу рассчитывать на помощь
 _aide_. Мне нужно отправиться
 в ___res_ и уничтожить врага
 в течение =2res_ дней.

QBN:
Clock _failsafe_ 180.00:00
Clock _trigger_ 00:01
Clock _2res_ 00:00 0 flag 1 range 0 2

Item _thing_ scarab
Item _reward_ gem

Place _res_ remote random
Place _near_ local magery
Place _dung_ remote dungeon

Person _qgiver_ group Questor male
Person _aide_ faction The_Mages_Guild
Person _enmage_ faction The_Mages_Guild

Foe _mage_ is Nightblade
Foe _daedr_ is 3 Daedroth

--	Quest start-up:
        start timer _failsafe_
        start timer _2res_
        log 1030 step 0
        place npc _aide_ at _near_
        place npc _enmage_ at _res_

_talkaide_ task:
        clicked npc _aide_
        say 1015
        get item _thing_

_talkmage_ task:
        clicked npc _enmage_
        say 1010
        hide npc _enmage_
        place foe _mage_ at _res_
        start timer _trigger_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        pick one of _dae_ _tele_ _dis_ _null_ _null_
        setvar _tele_
        _thing_ used do _cancel_
        change foe _mage_ infighting true

_null_ task:
        clear _null_

variable _dae_
variable _tele_
variable _dis_
variable _cancel_

_docancel_ task:
        when _cancel_ and not _slain_
        stop timer _trigger_
        say 1016

_trigger_ task:
        play sound magic_fizzle every 0 minutes 1 times
        say 1011

_curse1_ task:
        when _trigger_ and _dae_
        create foe _daedr_ every 0 minutes 1 times with 100% success

_daily_ task:
        daily from 0:00 to 23:59
        change foe _daedr_ infighting true
        clear _daily_

_curse2_ task:
        when _trigger_ and _tele_
        teleport pc to _dung_
        say 1012
        start task _delay_

_delay_ task:
        end quest

_curse3_ task:
        when _trigger_ and _dis_
        make pc ill with Plague

_slain_ task:
        killed _mage_
        stop timer _trigger_
        stop timer _2res_
        say 1013

_qgclicked_ task:
        clicked npc _qgiver_

_clearclick_ task:
        when _qgclicked_ and not _slain_
        clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
        when _qgclicked_ and _slain_
        give pc _reward_
        start quest JHMGC05
        end quest

_2res_ task:
        say 1020
        end quest

_failsafe_ task:
        end quest