Quest: JHMG106
DisplayName: Непонятная безделушка
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, мы считаем, что у нас есть для вас простое задание. Один
<ce> из наших членов принес вот этот _tri_, который, как мы полагаем,
<ce> обладает магическими свойствами. Однако обычные заклинания
<ce> идентификации не помогли нам, а прямое тестирование кажется
<ce> опасно абсурдным предложением, зная, что оно может оказать
<ce> абсолютно любой обратный эффект. То, что не могут определить
<ce> Одилические Маги, может определить один из наших великих
<ce> экспертов. Пожалуйста, доставьте это для идентификации.
   <--->
<ce> Одна из грандиозных загадок Тамриэля
<ce> заключается в том, что существуют просто
<ce> непостижимые для нас формы магии и силы. Мое
<ce> самое искреннее желание - чтобы Империя
<ce> просуществовала еще 1500 лет, чтобы у нас было
<ce> время спокойно разгадать все загадки в мире. Но
<ce> ближе к делу: недавно нам доставили очень
<ce> странный _tri_ со скрытыми чарами, которые мы
<ce> не можем разгадать. Обычные заклинания для
<ce> идентификации ничего не дают. Здесь требуется
<ce> помощь настоящего эксперта. Возьмете ли вы его
<ce> в руины, чтобы получить такую помощь?

RefuseQuest:  [1001]
<ce> Несмотря на это, мы отказываемся испытывать его в гильдейском зале.
<ce> Риски слишком велики.

AcceptQuest:  [1002]
<ce> Очень хорошо, вот он. Я бы попросил вас не пытаться использовать
<ce> это, но я сомневаюсь, что вы сможете это сделать в любом случае.
<ce> Если наши специалисты не смогли высвободить его силу, то вы тем
<ce> более не сможете. Скорее всего, вы найдете _npc_ в норе под
<ce> названием ___dung_. Пожалуйста, отнеситесь к н%g2 с уважением,
<ce> поскольку %g1 - бывший член Гильдии. Мы не хотим, чтобы возникли
<ce> разногласия только потому, что кто-то не может контролировать
<ce> свой язык. Я увижу вас здесь через =timer_ дней, чтобы выслушать
<ce> отчет _npc_. Пусть %god присматривает за вами.

QuestFail:  [1003]
<ce> _qgiver_ хмуро смотрит на вас.
<ce>
<ce> "Один из наших членов использовал ваше местоположение
<ce> в качестве якоря, чтобы просканировать ___dung_ и
<ce> посмотреть, как _npc_ справляется с _tri_. Никто не
<ce> ожидал увидеть вместо этого %g4 труп. Я бы
<ce> бросил вас в тюрьму, если бы у меня было время
<ce> доказать свою правоту. Проваливай, чурбан".

QuestComplete:  [1004]
<ce> О, наш бедный _npc_! Какая ужасная ловушка! Мы многим
<ce> обязаны вам в этот день, %pct. Если бы вы убили %g4, мы бы
<ce> потеряли одного из наших лучших экспертов по арканным
<ce> артефактам. Отличная работа, дитя мое! Пожалуйста, примите
<ce> эту награду за раскрытие тайны, заключенной в _tri_. Что
<ce> мы будем с ним делать дальше, решит Совет.

Message:  1010
<ce> "А, добрый день. Да, я _npc_. Что Вам от меня нужно? Хм.
<ce> Ну, полагаю, я могу взглянуть на это. Это всего лишь _tri_?
<ce> Очень хорошо. Позвольте мне немного поработать с ним".
<ce>
<ce> Вы держите безделушку, к которой %g1 прикасается, произнося
<ce> заклинание. За этим следует вспышка света, которая оставляет
<ce> вас в замешательстве. Когда ваши глаза адаптируются, Вы
<ce> видите, что лицо _npc_ изменилось.
<ce>
<ce> "Мерзкий, отвратительный %ra! Это мой дом! Что вы делаете
<ce> в моем доме?! Убивать! Убивать! Во имя Обливиона!"
   <--->
<ce> "А, добрый день. Да, я _npc_. Что Вам от меня нужно? Хм.
<ce> Ну, полагаю, я могу взглянуть на это. Это всего лишь _tri_?
<ce> Очень хорошо. Позвольте мне немного поработать с ним".
<ce>
<ce> Вы держите безделушку, к которой %g1 прикасается, произнося
<ce> заклинание. За этим следует вспышка света, которая оставляет
<ce> вас в замешательстве. Когда ваши глаза адаптируются, Вы
<ce> видите, что лицо _npc_ изменилось.
<ce>
<ce> "%oth! Мерзкий проклятый, сегодня ты встретишь свой конец!
<ce> Во имя Обливиона и его десяти тысяч благочестивых слуг! Умри!"

Message:  1011
<ce> _npc_ лежит мертвым. Вы кладете _tri_ в свою сумку.

Message:  1012
<ce> _npc_ останавливается на середине шага. В %g4 глаза
<ce> возвращается ясность, и %g1 дрожит.
<ce>
<ce> "Да как же так. Что же это такое? Я ведь не причинил вам
<ce> вреда, правда? Какая ужасная, ужасная ловушка была поставлена
<ce> на эту простую маленькую штучку! Один из нас мог быть убит! О,
<ce> я чувствую, что теряю сознание. Скажите _qgiver_, что теперь
<ce> мы знаем об этом устройстве все, что нужно. И скажите %g2,
<ce> чтобы %g1 сжег это. Я бы и сам так поступил, если бы не был
<ce> уверен, что это вызовет гнев архимагистра. Иди с миром, %ra, и
<ce> спасибо, что пощадили мою жизнь".

Message:  1013
<ce> Магическая сила продолжает исходить из
<ce> _tri_. Вы не можете точно сказать, что
<ce> она делает, но она не может быть
<ce> полезной. Если вы уверены в своих
<ce> силах, вы можете попытаться ослабить
<ce> магию в ее источнике. Вы попробуете?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Тауматургия, высокая сложность)

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы обращаете силу _tri_ обратно на себя.
<ce> Магия становится неактивной.

Message:  1015
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы пытаетесь обратить эту силу обратно на _tri_, но она
<ce> страновится все сильнее и _tri_ начинает дрожать от избытка силы.
<ce> _npc_ вскрикивает нечеловеческим голосом,
<ce>
<ce> "Имбецил! Прекрати это!
<ce> Прекрати причинять вред моему дому!"
<ce>
<ce> Из безделушки снова вырывается свет, и вы чувствуете,
<ce> что поблизости появились другие существа.

Message:  1030
%qdt:
 Гильдия Магов ___qgiver_
 поручила мне взять некий
 _tri_ к _npc_, который находится
 в __dung_, для проверки.
 Я должен вернуться через =timer_ дней
 с отчетом.

Message:  1031
%qdt:
 _npc_ взбесился после
 активации _tri_, и
 я убил %g4.

Message:  1032
%qdt:
 _npc_ был
 зачарован воздействием _tri_,
 но выжил. Теперь я могу доставить
 отчет обратно в Гильдию Магов.

QBN:
Item _reward_ gold
Item _tri_ trinket

Person _qgiver_ group Questor male
Person _npc_ faction The_Odylic_Mages

Place _dung_ remote dungeon10

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _charm_ 00:01
Clock _battle_ 00:05 00:07

Foe _nme_ is Spellsword
Foe _daedroth1_ is Daedroth
Foe _daedroth2_ is Daedroth

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _npc_ at _dung_
  reveal _dung_
  get item _tri_

_talk_ task:
  clicked npc _npc_
  say 1010
  hide npc _npc_
  place foe _nme_ at _dung_
  start timer _charm_

_fail_ task:
  killed 1 _nme_
  say 1011
  log 1031 step 2
  stop timer _charm_
  stop timer _battle_
  change repute with _qgiver_ by -23

_charm_ task:
  prompt 1013 yes _attempt_ no _flee_

_flee_ task:
  start timer _battle_

variable _attempt_

_skilled_ task:
  when skill Thaumaturgy is at least 65

_easywin_ task:
  when _attempt_ and _skilled_
  say 1014
  start task _battle_

_bigfail_ task:
  when _attempt_ and not _skilled_
  say 1015
  create foe _daedroth1_ every 0 minutes 1 times with 100% success
  create foe _daedroth2_ every 0 minutes 1 times with 100% success
  change foe _daedroth1_ infighting true
  change foe _daedroth2_ infighting true
  start timer _battle_

_battle_ task:
  say 1012
  log 1032 step 1
  restrain foe _nme_
  remove foe _daedroth1_
  remove foe _daedroth2_

_pcgetsgold_ task:
  when _qgclicked_ and _battle_ and not _fail_
  give pc _reward_
  end quest

_prompt_ task:
  when _qgclicked_ and _fail_
  say 1003
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _talk_
  clear _qgclicked_ _clearclick_

_timer_ task:
  make _tri_ permanent
  end quest