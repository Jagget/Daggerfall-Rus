Quest: JHMG103
DisplayName: Вторжение Псиджиков
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вы когда-нибудь слышали об Ордене Псиджиков,
<ce> %pct? Мало кто слышал. Они склонны оставаться 
<ce> в тени, позволяя событиям происходить естественным
<ce> образом... но иногда это далеко не так. Один из
<ce> их вида обокрал нас и укрылся в пещере, Обливион
<ce> его знает зачем. Пойдите и принесите обратно
<ce> нашу книгу. Вам разрешено убивать.

RefuseQuest:  [1001]
<ce> Что ж, я полагаю, трус не захотел бы
<ce> столкнуться с гневом конкурирующего ордена.

AcceptQuest:  [1002]
<ce> Хорошо. Если мы сейчас это не пресечем, то откроем
<ce> дверь для новых краж. Что бы вы ни решили сделать 
<ce> с этим вором-Псиджиком, это ваш собственный выбор.
<ce> Книга понадобится примерно через =timer_ дней, так
<ce> что будьте уверены, что не будете медлить.
<ce> ___dung_ находится на %di. Я отмечу это
<ce> на вашей карте и пожелаю вам счастливого пути.

QuestFail:  [1003]
<ce> Вам уже нужно было забрать
<ce> книгу у Ордена Псиджиков.

QuestComplete:  [1004]
<ce> Очень хорошая работа. Надеюсь, вы не столкнулись
<ce> с большой опасностью в ___dung_? Во всяком
<ce> случае, не для человека вашей квалификации.
<ce>
<ce> Я пытался найти приемлемое
<ce> вознаграждение, пока вас не было.
<ce> Возможно, этот _reward_ будет вам
<ce> полезен? Пригодится для зачарований.
<ce> Попробуйте, почему бы и нет?

QuestorPostSuccess:   [1008]
Ордену Псиджиков здесь не место.

Message:  1010
<ce> "Книга моя! Сгинь!"
<ce>
<ce> Псиджик начинает произносить заклинание.

Message:  1011
<ce> Ваш противник мертв, но заклинание уже заканчивается.
<ce> С громким ревом рядом начинает открываться портал.
<ce>
<ce> Если бы вы были уверены в своих способностях 
<ce> к Мистицизму, вы могли бы попытаться закрыть портал
<ce> до того, как что-либо пройдет через него.
<ce> Однако вмешательство в это без соответствующих
<ce> знаний может сделать ситуацию еще более опасной.
<ce>
<ce> (ПРОВЕРКА НАВЫКА: Мистицизм: очень высокая сложность)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Псиджики может и знают что-то о Мистицизме, но и вы
<ce> тоже. Вы беретесь за портал и начинаете уменьшать его
<ce> силу. Когтистая рука пытается вылезти через него, 
<ce> но слишком поздно - связь с Обливионом прервана. Какая
<ce> бы опасность вам ни грозила, она уже миновала.

Message:  1013
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы начинаете концентрироваться на портале, чтобы
<ce> закрыть его. Чего вам не хватает - силы или знаний,
<ce> вы не знаете; но ваши попытки закрыть портал придают
<ce> ему немного больше энергии, достаточной для того,
<ce> чтобы через него прошло больше демонов, чем могло бы.
<ce> С громким грохотом портал наконец-то исчезает,
<ce> оставляя вас один на один с новыми гостями.

Message:  1014
<ce> Вместо того чтобы возиться с порталом Псиджика, вы
<ce> готовитесь к тому, что через него вот-вот что-то появится.
<ce> Огненная даэдра уверенно шагает через портал, когда
<ce> его энергия иссякает.

Message:  1030
%qdt:
 Я должен спуститься в ___dung_,
 чтобы вернуть книгу, которую Орден
 Псиджиков украл из Гильдии Магов 
 в ___qgiver_. _qgiver_ хочет, чтобы я
 вернул ее через =timer_ дней.

QBN:
Item _item_ book
Item _reward_ diamond

Person _qgiver_ group Questor male

Place _dung_ remote dungeon5

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _mage_ is Mage
Foe _daedra1_ is Fire_daedra
Foe _daedra2_ is Fire_daedra
Foe _daedra3_ is Fire_daedra

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
    place foe _mage_ at _dung_
    give item _item_ to _mage_
	reveal _dung_

_inj_ task:
    injured _mage_
    clicked foe _mage_
    say 1010
    change foe _mage_ infighting true

_killed_ task:
    killed _mage_
	prompt 1011 yes _attempt_ no _flee_

variable _attempt_

_skilled_ task:
	when skill Mysticism is at least 85

_easywin_ task:
	when _attempt_ and _skilled_
	say 1012

_bigfail_ task:
	when _attempt_ and not _skilled_
	say 1013
    create foe _daedra1_ every 0 minutes 1 times with 100% success
    change foe _daedra1_ infighting true
    create foe _daedra2_ every 0 minutes 1 times with 100% success
    change foe _daedra2_ infighting true
    create foe _daedra3_ every 0 minutes 1 times with 100% success
    change foe _daedra3_ infighting true

_flee_ task:
    say 1014
    create foe _daedra1_ every 0 minutes 1 times with 100% success
    change foe _daedra1_ infighting true

_pcgetsgold_ task:
	toting _item_ and _qgiver_ clicked
	give pc _reward_
	end quest

_timer_ task:
	say 1003
	end quest