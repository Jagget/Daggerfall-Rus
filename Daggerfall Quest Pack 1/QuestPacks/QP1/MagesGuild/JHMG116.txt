Quest: JHMG116
DisplayName: Остановить эксперимент
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Дозвольте, %pcf, поделиться с вами историей о
<ce> _mage_. Мои чувства к н%g2 противоречивы: любовь
<ce> смешивается с отвращением. В прошлом %g1 оказал
<ce> значительные услуги Гильдии Магов, и мы не можем
<ce> бросить %g4 в беде. Однако есть эксперименты,
<ce> которые не следует продолжать, независимо от их
<ce> потенциальной пользы. Готовы ли вы отправиться в
<ce> ___res_, чтобы убедить %g4 остановиться?

RefuseQuest:  [1001]
<ce> Какая неприятность! Но скажите, волнует ли вас эта работа?

AcceptQuest:  [1002]
<ce> Наш уважаемый _mage_ постоянно проводит время в _res_,
<ce> ___res_. Я осведомлен о %g4 занятиях, но предпочитаю не
<ce> обсуждать их вслух, чтобы избежать публичного позора.
<ce> Некоторые не перенесут стыда, если их действия станут
<ce> достоянием общественности. Но я уверен: это должно
<ce> прекратиться, и прекратиться немедленно. Спешите, ведь от вас
<ce> зависит %g4 жизнь. Я настоятельно прошу вас не возвращаться
<ce> без н%g4. Пожелаю вам благополучного пути, %pct.

QuestFail:  [1003]
<ce> _mage_ погибает. Какие слова
<ce> найдете вы для _qgiver_?

QuestComplete:  [1004]
<ce> О, _mage_! Как я хочу выразить вам свое неодобрение
<ce> здесь и сейчас! Но я вижу, что вам необходимо
<ce> собраться с силами. И когда вы восстановитесь, я
<ce> выскажу вам самую гневную тираду, что вы слышали.
<ce> Чувства радости и гнева сражаются за право
<ce> господства в моих словах. До встречи через
<ce> несколько часов, после вашего отдыха.
<ce>
<ce> А вам, %pcf, я вручаю этот волшебный
<ce> _reward_. Великолепная работа во всех
<ce> отношениях.

QuestorPostFailure:  [1009]
Я помню _mage_. Не благодаря тебе.
   <--->
Ах, если бы только у нас была помощь _mage_ прямо сейчас. Какой позор.

Message:  1010
<ce> _mage_, кажется, находится в
<ce> каком-то оцепенении. Трудно
<ce> сказать, контролирует ли %g1
<ce> беса или бес контролирует %g4.

Message:  1011
<ce> Как только бес падает, _mage_ открывает
<ce> глаза смотрит на вас с ужасом. Вы начинаете
<ce> объяснять свою задачу, но прежде чем
<ce> успеваете что-либо сказать, слышите
<ce> поблизости шум. В свои последние мгновения
<ce> бес направляет на мага какую-то вешь,
<ce> которая рассыпается под напором силы.

Message:  1015
<ce> _mage_ бледнеет в лице.
<ce>
<ce> "Это... были заражающие чары. Я заболел, и моя
<ce> магическая сила покинула меня. Пожалуйста, мне
<ce> нужно вылечиться. Помогите мне! Произнесите
<ce> заклинание! Храм мне не поможет!"
<ce>
<ce> Затем %g1 начинает потеть и тяжело дышать.

Message:  1016
<ce> _mage_ стонет, %g4 шаги замедляются.
   <--->
<ce> _mage_ чуть не споткнулся. Вы помогаете %g2
<ce> восстановить равновесие, прежде чем идти дальше.
   <--->
<ce> _mage_ проводит несколько секунд в приступе кашля,
<ce> прежде чем снова начинает устало двигаться.

Message:  1017
<ce> _mage_ говорит с закрытыми глазами.
<ce> Вы понимаете очень мало.
   <--->
<ce> _mage_ speaks with %g4 eyes closed.
<ce> Эти слова - всего лишь шепот.

Message:  1018
<ce> Дыхание _mage_ становится поверхностным.
<ce> Вы помогаете %g2 держаться и продолжать двигаться.
   <--->
<ce> _mage_ начинает неконтролируемо трястись.
   <--->
<ce> _mage_ падает на колени, и его обильно рвет.
<ce> Вы помогаете %g2 подняться и продолжаете путь.

Message:  1020
<ce> Вспышка света начинает поглощать тело _mage_.
<ce>
<ce> "Это ловушка! Меня отправляютв ___dung_!
<ce> Пожалуйста, помогите!"
<ce>
<ce> Мгновение спустя %g1 исчезает. Некоторые близлежащие
<ce> каракули показывают местоположение ___dung_.

Message:  1021
<ce> В поле зрения появляется _mage_,
<ce> очевидно, в поисках выхода. С огромным
<ce> облегчением %g1 машет вам рукой.
<ce>
<ce> "О, спасибо вам за то, что пришели сюда и нашли меня. Дурацкий
<ce> артефакт оставил меня без магии, оставив бегство моим
<ce> единственным выходом в этой пещере. Пожалуйста, отведите меня
<ce> в ___qgiver_. Я полагаю, что в данный момент мне не помешало
<ce> бы перекинуться парой слов с _qgiver_".

Message:  1022
<ce> Цвет возвращается к лицу _mage_,
<ce> и %g1 выглядит достаточно бодро.
<ce>
<ce> "%oth! Никогда бы я не подумал, что мои исследования
<ce> так закончатся! Отведи меня в ___qgiver_. Мне есть
<ce> что сказать _qgiver_. Спасибо тебе".

Message:  1023
<ce> Ранение беса, кажется, на мгновение оглушает его. Предполагая,
<ce> что это существо было вызвано, у него наверняка должны быть
<ce> какие-то управляющие слова, чтобы заставить его остановиться.
<ce> Пытаетесь ли вы получить контроль над бесом?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Язык бесов, средняя сложность)

Message:  1024
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы выкрикиваете какие-то команды, частично на основе
<ce> догадок, частично прошлых знаний, приходящих вам на ум.
<ce> Лицо беса застывает, и он перестает атаковать. Затем вы
<ce> отправляете его в другую часть резиденции.
<ce>
<ce> _mage_ вскоре возвращается к реальности и опускается
<ce> перед тобой на колени. "Спасибо вам, спасибо, спасибо!
<ce> Это существо взбунтовалось против меня и продержало
<ce> меня здесь несколько часов. Какую катастрофу вы
<ce> предотвратили! Да, я уверен, что у _qgiver_ найдется
<ce> для меня несколько слов. Веди, и я пойду".

Message:  1025
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Что бы вы ни сказали, это возымело обратный
<ce> эффект. Бес пенится от ярости, а затем атакует
<ce> _mage_ и сильно бъет %g4. В ослабленном состоянии
<ce> %g1 не выдерживает удара и падает на пол.

Message:  1026
<ce> Вы хотите отказаться от этого задания?

Message:  1027
<ce> _qgiver_ недоволен, но вам все равно
<ce> разрешается отказаться от задания.

Message:  1028
<ce> Внутри этой резиденции вы слышите слабые стоны
<ce> и характерное рычание беса. Что бы здесь ни
<ce> происходило, к добру это не приведет.

Message:  1030
%qdt:
 Мне поручено вернуть _mage_ из
 ___res_ в Гильдию Магов ___qgiver_.
 Что бы ни случилось, я буду отвечать
 за безопасность своего коллеги.

Message:  1031
%qdt:
 _mage_ был перенесен в ___dung_
 с помощью %g4 собственной хитрости.

Message:  1032
%qdt:
 _mage_ страдает от тяжелой арканной
 болезни, подобной той, которую не
 может вылечить храм. Неободимо
 попытаться самостоятельно рассеять ее
 с помощью заклинания "Лечить болезнь".

QBN:
Person _qgiver_ group Questor male
Person _mage_ faction The_Mages_Guild

Clock _timer_ 90.00:00
Clock _bad1_ 0:06 0:12
Clock _bad2_ 00:05 00:08
Clock _bad3_ 00:10 00:12
Clock _death_ 1:00
Clock _wander_ 8:00 1.01:00

Place _res_ remote house
Place _dung_ remote dungeon15

Foe _imp_ is Imp

Item _reward_ magic_item

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place foe _imp_ at _res_
    place npc _mage_ at _res_
    pc at _res_ set _inside_
    pc at _dung_ set _idun_
    change foe _imp_ infighting true

_inside_ task:
    cast Shield spell on _imp_
    say 1028

_conv_ task:
    clicked npc _mage_

_conv1_ task:
    when _inside_ and _conv_ and not _slain_
    say 1010
    clear _conv_

_inj_ task:
    injured _imp_
    prompt 1023 yes _attempt_ no _flee_

variable _attempt_
variable _flee_

_bigwin_ task:
    when _attempt_ and _skilled_
    remove foe _imp_
    say 1024
    hide npc _mage_
    add _mage_ face
    start task _vict_

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1025
    hide npc _mage_
    start task _death_

_slain_ task:
    killed 1 _imp_

_skilled_ task:
    when skill Impish is at least 40

_killedimp1_ task:
    when _slain_ and _flee_
    start task _trap_

_killedimp2_ task:
    when _slain_ and not _inj_
    start task _trap_

_trap_ task:
    say 1011
    pick one of _tel_ _dis_

_dis_ task:
    start timer _bad1_
    say 1015
    hide npc _mage_
    add _mage_ face
    cast Cure_Disease spell do _cure_
    log 1032 step 2

_bad1_ task:
    start timer _bad2_
    say 1016

_bad2_ task:
    start timer _bad3_
    say 1017

_bad3_ task:
    start timer _death_
    say 1018

_death_ task:
    say 1003
    change repute with _mage_ by -2
    end quest

_cure_ task:
    say 1022
    stop timer _bad1_
    stop timer _bad2_
    stop timer _bad3_
    stop timer _death_
    start task _vict_

_tel_ task:
    say 1020
    hide npc _mage_
    log 1031 step 1
    reveal _dung_

_idun_ task:
    start timer _wander_

_notidun_ task:
    when not _idun_
    stop timer _wander_

_conv2_ task:
    when _conv_ and _idun_
    say 1021
    add _mage_ face
    start task _vict_

_wander_ task:
    when _idun_ and _wander_
    say 1021
    add _mage_ face
    start task _vict_

variable _vict_
_pcreward_ task:
    when _qgclicked_ and _vict_
    give pc _reward_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _vict_
    prompt 1026 yes _surrender_ no _continue_
    clear _qgclicked_ _clearclick_

_timer_ task:
    end quest

_surrender_ task:
    say 1027
    end quest

variable _continue_