Quest: JHMG104
DisplayName: Даэдрическое подчинение
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вопреки нашим настоятельным просьбам один 
<ce> из членов гильдии уединился в нечистой норе,
<ce> пытаясь управлять могущественным даэдра.
<ce> Такого рода ритуалы всегда опасны
<ce> и никогда не одобряются. Нам нужен кто-то, обладающий
<ce> хорошим мастерством и должной мудростью, чтобы найти
<ce> нашего друга и положить конец этому безумию.

RefuseQuest:  [1001]
<ce> Ох, беда. Мы должны найти кого-нибудь в ближайшее 
<ce> время.

AcceptQuest:  [1002]
<ce> Ожидайте серьезных опасностей, когда пойдете
<ce> туда. Это та сила, которая предназначена 
<ce> для высших уровней Гильдии Магов, и даже
<ce> представить себе, что вы сможете
<ce> контролировать ее целиком, - просто фантазия.
<ce> _mage_ отправился в ___dung_ несколько дней
<ce> назад, и наши умы не перестают волноваться 
<ce> за %g4 состояние. Пожалуйста, верните %g4 назад
<ce> в течение =timer_ дней. Если вы молитесь
<ce> богам, обратитесь к ним за помощью. Никто 
<ce> не может предсказать, что произойдет.

QuestFail:  [1003]
<ce> Хм? Я не вижу _mage_.
<ce> Вернитесь сюда, когда это изменится, %pct.

QuestComplete:  [1004]
<ce> _qgiver_ зовет нескольких слуг, которые
<ce> спешат из соседних комнат, чтобы взять
<ce> _mage_ с ваших рук.
<ce>
<ce> "Честное слово, %pcf, вы сделали невозможное.
<ce> Пятьдесят других членов гильдии, которых мы
<ce> могли послать, не знали бы, что делать. 
<ce> Ни благодарности, ни поздравления не помогут, мой
<ce> друг. Бриллиант кажется мне ничтожной платой,
<ce> но, надеюсь, он компенсирует ваши труды".

Message:  1010
<ce> Похоже, что _mage_ 
<ce> находится в глубокой концентрации. 
<ce> Вы пытаетесь привлечь %g4 внимание.
<ce>
<ce> Затем что-то идет не так. Из %g4 тела 
<ce> в разные стороны вырываются искры энергии.
<ce> Комната на мгновение становится темнее.
<ce> Когда %g4 глаза распахиваются, %g1 кричит.
<ce>
<ce> "Н-нет! Я не закончил! Ты дурак!
<ce> За то, что отвлек меня..."
<ce>
<ce> Вы осознаете, что рядом находится даэдра,
<ce> причем очень мощный. Благодаря огромному
<ce> умственному усилию _mage_, похоже,
<ce> похоже, удерживает его на месте.

Message:  1011
<ce> "Освободи меня", - кричит вам даэдра.
<ce> "Убей дурака, который меня вызвал, и я
<ce> вернусь в свое царство".
<ce>
<ce> "Нет! Не слушайте!" -
<ce> кричит в ответ _mage_.
<ce> "Это существо опасно 
<ce> и ему нельзя давать свободу!
<ce> Не вмешивайтесь!"

Message:  1012
<ce> Даэдра и маг начинают кричать
<ce> друг на друга на разных языках.
<ce> Странно, но голос _mage_ и голос
<ce> монстра начинают сливаться.
<ce> Вскоре они звучат очень похоже.
<ce>
<ce> "Вы не победите! Это мое царство!"
<ce>
<ce> "Прекратите это безумие! Я - победитель!"
<ce>
<ce> Вы чувствуете огромную силу, изливающуюся 
<ce> от обоих существ. Если даэдра прав, он покинет это
<ce> царство, как только освободится от заклинания.
<ce> Если нет, то вмешательство может иметь серьезные
<ce> последствия. Это далеко за пределами ваших
<ce> способностей. Что вы будете делать?

Message:  1013
<ce> Глаза _mage_ становятся
<ce> красными. И %g1 и даэдра кричат одни и те же слова.
<ce>
<ce> "Я =daed_, лорд Обливиона! Меня нельзя
<ce> убить! Окажите мне помощь, и я дам вам
<ce> силу! Освободите меня из этих оков!"

Message:  1014
<ce> _mage_ кричит и с огромной 
<ce> силой отбивается от ментальной атаки =daed_. 
<ce> Набирая силу, сначала незаметно, а затем все быстрее,
<ce> маг завершает заклинание связывания 
<ce> и возвращает даэдра в его собственный мир 
<ce> с ослепительной вспышкой света.
<ce>
<ce> Вы чувствуете себя измотанным, просто наблюдая
<ce> за этим зрелищем, но вызыватель полностью истощен. 
<ce> У _mage_ хватает энергии только
<ce> на то, чтобы пробормотать несколько фраз в ваш адрес,
<ce> прежде чем впасть в оцепенение. Вы подбираете
<ce> бесчувственное тело и забираете %g4 с собой.

Message:  1015
<ce> Вы наносите удар даэдра, и ментальная связь 
<ce> разделяет боль с _mage_. 
<ce> В этот момент слабости %g1 теряет бдительность
<ce> и монстр пользуется возможностью
<ce> взорвать %g4 разум изнутри. Вы видите,
<ce> как %g4 тело падает на пол.
<ce>
<ce> "Молодец, слабое создание! Теперь я могу свободно
<ce> путешествовать по этому миру, как мне 
<ce> заблагорассудится!" Оно не должно быть вам благодарно 
<ce> и идет убивать вас следующим.
<ce>
<ce> Вам придется рассказать об этом Гильдии Магов.

Message:  1016
<ce> Вы наносите удар _mage_ 
<ce> и нарушаете %g4 концентрацию. В этот момент
<ce> слабости %g1 теряет бдительность 
<ce> и монстр использует эту возможность,
<ce> чтобы взорвать %g4 разум изнутри. Вы
<ce> видите, как %g4 тело падает на пол.
<ce> Даэдра рычит от смеха.
<ce>
<ce> "Молодец, слабое создание! Теперь я могу свободно
<ce> путешествовать по этому миру, как мне 
<ce> заблагорассудится!" Оно не должно быть вам благодарно 
<ce> и идет убивать вас следующим.
<ce>
<ce> Вам придется рассказать об этом Гильдии Магов.

Message:  1017
<ce> Это просто ужасно. Я опасался худшего
<ce> и получил это. Теперь у нас хотя бы
<ce> есть поучительная история для многих
<ce> претендентов, которые входят в наши
<ce> двери. Спасибо, что вы пошли, %pcf,
<ce> хотя я и не могу вас наградить.

Message:  1030
%qdt:
 Гильдия Магов в ___qgiver_ послала
 меня вернуть члена гильдии, который
 пытается провести опасный эксперимент
 с даэдра в ___dung_. Я должен
 вернуться с нашим другом в целости 
 и сохранности в течение =timer_ дней.

Message:  1031
%qdt:
 Из-за моего вмешательства
 _mage_ погиб.

QBN:
Item _reward_ diamond

Person _qgiver_ group Questor male
Person _mage_ faction The_Cabal male

Foe _daed_ is Daedra_lord
Foe _sorc_ is Sorceror

Place _dung_ remote dungeon16

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _t1_ 00:01
Clock _t2_ 00:02
Clock _t3_ 00:03
Clock _t4_ 00:03

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _mage_ at _dung_
    reveal _dung_

_begin_ task:
    clicked npc _mage_
    say 1010
    hide npc _mage_
    place foe _sorc_ at _dung_
    create foe _daed_ every 0 minutes 1 times with 100% success
    restrain foe _sorc_
    restrain foe _daed_
    start task _sound_
    start timer _t1_

_sound_ task:
    play sound magic_savingthrow every 0 minutes 50 times

_t1_ task:
    say 1011
    start timer _t2_

_t2_ task:
    say 1012
    start timer _t3_

_t3_ task:
    say 1013
    start timer _t4_

_t4_ task:
    say 1014
    remove foe _daed_
    remove foe _sorc_
    add _mage_ face
    clear _sound_

_injtext1_ task:
    when _injd_ and not _injm_
    say 1015

_injtext2_ task:
    when _injm_ and not _injd_
    say 1016

_injm_ task:
    injured _sorc_
    clicked foe _sorc_
    stop timer _t1_
    stop timer _t2_
    stop timer _t3_
    stop timer _t4_
    remove foe _sorc_
    unrestrain foe _daed_
    change foe _daed_ infighting true
    start task _failure_
    clear _sound_
    clear _injm_

_injd_ task:
    injured _daed_
    stop timer _t1_
    stop timer _t2_
    stop timer _t3_
    stop timer _t4_
    remove foe _sorc_
    start task _failure_
    clear _sound_
    clear _injd_

_failure_ task:
    log 1031 step 1

_pcgetsgold_ task:
    when _qgclicked_ and _t4_
    give pc _reward_
    start quest JHMGC03
    end quest

_badend_ task:
    when _qgclicked_ and _failure_
    say 1017
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _begin_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest