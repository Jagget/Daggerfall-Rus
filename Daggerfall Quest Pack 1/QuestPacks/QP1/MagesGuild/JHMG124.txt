Quest: JHMG124
DisplayName: Зачистка лаборатории
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Их высокоблагородие, %rt региона %crn объявили
<ce> вне закона производство, продажу, торговлю, использование,
<ce> транспортировку и даже владение веществом, известным как
<ce> сурсум. Естественно, какой-то идиот-новичок из нашего
<ce> гильдейского зала стал "поклонником" этого вещества. Власти
<ce> обвиняют нас в "подстрекательстве", что бы это ни значило,
<ce> и требуют, чтобы мы в 24 часа зачистили здание, где
<ce> производят это вещество. Резиденция находится недалеко
<ce> отсюда. Мы предлагаем за это =reward_ золотых. Вы согласны?

RefuseQuest:  [1001]
<ce> О, да вы, должно быть, слишком важны для такого.

AcceptQuest:  [1002]
<ce> Большое спасибо, %pct. Ищите _res_ на %di отсюда.
<ce> Все должно быть очевидно, как только вы войдете.
<ce> Уничтожьте внутри все! Все инструменты для его изготовления, все!
<ce> И возвращайтесь сюда. Несомненно, это простая задача.
<ce> Принесите мне весточку завтра к этому времени.

QuestFail:  [1003]
<ce> Вы уже должны были сообщить об уничтожении лаборатории в _res_.

QuestComplete:  [1004]
<ce> Сохраняйте и впредь такую же преданность Гильдии, %pct.

Message:  1011
<ce> Как и сказал _qgiver_, наличие сурсума здесь очевидно.
<ce> Даже воздух в здании пропитан наркотическими испарениями.
<ce> Вы можете попытаться сделать все быстро, если чувствуете,
<ce> что можете выдержать испарения, или медленно, чтобы
<ce> не перегрузиться. Попробуете ли вы сделать все сразу?
<ce>
<ce> (ПРОВЕРКА АТРИБУТА: Выносливость, средняя сложность)

Message:  1012
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Выносливость)
<ce>
<ce> Это требует концентрации силы воли, но вы выдерживаете.
<ce> Вы находите и разбиваете все до самой последней мензурки.
<ce> Дешевый материал легко ломается от каждого удара.
<ce> Чтобы покончить с этим, вам понадобилось всего несколько минут.

Message:  1013
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Выносливость)
<ce>
<ce> Вы спешите выполнить задание и пытаетесь сломать как можно
<ce> больше приборов, но их слишком много. Ваше зрение становится
<ce> нечетким, а сознание заторможенным. Через некоторое время
<ce> вы просыпаетесь, сидя у стены. Вы видите, что большая часть
<ce> оборудования исчезла. У ваших ног лежит записка:
<ce> "Слишком медленно, придурок".
<ce>
<ce> Похоже, вы упустили свою возможность.

Message:  1014
<ce> Вы делаете ставку на осторожность и выполняете работу
<ce> медленно, делая частые перерывы, чтобы не выключиться под
<ce> воздействием паров вещества. Вы как раз заканчивали работу,
<ce> когда услышали два голоса. Похоже, что кто-то расстроен тем,
<ce> что их оборудование уничтожено и они хотят проучить вас за это.

Message:  1015
<ce> Вы не справились с заданием! Это объясняет сообщения о том,
<ce> что в городе можно купить борудование для производства
<ce> сурсума. Возможно, немного рассудительности с вашей стороны
<ce> позволит избежать подобных ошибок в будущем, %pct.
<ce> Я закрываю контракт, но вы ничего не получите. У нас будет
<ce> достаточно проблем, когда %rt прибудут с допросом.

Message:  1016
<ce> Отличная работа, %pct. Городская стража наблюдала за вами
<ce> со стороны и отметила вашу работу. Провинившийся новичок
<ce> уже изгнан, к моей радости. Что касается вас, то вот ваше
<ce> золото. Позвольте, я пересчитаю...


Message:  1030
%qdt:
 __qgiver_ попросили меня зачистить лабораторию сурсума
 в доме под названием _res_ в ___res_
 за =reward_ золотых. Мне нужно сообщить
 о выполнении задания в течение 24 часов.

Message:  1031
%qdt:
 Пары сурсума одурманили и усыпили меня,
 а в это время оборудование лаборатории было вынесено.
 У меня нет другого выбора, кроме как сообщить
 _qgiver_ о неудаче.

QBN:
Item _reward_ gold range 150 to 250

Person _qgiver_ group Questor male
Person _mg_ faction The_Mages_Guild

Place _res_ local house

Clock _timer_ 1.00:00

Foe _e1_ is Burglar
Foe _e2_ is Rogue

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
    pc at _res_ do _inside_

_inside_ task:
    start task _begin_

_begin_ task:
    prompt 1011 yes _attempt_ no _flee_

variable _attempt_

_skilled_ task:
	when attribute Endurance is at least 50

_easywin_ task:
	when _attempt_ and _skilled_
	say 1012
    start task _success_

_bigfail_ task:
	when _attempt_ and not _skilled_
	say 1013
    log 1031 step 0

_flee_ task:
    say 1014
    create foe _e1_ every 0 minutes 1 times with 100% success
    change foe _e1_ infighting true
    create foe _e2_ every 0 minutes 1 times with 100% success
    change foe _e2_ infighting true
    start task _success_

variable _success_

_qgclicked_ task:
	clicked npc _qgiver_

_badending_ task:
	when _bigfail_ and _qgclicked_
    say 1015
    change repute with _mg_ by -3
    give pc nothing
    end quest

_goodending_ task:
	when _success_ and _qgclicked_
    say 1016
    give pc _reward_
    end quest

_clearclick_ task:
	when _qgclicked_ and not _attempt_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest