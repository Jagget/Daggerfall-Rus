Quest: JHMG127
DisplayName: Гильдейские взносы
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Одного членов нашей Гильдии отправили собирать
<ce> гильдейские взносы в ___tavern_. К сожалению,
<ce> никаких вестей оттуда не поступало, и мы считаем,
<ce> что какое-то мерзкое существо одолело _scholar_.
<ce> Это крайне неловкая ситуация, но что еще хуже,
<ce> у н%g4 были все наши деньги. Не согласитесь ли вы
<ce> обыскать город, найти %g4 и вернуть средства?

RefuseQuest:  [1001]
<ce> Магия важна, но деньги тоже, %pct.

AcceptQuest:  [1002]
<ce> Очень хорошо. Отправляйтесь в ___tavern_,
<ce> в _tavern_. Это должен был быть обычный сбор
<ce> гильдейских взносов, но _scholar_ слишком
<ce> долго не возвращается и у нас нет никакой
<ce> информации почему. Пожалуйста, отправляйтесь
<ce> туда и найдите _scholar_, это =scholar_.
<ce> Будьте здесь не позже, чем через =timer_ дней,
<ce> чтобы получить =reward_ золотых.

QuestFail:  [1003]
<ce> Время сообщить о пропавших
<ce> средствах ___tavern_ прошло.

QuestComplete:  [1004]
<ce> Хммм. _scholar_ всего лишь Подмастерье и,
<ce> вероятно, останется им еще какое-то время.
<ce> Мы ведем учет тех, кто правильно выполняет
<ce> свою работу, и редко забываем такую небрежность.
<ce> Запомните это, %pcf. Но дело решено,
<ce> и все благодаря вам.

Message:  1010
<ce> «_scholar_? Нет, здесь нет никого с таким именем.
<ce> Неа. Может, стоит вернуться к вашим магам и сказать
<ce> им об этом. Я знаю всех этих людей, и никого из них
<ce> не зовут _scholar_».
<ce>     
<ce> Простое заклятие чарующей магии поможет развязать язык.
<ce> Пытаетесь ли вы затуманить разум? Если вас поймают,
<ce> последствия могут быть серьезными.
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Тауматургия, низкая сложность)

Message:  1011
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Глаза этого человека медленно закрываются, пока вы
<ce> тихо читаете заклинание. Когда они открываются снова,
<ce> вы видите совершенно другое отношение к себе.
<ce>      
<ce> «Знаете что? Мне нужно еще выпить. День был долгим.
<ce> Извините за то, как я с вами обращался. _scholar_
<ce> весь день то появляется, то исчезает. Говорит,
<ce> что последние несколько должников сопротивляются,
<ce> и не хочется говорить магам, пока все не будет собрано.
<ce> Подождите здесь несколько минут и сами все увидите».

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы пытаетесь применить чарующий эффект, но этот
<ce> посетитель сопротивляется ему. Следует гневный крик.
<ce>      
<ce> «СТРАЖА! %ra пытается меня загипнотизировать!
<ce> Злодей, вот кто это! Кто-нибудь, помогите!»
<ce>      
<ce> Следует поспешное бегство, и городская стража уже в пути.

Message:  1013
<ce> Без явной помощи в таверне лучшее, что вы можете
<ce> сделать - это остаться здесь, в ___tavern_, и продолжить
<ce> поиски. Наверняка что-нибудь случится, что подтолкнет
<ce> вас в нужном направлении.

Message:  1014
<ce> К вам подходит =scholar_ с недовольным видом.
<ce>      
<ce> «Они послали кого-то проверить меня! Эти идиоты!
<ce> Наглецы! Я почти собрал деньги, а теперь они тратят
<ce> и ваше время впустую!»
<ce>      
<ce> _scholar_ хмурится и показывает вам
<ce> довольно большой мешок с золотом.
<ce>      
<ce> «У меня готово заклинание Возврата, и я буду
<ce> в ___qgiver_, когда соберу последний взнос.
<ce> Возможно, я даже буду там раньше вас. Скажите им,
<ce> чтобы отстали от меня и были благодарны за то,
<ce> что я делаю их работу».
<ce>      
<ce> _scholar_ уходит не прощаясь, чтобы закончить задание.

Message:  1015
<ce> Что вы здесь делаете?
<ce> Вы должны были быть в ___tavern_!

Message:  1016
<ce> Затем этот посетитель уходит, ища кого-нибудь
<ce> более приятного для разговора.

Message:  1030
%qdt:
 Некто по имени _scholar_ отправился
 в ___tavern_ собирать гильдейские взносы,
 но срок задания вышел и у Гильдии нет
 никакой информации. Мне нужно отправиться туда,
 поискать в _tavern_ и убедиться,
 что средства прибудут в ___qgiver_
 в течение =timer_ дней. Я ищу =scholar_.
 Моя награда составит =reward_ золотых.

Message:  1031
%qdt:
 Состоялась встреча с _scholar_, и мне было обещано,
 что средства скоро прибудут в ___qgiver_.

Message:  1032
%qdt:
 Никого по имени _scholar_ в _tavern_
 обнаружено не было.
 Мне придется найти другой способ.

QBN:
Item _reward_ gold range 150 to 250

Person _qgiver_ group Questor male
Person _scholar_ faction The_Mages_Guild male
Person _friend_ faction The_Thieves_Guild

Place _tavern_ remote tavern

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _shortcut_ 0:01 0:04
Clock _longcut_ 6:00 8:00

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place npc _friend_ at _tavern_
    pc at _tavern_ set _inside_

_begin_ task:
	clicked npc _friend_
	prompt 1010 yes _attempt_ no _avoid_

_avoid_ task:
    say 1016
    hide npc _friend_

variable _attempt_

_skilled_ task:
	when skill Thaumaturgy is at least 25

_easywin_ task:
	when _attempt_ and _skilled_
	say 1011
    start timer _shortcut_

_shortcut_ task:
    start task _success_

_bigfail_ task:
	when _attempt_ and not _skilled_
	say 1012
	setplayercrime Assault
    spawncityguards immediate
    hide npc _friend_

_inside_ task:
    start task _setwire_

variable _setwire_

_reminder_ task:
    when _setwire_ and not _inside_ and not _easywin_
    say 1013
    start timer _longcut_
    hide npc _friend_
    log 1032 step 1

_longcut_ task:
    start task _success_

_success_ task:
	say 1014
    log 1031 step 1

_pcgetsgold_ task:
	when _qgclicked_ and _success_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _success_
    say 1015
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest