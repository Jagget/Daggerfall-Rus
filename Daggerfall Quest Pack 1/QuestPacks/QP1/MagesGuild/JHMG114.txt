Quest: JHMG114
DisplayName: Падение тьмы
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Один из наших местных ученых, _schol_,
<ce> вносит вклад в работу по борьбе с
<ce> последствиями вампиризма. Позже вечером
<ce> %g1 отправится в _res_, чтобы
<ce> сотрудничать с одним из наиболее
<ce> уважаемых охотников на вампиров в %crn.
<ce> Для этой встречи необходим надежный
<ce> эскорт, и мне почему-то пришли в голову
<ce> мысли о вас. Будете ли вы сопровождать его?

RefuseQuest:  [1001]
<ce> Похоже, задание вновь оказалось в стопке нерешенных проблем.
   <--->
<ce> Посмотрим на вашу реакцию, когда вы заболеете гемофилией.

AcceptQuest:  [1002]
<ce> Ага, вот и чемпион, достойный звания %pct.
<ce> Превосходно. Но боюсь, что сначала вам
<ce> предстоит найти _schol_, поскольку %g1, по
<ce> всем признакам, исчез. Такие риски работы
<ce> с эксцентричным интеллектом. Вы должны
<ce> быть в _res_ сегодня к 11:00. _hunt_ будет
<ce> координировать вашу встречу, а вы должны
<ce> доставить %g4 отчет мне. Не возвращайтесь
<ce> без документа и не опаздывайте в
<ce> резиденцию. Прощайте и удачи.

QuestFail:  [1003]
<ce> Вы должны были быть в _res_ сегодня к
<ce> 11:00. Вы пропустили важную встречу.

QuestComplete:  [1004]
<ce> Невзирая на все трудности, наше исследование несомненно приблизит нас
<ce> к искоренению вампиризма раз и навсегда. Гордитесь вкладом в нашу
<ce> общую работу. Пусть это небольшое вознаграждение не полностью отражает
<ce> глубину вашей заслуги, но мы настаиваем, чтобы вы приняли это как
<ce> выражение нашей благодарности из наших скромных запасов.

RumorsDuringQuest:  [1005]
В ___vampres_, по-видимому, проблема с вампирами в _vampres_.
   <--->
Кучка вампиров ошивается в _vampres_ в ___vampres_.

Message:  1010
<ce> А, это вы, %ra? Вы ищете меня? Какова причина?
<ce> О, все эти суматохи! Да, я признаю, пришло время
<ce> нам отправляться. Прошу прощения за мою кажущуюся
<ce> растерянность. Уверяю вас, это не намеренно.
<ce> Что ж, без промедления, давайте поторопимся.

Message:  1011
<ce> По всему видно, что ваше назначение было
<ce> не случайным. Звуки воя и шипения усиливаются,
<ce> отразившись от стен резиденции.
<ce>
<ce> _schol_, явно ошеломленный, отходит от вас,
<ce> принимая что-то вроде боевой позиции.
<ce> Внезапно %g1 исчезает.

Message:  1012
<ce> Из глубины резиденции доносится стон _schol_,
<ce> за которым следует звук падения чего-то
<ce> тяжелого на землю.

Message:  1013
<ce> _schol_ появляется из-за угла:
<ce>
<ce> "Эх, это был настоящий выброс адреналина! Не
<ce> удивительно, что они отправили именно вас! Мне
<ce> искренне жаль тех немногих, кто ранее пытался
<ce> противостоять вампиризму".
<ce>
<ce> Вы замечаете, как голос %g4 дрожит. Очевидно, что %g4
<ce> беспокойное поведение скорее результат недавних
<ce> переживаний, чем %g4 естественного характера.

Message:  1014
<ce> Осматривая тело _hunt_, вы
<ce> обнаруживаете пустой, грубо
<ce> разорванный конверт. Отчет исчез.

Message:  1015
<ce> _schol_ выглядит крайне взволнованным.
<ce>
<ce> "Исчез? Черт побери. Все, что у меня
<ce> есть — это предположение расспросить
<ce> местных о каких-либо слухах о вампирах.
<ce> Я не могу придумать более разумного пути
<ce> действий. Буду действовать без
<ce> промедления и обязательно сообщу вам,
<ce> если что-то выясню. О, %god, будем
<ce> надеяться, что у них были более веские
<ce> причины для кражи отчета, чем просто его уничтожение".
<ce>
<ce> Вздохнув глубоко, %g1, спотыкаясь, устремляется прочь.

Message:  1020
<ce> Переступив порог этого дома, вы становитесь
<ce> свидетелем того, как строение взрывается волнами
<ce> гневных воплей. Слухи, похоже, не обманули.

Message:  1021
<ce> Вы возвращаете себе отчет. _qgiver_ несомненно
<ce> захочет получить его в кратчайшие сроки.

Message:  1030
%qdt:
 _qgiver_ из Гильдии Магов
 поручил мне сопровождать
 ученого по вампиризму, _schol_,
 в _res_, ___res_
 сегодня в 11:00. Мне нужно
 получить отчет охотника _hunt_
 и доставить его _qgiver_.

Message:  1031
%qdt:
 В _res_ нам помешали
 вампиры. Они убили
 _schol_, и украли отчет.
 Мне нужно будет найти
 эту группу вампиров и
 забрать документ.

Message:  1032
%qdt:
 В _res_ нам помешали
 вампиры. Они украли
 отчет. Необходимо
 поспрашивать у
 горожан, чтобы узнать,
 где в ____res_ могут
 собираться вампиры.

Message:  1050
<ce> Небрежно выглядящий паж в ниспадающей мантии
<ce> вручает вам письмо, после чего исчезает.

Message:  1051
%pcn,
<ce>
  У меня есть хорошие новости. Мои наблюдения
показали, что в ___vampres_ есть вампирский
курорт под названием _vampres_. Мой путь привел
меня туда по другой причине, но мне известно,
что вам доверено хранение отчета _hunt_. Будем
надеяться, что документ хранится там.
<ce>
             _schol_
             ___res_

Message:  1060
<ce> С возвращением, %pct. Скорбные известия о
<ce> кончине _schol_ и _hunt_ дошли до меня. Их
<ce> вклад в благо %crn невозможно переоценить, и их
<ce> уход глубоко опечалил мою душу. Но я уверен,
<ce> что вы сделали все возможное. Тот факт, что вы
<ce> смогли принести отчет, несомненно радует.

Message:  1061
<ce> С возвращением, %pct! Глубокая печаль охватила
<ce> меня после вести о кончине _hunt_, но я ободрен,
<ce> что вы и _schol_ до сих пор с нами. Некоторых
<ce> трагедий, к сожалению, невозможно избежать. Вы
<ce> должны нести гордость за выполненную вами работу.
<ce> Я также хочу подчеркнуть, что _schol_ внес личный
<ce> вклад в ваше вознаграждение.

Message:  1080
<ce> Этот отчет насыщен специфической терминологией, связанной
<ce> с весьма узкоспециализированными аспектами восстановительной
<ce> магии и алхимии. Вы понимаете его только отчасти.

QBN:
Item _reward_ gold range 1000 to 1200
Item _bigreward_ gold range 1500 to 2000
Item _report_ letter used 1080
Item _letter_ letter used 1051
Item _body_ dead_body

Person _qgiver_ group Questor male
Person _schol_ faction The_Mages_Guild
Person _hunt_ faction Generic_Knightly_Order

Place _res_ local house
Place _hide_ local magery
Place _vampres_ remote house

Clock _failsafe_ 60.00:00
Clock _scholdead_ 00:05
Clock _lettersend_ 6.00:00 9.00:00

Foe _vamp_ is Vampire
Foe _vamp2_ is Vampire

--	Quest start-up:
    start timer _failsafe_
    log 1030 step 0
    pc at _res_ do _inside_
    pc at _vampres_ do _fight_
    have _report_ set _announce_
    place npc _schol_ at _hide_

_arrive_ task:
    daily from 23:00 to 23:59
    setvar _trip_

variable _inside_
variable _trip_
_toolate_ task:
    when _trip_ and not _meeting_ and not _arrive_
    say 1003
    end quest

_join1_ task:
    clicked npc _schol_
    say 1010
    hide npc _schol_
    add _schol_ face

_spawn_ task:
    when _arrive_ and _join1_ and _inside_
    start task _meeting_
    say 1011
    create foe _vamp_ every 0 minutes 2 times with 100% success
    place item _body_ at _res_
    drop _schol_ face

_infight_ task:
    daily from 0:00 to 23:59
    change foe _vamp_ infighting true
    change foe _vamp2_ infighting true
    clear _infight_

_startdeath_ task:
    injured _vamp_
    start timer _scholdead_

variable _meeting_
_scholdead_ task:
    say 1012

_slain_ task:
    killed 2 _vamp_
    stop timer _scholdead_

_rejoin_ task:
    when _slain_ and not _scholdead_
    say 1013
    add _schol_ face

_grabbody_ task:
    clicked item _body_
    take _body_ from pc
    say 1014

_info1_ task:
    when _grabbody_ and _slain_ and _scholdead_
    log 1031 step 1

_info2_ task:
    when _grabbody_ and _rejoin_
    log 1032 step 1
    say 1015
    start timer _lettersend_
    drop _schol_ face

_lettersend_ task:
    give pc _letter_ notify 1050

_fight_ task:
    say 1020
    create foe _vamp2_ every 0 minutes 4 times with 90% success
    give item _report_ to _vamp2_

_announce_ task:
    say 1021

_pcgetsgold_ task:
    toting _report_ and _qgiver_ clicked

_victory_ task:
    when _pcgetsgold_ and _scholdead_
    say 1060
    give pc _reward_
    end quest

_bigvictory_ task:
    when _pcgetsgold_ and not _scholdead_
    say 1061
    give pc _bigreward_
    end quest

_failsafe_ task:
    end quest