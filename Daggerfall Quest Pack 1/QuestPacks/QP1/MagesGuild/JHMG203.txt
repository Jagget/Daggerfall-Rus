Quest: JHMG203
DisplayName: Чернокнижник
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pct, %crn предоставляет нам значительную
<ce> автономию в отношении наших исследовательских
<ce> методов, целей, дисциплинарных занятий и так
<ce> далее, при условии, что наша ученая работа
<ce> будет способствовать "общему благу".
<ce> Занимательный термин, не так ли? Взамен у нас
<ce> есть нечто вроде негласного соглашения, если
<ce> хотите: когда студенты магических искусств
<ce> становятся угрозой обществу, мы первые
<ce> устраняем угрозу. Пожалуйста, помогите нам
<ce> выполнить это соглашение, %pcf.
   <--->
<ce> Вокруг Гильдии Магов кружатся великие скандалы,
<ce> многие из которых раздуваются теми, кто завидует
<ce> мощи, которой мы обладаем. Чтобы искра не переросла
<ce> в пожар, мы должны быстро искоренять любую причину
<ce> для подозрений. Один негодяй, входящий в сообщество
<ce> нашего почтенного учреждения, воспользовался нашим
<ce> добрым именем для проведения самых мерзких
<ce> исследований, подвергая нас всех опасности потерять
<ce> репутацию. Уничтожите этого отступника и поможете
<ce> нам сохранить лицо?

RefuseQuest:  [1001]
<ce> Уверяю вас, %pct, если бы вы позволили мне
<ce> объяснить больше, вы бы не отказались.

AcceptQuest:  [1002]
<ce> Достойный ответ. Эта обязанность
<ce> ложится на каждого члена гильдии в свое
<ce> время. Мы отправим вас в ___res_.
<ce> Член гильдии в городе, чрезвычайно надежный,
<ce> сообщает нам, что беглый маг, _necr_,
<ce> прячется и занимается своими делами
<ce> в _res_. Надеюсь, некромантия не вызывает
<ce> у вас страха, ведь именно ее вы
<ce> согласились подавить сегодня.
<ce> Превратите этого мятежного мага в то,
<ce> что так его завораживает, - труп.
<ce> Мы должны знать, что это сделано в течение
<ce> =timer_ дней. Смелости вам, добрый друг.

QuestFail:  [1003]
<ce> Гильдия Магов ждала вашего
<ce> объявления о смерти _necr_,
<ce> и теперь это время вышло.

QuestComplete:  [1004]
<ce> Великолепно выполнено, %pct. Эта работа
<ce> обеспечивает доверие общественности
<ce> к Гильдии Магов. Будет ли _diam_
<ce> достойной наградой за это задание,
<ce> по вашему мнению? Надеюсь, вы сумеете его
<ce> хорошо использовать.

Message:  1010
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Разрушение)
<ce>
<ce> Некромант активирует ловушку, посылая в вашу
<ce> сторону волшебный снаряд. Ваши быстрые рефлексы
<ce> и знание магии Разрушения позволяют вам остановить
<ce> снаряд прямо в воздухе и вернуть его отправителю.
<ce> "АЙ!" - вздрагивает ваш враг.

Message:  1011
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Разрушение)
<ce>
<ce> Некромант активирует ловушку, посылая
<ce> в вашу сторону волшебный снаряд. Кто-то,
<ce> более искусный в магии Разрушения, мог
<ce> бы избежать его тем или иным способом,
<ce> но вы получаете травму.

Message:  1020
<ce> "Какая жалость. Почему вы, дураки,
<ce> продолжаете сопротивляться мощи
<ce> и красоте искусства некромантии?
<ce> Разве вы не видите лицемерия в том,
<ce> чтобы убить меня? Но если вы настаиваете,
<ce> то у нас будет дуэль. Дерзайте".

Message:  1022
<ce> Некромант падает на землю.
<ce> Ваш долг выполнен.

Message:  1030
%qdt:
 Гильдия Магов послала меня
 в ___res_, чтобы избавиться
 от некроманта, который скрывается
 в _res_. Мне необходимо
 вернуться в ___questgiver_
 в течение =timer_ дней.

QBN:

Person _questgiver_ group Questor male
Person _necr_ group Spellcaster

Item _diam_ diamond

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Mage
Foe _und1_ is Ghost
Foe _und2_ is Wraith

-- Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _necr_ at _res_
  pc at _res_ set _inside_

variable _inside_

_skilled_ task:
  when skill Destruction is at least 40

_trapdamage_ task:
  when _skilled_ and _spawn_
  say 1010
  cast Wizard's_Fire spell on _enemy_

_suffertrap_ task:
  when _spawn_ and not _skilled_
  say 1011
  reduce player health by 18

_conv_ task:
  clicked npc _necr_
  say 1020
  hide npc _necr_
  place foe _enemy_ at _res_
  change foe _enemy_ infighting true
  change foe _enemy_ team 13

_spawn_ task:
  when _inside_ and _conv_ and not _slain_
  create foe _und1_ every 0 minutes 1 times with 10% success
  create foe _und2_ every 0 minutes 1 times with 10% success
  change foe _und1_ infighting true
  change foe _und2_ infighting true

_slain_ task:
  killed _enemy_
  say 1022
  remove foe _und1_
  remove foe _und2_

_victory_ task:
  when _qgclicked_ and _slain_
  give pc _diam_
  end quest

_qgclicked_ task:
  clicked npc _questgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_timer_ task:
  end quest