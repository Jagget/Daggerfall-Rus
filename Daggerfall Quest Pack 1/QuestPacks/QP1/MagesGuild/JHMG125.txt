Quest: JHMG125
DisplayName: Среди зверей
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Мы получили от члена верховного совета поручение,
<ce> к которому нельзя относиться легкомысленно. Наградой
<ce> станет магический предмет, способный защитить вас барьером,
<ce> а также несколько тысяч золотых. Но это задание постоянно
<ce> будет держать вас на грани смерти. Пожалуйста, выбирайте 
<ce> мудро.

RefuseQuest:  [1001]
<ce> Ну что ж, лучше быть осторожным героем, чем мертвым.

AcceptQuest:  [1002]
<ce> Пещера под названием ___dung_ хранит древний
<ce> и могущественный том. Его нужно доставить сюда для изучения.
<ce> Величайшая сложность заключается в том, что первооткрыватель
<ce> тома всего несколько недель назад рассказал нам, что пещера
<ce> истощает жизненную силу любого, кто в нее входит.
<ce> Надеюсь, ради вашего же блага, что вы владеете
<ce> магией невидимости, потому что восстановление сил
<ce> вам не поможет.
<ce>
<ce> Если сможете доставить том сюда за =timer_ д.,
<ce> вы получите награду. Если же пройдет больше времени, мне
<ce> придется передать ее другим, более быстрым кандидатам.
<ce> Счастливого пути, %pct!

QuestFail:  [1003]
<ce> __questgiver_ ожидает том из пещеры к этому времени.

QuestComplete:   [1004]
<ce> Хм, этот том в худшем состоянии, чем я ожидал.
<ce> Только %god знает, сколько он там пролежал.
<ce> Но хватит разговоров. Вы ведь хотите денег, не так ли?
<ce> Деньги, конечно, вот они. У меня есть =gold_ золотых,
<ce> и я отдаю их вам лично. И вот этот зачарованный
<ce> предмет, чтобы защитить вас в будущих приключениях.
<ce> Отличная работа, %pct.

Message:  1010
<ce> Вы сразу же ощущаете разрушительную силу, о которой
<ce> предупреждали в Гильдии. Эта пещера будет полна
<ce> опасностей из-за нее.

Message:  1011
<ce>  Вы нашли том для Гильдии Магов.

Message:  1030
%qdt:
 Том великой древности и ценности лежит в ___dung_.
 __questgiver_ из ___questgiver_ послали меня
 забрать его и доставить в течение =timer_ д. Пещера может
 причинить мне вред, пока я буду внутри.

QBN:
Item _gold_ gold range 3000 to 4000
Item _reward_ item class 4 subclass 15
Item _tome_ item class 10 subclass 12

Person _questgiver_ group Questor male

Place _dung_ remote dungeon6

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _tome_ at _dung_
    pc at _dung_ do _inside_
    reveal _dung_

_inside_ task:
    say 1010
    reduce player health by 2 every 5 seconds

_conv_ task:
    clicked item _tome_
    say 1011

_pcgetsgold_ task:
    toting _tome_ and _questgiver_ clicked
    get item _gold_
    make _gold_ permanent
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest