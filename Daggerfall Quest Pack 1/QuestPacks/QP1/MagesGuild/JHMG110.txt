Quest: JHMG110
DisplayName: Паршивая овца
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Тихо, %pcf. Говорите тише. Мы придумали задачу,
<ce> предназначенную именно для вас. В ней есть элементы
<ce> битвы, исследований, телепортации и, самое главное,
<ce> требуется ваша полная преданность Гильдии. Пожалуйста,
<ce> скажите мне, что вы согласны помочь нам в этом.
   <--->
<ce> Имеется весьма сложное и деликатное дело, достойное 
<ce> вашего внимания, %pcf. Нам необходимо избавиться 
<ce> от члена Гильдии, обладающего высоким рангом, 
<ce> и для успешного выполнения этой миссии потребуются 
<ce> ваши мастерство, ум и хитрость. 
<ce> Вы согласны принять это задание?

RefuseQuest:  [1001]
<ce> Но вы идеально подходите для этой задачи!
<ce> Что я могу сделать, чтобы убедить вас?

AcceptQuest:  [1002]
<ce> Тогда позвольте мне рассказать вам об этом деле. 
<ce> ___qgiver_ является домом 
<ce> для _mark_, одного из самых талантливых 
<ce> магов во всем %crn. Но %g1 также вызывает 
<ce> мертвых для своих грязных целей. Если ничего 
<ce> не предпринимать, через некоторое время %g1 будет 
<ce> новым архимагистром. Этого нельзя допустить.
<ce>
<ce> Возьмите этот _gem_, что зачарован на телепортацию
<ce> в ___dungeon_. Не направляйте 
<ce> на себя, потому что это сотрет эффект заклинания
<ce> "Возврат" и оставит вас в глубинах без возможности
<ce> выбраться. Приведите _mark_ 
<ce> к _res_, ___res_, и используйте _gem_ 
<ce> там. Затем тоже идите в пещеру и убейте %g4 там. 
<ce> Скорее всего, у вас будет не более =timer_ дней, 
<ce> как только %g1 будет телепортирован туда. 
<ce> Вы готовы? Желаю удачи.
<ce>
<ce> У-у-у, _mark_! Мы собираемся  
<ce> показать вам эксперимент, который нам так нравится!

QuestFail:  [1003]
<ce> _mark_ к этому времени, должно быть, 
<ce> уже замышляет месть, выбравшись из ___dungeon_.

QuestComplete:  [1004]
<ce> Честное слово, %pcf, вы оказали нам большую
<ce> услугу. Ущерб, который понесла бы Гильдия
<ce> Магов как учреждение, укрывающее некроманта
<ce> в своей администрации, не поддавался бы
<ce> описанию. Я сообщу архимагистру. Примите
<ce> этот волшебный _reward_.

RumorsDuringQuest:  [1005]
_mark_ всегда был таким милым человеком.
   <--->
Говорят, _mark_ воскрешает мертвых.
   <--->
_mark_ - восходящая звезда в Гильдии Магов.

Message:  1010
<ce> Из одной из комнат, удаленной от посторонних
<ce> глаз, выходит _mark_.
<ce>
<ce> "Вы мое сопровождение? Что ж, это подойдет.
<ce> Тогда давайте отправимся в ___res_".
<ce>
<ce> _qgiver_ нервно
<ce> улыбается вам и желает удачи.

Message:  1020
<ce> "Очень хорошо, %pct. Давайте продолжим.
<ce> Покажите мне эксперимент, о котором так
<ce> печется Архимагистр".
<ce>
<ce> Сейчас самое время использовать _gem_.

Message:  1021
<ce> Вы поднимаете _gem_ в воздух, и он ярко
<ce> вспыхивает. _mark_ вскрикивает 
<ce> в ступоре, %g4 магическая сила была
<ce> вытянута из н%g4 во время телепортации.
<ce> Через несколько секунд в комнате все
<ce> становится как прежде, только без
<ce> вашего коллеги. Осталось найти %g4 
<ce> в ___dungeon_ и положить этому конец.

Message:  1022
<ce> _mark_ яростно сверкает глазами.
<ce>
<ce> "Я знал, что это произойдет! Я знал это с самого 
<ce> начала! Когда я закончу с вами, 
<ce> все в ___qgiver_ будут 
<ce> жариться на медленном огне!"
<ce>
<ce> %g1 произносит заклинание.

Message:  1023
<ce> _mark_ падает замертво.
<ce> Сражение окончено. Вы берете %g4
<ce> официальную печать в качестве доказательства.

Message:  1030
%qdt:
 _qgiver_ дал мне секретное
 задание, касающееся _mark_ 
 из Гильдии Магов в ___qgiver_.
 Мы должны вместе отправиться
 в ___res_ в дом под названием
 _res_, где я использую _gem_,
 приготовленный Гильдией
 Магов. Затем мне нужно
 отправиться в ___dungeon_ 
 и завершить начатое дело.

Message:  1050
<ce> _mark_ находит ваше поведение слишком
<ce> подозрительным и отходит в сторону.
<ce>
<ce> "На мою жизнь было совершено слишком много
<ce> покушений. Неважно, являетесь ли вы %pct 
<ce> или учеником, для меня это ничего не значит. 
<ce> Я не желаю иметь с вами ничего общего".
<ce>
<ce> Прежде чем Вы успеваете сказать хоть слово, 
<ce> %g1 уходит.

QBN:
Item _reward_ magic_item
Item _gem_ gem

Person _qgiver_ group Questor male
Person _mark_ faction The_Mages_Guild

Place _dungeon_ remote dungeon12
Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _quick_ 00:02
Clock _failsafe_ 120.00:00

Foe _enemy_ is Mage
Foe _undead_ is Wraith

--	Quest start-up:
    pc at _res_ set _inside_
    log 1030 step 0
    reveal _dungeon_
    add _mark_ face saying 1010
    get item _gem_
    start timer _failsafe_

_inside_ task:
    _gem_ used do _send_
    say 1020
    start timer _quick_

_quick_ task:
    end quest saying 1050

_send_ task:
    stop timer _quick_
    drop _mark_ face
    place foe _enemy_ at _dungeon_
    take _gem_ from pc saying 1021
    start timer _timer_

_inj_ task:
    injured _enemy_
    clicked foe _enemy_
    say 1022
    create foe _undead_ every 0 minutes 1 times with 100% success
    change foe _enemy_ infighting true
    change foe _undead_ infighting true

_slain_ task:
    killed _enemy_
    say 1023

_qgclicked_ task:
    clicked npc _qgiver_

_pcgetsgold_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    change repute with _mark_ by +5
    start quest JHMGC07
    end quest

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest

_failsafe_ task:
    end quest