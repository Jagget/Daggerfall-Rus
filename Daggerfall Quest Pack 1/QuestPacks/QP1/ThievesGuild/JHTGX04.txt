Quest: JHTGX04
DisplayName: Одинокий волк
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX04

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX04

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX04

QuestFail:  [1003]
<ce> Ворон определенно не
<ce> хотел бы, чтобы вы потратили столько времени.

QuestComplete:  [1004]
<ce> Ворон приятно улыбается.
<ce>
<ce> "Что ж, отличная работа, мой друг.
<ce> Я не собираюсь много платить за эту работу,
<ce> так как делю свою зарплату на троих,
<ce> но следующее дело принесет гораздо больше,
<ce> обещаю. Надеюсь, это научит
<ce> Гильдию Бойцов не преследовать
<ce> нас больше, верно, дитя?"
<ce>
<ce> Он протягивает вам мешочек с монетами.

RumorsDuringQuest:  [1005]
Говорят, что Ворон снова появляется в заливе. Я думаю, что это самозванец.
   <--->
Вы слышали о том, что Ворон появился в каком-то городе? Скорее всего, это ложь.
   <--->
Говорят, что Ворон где-то рядом, но мне казалось, что он давно умер.

RumorsPostSuccess:  [1007]
Некто по имени %pcf ненавидит Гильдию Бойцов.
   <--->
Говорят, что %pcf и Ворон - убийцы.

Message:  1010
<ce> Мы должны кое-что изменить.
<ce> Я собираюсь отправиться в _tavern_
<ce> здесь, в городе. Работа уже сделана,
<ce> и я собираюсь сам съездить за органами. Проблема в том,
<ce> что кто-то предупредил группу Гильдии Бойцов
<ce> и они направились туда, как будто это их личная таверна.
<ce> Не знаю, кто им заплатил за то, чтобы они вернулись
<ce> за трупом. В любом случае, сначала проверьте тело,
<ce> а потом останьтесь и проследите, чтобы его никто не трогал.
<ce> Убивайте всех, кто туда заходит. Я буду там после того,
<ce> как закончится драка. Не теряйте времени.

Message:  1030
%qdt:
 Ворон сказал мне, чтобы я приехал
 в _tavern_ здесь, в ___tavern_,
 и отбился от группы воинов из Гильдии
 Бойцов, которые придут, чтобы забрать
 тело своего товарища.
 Он велел мне добраться туда как можно скорее.
 После того как я сам проверю труп, мне нужно
 находиться возле него на страже до тех пор, пока
 Ворон не появится.

Message:  1070
<ce> Похоже, это то самое тело.
<ce> Вы сдвигаете его с места, и проходит
<ce> всего несколько секунд, прежде чем тяжелые
<ce> шаги раздаются поблизости.

Message:  1080
<ce> Желаете ли вы выкинуть _lode_
<ce> и отказаться от своих связей с Вороном?

Message:  1081
<ce> Вы выбрасываете маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат
<ce> только ему.

Message:  1082
<ce> Вы возвращаете _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:
Person _fg_ faction The_Fighters_Guild

Place _tavern_ local tavern

Item _reward_ gold range 601 to 899
Item _lode_ lodestone
Item _body_ dead_body

Clock _delay_ 00:01
Clock _timer_ 8:00

Foe _warr_ is Warrior

--	Quest start-up:
  say 1010
  log 1030 step 0
  get item _lode_
  place item _body_ at _tavern_
  _lode_ used do _prompt_
  start timer _timer_

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_grab_ task:
  clicked item _body_
  take _body_ from pc
  say 1070
  create foe _warr_ every 0 minutes 3 times with 100% success

_infight_ task:
  daily from 0:00 to 23:59
  change foe _warr_ infighting true
  clear _infight_

_slain_ task:
  killed 3 _warr_
  start timer _delay_
  change repute with _fg_ by -5

_delay_ task:
  give pc _reward_
  start quest JHTGX05
  end quest

_timer_ task:
  say 1003
  end quest