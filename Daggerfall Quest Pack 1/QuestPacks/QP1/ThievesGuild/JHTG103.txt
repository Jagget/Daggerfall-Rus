Quest: JHTG103
DisplayName: Немного грабежа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> На этот раз у нас немного больше реальных действий.
<ce> Есть некий документ о праве собственности на дом
<ce> в ___dummy_. Он хранится у типа, что остановился
<ce> в _dest_ в ___dest_. Простое ковыряние в карманах
<ce> здесь не прокатит. Хороший удар, не смертельный,
<ce> позволит получить его гораздо быстрее.
<ce> Платим =reward_ золотых, если вы впишетесь.

RefuseQuest:  [1001]
<ce> Ну, я уверен, что могу предложить работу попроще.
   <--->
<ce> Почему? Из-за условия "не убивать"?

AcceptQuest:  [1002]
<ce> Итак, с нами %ra. Вернитесь сюда через =queston_ дней.
<ce> Передвигайтесь по ночам, например, после десяти. Меньше
<ce> шума. Так будет лучше. Карманничество тут не поможет;
<ce> хороший, сильный удар должен позволить вам наложить
<ce> руки на документ. Я скажу это один раз: если вы убьете
<ce> курьера, прощения не будет. По крайней мере, не с моей
<ce> стороны. Возвращайтесь и я отдам вам ваши деньги.
<ce> Приступайте.

QuestComplete:  [1004]
<ce> Вот и все. Такие вещи могут сделать вас
<ce> влиятельным человеком. Молодец. Вот ваше
<ce> золото. Я постараюсь организовать подобную
<ce> работу для вас в будущем.

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ сказал мне пойти
 в ___dest_, чтобы украсть
 документы на землю у _dest_.
 Я должен пойти туда после полуночи.
 Мне нельзя убивать, но, возможно,
 придется драться, чтобы получить документ.
 У меня есть =queston_ дней, чтобы сделать это
 и вернуться в ___qgiver_.

Message:  1020
<ce> "%oth!"
<ce>
<ce> Из сумки вашего противника выпадает бумажка.
<ce> Теперь у вас есть документ.

Message:  1021
<ce> Нанеся смертельный удар, вы ставите под вопрос
<ce> свое будущее в Гильдии Воров.

Message:  1030
<ce> Вы читаете часть пергамента наугад.
<ce>
<ce> "...по договору о законном представлении
<ce> на вышеупомянутых условиях одному _dummy_, который
<ce> до этого пользовался почетной репутацией среди жителей
<ce> ____qgiver_, и при отсутствии возражений со стороны
<ce> вышеупомянутого региона и его должным образом
<ce> присягнувших представителей, желает гарантировать,
<ce> что впредь не будет никаких неправильных действий..."
   <--->
<ce> Вы читаете часть пергамента наугад.
<ce>
<ce> "...настоящим передается в собственность одному _dummy_,
<ce>  который оказался достойным гражданином региона
<ce> ____qgiver_, не совершив ни убийства, ни растраты,
<ce> ни воровства, ни явного мошенничества, ни клеветы,
<ce> ни обмана стариков, ни незаконной ворожбы, ни клеветы
<ce> на королевских дам, ни опьянения в неподобающие часы,
<ce> ни поджога, ни занятия некромантией, ни открытия..."
   <--->
<ce> Вы читаете часть пергамента наугад.
<ce>
<ce> "...без каких-либо исключений в отношении материальной
<ce> стоимости. Банк ____qgiver_ также не наложил арест
<ce> на имущество, препятствующий этой передаче. Не было
<ce> и того, чтобы должным образом уполномоченный судья
<ce> не признал _dummy_ недостойным такой передачи
<ce> на основании предыдущего поведения, краткое изложение
<ce> которого представлено в параграфе _falsetimer_.
<ce> Посему, поскольку брак жителей двух провинций Тамриэля
<ce> не запрещен, настоящим предписывается, чтобы..."

Message:  1040
<ce> Фигура средних лет в капюшоне предлагает вам
<ce> письмо и уходит, не сказав ни слова.

Message:  1041
%ra,

Хотите сделать =bigmoney_ золотых? Принесите
этот акт мне. Вы же знаете, что ни _qgiver_ не имеет
законных притязаний на него, ни %g4 клиент. Мой агент
будет ждать в ___divers_. Загляните внутрь
_divers_.

Message:  1042
<ce> Очень мудрое решение. Гильдия Воров, как правило,
<ce> надежна, но иногда попадает впросак.
<ce> Вы избавили всех от большой головной боли.
<ce> Мой хозяин благодарит вас, хотя %g1
<ce> и не смог быть здесь. Прощайте.

Message:  1045
<ce> Все! %ra здесь! Бегом сюда!
   <--->
<ce> А вот и вы. Спасибо, что пришли. Вознаграждение?
<ce> Мне об этом никто не говорил. Я возьму этот
<ce> документ у вас буквально через минуту.

Message:  1050
<ce> Нет, я уже наслышан. У нас хорошие шпионы.
<ce> Вы жалки. Я возьму документ, а вы ничего не получите.
<ce> Проваливайте.

Message:  1055
<ce> Это была пустая трата времени. Теперь - к _qgiver_.

QBN:

Item _deed_ letter used 1030
Item _reward_ gold range 351 to 599
Item _falsetimer_ gold range 4 to 8
Item _lett_ letter used 1041
Item _bigmoney_ gold range 1401 to 2499

Person _qgiver_ group Questor male
Person _dummy_ group Local_3.0
Person _alt_ group Local_4.1

Place _dest_ remote tavern
Place _divers_ remote house

Foe _mark_ is Burglar
Foe _ambush_ is 3 Barbarian

Clock _queston_ 00:00 0 flag 17 range 1 2
Clock _cd_ 00:00

--	Quest start-up:
  start timer _queston_
  log 1010 step 0
  pick one of _E.01_ _E.02_
  pc at _dest_ set _inside_

variable _E.01_
variable _E.02_
variable _F.01_
variable _F.02_
variable _inside_

_day_ task:
  daily from 7:00 to 22:00

_spawn1_ task:
  when not _day_ and _inside_
  start task _spawn2_

_spawn2_ task:
  place foe _mark_ at _dest_

_steal_ task:
  injured _mark_
  clicked foe _mark_
  say 1020
  get item _deed_

_startcd_ task:
  when _steal_ and _E.02_
  start timer _cd_

_cd_ task:
  give pc _lett_ notify 1040
  pick one of _F.01_ _F.02_
  place npc _alt_ at _divers_

_murder_ task:
  killed _mark_
  say 1021
  change repute with _qgiver_ by -20

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _steal_
  clear _qgclicked_ _clearclick_

_alttalk_ task:
  clicked npc _alt_

_alttalk1_ task:
  when _alttalk_ and _F.01_
  get item _bigmoney_
  say 1042
  make _bigmoney_ permanent
  end quest

_alttalk2_ task:
  when _alttalk_ and _F.02_
  say 1045
  hide npc _alt_
  create foe _ambush_ every 0 minutes 1 times with 100% success

_daily_ task:
  daily from 0:00 to 23:59
  change foe _ambush_ infighting true
  clear _daily_

_finishalt_ task:
  killed 3 _ambush_
  say 1055

_S.13_ task:
  when _qgclicked_ and _steal_ and not _murder_
  take _deed_ from pc
  give pc _reward_
  end quest

_S.14_ task:
  when _qgclicked_ and _murder_
  say 1050
  end quest

_queston_ task:
  end quest