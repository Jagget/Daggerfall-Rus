Quest: JHTG103
DisplayName: Немного грабежа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> На этот раз у нас немного больше реальных действий.
<ce> Есть некий документ о праве собственности на дом в
<ce> ___dummy_. Он хранится у типа, что остановился в
<ce> _dest_ в ___dest_. Простое
<ce> ковыряние в карманах здесь не прокатит. Хороший удар,
<ce> не смертельный, позволит получить его гораздо быстрее.
<ce> Платим =reward_ золотых, если ты впишешься.

RefuseQuest:  [1001]
<ce> Ну, я уверен, что могу предложить работу попроще.
   <--->
<ce> Почему? Из-за условия "не убивать"?

AcceptQuest:  [1002]
<ce> Итак, с нами %ra. Вернись сюда через =queston_ дней.
<ce> Передвигайся по ночам, например, после десяти. Меньше шума.
<ce> Так будет лучше. Карманничество тут не поможет; хороший, сильный
<ce> удар должен позволить тебе наложить руки на документ. Я
<ce> скажу это один раз: если ты убьешь курьера,
<ce> тебе не будет прощения. По крайней мере, не с моей стороны.
<ce> Возвращайся и я отдам тебе твои деньги. Приступай.

QuestComplete:  [1004]
<ce> Вот и всё. Такие вещи могут сделать тебя
<ce> влиятельным человеком. Молодец. Вот твоё
<ce> золото. Я постараюсь организовать подобную
<ce> работу для тебя в будущем.

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ сказал мне пойти
 в ___dest_, чтобы украсть
 документы на землю у _dest_.
 Я должен пойти туда после полуночи.
 Мне нельзя убивать, но, возможно,
 придется драться, чтобы получить документ.
 У меня есть =queston_ дней, чтобы сделать это
 и вернуться к ___qgiver_.

Message:  1020
<ce> "%oth!"
<ce>
<ce> Из сумки твоего противника выпадает бумажка.
<ce> Теперь у тебя есть документ.

Message:  1021
<ce> Нанеся смертельный удар, ты ставишь под вопрос
<ce> своё будущее в Гильдии Воров.

Message:  1030
<ce> Ты читаешь часть пергамента наугад.
<ce>
<ce> "...по договору о законном представлении на вышеупомянутых
<ce> условиях одному _dummy_, который до этого был
<ce> в почтенной репутации среди жителей ____qgiver_,
<ce> и при отсутствии возражений со стороны вышеупомянутого
<ce> региона и его должным образом присягнувших представителей, желает
<ce> гарантировать, что впредь не будет никаких неправильных действий..."
   <--->
<ce> Ты читаешь часть пергамента наугад.
<ce>
<ce> "...настоящим передается в собственность одному _dummy_, который
<ce> оказался достойным гражданином региона ____qgiver_,
<ce> не совершив ни убийства, ни растраты, ни
<ce> воровства, ни явного мошенничества, ни клеветы, ни обмана
<ce> стариков, ни незаконного ворожбы, ни клеветы на
<ce> королевских дам, ни опьянения в неподобающие часы, ни
<ce> поджог, ни занятия некромантией, ни открытие..."
   <--->
<ce> Ты читаешь часть пергамента наугад.
<ce>
<ce> "...без каких-либо исключений в отношении материальной стоимости.
<ce> Банк ____qgiver_ также не наложил залог на
<ce> имущество, препятствующее этой передаче. Не было и того, чтобы должным образом
<ce> уполномоченный судья не признал _dummy_ недостойным
<ce> такой передачи на основании предыдущего поведения,
<ce> краткое изложение которого представлено в параграфе _falsetimer_.
<ce> Посему, поскольку брак жителей двух провинций
<ce> Тамриэля не запрещен, настоящим предписывается, чтобы..."

Message:  1040
<ce> Фигура средних лет в капюшоне предлагает тебе
<ce> письмо и уходит, не сказав ни слова.

Message:  1041
%ra,

Хочешь сделать =bigmoney_ золотых? Принеси
этот акт мне. Ты же знаешь, что ни _qgiver_ не имеет
законных притязаний на него, ни %g4 клиент. Мой агент
будет ждать в ___divers_. Загляни внутрь
_divers_.

Message:  1042
<ce> Очень мудрое решение. Гильдия Воров, как правило,
<ce> надежна, но иногда попадает впросак.
<ce> Ты избавил всех от большой головной боли.
<ce> Мой хозяин благодарит тебя, хотя %g1 и
<ce> не смог быть здесь. Прощай.

Message:  1045
<ce> Все! %ra здесь! Бегом сюда!
   <--->
<ce> А вот и ты. Спасибо, что пришел. Вознаграждение?
<ce> Деньги? Мне об этом никто не говорил. Я возьму этот
<ce> документ у тебя буквально через минуту.

Message:  1050
<ce> Нет, я уже наслышан. У нас хорошие шпионы.
<ce> Ты жалок. Я возьму документ, а ты ничего не получишь.
<ce> Проваливай.

Message:  1055
<ce> Это была пустая трата времени. Теперь - к _qgiver_.

QBN:

Item _deed_ letter used 1030
Item _reward_ gold range 351 to 599
Item _falsetimer_ gold range 4 to 8
Item _lett_ letter used 1041
Item _bigmoney_ gold range 1401 to 2499

Person _qgiver_ group Questor male
Person _dummy_ group Local_3.0
Person _alt_ group Local_4.1

Place _dest_ remote tavern
Place _divers_ remote house

Foe _mark_ is Burglar
Foe _ambush_ is 3 Barbarian

Clock _queston_ 00:00 0 flag 17 range 1 2
Clock _cd_ 00:00

--	Quest start-up:
  start timer _queston_
  log 1010 step 0
  pick one of _E.01_ _E.02_
  pc at _dest_ set _inside_

variable _E.01_
variable _E.02_
variable _F.01_
variable _F.02_
variable _inside_

_day_ task:
  daily from 7:00 to 22:00

_spawn1_ task:
  when not _day_ and _inside_
  start task _spawn2_

_spawn2_ task:
  place foe _mark_ at _dest_

_steal_ task:
  injured _mark_
  clicked foe _mark_
  say 1020
  get item _deed_

_startcd_ task:
  when _steal_ and _E.02_
  start timer _cd_

_cd_ task:
  give pc _lett_ notify 1040
  pick one of _F.01_ _F.02_
  place npc _alt_ at _divers_

_murder_ task:
  killed _mark_
  say 1021
  change repute with _qgiver_ by -20

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _steal_
  clear _qgclicked_ _clearclick_

_alttalk_ task:
  clicked npc _alt_

_alttalk1_ task:
  when _alttalk_ and _F.01_
  get item _bigmoney_
  say 1042
  make _bigmoney_ permanent
  end quest

_alttalk2_ task:
  when _alttalk_ and _F.02_
  say 1045
  hide npc _alt_
  create foe _ambush_ every 0 minutes 1 times with 100% success

_daily_ task:
  daily from 0:00 to 23:59
  change foe _ambush_ infighting true
  clear _daily_

_finishalt_ task:
  killed 3 _ambush_
  say 1055

_S.13_ task:
  when _qgclicked_ and _steal_ and not _murder_
  take _deed_ from pc
  give pc _reward_
  end quest

_S.14_ task:
  when _qgclicked_ and _murder_
  say 1050
  end quest

_queston_ task:
  end quest