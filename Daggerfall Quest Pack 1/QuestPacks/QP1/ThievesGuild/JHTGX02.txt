Quest: JHTGX02
DisplayName: Испытание
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX02

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX02

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX02

QuestFail:  [1003]
<ce> Что это такое? Ты дурак! Ты кретин!
<ce> Некомпетентный полудурок! Что я сказал?
<ce> Не надо, никогда не надо открывать мешок!
<ce> Да что с тобой не так?!
<ce> Убирайся с глаз моих! Сейчас же!

QuestComplete:  [1004]
<ce> Я же говорил вам, что все будет просто, не так ли?
<ce> Вот ваши деньги. Я всегда держу слово,
<ce> вот увидите. Возьмите деньги и продолжим разговор.

RumorsDuringQuest: [1005]
Говорят, что Ворон снова появляется в заливе. Я думаю, что это
самозванец.
   <--->
Вы слышали о том, что Ворон появился в каком-то городе?
Скорее всего, это ложь.
   <--->
Говорят, что Ворон где-то рядом, но мне казалось,
что он давно умер.

Message:  1010
<ce> Прежде всего, я даю вам _lode_.
<ce> Это позволит мне найти вас в случае, если все пойдет
<ce> не так. Мы оба знаем, что в этом бизнесе может случиться
<ce> все, что угодно. Если вы когда-нибудь потеряете его,
<ce> я не смогу отследить вас, так что если вы собираетесь
<ce> работать со мной, держите его в безопасности.
<ce>
<ce> Итак, вот вам тест. Один из моих бывших друзей
<ce> завязал с работой, которую вам предстоит выполнить.
<ce> Короче говоря, есть мешок с товаром. Он ждет, когда его
<ce> заберут. Просто подойдите к _apoc_ в ___apoc_. Там вы
<ce> увидите мешок. Не открывайте его, как бы вам ни хотелось.
<ce> То, что в нем лежит, обработано должным образом
<ce> и не испортится несколько месяцев, если не будет
<ce> соприкасаться с открытым воздухом.
<ce> Я собираюсь остаться в ___res_.
<ce> Ищите меня в _res_.
<ce> За это я плачу =reward_ золотых.
<ce> Нет ничего проще, верно?
<ce> Теперь идите, чемпион.

Message:  1030
%qdt:
 Ворон, якобы легендарный вор,
 завербовал меня в какую-то свою схему.
 Он хочет, чтобы я достал мешок
 с товаром из _apoc_
 в ___apoc_ и отнес
 к месту встречи,
 _res_ в ___res_.

Message:  1070
<ce> Это мешок, о котором вам говорил _crow_.
<ce> Желаете ли вы открыть его?

Message:  1071
<ce> Вы открываете мешок. Консервирующие вещества
<ce> начинают испаряться из-за контакта
<ce> с открытым воздухом, и вас встречает
<ce> специфический запах и вид различных
<ce> внутренних органов.

Message:  1072
<ce> Не открывая, вы кладете
<ce> мешок к себе в рюкзак.
<ce> Вы можете понять только, что
<ce> внутри находится что-то влажное.
<ce> Многочисленные слои ткани не позволяют
<ce> этому пролиться наружу.

Message:  1080
<ce> Желаете ли вы выкинуть _lode_
<ce> и отказаться от своих связей с _crow_?

Message:  1081
<ce> Вы выбрасываете маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат
<ce> только ему.

Message:  1082
<ce> Вы возвращаете _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:

Person _crow_ named The_Crow

Place _res_ remote house
Place _apoc_ remote apothecary

Item _reward_ gold range 1801 to 2199
Item _lode_ lodestone
Item _bag_ large_sack

--	Quest start-up:
  say 1010
  log 1030 step 0
  get item _lode_
  place item _bag_ at _apoc_
  _lode_ used do _prompt_

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_grab_ task:
  clicked item _bag_
  prompt 1070 yes _open_ no _close_
  place npc _crow_ at _res_

_open_ task:
  say 1071

_close_ task:
  say 1072

_talk_ task:
  toting _bag_ and _crow_ clicked

_fail_ task:
  when _open_ and _talk_
  say 1003
  end quest

_victory_ task:
  when _close_ and _talk_
  give pc _reward_
  start quest JHTGX03
  end quest