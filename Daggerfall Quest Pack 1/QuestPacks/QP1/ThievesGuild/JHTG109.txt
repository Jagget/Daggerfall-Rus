Quest: JHTG109
DisplayName: Только лучшие
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Да, у меня есть кое-что для вас. Именно для ВАС.
<ce> Могу сразу сказать, работа грязная.
<ce> Но если вы согласитесь, клиент предложит один из тех
<ce> знаменитых мощных артефактов. Вот насколько важна эта
<ce> работа. Возьметесь?

RefuseQuest:  [1001]
<ce> Ну что ж, вот и все.

AcceptQuest:  [1002]
<ce> Вот. Держите эту сумку и не трудитесь ее открывать.
<ce> Вы не сможете и не захотите узнать, что внутри. Теперь,
<ce> когда она у вас, начнут происходить две вещи.
<ce> Во-первых, вы начнете чувствовать сильное недомогание, 
<ce> и это не пройдет. Во-вторых, за вами начнут приходить 
<ce> даэдра. Вот почему эта работа специально для вас.
<ce> Для большинства людей это самоубийство.
<ce> Этот пакет должен попасть в ___tav_.
<ce> Контакт по имени _gpers_ будет находиться в таверне
<ce> под названием _tav_ примерно через =days_ дней.

QuestFail:  [1003]

QuestComplete:  [1004]
<ce> Добро пожаловать, дитя. Дай мне сумку, быстро. Ни одна 
<ce> душа не должна знать о том, что она здесь. А теперь, 
<ce> пока я не убил тебя и не вырвал твои глаза, которые 
<ce> все видели, прими эту награду. Это справедливый обмен.
<ce> Доброго пути, %ra.

Message:  1010
<ce> Это серьезно! Позволь мне задать один вопрос. 
<ce> В последний раз. Ты уверен, что хочешь это сделать?
<ce> У тебя только одна жизнь, и мне бы очень не хотелось,
<ce> чтобы ты ее потерял.

Message:  1011
<ce> Хорошо. Тогда в путь, солдат.
<ce> Обливион ждет тебя.

Message:  1012
<ce> Конечно, никаких обид. Как я уже сказал, это было бы 
<ce> самоубийство. Я скажу клиенту, этот заказ не стоит 
<ce> таких проблем.

Message:  1015
<ce> При желании вы можете выбросить сумку, хотя
<ce> это, скорее всего, создаст проблемы для Гильдии Воров.
<ce> Вы хотите выбросить ее?

Message:  1016
<ce> Вы выбрасываете сумку.

Message:  1020
<ce> Вдалеке раздается оглушительный рев.

Message:  1030
%qdt:
 _questgiver_ из Гильдии Воров
 передал мне сумку, которая должна попасть 
 к человеку по имени _gpers_.
 в _tav_, ___tav_,
 примерно через =days_ дней.

QBN:

Person _questgiver_ group Questor male
Person _tg_ faction The_Thieves_Guild
Person _gpers_ faction The_Anthotis

Item _bag_ small_sack
Item _art1_ artifact Auriels_Bow
Item _art2_ artifact Auriels_Shield
Item _art3_ artifact Chrysamere
Item _art4_ artifact Lords_Mail
Item _art5_ artifact Necromancers_Amulet
Item _art6_ artifact Staff_of_Magnus
Item _art7_ artifact Warlocks_Ring

Place _tav_ remote tavern

Foe _dae1_ is Daedroth
Foe _dae2_ is Frost_daedra
Foe _dae3_ is Fire_daedra
Foe _dae4_ is Daedra_seducer
Foe _dae5_ is Daedra_lord

Clock _delay_ 0:03
Clock _days_ 4.00:00 6.00:00

-- Quest start-up:
  log 1030 step 0
  prompt 1010 yes _begin_ no _quit_

_quit_ task:
  say 1012
  change repute with _tg_ by +2
  end quest

_begin_ task:
  say 1011
  pick one of _a1_ _a2_ _a3_ _a4_ _a5_ _a6_ _a7_
  get item _bag_
  _bag_ used do _endprompt_
  have _bag_ set _sick_
  start timer _delay_
  start timer _days_

_days_ task:
  place npc _gpers_ at _tav_

_delay_ task:
  say 1020
  send _dae1_ every 15 minutes with 10% success
  send _dae2_ every 15 minutes with 10% success
  send _dae3_ every 15 minutes with 10% success
  send _dae4_ every 15 minutes with 10% success
  send _dae5_ every 15 minutes with 10% success

variable _a1_
variable _a2_
variable _a3_
variable _a4_
variable _a5_
variable _a6_
variable _a7_
_sick_ task:
  daily from 00:01 to 23:59
  make pc ill with Leprosy
  clear _sick_

_endprompt_ task:
  prompt 1015 yes _fail_ no _continue_

_fail_ task:
  say 1016
  change repute with _tg_ by -13
  take _bag_ from pc
  end quest

variable _continue_
_victory_ task:
  toting _bag_ and _gpers_ clicked

_v1_ task:
  when _victory_ and _a1_
  give pc _art1_
  end quest

_v2_ task:
  when _victory_ and _a2_
  give pc _art2_
  end quest

_v3_ task:
  when _victory_ and _a3_
  give pc _art3_
  end quest

_v4_ task:
  when _victory_ and _a4_
  give pc _art4_
  end quest

_v5_ task:
  when _victory_ and _a5_
  give pc _art5_
  end quest

_v6_ task:
  when _victory_ and _a6_
  give pc _art6_
  end quest

_v7_ task:
  when _victory_ and _a7_
  give pc _art7_
  end quest