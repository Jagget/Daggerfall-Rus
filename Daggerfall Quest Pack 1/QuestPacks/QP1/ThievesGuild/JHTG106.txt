Quest: JHTG106
DisplayName: Козел отпущения
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Один из членов нашей гильдии выполняет серьезную работу,
<ce> приносящую нам много денег.
<ce> Проблема в том, что местные жители начинают что-то
<ce> подозревать. Нужно сбить их со следа.
<ce> Нам нужен кто-то, готовый подставиться,
<ce> чтобы они ослабили бдительность.
<ce> Ваша репутация в %crn сильно пострадает,
<ce> но мы за это хорошо заплатим.

RefuseQuest:  [1001]
<ce> Я говорю вам, дело того стоит.
<ce> Предлжение пока еще в силе,
<ce> но вы можете упустить момент.

AcceptQuest:  [1002]
<ce> Хорошо. Вы не пожалеете об этом.
<ce> Городская стража знает, что теперь он собирается
<ce> ограбить _res_ в ___res_.
<ce> Именно там вам и нужно быть.
<ce> Найдите в том доме что-нибудь ценное и возьмите это.
<ce> Скорее всего вас поймают,
<ce> у вас будет плохая репутация, но это уже мелочи.
<ce> Когда я увижу вас снова,
<ce> у меня здесь будет ваш заколдованный _reward_.
<ce> И это не обычная вещь, а именно то, что нужно вору.

QuestComplete:  [1004]
<ce> Жизнь будет тяжелой с плохой репутацией,
<ce> я знаю. Мы все там были.
<ce> Если вы решите пожить в другом регионе,
<ce> возможно, вы окажете себе услугу.
<ce> Вот _reward_ - возьмите.
<ce> На него наложены чары, открывающие двери.
<ce> Используйте это с умом, он быстро износится.

Message: 1011
<ce> Сторож застает вас за этим занятием.
<ce>
<ce> "Эй! Мы нашли %pg3! Вора из ___res_!"
<ce>
<ce> Похоже, что план сработал просто отлично.

Message:  1030
%qdt:
 В обмен на хорошую заколдованную награду
 _qgiver_ из ___qgiver_ посылает меня
 украсть все ценное, что я смогу найти
 в _res_, ___res_. Как только меня поймают,
 я смогу вернуться за своей наградой.

QBN:
Item _item_ gem
Item _reward_ item class 4 subclass 6

Place _res_ remote house

Clock _failsafe_ 180.00:00

Person _qgiver_ group Questor male

--	Quest start-up:
    start timer _failsafe_
    log 1030 step 0
    place item _item_ at _res_

_begin_ task:
    clicked item _item_
    say 1011
    legal repute -15
    setplayercrime Theft
    spawncityguards immediate
    start task _halt_
    start task _victory_

variable _victory_

_halt_ task:
    play sound halt every 0 minutes 1 times
    clear _halt_

_clickqgiver_ task:
    clicked npc _qgiver_

_notyet_ task:
    when _clickqgiver_ and not _victory_
    clear _clickqgiver_ _notyet_

_pcgetsgold_ task:
    when _clickqgiver_ and _victory_
    make _item_ permanent
    give pc _reward_
    end quest

_failsafe_ task:
    make _item_ permanent
    end quest