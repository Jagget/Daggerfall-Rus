Quest: JHTG117
DisplayName: Ежедневный грабеж, большой
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Одним из преимуществ членства в Гильдии Воров является
<ce> знание того, где в городе водятся деньги. В другом
<ce> городе есть дом, в котором лежат тысячи золотых.
<ce> Если вы умеете хорошо красться, то можете попробовать
<ce> взять их себе. Хотите, я зарезервирую дом для вас?
   <--->
<ce> Каждый день люди прячут свои сбережения по домам. Одни
<ce> делают это хорошо, другие - неаккуратно. У нас есть
<ce> наводка на дом, в котором хранится несколько тысяч
<ce> золотых. Если хотите, я могу отметить его для вас.
<ce> Это не принесет вам никаких очков в гильдии, но вы
<ce> получите немного денег. Возьметесь?

RefuseQuest:  [1001]
<ce> Без проблем. Люди начнут подозревать, если мы станем
<ce> слишком последовательными.
   <--->
<ce> Лучше не пытаться, чем потерпеть неудачу.

AcceptQuest:  [1002]
<ce> Ну ладно. Отправляйтесь в ___house_ и найдите _house_.
<ce> Они знают, что носить с собой столько денег - это риск,
<ce> поэтому вы должны быть хороши. Здесь вы очень рискуете.
<ce> Я не думаю, что у вас будет больше двух недель до того,
<ce> как деньги будут потрачены или перепрятаны, так что
<ce> поторопитесь. И помните, что если вас поймают,
<ce> то пострадает и тот, кто дал нам наводку на этот дом.
<ce> Не попадитесь.
   <--->
<ce> Ваши деньги лежат в _house_,
<ce> это в ___house_, на %di.
<ce> В нем, может быть, несколько тысяч золотых, и они
<ce> собираются следить за ними, как ястребы. Не попадитесь.
<ce> Тот, кто рассказал нам об этом доме, тоже пострадает.
<ce> Такие деньги тратятся за пару недель,
<ce> так что не тяните с этим.

QuestFail:  [1003]
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Скрытность)
<ce>
<ce> Вы немного умеете красться, но недостаточно.
<ce> Кто-то знает, что вы здесь, и кричит во весь голос.

QuestComplete:  [1004]
<ce> Похоже, вы в полной безопасности. Деньги в вашем
<ce> распоряжении.

QuestorPostFailure:  [1009]
Помните, что вы должны нам за деньги в _house_.
Найдите какую-нибудь работу и расплатитесь с нами.

Message:  1010
%qdt:
 Гильдия Воров сообщила мне
 о доме в ___house_,
 в котором должно храниться
 несколько тысяч золотых.
 Дом называется _house_.
 Если я смогу найти деньги в ближайшие
 пару недель, они будут моими.

Message:  1011
<ce> Вы находите сундук и видите, что на нем стоит простой
<ce> замок. Если вы хотите получить то, что находится внутри,
<ce> вам придется отпереть его самостоятельно.
<ce> Вы попытаетесь открыть его?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Взлом, сложность высокая)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Зная, что у вас не так много времени, вы приступаете
<ce> к работе. Проходит не более нескольких секунд, прежде
<ce> чем крышка откидывается. Вы хватаете содержимое.

Message:  1013
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Зная, что у вас мало времени, вы приступаете к работе.
<ce> Вы копаетесь в замке и запускаете то, что не должны были.
<ce> Громкая сирена разносится по всему дому.
<ce> Теперь вы влипли.

Message:  1014
<ce> Глядя на замок, вы начинаете сомневаться в своих
<ce> способностях. Вы все еще можете выбраться, не подняв
<ce> большого шума, говорите сами себе. Вы отходите
<ce> от сундука. Нет смысла тащить его с собой, если вы
<ce> не можете его открыть.

Message:  1015
<ce> Это правильный дом.
<ce> Сундук где-то внутри.

QBN:
Item _item_ item class 1 subclass 8
Item _reward_ gold range 2500 to 4000

Place _house_ local house

Person _qgiver_ group Questor

Clock _danger_ 00:02 00:05
Clock _ending_ 12.00:00 15.12:00

--	Quest start-up:
    start timer _ending_
    log 1010 step 0
    place item _item_ at _house_
    pc at _house_ do _inside_
    pick one of _seeskill1_ _seeskill2_ _seeskill3_

_inside_ task:
    start timer _danger_
    stop timer _ending_
    say 1015

_notinside_ task:
    when not _inside_
    stop timer _danger_
    start timer _ending_

_danger_ task:
    pick one of _go1_ _go2_ _go3_

_check1_ task:
    when skill Stealth is at least 80

_check2_ task:
    when skill Stealth is at least 85

_check3_ task:
    when skill Stealth is at least 90

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
    when _check1_ and _go1_

_c2_ task:
    when _check2_ and _go2_

_c3_ task:
    when _check3_ and _go3_

_d1_ task:
    when not _check1_ and _go1_
    start task _fail_

_d2_ task:
    when not _check2_ and _go2_
    start task _fail_

_d3_ task:
    when not _check3_ and _go3_
    start task _fail_

_pick1_ task:
    when skill Lockpicking is at least 60

_pick2_ task:
    when skill Lockpicking is at least 70

_pick3_ task:
    when skill Lockpicking is at least 80

_e1_ task:
    when not _pick1_ and _seeskill1_
    start task _skillfail_

_e2_ task:
    when not _pick2_ and _seeskill2_
    start task _skillfail_

_e3_ task:
    when not _pick3_ and _seeskill3_
    start task _skillfail_

variable _skillfail_

_fail_ task:
    setplayercrime Trespassing
    spawncityguards immediate
    say 1003
    change repute with _qgiver_ by -1
    end quest

_grab_ task:
    clicked item _item_
    prompt 1011 yes _attempt_ no _flee_
    take _item_ from pc

variable _attempt_
variable _seeskill1_
variable _seeskill2_
variable _seeskill3_

_flee_ task:
    say 1014

_bigfail_ task:
    when _attempt_ and _skillfail_
    setplayercrime Theft
    spawncityguards immediate
    say 1013
    change repute with _qgiver_ by -1
    end quest

_victory_ task:
    when _attempt_ and not _skillfail_
    get item _reward_
    make _reward_ permanent
    say 1012

_escape_ task:
    when _grab_ and _notinside_
    say 1004
    change repute with _qgiver_ by +2
    end quest

_ending_ task:
    change repute with _qgiver_ by +2
    end quest