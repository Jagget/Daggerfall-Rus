Quest: JHTG113
DisplayName: Знак Мастерства
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Ты сделал себе имя здесь
<ce> в гильдии, %pcf, но тебе
<ce> все еще не хватает кое-чего, чтобы называться
<ce> настоящим мастером. У нас есть испытание, которое
<ce> проходят величайшие из наших членов, прежде чем
<ce> они могут считать себя Мастерами.
<ce> Ты можешь попробовать в любое время, но только один раз,
<ce> и испытание начинается немедленно.
<ce> Готов поставить свою жизнь на кон?

RefuseQuest:  [1001]
<ce> Возьми время на подготовку и возвращайся.

AcceptQuest:  [1002]
<ce> _questgiver_ берёт какой-то предмет,
<ce> который %g1 прикрепляет к твоему запястью.
<ce> Ты чувствуешь, как тебя переносят
<ce> в другое место. Когда %g1 исчезает, ты
<ce> слышишь, как _questgiver_ говорит,
<ce>
<ce> "Просто выживи полчаса. Беги так быстро, как только сможешь".

QuestFail:  [1003]
<ce> Браслет, надетый на тебя для этого испытания, сгорел.
<ce> Твое заклинание "Возврат" не сработало.

QuestComplete:  [1004]
<ce> Ты сделал это, %pcf. Мы наблюдали
<ce> через браслет. Теперь мы можем
<ce> сказать, что ты на самом деле %pct.
<ce> Такое право хвастаться сложно
<ce> получить, но мы хотели дать тебе более 
<ce> осязаемую награду, чтобы
<ce> сказать спасибо за то, что ты нам помог.

QuestorPostSuccess:   [1008]
Настоящий Мастер вор. Добро пожаловать.

QuestorPostFailure:   [1009]
Я рад, что ты здесь, среди живых, но у тебя есть только один шанс получить Знак Мастерства.

Message:  1010
<ce> Браслет, надетый на тебя, растворяется.
<ce> Вампиры исчезают в темноте,
<ce> не оставляя и следа.

Message:  1011
<ce> Браслет как бы приклеен к твоей коже.
<ce> Неподалеку ты слышишь шипящий звук.
<ce> Пора бежать.
<ce>
<ce> (Подсказка: ты можешь оказаться в таком месте, откуда невозможно сбежать).
<ce> (например, на дне желоба. Если это так, то сделай сохранение).
<ce> (и попробуй выполнить консольную команду "teleport", чтобы выбраться).

Message:  1012
<ce> Похоже, у тебя нет возможности выбраться,
<ce> пока это испытание не закончится.

Message:  1030
%qdt:
 Я принял тест на мастерство
 от Гильдии Воров. Меня
 отправили в пещеру где-то
 в %crn, и мне нужно всего лишь
 выжить в течение получаса.
 Затем я смогу вернуться
 к ___questgiver_.

QBN:
Item _reward_ artifact Skeletons_Key
Item _brac_ bracelet

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon8

Clock _timer_ 120.00:00
Clock _recdelay_ 0:00
Clock _recdelay2_ 0:00
Clock _delay_ 0:30

Foe _vamp_ is Ancient_vampire

--	Quest start-up:
	start timer _timer_
    start timer _delay_ 
	log 1030 step 0 
    teleport pc to _mondung_
    start task _spawn_
    get item _brac_
	pc at _mondung_ set _inside_

variable _inside_
_spawn_ task:
    create foe _vamp_ every 1 minutes indefinitely with 100% success
    create foe _vamp_ every 0 minutes 1 times with 100% success
	cast Teleport-Effect effect do _recfail_
	say 1011

_teleport_ task:
	when _spawn_ and not _inside_
	teleport pc to _mondung_
	say 1012
	start task _clearing_

_clearing_ task:
	clear _teleport_ _clearing_

_recfail_ task:
	say QuestFail
	start timer _recdelay_

_recdelay_ task:
	start timer _recdelay2_

_recdelay2_ task:
	teleport pc to _mondung_
	clear _recfail_ _recdelay_

_delay_ task:
    clear _spawn_
    say 1010
    take _brac_ from pc
	remove foe _vamp_

_qgclicked_ task:
	clicked npc _questgiver_ 

_finish_ task:
	when _delay_ and _qgclicked_
	give pc _reward_
	end quest 

_timer_ task:
	end quest 

_clearclick_ task:
	when _qgclicked_ and not _delay_
	clear _qgclicked_ _clearclick_ 