Quest: JHTG112
DisplayName: Раздел сокровища
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Поступило сообщение о неких сокровищах в глубинах 
<ce> острова %crn, которые не мешало бы разграбить. Хотите 
<ce> принести его сюда, чтобы мы могли разделить добычу?

RefuseQuest:  [1001]
<ce> Видите, почему я не рассказываю все подробности первым.

AcceptQuest:  [1002]
<ce> Договорились. Это находится в ___dungeon_. Вам придется
<ce> искать мешок где-то там, внизу. У этой штуки очень 
<ce> хороший запорный механизм, так что сами вы
<ce> ее никогда не откроете. Просто принесите его сюда, и мы 
<ce> дадим вам часть денег, которые найдем внутри.

QuestComplete:  [1004]
<ce> Хорошая работа. Продолжайте в том же духе, и мы все 
<ce> разбогатеем.

Message:  1010
<ce> Как и сказал _qgiver_, этот мешок магически
<ce> запечатан. Никакие способы взлома замков не помогут.

Message:  1011
<ce> Ваше заклинание открывает мешок. Внутри находится 
<ce> несколько документов исторической и политической 
<ce> ценности, которые гильдия, скорее всего, сможет
<ce> использовать для шантажа важных людей. Для вас же важнее
<ce> всего =bigreward_ золотых, которые вы находите 
<ce> среди шуршащих пергаментов.

Message:  1012
<ce> Не смогли открыть, да? Не волнуйтесь. Некоторым из 
<ce> наших парней это дается легко. Просто подождите минутку.
<ce>
<ce> Ага, давайте посмотрим, что там внутри... О-о, классная 
<ce> штука. Готов поспорить, что большая часть этого будет 
<ce> бесполезна для вас, но наш Главный шпион будет занят  
<ce> день напролет. Наш перекупщик тоже. Возьмите свою долю.

Message:  1013
<ce> Мешок открыт? Странно. Главный шпион сказал, что на ней
<ce> будет очень сложный замок.
<ce>
<ce> Посмотрим... Денег нет. Очень жаль, но иногда так бывает.
<ce> Тут куча документов, которые, как мне кажется, мы могли 
<ce> бы использовать. Может быть, продадим их какому-нибудь
<ce> аристократу. Жаль, что вы вытянули короткую спичку.
<ce> Мы обещали, что вы получите долю, и она равна нулю.
<ce> Но мы все равно считаем, что вы оказали нам услугу.

Message:  1030
%qdt:
 В ___dungeon_ лежит запечатанный мешок,
 который Гильдия Воров в ___qgiver_
 хочет заполучить. Они помогут открыть его
 и дадут мне долю от всех денег, которые будут внутри.

QBN:
Item _reward_ gold range 150 to 350
Item _bigreward_ gold range 1350 to 1800
Item _bag_ small_sack

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon

Clock _timer_ 90.00:00

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    reveal _dungeon_
    place item _bag_ at _dungeon_

_found_ task:
    clicked item _bag_
    say 1010
    cast Open effect do _win_

_win_ task:
    say 1011
    get item _bigreward_

_victory_ task:
    toting _bag_ and _qgiver_ clicked

_winner_ task:
    when _victory_ and _win_
    say 1013
    give pc nothing
    end quest

_notwinner_ task:
    when _victory_ and not _win_
    say 1012
    give pc _reward_
    end quest

_timer_ task:
    end quest