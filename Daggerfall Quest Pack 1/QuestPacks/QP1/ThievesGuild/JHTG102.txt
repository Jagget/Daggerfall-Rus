Quest: JHTG102
DisplayName: Личный транспорт
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, мы разрабатываем план.
<ce> Обычно мы занимаемся тем, что перевозим
<ce> драгоценности, порошки и деньги, но
<ce> сейчас мы хотим перевести человека.
<ce> Хочешь помочь нам?

RefuseQuest:  [1001]
<ce> Тебе не нужно было поднимать его и
<ce> нести его на руках, полудурок.
<ce> Гильдии нужны эти деньги.

AcceptQuest:  [1002]
<ce> Хорошо. Всё что тебе нужно будет
<ce> сделать, это отправиться в ___res_.
<ce> Парень будет ждать тебя в
<ce> месте под названием _res_. Зовут
<ce> _kid_. Быстро бери %g4 и
<ce> двигайся к __dung_. В нутри
<ce> вас будут ждать, сдашь им %g4
<ce> из рук в руки. Тогда ты получишь свою
<ce> награду. Двигайся, %pcf.
<ce> У тебя есть =timer_ дней.

QuestFail:  [1003]
<ce> К этому времени городская стража уже
<ce> разнюхала схему похищения.

QuestComplete:  [1004]
<ce> Человек в серой тунике машет тебе рукой.
<ce>
<ce> "Гильдия Воров, верно? Хорошо.
<ce> _kid_ теперь моя забота. Нам
<ce> нужно найти место, где можно спрятаться,
<ce> пока мы не получим выкуп. Вот
<ce> твоя доля. А теперь оставь нас в покое".

Message:  1020
<ce> В доме тихо. Похоже никто
<ce> не заметил твоего проникновения.

Message:  1021
<ce> _kid_ в страхе уставился на тебя.
<ce> "Кто ты такой? Где папа?"
<ce>
<ce> Ты затыкаешь %g2 рот и накрываешь %g4 тяжелым
<ce> плащом, который лежит неподалеку. Теперь нужно доставить
<ce> %g4 в __dung_.
   <--->
<ce> _kid_ в ужасе уставился на тебя.
<ce> "Кто ты такой? Нет, оставь меня в покое!"
<ce>
<ce> Ты затыкаешь %g2 рот и накрываешь %g4 тяжелым
<ce> плащом, который лежит неподалеку. Теперь нужно доставить
<ce> %g4 в __dung_.

Message:  1025
<ce> Ты слышишь, как скрипит пол. Кто-то кричит:
<ce> "Там кто-то есть?!".

Message:  1030
%qdt:
 Гильдия Воров послала меня
 похитить _kid_, ребенка.
 из _res_ в ___res_.
 Затем мне нужно отвезти %g4 в
 __dung_. У меня есть =timer_ дней.

Message:  1099
<ce> Доставь ребенка из ___res_ в
<ce> ___dung_. Тогда ты получишь деньги.

QBN:
Person _questgiver_ group Questor male
Person _kid_ faction Children

Place _dung_ remote dungeon
Place _res_ remote house

Clock _timer_ 30.00:00
Clock _found_ 00:04 00:09

Item _reward_ gold range 201 to 399

Foe _enemy_ is Knight

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place npc _kid_ at _res_
	pc at _dung_ set _idun_
	pc at _res_ set _ires_
	reveal _dung_

_ires_ task:
	start timer _found_
	say 1020

variable _found_
_combat_ task:
	when _ires_ and _found_
	say 1025
	create foe _enemy_ every 0 minutes 1 times with 100% success

_kclicked_ task:
	clicked npc _kid_
	say 1021
	hide npc _kid_
	add _kid_ face

variable _idun_
_victory_ task:
	when _kclicked_ and _idun_
	drop _kid_ face
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _questgiver_
	say 1099
	clear _qgclicked_

_timer_ task:
	say 1003
	end quest