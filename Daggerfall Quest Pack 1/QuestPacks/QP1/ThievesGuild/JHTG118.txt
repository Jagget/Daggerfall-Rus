Quest: JHTG118
DisplayName: Похищение
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Здесь, в городе, есть ребенок, которого нужно
<ce> похитить и придержать для выкупа.
<ce> Нам нужен человек, который хорошо болтает и может
<ce> быстро перемещаться с места на место.
<ce> Соглашайтесь, если соответствуете этим требованиям.

RefuseQuest:  [1001]
<ce> Речь действительно важна для вора.
<ce> Надеюсь, когда-нибудь вы научитесь красноречию.

AcceptQuest:  [1002]
<ce> Хорошо. Ребенка зовут
<ce> _kid_. Каждый день %g4 родители оставляют %g4 
<ce> в _tavern_, пока работают, а потом
<ce> приходит родственник и забирает %g4.
<ce> Какие отговорки вы придумаете,
<ce> это ваша забота, но вы должны утащить это дитя
<ce> из таверны и привести его в _res_.
<ce> Вы можете рассчитывать на =reward_ золотых.
<ce> Облажаетесь - ничего не получите.
<ce> У вас на все про все 24 часа.

QuestFail:  [1003]
<ce> Прошло 24 часа, а вы
<ce> не доставили _kid_ в _res_.
<ce> Схема провалилась.

QuestComplete:  [1004]
<ce> Когда вы входите в резиденцию,
<ce> фигура в капюшоне берет _kid_ за плечо
<ce> и ведет %g4 в другую комнату.
<ce> Вы ждете свою оплату и тут понимаете, что мешок 
<ce> с золотом уже каким-то образом оказался у вас в руке.

Message:  1010
<ce> Типичная какофония _tavern_ бьет 
<ce> по ушам, когда вы входите. Громкий смех, радостные 
<ce> возгласы и чей-то храп, доносящийся из угла.

Message:  1011
<ce> _kid_ недоверчиво смотрит на вас. "Кто вы такой 
<ce> и почему мы так рано уходим? Мы только что приехали. Где
<ce> моя тетя?" Вы придумываете несколько объяснений, которые 
<ce> ребенок наивно принимает и начинает следовать за вами.

Message:  1012
<ce> Вас замечают сразу же после выхода из таверны.
<ce> "Стой!" К вам подбегает городской стражник. "Я много раз
<ce> видел, как этот ребенок выходил из таверны, но всегда 
<ce> с другим человеком. Кто вы такой и что здесь делаете?"
<ce>
<ce> Ваши мысли разбегаются. Попытаться солгать (да) или 
<ce> отдать ребенка охраннику (нет)?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Жаргон, сложность очень низкая)

Message:  1013
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> "Итак, тетя попросила вас приехать и забрать %g4, потому 
<ce> что она ранена? Ну, тогда ладно". Он опускается 
<ce> на колени и спрашивает _kid_: "Ты веришь %ra?" Ребенок
<ce> пожимает плечами и говорит: "Наверное. Моя тетя - 
<ce> довольно умная женщина. Надо будет посмотреть, что с ней 
<ce> не так, когда мы вернемся домой".
<ce>
<ce> "Ладно, проходите, вы двое. Но я буду следить за вами".
<ce> сказал охранник, махнув вам рукой.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Ваш разум застывает. Вы начинаете придумывать историю,
<ce> но этот рассказ никого не убеждает.
<ce> Ребенок вскрикивает и убегает, а городской стражник
<ce> отходит назад, громко зовя на помощь.

Message:  1015
<ce> Вы отдаете ребенка стражнику. Он смотрит на вас
<ce> с некоторым удивлением и понимает, что вы
<ce> влипли по уши. "Ладно. Давайте просто забудем 
<ce> об этом, а мы позаботимся о том, чтобы это
<ce> дитя добралось до дома в целости и сохранности".
<ce>
<ce> Вы отступаете назад и позволяете им уйти,
<ce> не ввязываясь больше в это дело.

Message:  1030
%qdt:
 Гильдия Воров послала меня
 похитить _kid_, ребенка
 в _tavern_ здесь, в ___tavern_.
 Затем мне нужно отвести %g4 
 в _res_. У меня есть 24 часа.

QBN:
Person _kid_ faction Children

Place _tavern_ local tavern
Place _res_ local house

Clock _timer_ 1.00:00
Clock _delay_ 0:05

Item _reward_ gold range 201 to 399

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _kid_ at _tavern_
    pc at _tavern_ set _idun_
    pc at _res_ set _ires_
    start timer _timer_

variable _ires_
_idun_ task:
    say 1010

_kidclick_ task:
    clicked npc _kid_
    say 1011
    hide npc _kid_
    add _kid_ face

_ambush_ task:
    when _kidclick_ and not _idun_ and not _ires_
    prompt 1012 yes _lie_ no _surrender_

variable _lie_

_skilled_ task:
    when skill Streetwise is at least 15

_winner_ task:
    when _lie_ and _skilled_
    say 1013

_failure_ task:
    when _lie_ and not _skilled_
    say 1014
    setplayercrime Assault
    drop _kid_ face
    clear _kidclick_
    spawncityguards immediate
    start timer _delay_

_surrender_ task:
    say 1015
    end quest

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest

_final_ task:
    when _kidclick_ and _ires_
    give pc _reward_
    end quest