Quest: JHTG118
DisplayName: Похищение
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Здесь, в городе, есть ребенок, которого нужно
<ce> похитить и придержать для выкупа.
<ce> Нам нужен человек, который хорошо болтает и может
<ce> быстро перемещаться с места на место.
<ce> Соглашайся, если ты соответствуешь этим требованиям.

RefuseQuest:  [1001]
<ce> Речь действительно важна для вора.
<ce> Надеюсь, когда-нибудь ты научишься красноречию.

AcceptQuest:  [1002]
<ce> Хорошо. Ребёнка зовут
<ce> _kid_. Каждый день %g4 родители оставляют %g4 в
<ce> _tavern_, пока они работают, а потом
<ce> позже приходит родственник и забирает %g4.
<ce> Какие отговорки ты придумаешь,
<ce> это твоя забота, но ты должен утащить этого парня
<ce> из таверны и привести его в _res_.
<ce> Ты можешь рассчитывать на =reward_ золотых.
<ce> Облажаешься - и ничего не получишь.
<ce> У тебя на всё про всё 24 часа.

QuestFail:  [1003]
<ce> Прошло 24 часа, а ты
<ce> не доставил _kid_ в _res_.
<ce> Схема провалилась.

QuestComplete:  [1004]
<ce> Когда ты входишь в резиденцию,
<ce> фигура в капюшоне берет _kid_ за плечо.
<ce> и ведет %g4 в другую комнату.
<ce> Ты ждешь, пока тебе заплатят, и тут понимаешь, что
<ce> мешок с золотом уже каким-то образом оказался у тебя в руке.

Message:  1010
<ce> Типичная какофония _tavern_ бьёт по
<ce> ушам, когда вы входите. Громкий смех, радостные возгласы и
<ce> чей-то храп, доносящийся из угла.

Message:  1011
<ce> _kid_ недоверчиво смотрит на тебя. "Кто ты такой, и
<ce> почему мы так рано уходим? Мы только что приехали.
<ce> Где моя тетя?" Ты придумываешь несколько объяснений, которые
<ce> ребенок наивно принимает, и начинает следовать за тобой.

Message:  1012
<ce> Тебя замечают сразу же после выхода из таверны.
<ce> "Стой!" К тебе подбегает городской стражник.
<ce> "Я много раз видел, как этот ребенок выходил из таверны, но
<ce> всегда с другим человеком. Кто ты такой и что делаешь?"
<ce>
<ce> Твои мысли разбегаются. Попытаться солгать (да) или отдать
<ce> ребенка охраннику (нет)?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Жаргон, сложность очень низкая)

Message:  1013
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> "Итак, тетя попросила тебя приехать и забрать %g4, потому что она
<ce> ранена? Ну, тогда ладно". Он опускается на колени и спрашивает
<ce> _kid_: "Ты веришь %ra?". Ребенок пожимает плечами и говорит,
<ce> "Наверное. Моя тетя - довольно умная женщина.
<ce> Надо будет посмотреть, что с ней не так, когда мы вернемся домой".
<ce>
<ce> "Ладно, проходите, вы двое. Но я буду следить за вами".
<ce> сказал охранник, махнув тебе рукой.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Твой разум застывает. Ты начинаешь придумывать историю,
<ce> но твой рассказ никого не убеждает.
<ce> Ребенок вскрикивает и убегает, а городской стражник
<ce> отходит назад, громко зовя на помощь.

Message:  1015
<ce> Ты отдаешь ребенка стражнику. Он смотрит на тебя
<ce> с некоторым удивлением, и понимает, что ты
<ce> влип по уши. "Ладно. Давай просто забудем об
<ce> этом, и мы позаботимся о том, чтобы этот
<ce> малыш добрался до дома в целости и сохранности".
<ce>
<ce> Ты отступаешь назад и позволяешь им двоим уйти,
<ce> не ввязывясь больше в это дело.

Message:  1030
%qdt:
 Гильдия Воров послала меня
 похитить _kid_, ребенка
 в _tavern_ здесь, в ___tavern_.
 Затем мне нужно отвести %g4 в
 _res_. У меня есть 24 часа.

QBN:
Person _kid_ faction Children

Place _tavern_ local tavern
Place _res_ local house

Clock _timer_ 1.00:00
Clock _delay_ 0:05

Item _reward_ gold range 201 to 399

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _kid_ at _tavern_
    pc at _tavern_ set _idun_
    pc at _res_ set _ires_
    start timer _timer_

variable _ires_
_idun_ task:
    say 1010

_kidclick_ task:
    clicked npc _kid_
    say 1011
    hide npc _kid_
    add _kid_ face

_ambush_ task:
    when _kidclick_ and not _idun_ and not _ires_
    prompt 1012 yes _lie_ no _surrender_

variable _lie_

_skilled_ task:
    when skill Streetwise is at least 15

_winner_ task:
    when _lie_ and _skilled_
    say 1013

_failure_ task:
    when _lie_ and not _skilled_
    say 1014
    setplayercrime Assault
    drop _kid_ face
    clear _kidclick_
    spawncityguards immediate
    start timer _delay_

_surrender_ task:
    say 1015
    end quest

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest

_final_ task:
    when _kidclick_ and _ires_
    give pc _reward_
    end quest