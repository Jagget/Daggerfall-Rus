Quest: JHTG116
DisplayName: Ежедневный грабеж, средний
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Одним из преимуществ членства в Гильдии Воров является
<ce> знание того, где в городе водятся деньги.
<ce> Здесь, в ___qgiver_, есть дом, в котором есть около
<ce> тысячи золотых. Если ты хорошо умеешь красться, то можешь попробовать
<ce> взять их себе. Хочешь, я зарезервирую дом для тебя?
   <--->
<ce> Каждый день здесь, в ___qgiver_, люди прячут свои
<ce> сбережения по домам. Одни делают это хорошо, другие - неаккуратно.
<ce> У нас есть наводка на дом, в котором хранится около тысячи золотых.
<ce> Если хочешь, я могу отметить его для тебя. Это не принесет тебе
<ce> никаких очков в гильдии, но ты получишь немного денег.
<ce> Возьмешь?

RefuseQuest:  [1001]
<ce> Без проблем. Все равно в этом городе нельзя разбогатеть.
   <--->
<ce> Этот уйдет в ближайший час, клянусь.

AcceptQuest:  [1002]
<ce> Ну ладно. Дом называется _house_, на %di отсюда.
<ce> Они знают, что носить с собой столько денег - это риск, поэтому ты
<ce> должен быть хорош. Ты здесь реально рискуешь.
<ce> Я не думаю, что у тебя будет больше двух
<ce> дней до того, как деньги будут потрачены или перепрятаны, так что поторопись.
<ce> И помни, что если тебя поймают, то пострадает и тот, кто дал
<ce> нам наводку на этот дом. Не попадайся.
   <--->
<ce> Твои деньги лежат в _house_,
<ce> это туда, на %di. Ты его сам видел.
<ce> В нем, может быть, тысяча золотых, и они собираются
<ce> следить за ними, как ястребы. И не попадись.
<ce> Тот, кто рассказал нам об этом доме тоже пострадает.
<ce> Такие деньги тратятся за пару дней,
<ce> так что не тяни с этим.

QuestFail:  [1003]
<ce> (ПРОВЕРКА НА СКРЫТНОСТЬ ПРОВАЛЕНА)
<ce>
<ce> Ты немного умеешь красться, но недостаточно.
<ce> Кто-то знает, что ты здесь, и кричит во весь голос.

QuestComplete:  [1004]
<ce> Похоже, ты в полной безопасности. Деньги в твоем распоряжении.

QuestorPostFailure:  [1009]
Помни, что ты должен нам за деньги в _house_.
Найди какую-нибудь работу и расплатись с нами.

Message:  1010
%qdt:
 Гильдия Воров сообщила мне.
 о доме здесь, в ___qgiver_.
 в котором должно храниться
 около тысячи золотых.
 Дом называется _house_.
 Если я смогу найти деньги в ближайшие
 пару дней, они будут моими.

Message:  1011
<ce> Ты находишь сундук и видишь, что на нем стоит
<ce> очень серьёзный замок. Если ты хочешь получить то, что находится внутри,
<ce> тебе придётся отпереть его самому.
<ce> Ты попытаешься открыть его?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Взлом, сложность высокая)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Зная, что у тебя не так много времени, ты приступаешь к работе.
<ce> Проходит не более нескольких секунд, прежде чем крышка
<ce> откидывается. Ты хватаешь содержимое.

Message:  1013
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Зная, что у тебя мало времени, ты приступаешь к работе.
<ce> Ты копаешься в замке и запускаешь то, что не должен был.
<ce> Громкая сирена разносится по всему дому.
<ce> Теперь ты влип.

Message:  1014
<ce> Глядя на замок, ты начинаешь сомневаться в своих способностях.
<ce> Ты все еще можешь выбраться, не подняв большого шума,
<ce> говоришь ты сам себе. Ты отходишь от сундука. Нет смысла
<ce> тащщить его с собой, если ты не можешь его открыть.

Message:  1015
<ce> Это правильный дом.
<ce> Сундук где-то внутри.

QBN:
Item _item_ item class 1 subclass 8
Item _reward_ gold range 800 to 1275

Place _house_ local house

Person _qgiver_ group Questor

Clock _danger_ 00:02 00:05
Clock _ending_ 2.00:00 2.12:00

--	Quest start-up:
    start timer _ending_
    log 1010 step 0
    place item _item_ at _house_
    pc at _house_ do _inside_
    pick one of _seeskill1_ _seeskill2_ _seeskill3_

_inside_ task:
    start timer _danger_
    stop timer _ending_
    say 1015

_notinside_ task:
    when not _inside_
    stop timer _danger_
    start timer _ending_

_danger_ task:
    pick one of _go1_ _go2_ _go3_

_check1_ task:
    when skill Stealth is at least 40

_check2_ task:
    when skill Stealth is at least 50

_check3_ task:
    when skill Stealth is at least 60

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
    when _check1_ and _go1_

_c2_ task:
    when _check2_ and _go2_

_c3_ task:
    when _check3_ and _go3_

_d1_ task:
    when not _check1_ and _go1_
    start task _fail_

_d2_ task:
    when not _check2_ and _go2_
    start task _fail_

_d3_ task:
    when not _check3_ and _go3_
    start task _fail_

_pick1_ task:
    when skill Lockpicking is at least 30

_pick2_ task:
    when skill Lockpicking is at least 35

_pick3_ task:
    when skill Lockpicking is at least 40

_e1_ task:
    when not _pick1_ and _seeskill1_
    start task _skillfail_

_e2_ task:
    when not _pick2_ and _seeskill2_
    start task _skillfail_

_e3_ task:
    when not _pick3_ and _seeskill3_
    start task _skillfail_

variable _skillfail_

_fail_ task:
    setplayercrime Trespassing
    spawncityguards immediate
    say 1003
    change repute with _qgiver_ by -1
    end quest

_grab_ task:
    clicked item _item_
    prompt 1011 yes _attempt_ no _flee_
    take _item_ from pc

variable _attempt_
variable _seeskill1_
variable _seeskill2_
variable _seeskill3_

_flee_ task:
    say 1014

_bigfail_ task:
    when _attempt_ and _skillfail_
    setplayercrime Theft
    spawncityguards immediate
    say 1013
    change repute with _qgiver_ by -1
    end quest

_victory_ task:
    when _attempt_ and not _skillfail_
    get item _reward_
    make _reward_ permanent
    say 1012

_escape_ task:
    when _grab_ and _notinside_
    say 1004
    change repute with _qgiver_ by +2
    end quest

_ending_ task:
    change repute with _qgiver_ by +2
    end quest