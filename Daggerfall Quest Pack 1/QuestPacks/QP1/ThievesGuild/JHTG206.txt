Quest: JHTG206
DisplayName: Плата за заметки
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Предлагается большая награда
<ce> за исследовательские заметки некоего волшебника
<ce> по имени _dummy_. Я полагаю,
<ce> это потому, что %g1 - член
<ce> Гильдии Магов. Если вы не беспокоитесь
<ce> о том, чтобы нажить себе врагов, 
<ce> то можете отправиться в ___res_
<ce> и принести сюда кое-какие бумаги.

RefuseQuest:  [1001]
<ce> Не хотите =reward_ золотых? Что ж.

AcceptQuest:  [1002]
<ce> Я не знаю, как долго будет существовать
<ce> спрос на работы конкретного этого ученого,
<ce> так что вернитесь сюда, скажем, через =timer_ дней.
<ce> Я буду держать вашу долю здесь. Я не думаю, что у него
<ce> есть охрана, так что просто найдите его 
<ce> и возвращайтесь. Ах да, он находится в месте
<ce> под названием _res_ в ___res_.
<ce> Чуть не забыл об этом упомянуть.

QuestFail:  [1003]
<ce> _questgiver_ хотел, чтобы вы
<ce> доставили записки к этому времени.

QuestComplete:  [1004]
<ce> Выглядит как что-то стоящее. Не то чтобы
<ce> я в этом что-то понимаю. Вот ваша доля.

Message: 1021
<ce> Вы берете записки _dummy_.

Message:  1030
%qdt:
 Если я принесу кое-какие заметки исследователя
 из ___res_ обратно в ___questgiver_
 в течение =timer_ дней, то _questgiver_
 готов заплатить за них. %g1 сказал мне,
 что они находятся в _res_.

QBN:

Person _questgiver_ group Questor male
Person _dummymg_ faction The_Mages_Guild
Person _dummy_ group Local_3.1

Item _reward_ gold
Item _notes_ letter

Place _res_ remote random

Clock _timer_ 00:00 0 flag 17 range 0 2

-- Quest start-up:
 start timer _timer_
 log 1030 step 0
 place item _notes_ at _res_

_got_ task:
 clicked item _notes_
 say 1021
 change repute with _dummymg_ by -10

_victory_ task:
  toting _notes_ and _questgiver_ clicked
  give pc _reward_
  end quest

_timer_ task:
  say 1003
  end quest