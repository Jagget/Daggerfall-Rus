Quest: JHTGX07
DisplayName: Как Ворон летает
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX07

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX07

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX07

QuestFail:  [1003]
<ce> ERROR 1003 JHTGX07

QuestComplete:  [1004]
<ce> Может быть, это трудно для других, но не для тебя. Возьми свое золото.
   <--->
<ce> Ты сделаешь нас обоих богатыми. Вот твоя зарплата.
   <--->
<ce> Как всё прошло? Довольно обыденно для тебя, я думаю. =reward_ золотых для тебя.
   <--->
<ce> Рад видеть тебя снова с полной сумкой. Держи.

RumorsDuringQuest:  [1005]
Говорят, вы с Вороном умрете вместе.
   <--->
Доволен именем, которое сам себе заработал? Гильдия Бойцов тебя ненавидит.
   <--->
Ты никогда не беспокоился о том, что Ворон нанесет тебе удар в спину?
   <--->
Что ты вообще имеешь против Гильдии Бойцов?

Message:  1010
<ce> Ладно, вот что у меня есть.
<ce> Куча обычных целей находится
<ce> в ___place_. Имя - _fg_.
<ce> Мне нужен один мозг, три руки и пять ног. Встретимся в
<ce> ___crowhome_, когда закончишь.
<ce> Я буду в _crowhome_.
<ce> На этот раз получишь =reward_ золотых.
   <--->
<ce> На этот раз ты будешь искать _fg_
<ce> в ___place_. Убей там всех и
<ce> принеси мне шесть лёгких и две правые ноги.
<ce> После я буду в ___crowhome_, в
<ce> _crowhome_. Это стоит =reward_ золотых.
   <--->
<ce> Этот довольно просто. Шесть ушей и шесть глаз.
<ce> Ищи _fg_ в ___place_.
<ce> Я буду в ___crowhome_, когда ты закончишь.
<ce> Ищи меня в _crowhome_.
<ce> На этот раз получишь =reward_ золотых.
   <--->
<ce> Я посылаю тебя в ___place_, чтобы найти
<ce> человека по имени _fg_. Убей %g3 и %g3
<ce> друзей и возьми два сердца и шесть больших пальцев. Я буду
<ce> в ___crowhome_, в _crowhome_.

Message:  1030
%qdt:
 Ворон посылает меня в
 ___place_ на поиски
 кого-то по имени _fg_.
 Выполнение заказа означает =reward_
 золотых. Затем я смогу найти
 его в ___crowhome_,
 в _crowhome_.

Message:  1060
<ce> "Кто-нибудь, помогите! %pcf здесь!"
   <--->
<ce> "Помогите! Помогите! Я сейчас умру!"
   <--->
<ce> "Убийца здесь! Остановите %pg3!"
   <--->
<ce> "Я не хочу умирать! Помогите мне!"

Message:  1070
<ce> Похоже, что это всё. Пора собирать органы.
   <--->
<ce> Больше противников не осталось. Этих тел хватит для Ворона.
   <--->
<ce> Поскольку все мертвы, пришло время собрать то, что нужно Ворону.
   <--->
<ce> С убийством покончено, осталось собрать урожай.

Message:  1071
<ce> Ты помещаешь органы в мешок в соответствии с инструкциями Ворона.
   <--->
<ce> Ты вынимаешь те части, которые просил Ворон, и запечатываешь мешок.
   <--->
<ce> Ты наполняешь мешок, как просил Ворон, и закрываешь его.
   <--->
<ce> Ты берешь из тел то, что тебе нужно, и закрываешь сумку.

Message:  1080
<ce> Желаешь ли ты выкинуть _lode_
<ce> и отказаться от своих связей с _crow_?

Message:  1081
<ce> Ты выбрасываешь маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат только ему.

Message:  1082
<ce> Ты возвращаешь _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:
Person _crow_ named The_Crow
Person _fg_ faction The_Fighters_Guild

Place _crowhome_ remote house
Place _place_ remote random

Item _reward_ gold range 2501 to 3999
Item _lode_ lodestone
Item _bag_ small_sack
Item _bag2_ small_sack

Foe _warr_ is Warrior
Foe _rog_ is Rogue
Foe _barb_ is Barbarian
Foe _kni_ is Knight

--	Quest start-up:
  say 1010
  log 1030 step 0
  get item _lode_
  get item _bag_
  place npc _fg_ at _place_
  _lode_ used do _prompt_
  pc at _place_ do _inside_
  pick one of _a1_ _a2_ _a3_ _a4_

variable _a1_
variable _a2_
variable _a3_
variable _a4_

_inside_ task:
  hide npc _fg_
  say 1060

_s1_ task:
  when _inside_ and _a1_
  create foe _warr_ every 0 minutes 3 times with 100% success

_s2_ task:
  when _inside_ and _a2_
  create foe _rog_ every 0 minutes 3 times with 100% success

_s3_ task:
  when _inside_ and _a3_
  create foe _barb_ every 0 minutes 3 times with 100% success

_s4_ task:
  when _inside_ and _a4_
  create foe _kni_ every 0 minutes 3 times with 100% success

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_infight_ task:
  daily from 0:00 to 23:59
  change foe _warr_ infighting true
  change foe _rog_ infighting true
  change foe _barb_ infighting true
  change foe _kni_ infighting true
  clear _infight_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_slain_ task:
  killed 3 _warr_
  killed 3 _rog_
  killed 3 _barb_
  killed 3 _kni_
  say 1070
  change repute with _fg_ by -10
  _bag_ used do _take_
  place npc _crow_ at _crowhome_

variable _take_
_take2_ task:
  when _inside_ and _take_
  take _bag_ from pc
  get item _bag2_
  say 1071

_talk_ task:
  toting _bag2_ and _crow_ clicked
  give pc _reward_
  start quest JHTGX07
  end quest