Quest: JHTG120
DisplayName: Замок аристократа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас в городе есть член гильдии, которому удалось
<ce> сорвать большой куш, совершив взлом. Украден ящик
<ce> с очень ценными вещами внутри,
<ce> но нужен слесарь, чтобы открыть его.
<ce> Это не будет свершением мирового масштаба,
<ce> но вам лучше знать, как взламывать сложные замки,
<ce> если вы собираетесь взяться за это дело.

RefuseQuest:  [1001]
<ce> Хорошо. Не притворяйтесь тем, кем вы не являетесь.

AcceptQuest:  [1002]
<ce> Отправляйтесь в _tav_ и ищите _thief_.
<ce> Мы сказали %g2 не приносить сюда ящик
<ce> на всякий случай, вдруг что-то пойдет не так.
<ce> Нам здесь не нужны городские стражники.
<ce> Если вы откроете его, ваша репутация в гильдии повысится,
<ce> как и за любую другую работу. 
<ce> _thief_ заплатит гильдийский взнос, так что все, что %g1
<ce> даст вам, будет вашим. Не ждите дольше, чем 1 день,
<ce> чтобы добраться туда, %g1 не может ждать вечно.

QuestFail:  [1003]
<ce> _thief_ отказался ждать вас в _tav_.

QuestComplete:  [1004]
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы были созданы для таких моментов.
<ce> Вы садитесь перед коробкой и приступаете к работе.
<ce> Есть несколько сюрпризов, которые вас беспокоят,
<ce> но вы справляетесь с ними и открываете засов.
<ce> _thief_ громко выдыхает, когда ящик открывается.
<ce>
<ce> "Очень мило. Позвольте мне взглянуть...
<ce> Пачки денег, это хорошо.
<ce> Позвольте мне взять долю гильдии 
<ce> и вы получите =gold_ золотых. А это что такое?
<ce> Пара зачарованных предметов.
<ce> Я забираю свой, а вам достанется второй.
<ce> Приятно было познакомиться".
<ce>
<ce> _thief_ быстро собирает в кучу свою долю
<ce> и поспешно выходит за дверь.

Message:  1010
<ce> "Наконец-то они прислали помощь. Эта штука меня пугает,
<ce> так что вам лучше знать, что вы делаете. Я отойду
<ce> и понаблюдаю издалека на всякий случай".
<ce>
<ce> Замок сделан хорошо и витиевато, но ничего необычного.
<ce> Судя по всему, его легко взломает вор среднего уровня.
<ce> Вы все еще можете отступить, если не уверены.
<ce> Попытаетесь ли вы открыть шкатулку?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Взлом, сложность средняя)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы садитесь перед коробкой и приступаете к работе.
<ce> Вы удивлены тем, что находите внутри замка. Вещи находятся
<ce> не на своих обычных местах, и вы находите трудности
<ce> и секреты там, где не ожидаете. У вас возникает плохое 
<ce> предчувствие, и как раз в тот момент, когда вы собираетесь
<ce> вынуть отмычку, слышите громкий треск, доносящийся изнутри 
<ce> коробки. Из верхней части начинает вырываться пар, 
<ce> и громкий свистящий звук разносится по таверне.
<ce> Вы не можете его выключить.
<ce>
<ce> "ИДИОТ! СТРАЖНИКИ ЭТО УСЛЫШАТ! БЕГИТЕ!!!"
<ce>
<ce> _thief_ хватает коробку и убегает.
<ce> Теперь вам придется разбираться с последствиями.

Message:  1013
<ce> "Правда? Ну что ж. Я рад, что вы достаточно умны,
<ce> чтобы отступить, когда понимаете, что это выше ваших сил.
<ce> Некоторые из этих придурков в гильдии так бы и продолжали
<ce> бить по этой коробке, пока не сломают запястье. Я скажу 
<ce> _qgiver_, чтобы он прислал кого-нибудь другого. Пока!".
<ce>
<ce> _thief_ относит ящик в другой угол
<ce> таверны, чтобы не вызывать подозрений.

Message:  1030
%qdt:
 Я подписался на то, чтобы
 прийти в _tav_, ___tav_,
 и открыть запертый ящик,
 который держит _thief_
 из Гильдии Воров.
 Я должен прибыть туда в течение
 24 часов, чтобы открыть его.

QBN:
Item _gold_ gold range 500 to 700
Item _reward_ magic_item

Place _tav_ local tavern

Person _thief_ faction The_Thieves_Guild
Person _qgiver_ group Questor

Clock _timer_ 1.00:00
Clock _delay_ 0:03

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _thief_ at _tav_

_found_ task:
    clicked npc _thief_
    prompt 1010 yes _attempt_ no _flee_

variable _attempt_
_flee_ task:
    say 1013
    hide npc _thief_
    end quest

_skilled_ task:
    when skill Lockpicking is at least 40

_victory_ task:
    when _attempt_ and _skilled_
    get item _gold_
    make _gold_ permanent
    give pc _reward_
    hide npc _thief_
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    hide npc _thief_
    remove log step 0
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_
    stop timer _timer_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest