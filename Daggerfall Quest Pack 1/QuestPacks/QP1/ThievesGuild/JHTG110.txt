Quest: JHTG110
DisplayName: Предотвращение шантажа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас тут есть просьба для человека твоих талантов.
<ce> Запечатанный пакет с очень важным содержимым
<ce> был украден у одного из наших высокопоставленных членов.
<ce> Он будет использован для шантажа, если кто-нибудь не вернёт его.
<ce> Нам нужен кто-то быстрый, умный и достаточно выносливый, чтобы
<ce> спуститься в пещеру и достать его, пока там не
<ce> поднялась тревога. Ты в настроении?

RefuseQuest:  [1001]
<ce> Что ж, я полагаю, зачарованный _item_ достанется кому-то другому.

AcceptQuest:  [1002]
<ce> Хорошо. Теперь никаких отступлений, потому что я посылаю
<ce> для этого только одного человека, и это ты. Ищи
<ce> небольшую холщовую сумку где-то в ___dungeon_. Я ожидаю,
<ce> что она будет хорошо спрятана, но на самом деле я не
<ce> знаю. Главная проблема - это сигнализация, которой они
<ce> оснастили это место. Если она узнает, что незнакомец
<ce> находится внутри уже 4 часа, она начнет издавать звуки,
<ce> и начнутся плохие вещи. Не знаю, что именно, но лучше
<ce> тебе этого не проверять. Выйди на улицу, если тебе нужно
<ce> отдохнуть. Не делай этого в помещении. Всего у тебя
<ce> будет только четыре часа, поэтому используй время с
<ce> пользой. Вернись сюда через =timer_ дней, и ты получишь
<ce> в награду этот зачарованный _item_.

QuestFail:  [1003]
<ce> _qgiver_ ожидал, что ты уже вернешься с запечатанным мешком.

QuestComplete:  [1004]
<ce> Будь я проклят! Шантаж - дело суровое, но ты еще суровее. Мы
<ce> найдем того гнусного коротышку, который подставил нашего брата, не
<ce> волнуйся. Все, что тебя должно волновать - этот _item_, который
<ce> я тебе обещал. Держи.

Message:  1010
<ce> Ты вспоминаешь слова _qgiver_:
<ce>
<ce> "Если оно узнает, что незнакомец находится внутри в течение 4 часов,
<ce> оно начнет издавать звуки, и начнут происходить плохие вещи. Понятия
<ce> не имею, что именно, но лучше тебе этого не проверять. Выйди на улицу,
<ce> если тебе нужно отдохнуть. Не делай этого в помещении. В общей
<ce> сложности у тебя будет только четыре часа".
<ce>
<ce> Пора приступать к работе.

Message:  1011
<ce> Ты нашел сумку. Однако ты не находишь на ней
<ce> ничего, связанного с сигнализацией. Все же тебе
<ce> лучше поспешить убраться отсюда.

Message:  1012
<ce> В воздухе начинает раздаваться легкий свистящий звук. Если бы ты не
<ce> прислушивался к нему, то, скорее всего, даже не заметил бы. В ответ на
<ce> это стены вокруг тебя начинают двигаться и принимать человеческую форму.
<ce> Хотя в прошлом ты сталкивался с более серьезными опасностями, это станет
<ce> серьезным препятствием для выполнения твоей миссии.

Message:  1013
<ce> Даже твоя лучшая магия не помогает вскрыть этот мешок.
<ce> Его содержимое, должно быть, очень конфиденциально.

Message:  1030
%qdt:
 В обмен на зачарованный _item_,
 Я отправляюсь в ___dungeon_ за мешком
 с уликами против одного из членов
 Гильдии Воров. Я могу находиться внутри только четыре часа
 прежде чем начнет звучать сигнал тревоги. Мне нужно получить его
 и вернуться к ___qgiver_ через =timer_ дней.

QBN:
Item _item_ magic_item
Item _bag_ small_sack

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _hunt_ 4:00

Foe _enemy1_ is Iron_atronach

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    reveal _dungeon_
    pc at _dungeon_ set _inside_
    place item _bag_ at _dungeon_

_inside_ task:
    say 1010
    start timer _hunt_

_stop_ task:
    when not _inside_
    stop timer _hunt_

_found_ task:
    clicked item _bag_
    say 1011
    cast Open effect do _fail_

_fail_ task:
    say 1013

_clearclick_ task:
    toting _bag_ and _qgiver_ clicked
    give pc _item_
    end quest

_hunt_ task:
    say 1012

_spawn_ task:
    when _hunt_ and _inside_
    create foe _enemy1_ every 1 minutes indefinitely with 50% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _enemy1_ infighting true

_timer_ task:
    say 1003
    end quest