Quest: JHTG110
DisplayName: Предотвращение шантажа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас тут есть просьба для человека ваших талантов.
<ce> Запечатанный мешок с очень важным содержимым был 
<ce> украден у одного из наших высокопоставленных членов. Он 
<ce> будет использован для шантажа, если кто-нибудь не вернет
<ce> его. Нам нужен кто-то быстрый, умный и достаточно 
<ce> выносливый, чтобы спуститься в пещеру и достать его, 
<ce> пока не поднялась тревога. Вы в настроении?

RefuseQuest:  [1001]
<ce> Что ж, полагаю, зачарованный _item_ достанется кому-то
<ce> другому.

AcceptQuest:  [1002]
<ce> Хорошо. Теперь никаких отступлений, потому что я посылаю
<ce> для этого только одного человека, и это вы. Ищите
<ce> небольшой холщовый мешок где-то в ___dungeon_. Я ожидаю,
<ce> что он будет хорошо спрятан, но на самом деле я не
<ce> знаю. Главная проблема - это сигнализация, которой они
<ce> оснастили это место. Механизм среагирует, если вы будете
<ce> находиться внутри больше 4 часов, он начнет издавать 
<ce> звуки, и начнутся плохие вещи. Не знаю, что именно, 
<ce> но лучше вам это не проверять. Выйдите на улицу, если 
<ce> вам нужно отдохнуть. Не делайте этого в помещении. Всего
<ce> у вас будет только четыре часа, поэтому используйте 
<ce> время с пользой. Вернитесь сюда через =timer_ дней 
<ce> и вы получите в награду этот зачарованный _item_.

QuestFail:  [1003]
<ce> _qgiver_ ожидал, что вы уже вернетесь с запечатанным 
<ce> мешком.

QuestComplete:  [1004]
<ce> Будь я проклят! Шантаж - дело суровое, но вы еще суровее. 
<ce> Мы найдем того гнусного коротышку, который подставил 
<ce> нашего брата, не волнуйтесь. Все, что вас должно 
<ce> волновать - этот _item_, который я вам обещал. Держите.

Message:  1010
<ce> Вы вспоминаете слова _qgiver_:
<ce>
<ce> "Механизм среагирует, если вы будете находиться внутри 
<ce> больше 4 часов, он начнет издавать звуки, и начнутся
<ce> плохие вещи. Не знаю, что именно, но лучше вам это 
<ce> не проверять. Выйдите на улицу, если вам нужно отдохнуть.
<ce> Не делайте этого в помещении. Всего у вас будет только 
<ce> четыре часа".
<ce> Пора приступать к работе.

Message:  1011
<ce> Вы нашли мешок. Однако вы не видите на нем
<ce> ничего, связанного с сигнализацией. Все же вам
<ce> лучше поспешить убраться отсюда.

Message:  1012
<ce> В воздухе начинает раздаваться легкий свистящий звук. 
<ce> Если бы вы не прислушивались к нему, то, скорее всего,
<ce> даже не заметили бы. В ответ на это стены вокруг вас
<ce> начинают двигаться и принимать человеческую форму. 
<ce> Хотя в прошлом вы сталкивались и с более серьезными 
<ce> опасностями, это станет серьезным препятствием 
<ce> для выполнения вашей миссии.

Message:  1013
<ce> Даже ваша лучшая магия не помогает вскрыть этот мешок.
<ce> Его содержимое, должно быть, очень конфиденциально.

Message:  1030
%qdt:
 В обмен на зачарованный _item_
 я отправляюсь в ___dungeon_ за мешком
 с уликами против одного из членов
 Гильдии Воров. Я могу находиться внутри только четыре часа,
 прежде чем начнет звучать сигнал тревоги. Мне нужно получить
 его и вернуться к ___qgiver_ через =timer_ дней.

QBN:
Item _item_ magic_item
Item _bag_ small_sack

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _hunt_ 4:00

Foe _enemy1_ is Iron_atronach

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    reveal _dungeon_
    pc at _dungeon_ set _inside_
    place item _bag_ at _dungeon_

_inside_ task:
    say 1010
    start timer _hunt_

_stop_ task:
    when not _inside_
    stop timer _hunt_

_found_ task:
    clicked item _bag_
    say 1011
    cast Open effect do _fail_

_fail_ task:
    say 1013

_clearclick_ task:
    toting _bag_ and _qgiver_ clicked
    give pc _item_
    end quest

_hunt_ task:
    say 1012

_spawn_ task:
    when _hunt_ and _inside_
    create foe _enemy1_ every 1 minutes indefinitely with 50% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _enemy1_ infighting true

_timer_ task:
    say 1003
    end quest