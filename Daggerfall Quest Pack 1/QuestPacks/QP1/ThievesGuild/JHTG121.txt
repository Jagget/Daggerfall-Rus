Quest: JHTG121
DisplayName: Мелкое воровство
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вы знаете, как чистить карманы, %pct?
<ce> У нас есть кое-кто в таверне в городе.
<ce> И этот кто-то держит в руках то, что не должен.
<ce> Нам нужно, чтобы это было у нас к концу дня.

RefuseQuest:  [1001]
<ce> Не самое популярный навык. Позор нам как ворам.

AcceptQuest:  [1002]
<ce> Хорошо, цель в _tav_,
<ce> это на %di, в ___tav_.
<ce> Ищите парня со шрамом на подбородке,
<ce> рыжие волосы. В одном из карманов у него лежит
<ce> листок бумаги, на котором написана очень важная информация.
<ce> Что это такое, вам знать необязательно.
<ce> Принесите ее сюда, пока не истек один день.

QuestFail:  [1003]
<ce> Гильдия Воров хотела, чтобы к этому времени вы уже
<ce> украли и доставили бумагу от рыжего человека.

QuestComplete:  [1004]
<ce> Вот оно. Я не слышу, чтобы кто-то кричал "стой",
<ce> так что буду считать, что все было выполнено чисто.
<ce> Отличная работа. Нам бы не помешало побольше
<ce> таких карманников, как вы.

QuestorPostSuccess:   [1008]
Как поживают пальцы? Украли что-нибудь хорошее в последнее
время?

QuestorPostFailure:   [1009]
А-а, наш так называемый карманник. Такими темпами гильдия
превратится в посмешище.

Message:  1010
<ce> Так совпало, что ваша цель вот-вот выйдет из таверны.
<ce> Он соответствует описанию. Если вы собираетесь обокрасть
<ce> его, то это ваш шанс. Воспользуетесь ли вы им?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Карманные кражи, низкая сложность)

Message:  1011
<ce> Слова на этой бумаге постоянно смещаются.
<ce> Чтобы прочитать их, вам понадобится заклинание.

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Вы ждете, пока он пройдет мимо, прежде чем сунуть руку
<ce> в задний карман его одеяния. Карман оказался пуст, и цель
<ce> замечает вашу попытку. Он наносит вам быстрый удар
<ce> наотмашь, прежде чем позвать стражу и скрыться в городе.

Message:  1013
<ce> У вас могут возникнуть серьезные проблемы, если вы сделаете
<ce> что-то подобное. Вы отходите в сторону и позволяете
<ce> мужчине покинуть таверну. _qgiver_ может быть недоволен,
<ce> но так вы чувствуете себя лучше.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы знаете, как с этим справиться. Вы нацеливаетесь
<ce> на карман мужчины и как бы случайно натыкаетесь на него.
<ce> Он злится и ворчит из-за этой стычки, но не замечает
<ce> вашей кражи. Вы извиняетесь, и он уходит, ворча под нос.

Message:  1030
%qdt:
 Какой-то мужчина с рыжими волосами и шрамом на подбородке
 находится в _tav_, имея на руках бумагу, представляющую
 ценность для Гильдии Воров. Мне нужно стащить этот документ
 и доставить _qgiver_ сюда
 в ___tav_ до того, как истечет день.

QBN:
Item _gold_ gold range 150 to 200
Item _letter_ letter used 1011

Place _tav_ local tavern

Person _qgiver_ group Questor

Clock _timer_ 1.00:00
Clock _delay_ 0:02

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _tav_ do _inside_

_skilled_ task:
    when skill Pickpocket is at least 25

_inside_ task:
    prompt 1010 yes _attempt_ no _flee_

_flee_ task:
    say 1013
    end quest

variable _attempt_

_theft_ task:
    when _attempt_ and _skilled_
    get item _letter_
    say 1014

_victory_ task:
    toting _letter_ and _qgiver_ clicked
    give pc _gold_
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    remove log step 0
    stop timer _timer_
    reduce player health by 5

_outside_ task:
    when _bigfail_ and not _inside_
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest
