Quest: JHTG122
DisplayName: Карманы мага
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Какой-то кадр из Гильдии Магов перешел границы
<ce> дозволенного, и нам нужно отправить сообщение.
<ce> Для этого нужно незаметно украсть кое-что из одного плаща.
<ce> Если вы собираетесь взяться за это дело, убедитесь,
<ce> что у вас это хорошо получается.

RefuseQuest:  [1001]
<ce> Ну, злить волшебников может быть плохой идеей.

AcceptQuest:  [1002]
<ce> Парня зовут _mage_. Тусуется в Гильдии Магов здесь,
<ce> в ___magery_, большую часть времени. Зайдите туда
<ce> в приемные часы и поищите его.
<ce> Ходят слухи, что он выглядит как =mage_.
<ce> Где-то в его вещах лежит зачарованный _reward_.
<ce> Стащите это, убедитесь, что вас не видят, и оставьте себе
<ce> в качестве награды. Гильдия Воров позаботится о том,
<ce> чтобы он узнал, почему вещь пропала.
<ce> Сделайте это до завтра к этому времени.

QuestFail:  [1003]
<ce> Вы должны были украсть зачарованный _reward_
<ce> у мага в ___magery_ к этому времени.

QuestComplete:  [1004]
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Прикинувшись невеждой, вы спрашиваете, где выход.
<ce> _mage_ выглядит растерянным и указывает
<ce> вниз по коридору. Он отвлекается достаточно для того,
<ce> чтобы вы могли украсть _reward_.
<ce> Вы благодарите его, и он возвращается к своим занятиям.
<ce>
<ce> Гильдия Воров сегодня побеждает.

QuestorPostSuccess:   [1008]
Передайте от меня привет Гильдии Магов, когда будете там
в следующий раз.

QuestorPostFailure:   [1009]
Вы записались в карманники и потерпели неудачу.
Это на вашей совести.

Message:  1010
<ce> _mage_, =mage_, находится где-то здесь.

Message:  1011
<ce> Прежде чем вы успеваете обокрасть _mage_,
<ce> он поворачивается и смотрит на вас.
<ce>
<ce> "Да? Могу ли я вам помочь?".
<ce>
<ce> С его внимательностью вам будет
<ce> нелегко украсть _reward_. Но если вы собираетесь
<ce> это сделать, то сейчас у вас есть шанс.
<ce> Будете ли вы пытаться?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Карманные кражи, средняя сложность)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Знать, что у него есть _reward_ - это одно,
<ce> но реально совершить кражу - это другое.
<ce> Вы тянете время и вызываете подозрения у _mage_.
<ce> Вскоре он телепортируется в безопасное место,
<ce> а рядом появляются охранники.

Message:  1013
<ce> В данный момент это слишком рискованно.
<ce> Вы игнорируете его и оставляете это задание.

Message:  1030
%qdt:
 _mage_ - это =mage_,
 маг в _magery_
 в ___magery_, которому нужно преподать урок.
 Он носит с собой магический _reward_,
 который нужно украсть.
 У меня есть 24 часа, чтобы сделать это,
 чтобы заслужить благосклонность Гильдии Воров.

QBN:
Item _reward_ magic_item

Place _magery_ local magery

Person _mage_ faction The_Mages_Guild male remote

Foe _atr1_ is Iron_atronach
Foe _atr2_ is Flesh_atronach

Clock _timer_ 1.00:00
Clock _delay_ 0:02

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _magery_ do _inside_

_skilled_ task:
    when skill Pickpocket is at least 45

variable _inside_

_daily_ task:
    daily from 11:00 to 23:00

_placenpc_ task:
    when _inside_ and _daily_
    say 1010
    place npc _mage_ at _magery_

_talk_ task:
    clicked npc _mage_
    prompt 1011 yes _attempt_ no _flee_

_flee_ task:
    say 1013
    end quest

variable _attempt_

_theft_ task:
    when _attempt_ and _skilled_
    give pc _reward_
    change repute with _mage_ by -10
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    hide npc _mage_
    remove log step 0
    stop timer _timer_
    create foe _atr1_ every 0 minutes 1 times with 100% success
    create foe _atr2_ every 0 minutes 1 times with 100% success
    change repute with _mage_ by -5
    change foe _atr1_ infighting true
    change foe _atr2_ infighting true
    start timer _delay_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest
