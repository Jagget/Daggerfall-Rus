Quest: JHTGX05
DisplayName: Агнец
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX05

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX05

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX05

QuestFail:  [1003]
<ce> ERROR 1003 JHTGX05

QuestComplete:  [1004]
<ce> О да, это именно то, что мы искали.
<ce> Совсем неплохо. За следующий заказ вы получите еще больше,
<ce> поверьте мне. Продолжайте в том же духе.

RumorsDuringQuest:  [1005]
Говорят, что Ворон снова появляется в заливе. Я думаю, что это самозванец.
   <--->
Вы слышали о том, что Ворон появился в каком-то городе? Скорее всего, это ложь.
   <--->
Говорят, что Ворон где-то рядом, но мне казалось, что он давно умер.

RumorsPostSuccess:  [1007]
Если %ra когда-нибудь попадется Гильдии Бойцов, они разорвут %pg3 на части.

Message:  1010
<ce> "На этот раз я собираюсь рассказать вам небольшую
<ce> предысторию. Полагаю, что вы доказали свою состоятельность
<ce> и все равно уже увязли в этом по уши. Я знаю, о чем вы
<ce> думаете, и нет, я не каннибал, и да, те деньги,
<ce> которые я вам плачу, не мои. Мне платят также, как и вам,
<ce> наши добрые работодатели. Они управляют рядом организаций
<ce> в Имперской провинции.
<ce> Какое-то место под названием Бруна, Бруннер или что-то
<ce> в этом роде. Не могу вспомнить, так как никогда туда
<ce> не ездил. Я не спрашиваю их, для чего нужны органы, и они
<ce> не говорят. Что я знаю точно, так это то, что они платят
<ce> как %t. Некоторые люди смотрят свысока на то, что мы
<ce> делаем, и это их точка зрения, но, как по мне, мы
<ce> удовлетворяем законный спрос для нуждающихся клиентов.
<ce> И если мы этого не сделаем, то это сделает кто-то другой.
<ce>
<ce> Вот и все. Одно условие, которое они мне озвучили,
<ce> заключается в том, что они предпочитают "хорошо
<ce> тренированные" тела. Я посылал им образцы от магов,
<ce> рыцарей и воров, но они продолжают говорить мне,
<ce> что лучшие из них - от Гильдии Бойцов. Не знаю почему".

Message:  1011
<ce> "Итак, ваша следующая работа проста. Вы будете заниматься
<ce> убийствами и мародерством. Есть еще один придурок
<ce> из Гильдии Бойцов, живущий в ___res_, в доме,
<ce> который называется _res_. Найдите этого гада
<ce> ночью, чтобы не привлекать к себе внимания.
<ce> В этот раз вы принесете мне легкие и мозг.
<ce> Возьмите этот мешок и действуйте, как обычно.
<ce> Если мне придется объяснять это сейчас, то вы уволены.
<ce> Приходите ко мне в _crowhome_ в ___crowhome_,
<ce> когда закончите. За это платят =reward_ золотых".
<ce>
<ce> Ворон машет вам рукой и уходит.

Message:  1030
%qdt:
 Моя следующая цель для Ворона
 находится в _res_
 в ___res_, это
 член Гильдии Бойцов.
 Я должен прийти ночью.
 Ворон заплатит мне
 в _crowhome_
 в ___crowhome_.

Message:  1060
<ce> Ваш вход не остался незамеченным. Вскоре ваша
<ce> цель начинает идти в вашу сторону.

Message:  1070
<ce> Если убийства всегда будут такими свежими, надеюсь,
<ce> что люди из Имперской провинции начнут слать
<ce> больше денег за заказы. Теперь нужно
<ce> использовать мешок, чтобы собрать материалы.

Message:  1071
<ce> Вы собираете легкие и мозг и завязываете мешок.

Message:  1080
<ce> Желаете ли вы выкинуть _lode_
<ce> и отказаться от своих связей с _crow_?

Message:  1081
<ce> Вы выбрасываете маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат
<ce> только ему.

Message:  1082
<ce> Вы возвращаете _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:

Person _crow_ named The_Crow
Person _fg_ faction The_Fighters_Guild

Place _crowhome_ remote house
Place _res_ remote house

Item _reward_ gold range 2201 to 2600
Item _lode_ lodestone
Item _bag_ small_sack
Item _bag2_ small_sack

Foe _warr_ is Warrior

--	Quest start-up:
  say 1010
  say 1011
  log 1030 step 0
  get item _lode_
  get item _bag_
  _lode_ used do _prompt_
  pc at _res_ do _inside_

variable _inside_

_day_ task:
  daily from 6:00 to 18:00

_spawn_ task:
  when _inside_ and not _day_
  say 1060
  create foe _warr_ every 0 minutes 1 times with 100% success

_infight_ task:
  daily from 0:00 to 23:59
  change foe _warr_ infighting true
  clear _infight_

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_slain_ task:
  killed 1 _warr_
  say 1070
  change repute with _fg_ by -10
  _bag_ used do _take_
  place npc _crow_ at _crowhome_

variable _take_
_take2_ task:
  when _inside_ and _take_
  take _bag_ from pc
  get item _bag2_
  say 1071

_talk_ task:
  toting _bag2_ and _crow_ clicked
  give pc _reward_
  start quest JHTGX06
  end quest