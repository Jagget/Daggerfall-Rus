Quest: JHTGX05
DisplayName: Little Lamb
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX05

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX05

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX05

QuestFail:  [1003]
<ce> ERROR 1003 JHTGX05

QuestComplete:  [1004]
<ce> О да, это именно то, что мы искали.
<ce> Совсем неплохо. За следующий заказ ты получишь еще больше,
<ce> поверь мне. Продолжай в том же духе.

RumorsDuringQuest:  [1005]
Говорят, что Ворон снова появляется в заливе. Я думаю, что это самозванец.
   <--->
Ты слышал о том, что Ворон появился в каком-то городе? Скорее всего, это ложь.
   <--->
Говорят, что Ворон где-то рядом, но я думал, что он давно умер.

RumorsPostSuccess:  [1007]
Если %ra когда-нибудь попадётся Гильдии Бойцов, они разорвут %pg3 на части.

Message:  1010
<ce> "На этот раз я дам тебе небольшую предысторию.
<ce> Полагаю, что ты доказал свою состоятельность и уже
<ce> в любом случае по уши в этом. Я знаю, о чём ты думаешь
<ce> и нет, я не каннибал, и да, те
<ce> деньги, которыми я тебе плачу, не мои. Мне платят
<ce> так же, как и тебе, наши добрые работодатели. Они управляют
<ce> рядом организаций в Имперской провинции.
<ce> Какое-то место под названием Бруна, Бруннер или что-то в этом роде.
<ce> Не могу вспомнить, так как никогда туда не ездил. Я не
<ce> спрашиваю их, для чего нужны органы, а они не говорят.
<ce> Что я знаю точно, так это то, что они платят как %t.
<ce> Некоторые люди смотрят свысока на то, что мы делаем, и
<ce> это их точка зрения, но как по мне, мы
<ce> обеспечиваем законный спрос для нуждающихся клиентов.
<ce> И если мы этого не сделаем, то это сделает кто-то другой.
<ce>
<ce> Вот и все. Одно условие, которое они мне сказали, заключается в том, что они
<ce> предпочитают "хорошо тренированные" тела. Я послал им
<ce> образцы от магов, от рыцарей, от воров,
<ce> и они продолжают говорить мне, что лучшие из них из
<ce> Гильдии Бойцов. Не знаю почему".

Message:  1011
<ce> "Итак, твоя следующая работа проста. Ты будешь заниматься убийствами
<ce> и мародерством. Есть еще один из этих идиотов Гильдии Бойцов
<ce> живущий в ___res_, в доме, 
<ce> который называется _res_. Найди этого гада
<ce> ночью, чтобы не привлекать к себе внимания. В этот
<ce> раз ты принесешь мне легкие и мозг.
<ce> Возьми эту сумку и действуй как обычно. Если мне придется
<ce> объяснять это сейчас, то ты уволен. Приходи ко мне
<ce> в _crowhome_ в ___crowhome_,
<ce> когда закончишь. За это платят =reward_ золотых".
<ce>
<ce> Ворон машет тебе рукой и уходит.

Message:  1030
%qdt:
 Моя следующая цель для Ворона
 находится в _res_
 в ___res_, это
 член Гильдии Бойцов.
 Я должен прийти ночью.
 Ворон заплатит мне
 в _crowhome_
 в ___crowhome_.

Message:  1060
<ce> Твой вход не остался незамеченным. Вскоре твоя
<ce> цель начинает идти в твою сторону.

Message:  1070
<ce> Если убийства всегда будут такими свежими, надеюсь, что
<ce> люди из Имперской провинции начнут слать
<ce> больше денег за заказы. Теперь нужно
<ce> использовать сумку, чтобы собрать материалы.

Message:  1071
<ce> Ты собираешь легкие и мозг и запечатываешь сумку.

Message:  1080
<ce> Желаешь ли ты выкинуть _lode_
<ce> и отказаться от своих связей с _crow_?

Message:  1081
<ce> Ты выбрасываешь маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат только ему.

Message:  1082
<ce> Ты возвращаешь _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:

Person _crow_ named The_Crow
Person _fg_ faction The_Fighters_Guild

Place _crowhome_ remote house
Place _res_ remote house

Item _reward_ gold range 2201 to 2600
Item _lode_ lodestone
Item _bag_ small_sack
Item _bag2_ small_sack

Foe _warr_ is Warrior

--	Quest start-up:
  say 1010
  say 1011
  log 1030 step 0
  get item _lode_
  get item _bag_
  _lode_ used do _prompt_
  pc at _res_ do _inside_

variable _inside_

_day_ task:
  daily from 6:00 to 18:00

_spawn_ task:
  when _inside_ and not _day_
  say 1060
  create foe _warr_ every 0 minutes 1 times with 100% success

_infight_ task:
  daily from 0:00 to 23:59
  change foe _warr_ infighting true
  clear _infight_

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_slain_ task:
  killed 1 _warr_
  say 1070
  change repute with _fg_ by -10
  _bag_ used do _take_
  place npc _crow_ at _crowhome_

variable _take_
_take2_ task:
  when _inside_ and _take_
  take _bag_ from pc
  get item _bag2_
  say 1071

_talk_ task:
  toting _bag2_ and _crow_ clicked
  give pc _reward_
  start quest JHTGX06
  end quest