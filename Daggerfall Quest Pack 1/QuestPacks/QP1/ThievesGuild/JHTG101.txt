Quest: JHTG101
DisplayName: Тест на скорость
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Иногда в гильдии мы любим позабавиться спортом.
<ce> У меня есть приглашение, к которому вы, возможно,
<ce> захотите прислушаться. В одном из склепов неподалеку,
<ce> который кишит летучими мышами, есть сундук
<ce> с сокровищами. Если вы сможете найти сундук
<ce> и принести его сюда, не убив ни одной летучей мыши,
<ce> то получите нечто зачарованное. Хотите поучаствовать?

RefuseQuest:  [1001]
<ce> Посмотрим, может быть, вы передумаете.

AcceptQuest:  [1002]
<ce> "Хорошо, мы должны сделать кое-что, прежде чем
<ce> скажем вам, где он находится".
<ce>
<ce> Входит закутанная в мантию фигура, накладывает на вас
<ce> два коротких заклинания и уходит. Вы не чувствуете
<ce> никакой разницы.
<ce>
<ce> "Вот. Теперь мы сможем вас видеть, а летучие мыши
<ce> будут роем лететь за вами. Не убейте ни одной из них
<ce> в склепе. Место называется ___mondung_.
<ce> Заклинание не будет длиться вечно, так что возвращайтесь
<ce> обратно через =timer_ дней с сундуком в руках.
<ce> Вы получите =reward_ золотых,
<ce> если сделаете это. Берегите себя".

QuestFail:  [1003]
<ce> Вам нужно было доставить сундук
<ce> в Гильдию Воров к этому времени.

QuestComplete:  [1004]
<ce> Это было захватывающее зрелище. Многие собрались
<ce> посмотреть и выпить медовухи, даже делали ставки.
<ce> Хотелось бы повторять это почаще.
<ce> Вот ваше золото. Надеюсь, оно хорошо послужит вам.

QuestLogEntry:  [1010]
%qdt:
 Гильдия Воров поставила передо мной задачу
 найти определенный сундук в ___mondung_
 и доставить его им в ___questgiver_
 в течение =timer_ дней. При этом я не могу убить ни одной
 летучей мыши внутри этого места.

Message:  1011
<ce> Как только ваша рука касается этого сундука, перед вами
<ce> появляется магический знак отличия. Должно быть, это тот
<ce> сундук. Вы разочарованы, увидев, что он пуст, но сундук
<ce> все же нужно доставить.

Message:  1012
<ce> Как только летучая мышь падает, перед вашими глазами
<ce> появляется видение черепа. Гильдия Воров знает, что вы
<ce> потерпели неудачу.

Message:  1013
<ce> Вы уже слышите слабое стрекотание летучих мышей.

QBN:
Item _reward_ gold range 2300 to 3000
Item _chest_ item class 1 subclass 10

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon0

Clock _timer_ 00:00 0 flag 17 range 1 2

Foe _bat_ is Giant_bat

--	Quest start-up:
  start timer _timer_
  log 1010 step 0
  pc at _mondung_ set _inside_
  place item _chest_ at _mondung_
  reveal _mondung_

_inside_ task:
  say 1013
  create foe _bat_ every 5 minutes indefinitely with 33% success

_find_ task:
  clicked item _chest_
  say 1011

_slain_ task:
  killed _bat_
  say 1012
  make _chest_ permanent
  end quest

_timer_ task:
  say 1003
  make _chest_ permanent
  end quest

_victory_ task:
  toting _chest_ and _questgiver_ clicked
  give pc _reward_
  end quest