Quest: JHTGX03
DisplayName: Образец
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHTGX03

RefuseQuest:  [1001]
<ce> ERROR 1001 JHTGX03

AcceptQuest:  [1002]
<ce> ERROR 1002 JHTGX03

QuestComplete:  [1004]
<ce> Разве это не самые легкие деньги, которые ты когда-либо зарабатывал?
<ce> Один из моих более щепетильных коллег в
<ce> ___res_ знал, что ты идёшь. Бедный дурак получил всего
<ce> около 200 золотых за убийство. Настоящие
<ce> деньги только за реальную работу. Возьми.

RumorsDuringQuest: [1005]
Говорят, что Ворон снова появляется в заливе. Я думаю, что это самозванец.
   <--->
Ты слышал о том, что Ворон появился в каком-то городе? Скорее всего, это ложь.
   <--->
Говорят, что Ворон где-то рядом, но я думал, что он давно умер.

Message:  1010
<ce> Ну что ж, раз ты прошел тест, пора тебя приобщить к делу,
<ce> заняться настоящим бизнесом. Не могу
<ce> вечно с тобой нянчиться, знаешь ли. До сих пор
<ce> тебе помогали, но теперь ты должен стоять на своих
<ce> собственных ногах. Возьми эту сумку. Отправляйся в ___res_.
<ce> Прямо перед тем, как ты туда попадешь, кое-кто убьет
<ce> другого человека в _res_. Когда ты
<ce> доберешься туда, поторопись и забери сердце,
<ce> печень и глаза. Положи их в мешок и
<ce> высыпь на органы белый порошок, который находится в одном из его
<ce> карманов. Это консервирующее вещество.
<ce> Затем у тебя есть около 5 секунд, чтобы завязать его, прежде чем
<ce> он начнет плохо реагировать на открытый воздух.
<ce> Сделай все это быстро, и товар останется хорошим в течение нескольких месяцев.
<ce> После этого я буду в ___crowhome_. Найди меня в
<ce> _crowhome_. Там тебя ждёт =reward_ золотых.

Message:  1030
%qdt:
 "Ворон" научил меня, как
 разобрать труп в
 _res_ в ___res_.
 и извлечь жизненно важные органы,
 которые просили его работодатели.
 Я буду получу =reward_ золотых
 от него в ___crowhome_, где он
 остановился в _crowhome_.

Message:  1031
%qdt:
 Я вытащил органы из
 тела и положил их в сумку.

Message:  1070
<ce> Как и обещал _crow_, этот
<ce> труп очень свежий, вероятно
<ce> убит нескольких часов назад.
<ce> Ты следуешь его инструкциям и
<ce> вынимаете глаза, печень и сердце,
<ce> кладешь их в пакет,
<ce> посыпав сверху порошком.
<ce> Затем ты закрываешь пакет.

Message:  1080
<ce> Желаешь ли ты выкинуть _lode_
<ce> и отказаться от своих связей с _crow_?

Message:  1081
<ce> Ты выбрасываешь маленький кусочек металла.
<ce> Какие бы у него ни были планы, теперь они принадлежат только ему.

Message:  1082
<ce> Ты возвращаешь _lode_ в свой
<ce> рюкзак, по крайней мере, пока.

QBN:

Person _crow_ named The_Crow

Place _res_ remote house
Place _crowhome_ remote house

Item _reward_ gold range 1801 to 2199
Item _lode_ lodestone
Item _body_ dead_body
Item _bag_ small_sack
Item _bag2_ large_sack

--	Quest start-up:
  say 1010
  log 1030 step 0
  get item _lode_
  get item _bag_
  place item _body_ at _res_
  _lode_ used do _prompt_

_prompt_ task:
  prompt 1080 yes _quit_ no _stay_

_quit_ task:
  say 1081
  end quest

_stay_ task:
  say 1082

_grab_ task:
  clicked item _body_
  take _body_ from pc
  take _bag_ from pc
  get item _bag2_
  say 1070
  log 1031 step 1
  place npc _crow_ at _crowhome_

_talk_ task:
  toting _bag2_ and _crow_ clicked
  give pc _reward_
  start quest JHTGX04
  end quest