Quest: JHTG111
DisplayName: Небольшой подкуп
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У одной из семей есть довольно злопамятный враг при
<ce> дворе %crn. Этот аристократ каждые несколько
<ce> месяцев таскает парня в тюрьму за мелкие преступления.
<ce> Нам нужен кто-то, кто поможет купить решение.

RefuseQuest:  [1001]
<ce> Обязательно найдётся кто-то кто это сделает.

AcceptQuest:  [1002]
<ce> Ходят слухи, что правая рука благородной шавки,
<ce> _kni_ неравнодушна к драгоценным камням. Мы
<ce> наблюдаем за особой партией _gem_  безупречного
<ce> качества, которая должен привлечь %g4 внимание.
<ce> Возьми образец у _jewel_ в ___jewel_, а потом
<ce> отнеси его _kni_. Живет в ___knitown_. Сделай это
<ce> ночью, когда %g1 не работает и мало свидетелей. У
<ce> меня будет для тебя =reward_ золотом, если ты
<ce> придешь через =timer_ дней.

QuestFail:  [1003]
<ce> Ты должен был доставить украденный _gem_
<ce> и явиться к _questgiver_ уже сейчас.

QuestComplete:  [1004]
<ce> Скажи мне, как отреагировал _kni_?
<ce> Я так и думал, что %g1 устроит шоу. Хорошая работа. Возьми деньги.

Message:  1011
<ce> (ПРОВЕРКА НА КАРМАННИЧЕСТВО ПРОВАЛЕНА)
<ce>
<ce> "Вор! Как вы мне надоели! Я сам тебя убью!"

Message:  1012
<ce> (ПРОВЕРКА НА КАРМАННИЧЕСТВО ПРОЙДЕНА)
<ce>
<ce> Ты ловко убираешь драгоценный камень. Ни сном ни духом.

Message:  1013
<ce> "Добрый вечер. Я не узнаю тебя в лицо. По какому ты делу?"

Message:  1014
<ce> "Боже мой, какую мерзость ты мне предлагаешь. Ты говоришь, что я
<ce> должен отпустить твоего друга, а взамен ты дашь мне этот _gem_?
<ce> Скажи мне, есть ли у тебя веские причины для такого извращенного
<ce> представления о справедливости?"

Message:  1015
<ce> "Какая нелепость. И что же это может быть?"
<ce>
<ce> Ты объясняешь, что член гильдии страдает от мстительного дворянского
<ce> гнева, а не от тяжести какого-либо преступления.
<ce>
<ce> "В это мне довольно трудно поверить. Наша система правосудия
<ce> безупречна, и каждое обвинение со стороны нашей справедливой
<ce> монархии должно быть вполне обоснованным. Разве ты не веришь в это?"

Message:  1016
<ce> "Ха! Я должен вызвать городскую стражу за незаконное
<ce> проникновение. Я не давал тебе разрешения войти. Но, чтобы
<ce> меня не называли неучтивым, я разрешаю тебе убраться
<ce> быстро и прямо сейчас. Всего хорошего".
<ce>
<ce> _kni_ предлагает ударить по рукам.
<ce> Сделаешь ли ты это?

Message:  1017
<ce> Всё ещё держа _gem_, ты жмёшь руку, незаметно передавая камень.
<ce> _kni_ понимающе кивает и подмигивает.
<ce> Твой товарищ по гильдии скоро будет свободен.

Message:  1018
<ce> "Как я и думал. Сгинь, болван!"

Message:  1019
<ce> "Да, и я был бы глупцом, если бы стал общаться с тобой дальше. Уходи".

Message:  1020
<ce> _kni_ хмурится и убирает руку.
<ce> Похоже, твой товарищ по гильдии не будет освобожден.

Message:  1030
%qdt:
 Меня посылают в ___jewel_, чтобы я получил
 определенный драгоценный камень,
 который я должен отдать некоему слуге
 по имени _kni_, который живет в ___knitown_.
 Мне заплатят =reward_ золотых, если я вернусь
 в Гильдию Воров в ___questgiver_
 через =timer_ дней. Если я не найду
 слугу дома, мне следует подождать
 до ночи.

QBN:

Person _questgiver_ group Questor male
Person _kni_ faction Random_Knight

Item _gem_ gem
Item _reward_ gold

Place _knitown_ remote house
Place _jewel_ remote jewelryshop

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Healer

--   Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _gem_ at _jewel_
    place npc _kni_ at _knitown_

_got_ task:
    clicked item _gem_

_skill_ task:
    when skill Pickpocket is at least 60

_notfree_ task:
    when _got_ and not _skill_
    say 1011
    create foe _enemy_ every 0 minutes 1 times with 100% success

_free_ task:
    when _got_ and _skill_
    say 1012

_day_ task:
    daily from 6:00 to 18:00

_hide_ task:
    when _day_
    hide npc _kni_

_restore_ task:
    when not _day_
    restore npc _kni_

_kclearclick_ task:
    when _kniclicked_ and not _got_
    clear _kniclicked_ _kclearclick_

_kniclicked_ task:
    clicked npc _kni_

_kcontinue_ task:
    when _kniclicked_ and _got_
    say 1013
    _gem_ used do _talk1_

_talk1_ task:
    prompt 1014 yes _talk2_ no _fail1_

_fail1_ task:
    say 1018
    make _gem_ permanent
    end quest

_talk2_ task:
    prompt 1015 yes _fail2_ no _talk3_

_fail2_ task:
    say 1019
    make _gem_ permanent
    end quest

_talk3_ task:
    prompt 1016 yes _victory_ no _fail3_

_fail3_ task:
    say 1020
    make _gem_ permanent
    end quest

_victory_ task:
    say 1017
    take _gem_ from pc

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _clearclick_

_pcgetsreward_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest