Quest: JHTG105
DisplayName: Горячий камень
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Один очень ценный _gem_ хранится
<ce> в ___res_, охраняемый кем-то
<ce> из Гильдии Бойцов. Нам нужен кто-то
<ce> достаточно умный, чтобы избежать драки, чтобы
<ce> доставить его сюда за =timer_ дней.
<ce> Думаешь, ты сможешь это сделать?

RefuseQuest:  [1001]
<ce> Ты хочешь навсегда остаться нашим принеси-подай?
<ce> У нас не всегда будут лёгкие поручения.

AcceptQuest:  [1002]
<ce> Я сказал избегать драки, но если быть честным,
<ce> меня это не особо волнует. Это скорее для твоей пользы.
<ce> Если Гильдия Бойцов пошлет за тобой людей,
<ce> мы просто скажем это была личная инициатива.
<ce> Если ты просто войдешь, украдешь и выйдешь,
<ce> они, скорее всего, даже не узнают тебя.
<ce> Иди и посмотри в _res_, ___res_.
<ce> У меня будет =reward_ золотом для
<ce> тебя здесь, если ты вернешься через =timer_ дней.

QuestFail:  [1003]
<ce> _questgiver_ сказал тебе, чтобы ты принёс
<ce> _gem_ в ___questgiver_ к этому времени.

QuestComplete:  [1004]
<ce> Прекрасно! Как ты это сделал, быстро бегая
<ce> или оставаясь вне поля зрения? Хорошая работа.
<ce> Вот твоя награда.

Message:  1020
<ce> Нет! Кто-нибудь, помогите!
   <--->
<ce> Гильдия Воров здесь!

Message:  1021
<ce> Это, должно быть, охраняемый _gem_.

Message:  1030
%qdt:
 _questgiver_ хочет, чтобы я стащщил
 драгоценный камень из _res_ в
 ___res_ и вернулся с ним в
 ___questgiver_ через =timer_ дней.
 Здание будет охранять наемник
 из Гильдии Бойцов.

QBN:

Person _questgiver_ group Questor male
Person _dummyfg_ faction The_Fighters_Guild

Item _reward_ gold
Item _gem_ gem

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2
Foe _perp_ is Warrior

-- Quest start-up:
  start timer _timer_
  log 1030 step 0
  place item _gem_ at _res_
  place foe _perp_ at _res_
  change foe _perp_ infighting true

_got_ task:
  clicked item _gem_
  say 1021

_inj_ task:
  injured _perp_ saying 1020
  change repute with _dummyfg_ by -10

_victory_ task:
  toting _gem_ and _questgiver_ clicked
  give pc _reward_
  end quest

_timer_ task:
  say 1003
  end quest