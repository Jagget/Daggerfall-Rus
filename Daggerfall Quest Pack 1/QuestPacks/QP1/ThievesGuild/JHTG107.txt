Quest: JHTG107
DisplayName: Заламывание рук
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Похищение означает, что нам нужно хорошо подготовиться,
<ce> чтобы получить большой куш. Заложник
<ce> связан, семья находится в отчаянии,
<ce> %t не в курсе или подкуплен и так далее.
<ce> Но время от времени мы получаем дело вроде 
<ce> этого, когда семья не верит
<ce> нам и отказывается платить. Помогите нам
<ce> показать им, что мы настроены серьезно.

RefuseQuest:  [1001]
<ce> Ну, в конце концов, однажды что-то поменяется.
<ce> Наверное. Надеюсь, нам не придется его хоронить.

AcceptQuest:  [1002]
<ce> Самый упрямый - _fam_,
<ce> один из самых богатых людей 
<ce> в ___res_. Похоже, %g4 не пугает
<ce> упоминание Гильдии Воров, но %g1 научится.
<ce> Похоже, %g1 считает, что у н%g4 слишком много власти,
<ce> чтобы платить нам. Возьмите и покажите вот
<ce> этот палец. Это обычно выбивает таких
<ce> людей из колеи. Получите деньги, 
<ce> вернитесь сюда через =timer_ дней 
<ce> и постарайтесь не распространяться об этом.

QuestFail:  [1003]
<ce> _qgiver_ сказал вам, чтобы вы доставили
<ce> выкуп в ___qgiver_ к сегодняшнему дню.

QuestComplete:  [1004]
<ce> Давайте я его открою. Да, похоже, что все верно.
<ce> Я должен взвесить его, чтобы убедиться, 
<ce> но выглядит правильно. Сейчас я отправлю сообщение.
<ce> Забирайте свою долю.

RumorsDuringQuest:  [1005]
Кажется, что _fam_ чем-то ужасно расстроен.

Message:  1020
<ce> Жалкий %ra, меня от тебя тошнит. Почему бы мне
<ce> не убить тебя прямо сейчас? %t уже знает об этом.
<ce> Скорее всего, они уже нашли %g4.
<ce> Скажи, почему я должен тебе что-то давать?

Message:  1021
<ce> _fam_ меняется в лице, когда
<ce> %g1 осматривает палец.
<ce>
<ce> "О, %god. Ты же не серьезно. Ты..."
<ce>
<ce> Признав поражение, %g1 достает из шкафа тканевый 
<ce> мешочек и наполняет его различными драгоценными камнями,
<ce> прежде чем бросить его вам. Затем %g1,
<ce> дрожа, опускается на пол.
<ce> Вы молча уходите.

Message:  1030
%qdt:
 Заговор с целью выкупа в ___res_
 нуждается в помощи. Мне нужно
 поговорить с _fam_ 
 и посмотреть, смогу ли я убедить %g4
 заплатить вознаграждение. Если нет,
 _qgiver_ дал мне
 отрезанный палец для мотивации. 
 Я должен доставить плату 
 в Гильдию Воров в ___qgiver_.
 через =timer_ дней.

QBN:
Item _reward_ gold range 250 to 350
Item _finger_ finger
Item _pay_ large_sack

Person _qgiver_ group Questor male
Person _fam_ faction Dancers

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
  log 1030 step 0
  get item _finger_
  start timer _timer_
  pc at _res_ set _inside_
  place npc _fam_ at _res_

_talk_ task:
  clicked npc _fam_
  say 1020
  _finger_ used do _persuade_

variable _inside_
variable _persuade_
_finish_ task:
  when _inside_ and _persuade_
  take _finger_ from pc saying 1021
  get item _pay_

_pcgetsgold_ task:
  toting _pay_ and _qgiver_ clicked
  give pc _reward_
  end quest

_timer_ task:
  say 1003
  end quest