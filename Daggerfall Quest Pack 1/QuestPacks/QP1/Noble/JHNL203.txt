-- Noble
Quest: JHNL203
DisplayName: Неудачные переговоры
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, в этой сфере деятельности люди
<ce> должны отвечать за свои поступки.
<ce> Как только пройдет слух, что _qgvr_
<ce> слабохарактерный трус, в дверь полезет
<ce> всякая мерзость, чтобы воспользоваться
<ce> мной. Я должен все исправить, пока все
<ce> не стало еще хуже. Отомстите ли вы
<ce> за меня моему недругу за =rwrd_ золотых?

RefuseQuest:  [1001]
<ce> Тогда оставьте меня, пока и вы тоже не предали.

AcceptQuest:  [1002]
<ce> Согласно предварительной договоренности
<ce> я купил _rsdn_ у банка и передал его человеку,
<ce> который в то время был обездолен и лишен
<ce> надежды. Прошло три года, и мне сообщили
<ce> о нескольких процветающих предприятиях, которыми
<ce> руководит ранее безнадежный мошенник, и все же
<ce> я не получаю никаких платежей за дом. Мои
<ce> прежние курьеры были отвергнуты без ответа,
<ce> а это значит, что я больше в них не нуждаюсь.
<ce> Этот кретин наверняка попытается
<ce> выдворить вас с территории дома силой. Ответьте
<ce> взаимностью, %ra, и избавьте меня от этого врага.
<ce> Банк выкупит дом в течение 72 часов. К этому
<ce> времени я должен получить от вас весточку.

QuestFail:  [1003]
<ce> Что, вы тоже просите дом
<ce> после того как подвели меня?

QuestComplete:  [1004]
<ce> Это радует меня больше, чем я могу выразить,
<ce> %pcf. Почувствуйте гордость за то, что в этот
<ce> день вы стали орудием правосудия. Вот =rwrd_
<ce> золотых. Пусть ни один человек
<ce> не скажет вам, что _qgvr_ не поступает
<ce> благородно со своими.

Message:  1020
<ce> Противника _qgvr_ больше нет.
<ce> Справедливость торжествует.

Message:  1030
%qdt:
 _qgvr_ в ___qgvr_ нуждается в том,
 чтобы я принудительно удалил
 из _rsdn_ его владельца, который
 отказался выполнять соглашение
 о собственности. Я должен вернуться
 в течение 72 часов.

Message:  1099
<ce> Я не отдам _rsdn_ вам, %pcf, и уж точно
<ce> не отдам, если вы не желаете избавиться
<ce> от его самовольного владельца. Я дал
<ce> вам три дня, чтобы закончить это.

QBN:
Item _rwrd_ gold range 350 to 400

Person _qgvr_ group Questor male

Place _rsdn_ local house

Clock _timer_ 3.00:00

Foe _enemy_ is Barbarian

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _enemy_ at _rsdn_
	restrain foe _enemy_
	change foe _enemy_ infighting true

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _rwrd_
	end quest

_slain_ task:
	killed 1 _enemy_ saying 1020

_qgclicked_ task:
	clicked npc _qgvr_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	say 1099
	clear _qgclicked_ _clearclick_

_timer_ task:
	end quest
