-- Noble
Quest: JHNL202
DisplayName: Концы в воду
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Темное Братство служит полезной
<ce> и законной цели до тех пор, пока о нем
<ce> не говорят публично. Невежественные
<ce> умы возмущаются тем, какую работу
<ce> они выполняют. Тем не менее
<ce> в последний месяц они подвели меня
<ce> в том, что должно было быть простой
<ce> задачей. Вы должны стереть с лица
<ce> земли все осведомленные стороны.

RefuseQuest:  [1001]
<ce> Хорошо ли вы умеете хранить секреты, %ra?

AcceptQuest:  [1002]
<ce> Единственный правильный ответ.
<ce> Ваши цели находятся в _loc1_
<ce> в ___loc1_, и _loc2_, который
<ce> находится в ___loc2_. Справитесь
<ce> с обеими, и я подарю вам
<ce> волшебный _reward_ из своего
<ce> личного хранилища. Займет это
<ce> не более =timer_ дней. Не подведите
<ce> меня, как это сделали они.

QuestFail:  [1003]
<ce> Долго ты не проживешь!

QuestComplete:  [1004]
<ce> Умно сработано, %pcf. Ваша работа позволяет
<ce> мне спать по ночам. Пусть у вас будет
<ce> _reward_, как мы и договаривались.

Message:  1020
<ce> Этого убийцы-неудачника больше нет.

Message:  1021
<ce> Цель _questgiver_ больше не существует.

Message:  1030
%qdt:
 _questgiver_ приказал мне
 уничтожить нескольких врагов.
 Один из них прячется
 в _loc1_ в ___loc1_, а другой
 в _loc2_, в пределах
 ___loc2_. Я должен вернуться
 в ___questgiver_ через
 =timer_ дней за своей
 наградой - зачарованным
 _reward_.

Message:  1031
%qdt:
 Я уничтожил убийцу от имени _questgiver_.

Message:  1032
%qdt:
 Я убил бывшего врага _questgiver_.

Message:  1099
<ce> Я не знаю, как долго мои
<ce> враги будут оставаться
<ce> в ___loc1_ и ___loc2_. Найдите
<ce> их и убейте.

QBN:
Item _reward_ magic_item

Person _questgiver_ group Questor male

Foe _assa_ is Assassin
Foe _mark_ is Acrobat

Place _loc1_ remote random
Place _loc2_ remote random

Clock _timer_ 30.00:00

--	Quest start-up:
	start timer _timer_
	place foe _assa_ at _loc1_
	place foe _mark_ at _loc2_
	log 1030 step 0
	change foe _assa_ infighting true
	change foe _mark_ infighting true

_aslain_ task:
	killed 1 _assa_ saying 1020
	log 1031 step 1

_mslain_ task:
	killed 1 _mark_ saying 1021
	log 1032 step 2

_pcgetsgold_ task:
	when _qgclicked_ and _aslain_ and _mslain_
	give pc _reward_
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _aslain_ and not _mslain_
	say 1099
	clear _qgclicked_ _clearclick_

_timer_ task:
	end quest
