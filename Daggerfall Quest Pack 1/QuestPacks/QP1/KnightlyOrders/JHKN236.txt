Quest: JHKN236
DisplayName: Копыта и копья
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Пара кентавров совершила набег на отдаленный
<ce> дом в дикой местности %crn. Клянусь честью,
<ce> мы должны защитить обитателей этого дома.
<ce> Поднимите ли вы наше знамя ради этой цели?

RefuseQuest:  [1001]
<ce> Я отказываюсь в это верить.
<ce> Вы не понимаете их страданий.

AcceptQuest:  [1002]
<ce> Я отмечу ___place_ на вашей карте.
<ce> Отправляйтесь туда и, если повезет, вы найдете кентавров
<ce> где-то поблизости. Убейте их и возвращайтесь сюда.
<ce> Не получив известий о вашей победе за =timer_ дней,
<ce> я пошлю вместо вас других, более быстрых рыцарей.

QuestFail:  [1003]
<ce> _qgiver_ уже ожидал услышать о том,
<ce> что вы убили кентавров.

QuestComplete:  [1004]
<ce> Ах, и даже с доказательством! Молодец, %pcf.
<ce> Вы оказываете нам большую честь своей работой.

Message:  1010
<ce> Вы прибываете в ___place_ и обнаруживаете,
<ce> что кентавры разбили лагерь на территории, ожидая
<ce> появления незадачливой добычи. Похоже, что вместо
<ce> этого они будут довольствоваться вами.

Message:  1011
<ce> Кентавры были убиты. Вы решаете взять зуб у одной
<ce> из их туш в качестве доказательства.

Message:  1030
%qdt:
 _qgiver_ из __qgiver_ послал
 меня в дом под названием
 ___place_, чтобы я победил пару
 кентавров, нападающих
 на его жителей. Я должен вернуться
 в ___qgiver_ в течение =timer_ дней.

QBN:
Person _qgiver_ group Questor male

Place _place_ remote poorhome

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Centaur
Foe _enemy2_ is Centaur

Item _item_ item class 20 subclass 2

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _place_ do _spawn_

_spawn_ task:
    create foe _enemy_ every 0 minutes 1 times with 100% success
    create foe _enemy2_ every 0 minutes 1 times with 100% success
    change foe _enemy_ infighting true
    change foe _enemy2_ infighting true
    say 1010

_killed_ task:
    killed _enemy_

_killed2_ task:
    killed _enemy2_

_slain_ task:
    when _killed_ and _killed2_
    say 1011
    get item _item_

_timer_ task:
    say 1003
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc nothing
    legal repute +4
    start quest JHKNC04
    end quest