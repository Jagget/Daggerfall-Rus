Quest: JHKN116
DisplayName: Переговоры с орком
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Орки - жестокие существа, с чем все
<ce> в %crn согласятся. Очень редко кто-то
<ce> из них вырастает с мозгом, превышающим
<ce> размер грейпфрута, желая вести переговоры
<ce> о мирной уступке земель или других территорий,
<ce> на которых может проживать его народ.
<ce> Такие просьбы всегда вызывают подозрение,
<ce> и мы поручаем одну из них вам.
<ce> Возможно, вы найдете способ принести
<ce> пользу в этом случае.

RefuseQuest:  [1001]
<ce> А может быть, и нет.

AcceptQuest:  [1002]
<ce> Это существо было временно размещено
<ce> в _res_, ___res_.
<ce> Вы должны прибыть туда в течение =2res_ дней,
<ce> но само собой разумеется, что
<ce> семья, владеющая этим домом, желает,
<ce> чтобы этот вопрос был решен немедленно.
<ce> Что бы ни случилось, не убивайте
<ce> орка внутри. Они являются грязной
<ce> и хитрой расой зверей и воспользуются
<ce> смертью соплеменника для продвижения
<ce> своих варварских целей.
<ce> Мы даем вам этот _trink_.
<ce> Ранее было решено сделать это знаком
<ce> для нашего орка-дипломата. То, что произойдет
<ce> дальше - решать вам.

QuestFail:  [1003]
<ce> Военачальник орков лежит мертвый у ваших ног.
<ce> Теперь у его племени есть еще больше причин
<ce> для набегов на этот регион.

QuestComplete:  [1004]
<ce> Молодец, %pct. Вы принесли мир
<ce> и убавили напряженность. Помните, что предотвратить
<ce> войну благороднее, чем ее вести.

Message:  1019
<ce> Вы слышите глубокий, горловой смех.
<ce> Орк находится внутри и ждет вас.

Message:  1021
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Язык орков)
<ce>
<ce> Вы вручаете _trink_ орку.
<ce> Он берет подарок и сминает его
<ce> одной рукой, бросая остатки
<ce> на пол. Затем зеленый воин начинает задавать
<ce> вопросы. Вы отвечаете неопределенно, стараясь
<ce> не обещать ничего такого, что %t
<ce> не смогли бы выполнить. Он выдвигает несколько грубых
<ce> обвинений в отношении %t, %crn
<ce> и сволочи %ra в его присутствии, прежде чем
<ce> немного успокаивается. Не видя никакой
<ce> возможности продвинуть переговоры, существо
<ce> замолкает и теряет дальнейший интерес.
<ce> Вы полагаете, что это считается успехом.

Message:  1023
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Язык орков)
<ce>
<ce> Вы вручаете _trink_ орку,
<ce> не зная, что делать дальше. Он берет
<ce> подарок и сминает его одной рукой, бросая
<ce> остатки на пол. Затем воин
<ce> начинает задавать вопросы, сначала грубо, затем
<ce> насмешливо. Как только он понимает, что вы
<ce> почти не говорите на его языке, он вытаскивает свое
<ce> оружие и решает рискнуть ради спортивного интереса.

Message:  1024
<ce> Орк озадачен вашим упорным нежеланием
<ce> драться. Он выкрикивает несколько сбивчивых
<ce> и непонятных заявлений, выходя из себя.
<ce> Затем он постепенно успокаивается. Похоже,
<ce> он чувствует вину за то, что сражается с врагом,
<ce> который отказывается защищаться. Не видя никакой
<ce> возможности продвинуть переговоры, существо
<ce> замолкает и теряет дальнейший интерес.
<ce> Вы полагаете, что это считается успехом.

Message:  1025
<ce> Вы слышите, как орк разражается громогласным хохотом
<ce> внутри резиденции, а затем тяжелые шаги
<ce> удаляются в другом направлении. Переговоры,
<ce> похоже, уже не состоятся.

Message:  1030
%qdt:
 _qgiver_ послал меня
 встретиться с орком
 в _res_, ___res_,
 по поводу некоторых переговоров
 по неизвестному поводу. Мне был
 выдан _trink_, который действует
 как знак для переговоров.
 Я должен найти мирное решение
 и не убивать орка. У меня есть
 =2res_ дней, чтобы выполнить задание
 и вернуться в ___qgiver_.

QBN:
Item _trink_ trinket

Person _qgiver_ group Questor male

Place _res_ remote house1

Foe _orc_ is Orc_warlord

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _fight_ 00:09 00:12
Clock _failsafe_ 120.00:00

--	Quest start-up:
  log 1030 step 0
  start timer _2res_
  start timer _failsafe_
  get item _trink_
  pc at _res_ set _inside_

_inside_ task:
  say 1019
  take _trink_ from pc
  create foe _orc_ every 0 minutes 1 times with 100% success

_easy_ task:
  when _inside_ and _skilled_
  restrain foe _orc_
  say 1021
  start task _easywin_

variable _easywin_
_attack_ task:
  when _inside_ and not _skilled_
  say 1023
  start timer _fight_
  start task _wicked_

variable _wicked_
_fail1_ task:
  when _wicked_ and not _inside_ and not _fight_
  play sound pounding every 0 minutes 1 times
  say 1025
  end quest

_fight_ task:
  restrain foe _orc_
  say 1024

_fail2_ task:
  killed _orc_
  say 1003
  end quest

_skilled_ task:
  when skill Orcish is at least 20

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _fight_ and not _easywin_
  clear _qgclicked_ _clearclick_

_victory1_ task:
  when _qgclicked_ and _fight_
  start task _final_

_victory2_ task:
  when _qgclicked_ and _easywin_
  start task _final_

_final_ task:
  give pc nothing
  legal repute +3
  end quest

_2res_ task:
  say 1003
  end quest

_failsafe_ task:
  end quest