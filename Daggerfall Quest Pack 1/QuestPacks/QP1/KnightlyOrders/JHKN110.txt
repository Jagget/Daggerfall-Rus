Quest: JHKN110
DisplayName: Обманщик
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Демон из Обливиона поселился в разрушенном замке
<ce> и в последнее время причиняет множество бед местным жителям.
<ce> Если у вас есть средства и возможности нанести вред даэдра,
<ce> мы просим вас заняться этим вопросом.

RefuseQuest:  [1001]
<ce> Действительно, это не та задача, к которой стоит относиться
<ce> легкомысленно.

AcceptQuest:  [1002]
<ce> ___dung_ - вот название этого места, оно находится
<ce> на %di отсюда. Это порождение даэдра будет
<ce> защищаться, и ваша задача - уничтожить его. Нам неважно как,
<ce> главное, чтобы оно было мертво. Выполните задание
<ce> в течение =timer_ д., предъявив личный доклад в качестве
<ce> доказательства.

QuestFail:  [1003]
<ce> __questgiver_ ожидает, что к настоящему времени вы уже 
<ce> уничтожили даэдра.

QuestComplete:  [1004]
<ce> Вы говорите, была расставлена ловушка? Я рад, что мы послали
<ce> кого-то достаточно способного, чтобы выжить в ней. Хорошего
<ce> рыцаря трудно найти. Народ региона %crn
<ce> аплодирует вам, %pct.

Message:  1010
<ce> Даэдра издает рев, надвигаясь на вас.

Message:  1011
<ce> Демон падает, но указывает на вас. В вашем сознании возникает
<ce> видение, в котором вы видите себя, падающего
<ce> замертво в этих руинах.
<ce>  
<ce> "Через два часа... ты присоединишься ко мне..." - смеется
<ce> даэдра, падая замертво.
<ce>  
<ce> Его чары вырываются наружу, и вы чувствуете, как нечистая
<ce> сила привязывает вас к себе. Если вам удастся выбраться
<ce> из ___dung_ до истечения
<ce> времени, возможно, вы еще сможете спастись.

Message:  1012
<ce> Вы заметили в этих руинах какую-то энергетическую силу.
<ce> Должно быть, даэдра орудовали здесь, пока вы шли сюда.

Message:  1013
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Выносливость)
<ce>  
<ce> Увы, прошло два часа, а вы все еще внутри. Ваше тело
<ce> скручивает мучительная боль, которую вы не сможете пережить.
<ce> __questgiver_ устроит вам пышные похороны.

Message:  1014
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Выносливость)
<ce>  
<ce> Увы, прошло уже два часа, а вы все еще внутри. Ваше тело
<ce> скручивает мучительная боль, и вы едва выживаете.
<ce> Более слабая душа умерла бы мгновенно. 

Message:  1015
<ce> Вы вырвались из руин замка, и никакое проклятие вас
<ce> не постигло. Похоже, пророчество даэдра не сбылось.

Message:  1030
%qdt:
 __questgiver_ поручили мне уничтожить даэдра
 где-то внутри ___dung_. Мне нужно вернуться
 в ___questgiver_ и доложить о своем успехе
 в течение =timer_ д.

QBN:
Person _questgiver_ group Questor male

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _deathtimer_ 2:00

Place _dung_ remote dungeon11

Foe _enemy_ is Fire_daedra

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	reveal _dung_
    place foe _enemy_ at _dung_
    pc at _dung_ set _inside_

_inside_ task:
    say 1012

_inj_ task:
    clicked foe _enemy_
    injured _enemy_
    say 1010
    change foe _enemy_ infighting true

_slain_ task:
    killed _enemy_
    say 1011
    start timer _deathtimer_

_cleartimer_ task:
    when _slain_ and not _inside_ and not _deathtimer_
    stop timer _deathtimer_
    say 1015

variable _deathtimer_

_skilled_ task:
    when attribute Endurance is at least 90

_instantdeath_ task:
    when _deathtimer_ and not _skilled_
    say 1013
    reduce player health by 100 every 1 seconds can kill

_neardeath_ task:
    when _deathtimer_ and _skilled_
    say 1014
    reduce player health by 75 every 1 seconds 1 times

_victory_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +4
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
	end quest