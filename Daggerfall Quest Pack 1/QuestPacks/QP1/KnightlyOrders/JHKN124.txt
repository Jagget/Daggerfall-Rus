Quest: JHKN124
DisplayName: Промывка мозгов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Какой-то негодяй в этом регионе узурпировал разум
<ce> некоторых женщин %crn, промывая им мозги и заставляя
<ce> выполнять его приказы. Остановите ли вы это?

RefuseQuest:  [1001]
<ce> Почему? Разве это недостаточно срочно?

AcceptQuest:  [1002]
<ce> Мы не знаем, где прячется этот человек, но предполагаем,
<ce> что его заколдованные девицы знают. Одна из таких
<ce> пострадавших девиц - _maid_ из ___maidh_.
<ce> В этом деле вам понадобится помощь Гильдии Магов.
<ce> Отправляйтесь в ___mager_ и ищите мага
<ce> по имени _mage_ в зале гильдии. Нам сообщили,
<ce> что у н%g4 будет безделушка, способная временно приостановить
<ce> действие заклинания очарования, и, надеюсь, вам удастся
<ce> выведать пристанище негодяя во время этого перерыва.
<ce> Ради чести %crn, покончите с этим делом
<ce> в течение =timer_ дней.

QuestFail:  [1003]
<ce> Ваше время на выполнение задания остановить чародея
<ce> из ____mager_ истекло.

QuestComplete:  [1004]
<ce> Отличная работа, %pcf. Я уже получил известие,
<ce> что подчиненные этим человеком были освобождены от проклятия.
<ce> Я не сомневаюсь, что они будут чтить ваше имя.

Message:  1010
<ce> Добрый день, %pct. В какие тревожные времена мы живем,
<ce> не правда ли? Возьмите _tri_. Здесь есть сила
<ce> на несколько мгновений снять чары с этих женщин. Быстро
<ce> задавайте свои вопросы сразу после использования.
<ce> Если повезет, они будут злы из-за того, что их контролируют,
<ce> и предоставят вам желаемую информацию. Рад встрече,
<ce> %ra, и желаю вам удачи в поисках.
   <--->
<ce> Приятно познакомиться. Возьмите _tri_,
<ce> это на несколько мгновений приостановит действие чар на этих
<ce> женщин. При его использовании быстро задавайте вопросы.
<ce> Если повезет, они будут злы из-за того, что их контролируют,
<ce> и дадут вам желаемую информацию. Пожалуйста, свершите
<ce> правосудие в %crn, добрый %ra. Прощайте.

Message:  1011
<ce> _maid_ кричит и дрожит от ветра.
<ce> Она смотрит на вас безумными глазами, желая сказать
<ce> что-то, но магические силы не дают ей этого сделать.
   <--->
<ce> _maid_ пытается говорить, но молчит. Она оглядывается
<ce> по сторонам, явно боясь чего-то, чего вы не можете увидеть.

Message:  1012
<ce> Волна магической силы растекается от _tri_,
<ce> создавая кратковременный раскол в чарах. Вам не нужно
<ce> задавать вопрос, так как девушка сама начинает говорить.
<ce>
<ce> "_capt_! Убейте его! Пожалуйста!
<ce> Там, в ___farm_! Помогите мне! Я..."
<ce>
<ce> Через несколько мгновений ее глаза стекленеют, и способность
<ce> говорить исчезает. Теперь вы знаете имя и местоположение 
<ce> чародея.
   <--->
<ce> Волна магической силы растекается от _tri_,
<ce> обеспечивая кратковременный раскол в чарах. Вам не нужно
<ce> задавать вопрос, так как девушка сама начинает говорить.
<ce>
<ce> "Он в ___farm_! Его зовут _capt_!
<ce> Пожалуйста, если вы можете сделать что-нибудь..."
<ce>
<ce> Через несколько мгновений ее глаза стекленеют, и способность
<ce> говорить исчезает. Теперь у вас есть имя и местоположение 
<ce> чародея.

Message:  1013
<ce> Вы сразу же отличаете _capt_ по его внешнему виду.
<ce> Он произносит короткое заклинание телепортации
<ce> для получения преимущества.

Message:  1014
<ce> Вы победили могущественного мага. Надеюсь, этого будет
<ce> достаточно, чтобы покончить с проклятием. Вам следует
<ce> доложить о своих результатах _qgiver_.

Message:  1020
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Тауматургия)
<ce>
<ce> Ваших знаний по тауматургии достаточно, чтобы дать вам
<ce> преимущество. Вы произносите быстрое заклинание, которое 
<ce> вызывает у вампира временный паралич.

Message:  1021
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Скрытность)
<ce>
<ce> Вампиры - эксперты в скрытности, но вы лучше.
<ce> Вам удается ускользнуть от слежки этого вампира.

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_
 приказал мне найти
 чародея в ____qgiver_, который
 заколдовал несколько женщин, чтобы они выполняли
 его приказы. Я должен получить безделушку
 у _mage_ из Гильдии Магов
 в ___mager_ и отнести ее женщине
 по имени _maid_ в ___maidh_.
 У меня есть =timer_ дней, чтобы победить
 человека, стоящего за этим планом.

Message:  1031
%qdt:
 _maid_ смогла говорить
 достаточно долго, чтобы сказать мне, что чародея
 зовут _capt_, и что он прячется
 в ___farm_.

QBN:
Foe _q1_ is Ancient_vampire

Person _qgiver_ group Questor male
Person _maid_ faction Dancers female
Person _mage_ faction The_Mages_Guild
Person _capt_ group Local_4.1 male

Place _maidh_ remote house1
Place _farm_ remote poorhome
Place _mager_ remote magery

Item _tri_ trinket

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _mage_ at _mager_
  place npc _maid_ at _maidh_
  pc at _maidh_ do _inside_
  pc at _farm_ do _spawn_

_talk1_ task:
  clicked npc _mage_
  say 1010
  get item _tri_

_talk2_ task:
  clicked npc _maid_
  say 1011
  _tri_ used do _effect_

variable _inside_
variable _effect_
_talk3_ task:
  when _inside_ and _effect_
  start task _go_
  say 1012
  log 1031 step 1
  take _tri_ from pc
  reveal _farm_

variable _go_
variable _spawn_
_spawn2_ task:
  when _go_ and _spawn_ and not _slain_
  say 1013
  create foe _q1_ every 0 minutes 1 times with 100% success
  change foe _q1_ infighting true
  start task _ch1_

_thau_ task:
  when skill Thaumaturgy is at least 50

variable _ch1_
_e1_ task:
  when _ch1_ and _thau_
  say 1020
  restrain foe _q1_

_e1b_ task:
  when _ch1_ and not _thau_
  start task _ch2_

_stealth_ task:
  when skill Stealth is at least 70

variable _ch2_
_e2_ task:
  when _ch2_ and _stealth_
  say 1021
  restrain foe _q1_

_slain_ task:
  killed _q1_
  say 1014

_qgclicked_ task:
  clicked npc _qgiver_

_victory_ task:
  when _qgclicked_ and _slain_
  give pc nothing
  legal repute +8
  end quest

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_timer_ task:
  say 1003
  end quest