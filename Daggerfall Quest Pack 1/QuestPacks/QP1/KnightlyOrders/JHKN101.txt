Quest: JHKN101
DisplayName: Гнездо дракончика
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Меня зовут _qgiver_. Сегодня утром
<ce> мы получили курьера от имени %t с
<ce> просьбой о помощи в ___dungeon_.
<ce> По-видимому, где-то внутри находится
<ce> гнездо дракончиков редкой породы,
<ce> которую стоит охранять. Браконьеры
<ce> уже начали лазить туда, и это лишь
<ce> вопрос времени, когда дракончиков
<ce> совсем не останется. Предложите ли
<ce> вы свою помощь в этом деле?

RefuseQuest:  [1001]
<ce> Неудачный ответ.

AcceptQuest:  [1002]
<ce> %t ясно дали понять, что эта
<ce> деятельность незаконна. Вы
<ce> уполномочены предать смерти любого,
<ce> кто попытается сделать это. Поспешите
<ce> в ___dungeon_ и разъясните намерения
<ce> нашего доброго правителя. Я ожидаю,
<ce> что все будет улажено через =timer_
<ce> дней. Отправляйтесь сейчас, %pcf.

QuestFail:  [1003]
<ce> _qgiver_ уже ожидал вашего
<ce> отчета о дракончиках.

QuestComplete:  [1004]
<ce> Отличная работа. Пусть никто не
<ce> нарушит закон безнаказанно.

RumorsDuringQuest:  [1005]
Какую-то невезучую душу из Гильдии Бойцов послали убить даэдрота.

Message:  1020
<ce> Послание предельно ясно.
<ce> Эти трупы будут служить
<ce> предупреждением для тех,
<ce> кто вторгнется сюда.

Message:  1021
<ce> У вас возникает тоскливое чувство,
<ce> что это не то,
<ce> что вы должны были делать.

Message:  1022
<ce> Помедленнее, рыцарь-головорез. От нас больше
<ce> проблем, чем вы можете выдержать. Мы с
<ce> приятелями найдем этого дракона, нравится вам
<ce> это или нет, так что я облегчу вам задачу. Вот
<ce> =bribe_ золотых, если вы оставите нас в покое.
   <--->
<ce> Думай быстрее, рыцарь. У меня есть для вас
<ce> =bribe_ золотых, если вы посмотрите в другую
<ce> сторону и оставите меня и моих приятелей в покое.
<ce> Нам нужен этот дракон, через ваш труп или нет.

Message:  1023
<ce> Молодец, %ra. А теперь проваливай.

Message:  1024
<ce> Твои похороны. Ты пожалеешь, %ra.

Message:  1030
%qdt:
 _qgiver_ из
 ___qgiver_ попросил
 меня о помощи в защите гнезда
 дракончиков где-то в
 __dungeon_. Я должен уничтожить
 вторгшихся браконьеров, но ни одного
 из гнездящихся драконов.
 У меня есть =timer_ дней.

QBN:

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon6

Item _bribe_ gold range 1201 to 1999

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _drag_ is Dragonling
Foe _enemy1_ is Knight
Foe _enemy2_ is Warrior

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _drag_ at _dungeon_
	pc at _dungeon_ set _inside_
	reveal _dungeon_
	pick one of _e1_ _e2_

variable _e1_
variable _e2_
variable _inside_
_quest_ task:
	when _inside_ and not _slain_
	create foe _enemy1_ every 24 minutes indefinitely with 7% success
	create foe _enemy2_ every 15 minutes indefinitely with 3% success

_daily_ task:
	daily from 0:00 to 23:59
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	clear _daily_

_inj_ task:
	injured _enemy1_
	injured _enemy2_

_ask_ task:
	when _inj_ and _e2_
	prompt 1022 yes _br_ no _avoid_

_br_ task:
	say 1023
	get item _bribe_
	remove foe _enemy1_
	remove foe _enemy2_
	end quest

_avoid_ task:
	say 1024

_timer_ task:
	end quest

_slain_ task:
	killed 3 _enemy1_ saying 1020
	killed 3 _enemy2_ saying 1020

_fail_ task:
	killed 1 _drag_ saying 1021
	end quest

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +3
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_