Quest: JHKN203
DisplayName: Охота на вампира
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Меня зовут _qgiver_. Недавно Ордену
<ce> стало известно об ужасе ночи, вампире
<ce> немалой известности, скрывающемся
<ce> в жалкой дыре в %crn. Мы являемся
<ce> протекторатом этой земли и не можем
<ce> мириться с такой угрозой для ее
<ce> жителей. Откликнетесь ли вы на призыв
<ce> и спуститесь ли в глубины ___dung_,
<ce> чтобы уничтожить это чудовище?

RefuseQuest:  [1001]
<ce> Конечно, мы с радостью позволим ему поглотить еще несколько
<ce> невинных людей и стать более могущественным, да.

AcceptQuest:  [1002]
<ce> Тогда отправляйтесь в ___dung_,
<ce> чтобы избавить нас от этого зла.
<ce> Вас будет сопровождать _mage_
<ce> из Гильдии Магов, %g4 главной целью
<ce> будет обеспечение неспособности
<ce> вампира восстать вновь.
<ce> При этом %g1 может предложить что-то
<ce> значимое в бою. Вы оба вернетесь
<ce> сюда через =timer_ дней с отчетом
<ce> о ваших успехах. Прощайте.

QuestFail:  [1003]
<ce> Истек срок, отведенный для убийства вампира
<ce> и доклада _qgiver_.

QuestComplete:  [1004]
<ce> Вы - гордость для %crn, %pcf.

RumorsDuringQuest:  [1005]
Говорят, что %t встречались с Темным Братством.

Message:  1010
<ce> _mage_ сразу же узнает этого вампира.
<ce>
<ce> "Хо! =vam1_, конец близок!"
<ce>
<ce> Затем следует связывающее заклинание, которое
<ce> на мгновение замедляет гадину. Это, должно быть,
<ce> подготовка к блокировке трупа.

Message:  1011
<ce> _mage_ сразу же узнает этого вампира.
<ce>
<ce> "Хо! =vam2_, конец близок!"
<ce>
<ce> Затем следует связывающее заклинание, которое
<ce> на мгновение замедляет гадину. Это, должно быть,
<ce> подготовка к блокировке трупа.

Message:  1020
<ce> Вы победили вампира. _mage_ занимается
<ce> чем-то вроде пиротехнического шоу
<ce> с использованием острых инструментов,
<ce> которые, по всей логике, не могут положить
<ce> конец вампиризму навсегда, но вы не испытываете
<ce> по этому поводу никаких угрызений совести.
<ce> Вскоре вы будете готовы вместе уйти.
   <--->
<ce> Вы победили вампира. _mage_ произносит
<ce> несколько таинственных заклинаний
<ce> над телом, которые, кажется, удерживают его
<ce> на месте. Будет это действенно или нет,
<ce> выходит за рамки вашего понимания
<ce> и ответственности. Вскоре вы будете готовы
<ce> вместе уйти.

Message:  1021
<ce> Этот вампир владеет мощной магией.
<ce> Шокирующий заряд вылетает в вашем
<ce> направлении, и _mage_ падает.

Message:  1022
<ce> Вы победили вампира. _mage_ получит
<ce> настолько достойное погребение, насколько
<ce> это возможно в данных обстоятельствах.
<ce> Несомненно, %g4 жертва не была напрасна.

Message:  1023
<ce> Приветствуем наших истребителей
<ce> вампиров! Такой крестовый поход
<ce> всегда должен заслуживать внимания
<ce> рыцарей. Мы будем говорить о ваших
<ce> деяниях, чтобы вдохновить молодое
<ce> поколение расти над собой. Вы,
<ce> конечно, расскажете Гильдии Магов
<ce> о великой службе %pcf, _mage_?
<ce> Я уверен, что %pg3 действия
<ce> сохранили вам жизнь.

Message:  1024
<ce> Приветствую %pcn, грозу вампиров!
<ce> Что это? _mage_ был убит в битве?
<ce> Это действительно печальная новость,
<ce> хотя мы рады, что нам больше не нужно
<ce> бояться посягательств вампиров на нашу
<ce> землю. Я благодарю вас за то, что вы
<ce> отважились на такое опасное задание.

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_
 отправил меня и члена
 Гильдии Магов в ___dung_,
 чтобы победить вампира.
 Нам нужно вернуться через
 =timer_ дней.

QBN:
Person _qgiver_ group Questor male
Person _mage_ faction The_Mages_Guild

Place _dung_ remote dungeon8

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:03 00:05

Foe _vam1_ is Ancient_vampire
Foe _vam2_ is Vampire

--	Quest start-up:
	start timer _timer_
	pick one of _D.01_ _D.02_
	log 1030 step 0
	reveal _dung_
	pick one of _e1_ _e2_

variable _e1_
variable _e2_

_D.01_ task:
	place foe _vam1_ at _dung_

_D.02_ task:
	place foe _vam2_ at _dung_

_inj1_ task:
	injured _vam1_ saying 1010
	clicked foe _vam1_
	restrain foe _vam1_
	start timer _delay_
	change foe _vam1_ infighting true

_inj2_ task:
	injured _vam2_ saying 1011
	clicked foe _vam2_
	restrain foe _vam2_
	start timer _delay_
	change foe _vam2_ infighting true

_delay_ task:
	drop _mage_ face
	say 1021

_slain_ task:
	killed 1 _vam1_
	killed 1 _vam2_
	stop timer _delay_

_livemage_ task:
	when _slain_ and not _delay_
	say 1020

_deadmage_ task:
	when _slain_ and _delay_
	say 1022

_qgclicked_ task:
	clicked npc _qgiver_

_pcgetsgold_ task:
	when _qgclicked_ and _livemage_
	say 1023
	change repute with _mage_ by +5
	legal repute +3
	give pc nothing
	end quest

_pcgetsnothing_ task:
	when _qgclicked_ and _deadmage_
	say 1024
	legal repute +3
	give pc nothing
	end quest

_timer_ task:
	say 1003
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_