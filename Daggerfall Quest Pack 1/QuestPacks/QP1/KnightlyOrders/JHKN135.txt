Quest: JHKN135
DisplayName: В поисках пропавшего сына
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Местная семья в ___questgiver_ ужасно
<ce> беспокоится о своем сыне, который спустился в какие-то
<ce> руины во время какой-то героической авантюры. Это было
<ce> несколько недель назад. Они хотят услышать что-нибудь
<ce> о его судьбе и чтобы кто-нибудь вернул его
<ce> обратно, если он жив. Поможете ли вы этому делу?

RefuseQuest:  [1001]
<ce> Хорошо.

AcceptQuest:  [1002]
<ce> Юноша _dummy_ пропал в ___dung_. 
<ce> Вам отведено =timer_ дней на то, чтобы съездить и вернуться,
<ce> прежде чем они наймут более надежного помощника и мы 
<ce> опозоримся. Если вы найдете мальчика, верните его как можно
<ce> скорее. Если вы найдете доказательства его присутствия,
<ce> также принесите их сюда. И если дела пойдут еще хуже,
<ce> мы все равно должны узнать о его судьбе.
<ce> Да ускорит %god ваш путь.

QuestFail:  [1003]
<ce> К настоящему времени вы должны были вернуться
<ce> с новостями о _dummy_ в __questgiver_,
<ce> ___questgiver_.

QuestComplete:  [1004]
<ce> Как это прискорбно. Я благодарю вас за то, 
<ce> что вы устроили ребенку надлежащие похороны. Я возьму на себя
<ce> трудную задачу проинформировать семью. Мы бы хотели, чтобы
<ce> у всех историй был счастливый конец, но мы должны смотреть
<ce> правде в глаза. Отдохните, %pcf.
<ce> Наш Орден помог еще одной семье в %crn.

Message:  1011
<ce> Вы находите здесь почти полностью обглоданное тело
<ce> с признаками, указывающими на то, что с момента смерти
<ce> до вашего прибытия прошло уже несколько дней, возможно,
<ce> неделя. Вам требуется время, чтобы запомнить черты лица
<ce> юноши и рассмотреть причину его гибели. Затем вы
<ce> надлежащим образом хороните тело. У вас будут печальные
<ce> новости для __questgiver_.

Message:  1030
%qdt:
 _questgiver_ отправил меня в ___dung_
 искать _dummy_, семья которого
 надеется, что он все еще жив. Я должен
 найти его или какие-либо признаки его
 присутствия и вернуться
 в ___questgiver_ в течение =timer_ дней.

Message:  1031
%qdt:
 Нашлось тело, очень похожее
 на то, как выглядел _dummy_.
 Мне нужно сообщить о своей находке
 __questgiver_ в ___questgiver_.

QBN:

Person _questgiver_ group Questor male
Person _dummy_ faction Children male

Item _body_ dead_body

Place _dung_ remote dungeon6

Clock _timer_ 00:00 0 flag 17 range 0 2

-- Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _body_ at _dung_
    have _body_ set _remove_
    reveal _dung_

_remove_ task:
    start task _victory_
    take _body_ from pc
    say 1011
    log 1031 step 1

variable _victory_

_qgclicked_ task:
    clicked npc _questgiver_

_pcgetsgold_ task:
    when _victory_ and _qgclicked_
    give pc nothing
    legal repute +5
    start quest JHKNC05
    end quest

_S.04_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _S.04_

_timer_ task:
    say 1003
    end quest