Quest: JHKNC02
DisplayName: Загадка, окутанная эскортом
-- Message panels
--used in KN111
QRC:

QuestComplete:   [1004]
<ce> _witch_ и _friend_ радостно обнимаются.
<ce> Затем они поворачиваются к вам и кланяются.
<ce>
<ce> "Мне жаль, что я скрывала от вас правду,
<ce> %pcf. Все верно - я ведьма. Стражники
<ce> вели против меня законное преследование.
<ce> Но я не могла позволить им лишить меня
<ce> жизни! Вы бы сделали то же самое на моем
<ce> месте!" - восклицает _witch_.
<ce>
<ce> _friend_ кладет руку ей на плечо.
<ce> "Нам пора идти. Я молюсь, чтобы этот жетон был
<ce> достойной благодарностью вам за то, что
<ce> вы поступили правильно", - говорит %g1.
<ce> Пока вы осматриваете свою награду, эти
<ce> двое исчезают из виду.

Message:  1010
<ce> Темнокрылая птица довольно странной формы
<ce> роняет письмо к вашим ногам и улетает.

Message:  1011
%pct, рыцарь из ____home_,
<ce>
  в отчаянии я обращаюсь к вам, ибо слышала, что вы -
рыцарь чести. Я прячусь в ___home_, в _home_.
Пожалуйста, выведите меня отсюда. Моя жизнь находится
в серьезной опасности. Вы поступите правильно, пожалуйста.
Я могу ждать только =timer_ дней.
<ce>
                   _witch_
<ce> ----

Message:  1012
<ce> %oth! Вы пришли! Вы здесь!
<ce> О, клянусь слезами Мефалы, надежда все-таки есть.
<ce> Злые заговорщики преследуют меня и хотят лишить жизни.
<ce> Пожалуйста, сделайте все возможное, чтобы меня
<ce> не нашли. Мои друзья в ___dest_
<ce> смогут защитить меня, если вы поможете мне
<ce> добраться до _dest_ в течение =2dest_ дней.
<ce> Я готова отправиться, рыцарь.

Message:  1013
<ce> Едва вы вышли из _home_,
<ce> как вас увидел встревоженный стражник.
<ce>
<ce> "Стоять! Эта женщина разыскивается за колдовство
<ce> и связь с даэдра. Вы немедленно выдадите ее
<ce> и не окажете никакого сопротивления при аресте!"
<ce>
<ce> _witch_ шепчет: "Эти люди, они убьют меня!
<ce> Пожалуйста, давайте уйдем отсюда!"
<ce>
<ce> Сторож готовит свой топор.
<ce> Сдадите ли вы ему эту женщину?

Message:  1014
<ce> Видя ваше сопротивление, стражник побежал звать на помощь.
<ce> Вы слышите приближающийся звук множества шагов.

Message:  1015
<ce> Вы отступаете назад и позволяете стражнику
<ce> схватить _witch_. Она начинает произносить
<ce> заклинание, но он ударом руки в латной перчатке разбивает
<ce> ей лицо и лишает сознания. Затем он связывает ее руки.
<ce>
<ce> "Вы сделали правильный выбор, рыцарь. Эти ведьмы
<ce> слишком опасны для цивилизации. Мы позаботимся о том,
<ce> чтобы она сгорела до того, как очнется".
<ce>
<ce> Вы смотрите, как он уносит ее бесчувственное тело.

Message:  1016
<ce> "Хорошо! Мы здесь! Просто помогите мне найти
<ce> _friend_ здесь, в резиденции", - говорит
<ce> _witch_. "Ищите кого-нибудь, кто выглядит
<ce> как =friend_".

Message:  1018
<ce> Вас замечает один из городских стражников.
<ce> Он начинает бить тревогу.
<ce>
<ce> "Стоять! Здесь защитники ведьмы!
<ce> Сдавайтесь немедленно и предстаньте
<ce> перед судом за свои преступления!"

Message:  1019
<ce> Вы с удивлением осознаете, что _witch_
<ce> внезапно исчезла.
<ce> От нее не осталось и следа.

Message:  1030
%qdt:
 Я сопровождаю женщину по имени _witch_
 в ___dest_, _dest_. Она, по-видимому,
 опасается за свою жизнь и просит меня
 уберечь ее от любого, кто будет охотиться
 за ней. Ее защитники будут ждать =2dest_ дней.

QBN:
Item _letter_ letter used 1011
Item _reward_ magic_item

Person _witch_ faction Anticlere female
Person _friend_ faction The_Glenmoril_Witches

Place _home_ remote house

Clock _timer_ 00:00 0 flag 1 range 0 2

Place _dest_ remote house

Clock _2dest_ 00:00 0 flag 1 range 0 2

Clock _delay_ 00:01

--	Quest start-up:
	pc at _home_ set _inside_
	pc at _dest_ set _inside2_
    give pc _letter_ notify 1010
    hide npc _witch_
    hide npc _friend_
    start timer _timer_

_timer_ task:
	end quest

_inside_ task:
    place npc _witch_ at _home_
    restore _witch_

_1sttalk_ task:
    clicked npc _witch_
    say 1012
    hide npc _witch_
    add _witch_ face
    log 1030 step 0
    stop timer _timer_
    start timer _2dest_

_ambush_ task:
    when _1sttalk_ and not _inside_
    prompt 1013 yes _surrender_ no _resist_

_surrender_ task:
    drop _friend_ face
    say 1015
    legal repute +10
    start quest JHKNC02a
    end quest

_resist_ task:
    say 1014
    setplayercrime Criminal_Conspiracy
    spawncityguards immediate

_inside2_ task:
    say 1016
    place npc _friend_ at _dest_
    restore _friend_

_finish_ task:
    clicked npc _friend_
    give pc _reward_
    drop _witch_ face
    remove log step 0
    stop timer _2dest_
    legal repute -10
    hide npc _friend_

_2ndambush_ task:
    when _finish_ and not _inside2_
    say 1018
    setplayercrime Criminal_Conspiracy
    spawncityguards immediate
    start timer _delay_

_delay_ task:
    end quest

_2dest_ task:
    say 1019
    end quest

