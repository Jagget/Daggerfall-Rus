Quest: JHKN119
DisplayName: Обряд изгнания вампиров

QRC:

QuestorOffer:  [1000]
<ce> Слишком долго мы позволяли вампирам властвовать над одним
<ce> из кладбищ %crn. Мы пригласили одного из местных священников
<ce> для проведения мощного ритуала, который убьет их всех. Рыцарь
<ce> должен обеспечить защиту священника во время проведения
<ce> обряда. Докажете ли вы свое бесстрашие?

RefuseQuest:  [1001]
<ce> Мы не можем больше откладывать изгнание этих паразитов.

AcceptQuest:  [1002]
<ce> Я знал, что вы не откажетесь. Вам нужно найти священника
<ce> =priest_ по имени _priest_
<ce> в _temple_ здесь, в городе. Вместе вы отправитесь
<ce> в сторону ___graveyard_ и поможете %g2 сделать свою работу.
<ce> Ритуал можно проводить только ночью, чтобы получить
<ce> максимальный эффект против вампиров. Заботьтесь
<ce> о _priest_, оставьте себе любые найденные
<ce> там трофеи. Вы должны прибыть туда не позже =timer_ дней.
<ce> Пусть %god будет с вами и защитит вас.

QuestFail:  [1003]
<ce> _priest_ выглядит глубоко разочарованным.
<ce>
<ce> "Мы уже давно пропустили назначенное время для изгнания
<ce> вампиров! Я не смогу продолжить этот ритуал!"
<ce>
<ce> После этого %g1 уходит.

QuestComplete:  [1004]
<ce> Внезапно из тела _priest_ в небо вырывается луч света.
<ce> Он начинает расширяться, постепенно охватывая все вокруг.
<ce> Вы слышите, как вампиры кричат от боли и пытаются укрыться,
<ce> но уже слишком поздно. Скоро они превращаются в безжизненную
<ce> кучу, не в силах противостоять священной силе.
<ce>
<ce> "Мы сделали это!" - воскликнул _priest_,
<ce> упав в истощении на землю.
<ce> Вы видите, как %g1 недолго отдыхает, прежде чем снова
<ce> подняться. "Да, мы уничтожили их всех. Теперь это племя
<ce> ночных пожирателей уничтожено! Я смогу сам вернуться
<ce> в ___temple_. Возьмите любые трофеи
<ce> среди их вещей. Да благословит вас %god".
<ce>
<ce> Вещи вампиров - ваши. Но, кажется, их тела скоро разложатся,
<ce> поэтому не стоит тратить время.

Message:  1010
<ce> Приветствую вас! %oth, я страшно нервничал из-за всего этого.
<ce> Не спал два дня, только и думал о том, что нас ожидает.
<ce> Вампиры, надо же! %god храни нас, ибо никто другой не сможет
<ce> справиться. Ритуал должен начаться ближе к полуночи, так что 
<ce> если мы придем слишком рано или слишком поздно, нам придется
<ce> устроить лагерь в ___graveyard_.
<ce> Идите впереди, а я буду следовать за вами на безопасном 
<ce> расстоянии.

Message:  1011
<ce> Это ___graveyard_, но нам лучше подождать
<ce> до полуночи, чтобы начать ритуал.
<ce>
<ce> "Мы можем разбить лагерь здесь, - говорит
<ce> _priest_. - Вампиры не атакуют нас,
<ce> пока мы не представляем для них угрозу... Думаю, 
<ce> это безопасно".

Message:  1012
<ce> "Да! Мы можем начать прямо сейчас, - говорит
<ce> _priest_. - Только будьте абсолютно уверены,
<ce> что вы не покинете территорию кладбища ни при каких
<ce> обстоятельствах. Это займет некоторое время, и наши жизни
<ce> в ваших руках. Молитесь %god, чтобы мы выжили!"
<ce>
<ce> С этими словами %g1 опускается на колени и начинает
<ce> читать заклинания. Чувствуя приближающуюся опасность,
<ce> вампиры начинают выползать, чтобы противостоять вам.

Message:  1013
<ce> Тело _priest_ начинает светиться.
<ce> Ритуал начинает действовать.
<ce> Почувствовав опасность, начинают прибывать новые вампиры.

Message:  1014
<ce> Некоторые из вампиров начинают поддаваться воздействию
<ce> заклинания _priest_.
<ce> Интенсивность ритуала продолжает увеличиваться.

Message:  1015
<ce> "Мы близки к цели! Продолжайте защищаться!" - кричит
<ce> _priest_, и %g4 тело ярко светится.

Message:  1016
<ce> Вы удалились слишком далеко от ___graveyard_,
<ce> оставив _priest_ без защиты.
<ce> Вампиры массово атакуют
<ce> и разрывают %g4 на куски.

Message:  1030
%qdt:
 __questgiver_ из ___questgiver_
 поручил мне сопровождать священника _priest_ 
 из _temple_ на кладбище под названием
 ___graveyard_, чтобы изгнать
 скрывающийся там клан вампиров. Ритуал проводится ночью 
 и должен быть завершен в течение =timer_ дней.



QBN:

Person _questgiver_ group Questor
Person _priest_ faction Generic_Temple

Place _temple_ local temple
Place _graveyard_ remote graveyard

Clock _timer_ 00:00 0 flag 1 range 0 2
Clock _combat1_ 0:05
Clock _combat2_ 0:05
Clock _combat3_ 0:05
Clock _combat4_ 0:05
Clock _delay_ 0:20

Foe _enemy1_ is Vampire
Foe _enemy2_ is Vampire
Foe _enemy3_ is Vampire
Foe _enemy4_ is Vampire
Foe _enemy5_ is Ancient_vampire
Foe _enemy6_ is Ancient_vampire

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
    place npc _priest_ at _temple_

_clicky_ task:
    clicked npc _priest_
    say 1010
	reveal _graveyard_
	pc at _graveyard_ set _onsite_
    hide npc _priest_
    add _priest_ face

_midnight_ task:
    daily from 3:00 to 22:00

variable _onsite_

_timer_ task:
	end quest

_priestleaves_ task:
    when _timer_ and _clicky_
    say 1003

_notyet_ task:
    when _clicky_ and _onsite_ and _midnight_
    say 1011

_begin_ task:
    when _clicky_ and _onsite_ and not _midnight_
    say 1012
    start timer _combat1_
    start task _spawn1_
    start task _hasbegun_
    stop timer _timer_

variable _hasbegun_

_abandon_ task:
    when _hasbegun_ and not _onsite_ and not _combat4_
    drop _priest_ face
    say 1016
    end quest

_spawn1_ task:
    create foe _enemy1_ every 2 minutes indefinitely with 100% success

_combat1_ task:
    say 1013
    start timer _combat2_
    start task _spawn2_

_spawn2_ task:
    create foe _enemy2_ every 2 minutes indefinitely with 100% success

_combat2_ task:
    say 1014
    start timer _combat3_
    start task _spawn3_
    kill foe _enemy2_

_spawn3_ task:
    create foe _enemy3_ every 3 minutes indefinitely with 100% success
    clear _spawn2_

_combat3_ task:
    say 1015
    start timer _combat4_
    start task _spawn4_
    kill foe _enemy1_
    kill foe _enemy4_
    kill foe _enemy6_
--this is intentional for dramatic effect

_spawn4_ task:
    create foe _enemy4_ every 2 minutes indefinitely with 100% success
    create foe _enemy5_ every 2 minutes indefinitely with 100% success
    create foe _enemy6_ every 3 minutes indefinitely with 100% success

_combat4_ task:
    kill foe _enemy3_
    kill foe _enemy5_
    clear _spawn1_
    clear _spawn2_
    clear _spawn3_
    clear _spawn4_
    give pc nothing
	legal repute +8
    start timer _delay_
    drop _priest_ face
    remove log step 0

_delay_ task:
    end quest

_infight_ task:
	daily from 0:00 to 23:59
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	change foe _enemy3_ infighting true
	change foe _enemy4_ infighting true
	change foe _enemy5_ infighting true