Quest: JHKN121
DisplayName: Охота на ведьм
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Добрый день. Недавно поступила информация о ведьме,
<ce> скрывающейся в другом городе и, возможно, замышляющей
<ce> нехорошие дела. Нам нужен тот, кто сможет поймать
<ce> ее и положить конец ее деятельности.

RefuseQuest:  [1001]
<ce> Тогда чем вы хотите заняться, %pct?

AcceptQuest:  [1002]
<ce> Ее зовут _witch_. Отправляйтесь
<ce> в ___res_, где она проживает. Вам поручено
<ce> схватить ее и доставить сюда для заключения
<ce> под стражу до тех пор, пока не будет проведен должный
<ce> суд. Если она станет сопротивляться, это будет достаточным
<ce> свидетельством ее колдовства, и вам разрешается
<ce> немедленно устранить угрозу. Вне зависимости от исхода
<ce> вы должны вернуться сюда в течение =timer_ дней и доложить
<ce> о вашем успехе. Счастливого пути, %pct.

QuestFail:  [1003]
<ce> Вы опоздали на суд. Не важно, какие обстоятельства у вас
<ce> были, срок задержания _witch_ истек.

QuestComplete:  [1004]
<ce> Отличная работа, %pcf. Отличная работа.

RumorsDuringQuest:  [1005]
Ведьмы повсюду в ___res_.

RumorsPostfailure:  [1006]
Вы слышали, что ведьма сбежала из ___res_?

RumorsPostsuccess:  [1007]
Хорошо, что есть рыцари, которые избавляются от ведьм.

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ из ___qgiver_
 поручил мне задержать ведьму
 _witch_ из ___res_.
 Если она признает свою вину, я верну ее
 в зал Ордена, где она будет дожидаться
 суда, но если будет сопротивляться,
 мне разрешено лишить ее жизни.
 У меня есть =timer_ дней.

Message:  1011
<ce> _witch_ смотрит на вас с возмущением.
<ce>
<ce> "Я ведьма? Это абсурд! %t подняли эту шумиху?
<ce> Я знала, что они хотят навредить мне! Пожалуйста,
<ce> не могли бы вы отпустить меня?
<ce> Неужели я должна предстать перед судом ни за что?
<ce> Достопочтенный рыцарь, это несправедливо!"
<ce>
<ce> Несмотря на ее жалобы, вы связываете ей руки
<ce> и отправляетесь в ___qgiver_.
   <--->
<ce> _witch_ смотрит на вас с негодованием.
<ce>
<ce> "Это нелепо! Я прожила здесь =sum_ лет,
<ce> и только сейчас меня обвиняют в колдовстве?
<ce> Поговорите с владельцами таверн и торговцами.
<ce> Они знают правду! Какой абсурд!"
<ce>
<ce> Несмотря на ее жалобы, вы связываете ей руки
<ce> и отправляетесь в ___qgiver_.

Message:  1015
<ce> О, я ведьма, не так ли? Глупцы никогда не ценят добро,
<ce> которое я творю для ___res_ 
<ce> или для ____res_. Что ж, в таком случае,
<ce> почтенный рыцарь, делайте все, что в ваших силах! 
<ce> Я готова сразиться!
   <--->
<ce> Ха! Вы называете меня ведьмой и желаете мне смерти?
<ce> Прекрасное вознаграждение за защиту ___res_
<ce> в трудное время. Обливион с вами! 
<ce> Ваше время пришло, %ra!

Message:  1016
<ce> "Добрый %ra, я обдумала этот вопрос,
<ce> пока мы путешествовали, и считаю позором, что я должна
<ce> подвергаться презрению со стороны невежественных глупцов.
<ce> Я должна протестовать против своего заключения, сию минуту!"
<ce>
<ce> Поведение _witch_ меняется, и она произносит
<ce> какое-то заклинание, освобождая
<ce> свои руки от узлов.

Message:  1017
<ce> Вы справились с ведьмой и устранили угрозу, которую она
<ce> представляла. Пора вернуться к _qgiver_.

Message:  1018
<ce> Здравствуйте, _witch_. Я полагаю,
<ce> что этот %pct объяснил
<ce> вам обвинения, выдвинутые в ваш адрес? Вы будете содержаться
<ce> под стражей, чтобы предотвратить ваше бегство. Суд состоится
<ce> через день, так что ваша судьба будет решена очень скоро.

Message:  1020
<ce> Итак, слухи оказались правдой, не так ли, %pcf?
<ce> Вы избавили нас от многих проблем, и за это мы вам благодарны.

Message:  1021
<ce> Эта ведьма несла какие-то каракули, говорящие
<ce> о шабаше. Вы не уверены, что сможете понять
<ce> смысл этого, хотя и прилагаете большие усилия.
<ce> В любом случае, это не имеет никакого значения
<ce> для задания, которое дал вам _qgiver_.

QBN:

Person _qgiver_ group Questor
Person _witch_ group Local_3.0 female

Place _coven_ remote coven
Place _res_ remote house1

Item _sum_ gold range 4 to 12
Item _map_ letter

Foe _w1_ is Mage
Foe _d1_ is Daedroth
Foe _d2_ is Daedroth

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _surprise_ 1:00 8.00:00

--	Quest start-up:
  start timer _timer_
  log 1010 step 0
  place npc _witch_ at _res_
  pick one of _E.01_ _E.02_
  pick one of _map_ _nomap_ _nomap_ _nomap_ _nomap_

variable _E.01_
variable _E.02_
_timer_ task:
  say 1003
  end quest

_conv_ task:
  clicked npc _witch_

_pac_ task:
  when _E.01_ and _conv_
  start task _pac2_
  say 1011
  hide npc _witch_
  add _witch_ face
  start timer _surprise_

_surprise_ task:
  say 1016
  drop _witch_ face
  create foe _w1_ every 0 minutes 1 times with 100% success
  pick one of _K01_ _K02_ _K03_
  clear _pac2_

variable _pac2_
_fi_ task:
  when _E.02_ and _conv_
  say 1015
  hide npc _witch_
  place foe _w1_ at _res_
  pick one of _K01_ _K02_ _K03_
  change foe _w1_ infighting true
  change foe _d1_ infighting true
  change foe _d2_ infighting true

variable _K01_
_K02_ task:
  create foe _d1_ every 0 minutes 1 times with 100% success

_K03_ task:
  create foe _d1_ every 0 minutes 1 times with 100% success
  create foe _d2_ every 0 minutes 1 times with 100% success

_S1_ task:
  killed _w1_
  say 1017

variable _nomap_
_map_ task:
  give item _map_ to _w1_
  have _map_ set _mark_

_mark_ task:
  take _map_ from pc
  say 1021
  reveal _coven_

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _S1_ and not _pac_
  clear _qgclicked_ _clearclick_

_rew1_ task:
  when _qgclicked_ and _S1_
  say 1020
  give pc nothing
  legal repute +3
  end quest

_rew2_ task:
  when _qgclicked_ and _pac2_
  say 1018
  give pc nothing
  legal repute +3
  end quest