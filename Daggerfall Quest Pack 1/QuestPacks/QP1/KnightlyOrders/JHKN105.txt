Quest: JHKN105
DisplayName: Доказательственное производство
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pct, у нас есть задание, которое обычно
<ce> поручается только нашим оруженосцам, проходящим обучение.
<ce> Если бы не некоторый элемент опасности, я не стал бы тратить
<ce> впустую ни свое дыхание, ни ваше время. Необходимо добыть
<ce> предмет, являющийся уликой по делу о недавнем убийстве
<ce> и который городская стража не успела забрать во время
<ce> ареста. Поскольку в этом деле замешан серийный убийца
<ce> и нескольких городов, юрисдикция принадлежит
<ce> %crn, и %t попросили
<ce> нас помочь. Возьметесь ли вы вернуть улики?

RefuseQuest:  [1001]
<ce> Тогда проваливайте. Ищите какую-нибудь более
<ce> благородную цель, чем правосудие.

AcceptQuest:  [1002]
<ce> Хорошо. _clothes_ будет находиться
<ce> в ___res_, внутри дома под названием
<ce> _res_, за которым наблюдает городская
<ce> стража. Если кто-то из них будет задавать вам вопросы
<ce> при входе, просто назовите свое имя и принадлежность.
<ce> Как только вы получите то, за чем пришли, быстро
<ce> возвращайтесь сюда. Негодяй действовал совместно
<ce> с сообщниками, так что у нас есть подозрения,
<ce> что вам могут сильно помешать.
<ce> Будьте здесь в течение =timer_ дней, не позже.
<ce> Судебный процесс не может быть отложен еще раз.
<ce> Да пребудет с вами %god.

QuestFail:  [1003]
<ce> Задание провалено.

QuestComplete:  [1004]
<ce> Ни на минуту не опоздали, %pcf, ибо суд был
<ce> ускорен. _clothes_ и показания
<ce> свидетелей, несомненно, помогут свершиться
<ce> правосудию в %crn завтра. Отличная работа.
   <--->
<ce> Надежность во всех вопросах, малых и великих, достойна героя.
<ce> Я благодарю вас за то, что вы совершили это путешествие.
   <--->
<ce> Отлично, %pct. Теперь мы можем ускорить
<ce> судебное разбирательство. Великолепная работа по всем пунктам.

Message:  1010
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Сила)
<ce>
<ce> Мужество ваших противников начинает ослабевать.
<ce> Один кричит другому: "Да %pg вообще псих!
<ce> Надо убираться отсюда!"
<ce>
<ce> Ваши враги убегают, пока вы не разорвали их на части.
<ce> Вы полагаете, что ваше возвращение не будет
<ce> больше прерываться.

Message:  1019
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Скрытность)
<ce>
<ce> Кто-то менее опытный не заметил бы
<ce> сигилы, начертанные на дверном косяке. Вы аккуратно
<ce> шагнули внутрь, избежав обнаружения.

Message:  1020
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Скрытность)
<ce>
<ce> Охранники не оставили ваш вход без внимания. Теперь
<ce> вы должны найти _clothes_.
   <--->
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Скрытность)
<ce>
<ce> Вы внутри, теперь вы должны найти _clothes_.

Message:  1021
<ce> Вам кажется, что вы слышите шаги.

Message:  1022
<ce> Вы нашли _clothes_. Теперь
<ce> нужно вернуться к ___questgiver_.

Message:  1030
%qdt:
 _questgiver_ отправил меня
 в ___res_ найти
 _clothes_ внутри _res_
 и вернуть в ___questgiver_
 через =timer_ дней.

QBN:

Person _questgiver_ group Questor male

Item _clothes_ mens_clothing

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:02

Foe _enemy_ is 2 Rogue

-- Quest start-up:
  start timer _timer_
  log 1030 step 0
  place item _clothes_ at _res_
  pc at _res_ set _inside_

variable _inside_
variable _silent_

_will_ task:
  when attribute Strength is at least 80

_inj_ task:
  injured _enemy_

_goodwill_ task:
  when _will_ and _inj_
  say 1010
  remove foe _enemy_

_stealth_ task:
  when skill Stealth is at least 70
  start task _silent_

_entry_ task:
  when _inside_ and not _silent_
  say 1020
  start timer _delay_

_entry2_ task:
  when _inside_ and _silent_
  say 1019

_got_ task:
  clicked item _clothes_
  say 1022

_delay_ task:
  say 1021
  create foe _enemy_ every 6 minutes 2 times with 80% success
  create foe _enemy_ every 1 minutes 1 times with 80% success

_infight_ task:
  daily from 0:00 to 23:59
  change foe _enemy_ infighting true
  clear _infight_

_victory_ task:
  toting _clothes_ and _questgiver_ clicked
  give pc nothing
  legal repute +3
  end quest

_timer_ task:
  end quest