Quest: JHKN122
DisplayName: Залы с ловушками
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Ведьмы - это существа, обладающие огромной силой,
<ce> с которыми нельзя шутить. Мы согласны в этом вопросе,
<ce> не так ли, %pcf? %crn нуждается в помощи нашего ордена,
<ce> чтобы освободить несчастную дыру от ведьминого
<ce> проклятия, которое заманит в ловушку любого, кто туда
<ce> войдет. Станете ли вы тем героем, который нужен?
   <--->
<ce> Заброшенный лабиринт в пустошах стал домом для
<ce> ведьмы, которая заманивает в ловушку всех, кто
<ce> входит в его залы. Нам нужен чемпион, способный
<ce> убить её, чтобы снять проклятие. Вы вызоветесь
<ce> на этот героический поступок?

RefuseQuest:  [1001]
<ce> Каким рыцарем вы собираетесь стать, %pct?

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___dung_, как отмечено на вашей
<ce> карте. Я предупреждаю вас, что, оказавшись внутри,
<ce> вам не разрешат выйти. Я ожидаю, что внутри будет
<ce> много растерянных искателей приключений, которые не
<ce> смогут найти выход. Защищайтесь, при необходимости,
<ce> но помните, что ваш единственный способ вернуться на
<ce> свободу - убить ведьму. Пожалуйста, доберитесь до
<ce> подземелья в течение =2dung_ дней, чтобы проклятие
<ce> не захватило еще больше неразумных душ.

QuestFail:  [1003]
<ce> _qgiver_ поручил вам отправиться
<ce> в ___dung_, чтобы найти ведьму к сегодняшнему дню.

QuestComplete:  [1004]
<ce> Итак, наша ведьма на самом деле была даэдра?
<ce> Возможно, это было заклинание иллюзии. Я не знаю,
<ce> кто из этих двух обладает большей магической
<ce> силой, ведь оба они - враги, которых следует
<ce> бояться. Мои самые искренние поздравления вам,
<ce> %pct. %t узнает о вашем подвиге.

Message:  1010
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Язык даэдра)
<ce>
<ce> Эта ведьма выкрикивает в вашу сторону несколько
<ce> диссонансных фраз с враждебным оттенком.
<ce> Это незнакомый язык и вы не понимаете ее слов.

Message:  1011
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Язык даэдра)
<ce>
<ce> Эта ведьма выкрикивает в вашу сторону несколько диссонансных
<ce> фраз, которые вы тут же распознаете как даэдрические.
<ce> Вы отвечаете достаточно быстро и остроумно, застав ее врасплох,
<ce> что дает вам преимущество для нанесения первого удара.

Message:  1012
<ce> Сила, сковывающая эти залы, кажется,
<ce> ослабевает. Ведьма, скорее всего, уже
<ce> сбежала. Ваша задача провалена.

Message:  1013
<ce> Воздух здесь очень тяжелый, насыщенный магическими
<ce> чарами. Если _qgiver_ сказал правду, вы не сможете уйти,
<ce> пока ведьма не будет убита или не покинет пещеру.

Message:  1014
<ce> Поблизости раздается громкий и сердитый голос.
<ce>
<ce> "Если я не смогу выбраться отсюда, я убью
<ce> вас всех! Каждого из вас! Идите сюда!"
   <--->
<ce> Неподалеку раздается громкий и испуганный голос.
<ce>
<ce> "Для нас нет никакой надежды! Мы все умрем!
<ce> Мы никогда отсюда не выберемся!"

Message:  1015
<ce> Могущественная магия возвращает тебя обратно
<ce> в ___dung_. Это работа не обычного врага.

Message:  1016
<ce> Вы убили ведьму. Немедленно тайные силы,
<ce> действующие в ___dung_, начинают рассеиваться.
<ce> Предположительно, теперь вы сможете уйти.

Message:  1030
%qdt:
 _qgiver_ дал мне
 =2dung_ дней, чтобы войти
 в ___dung_, чтобы найти
 ведьму, наложившую проклятье.
 Сколько времени я проведу внутри
 это мое собственное решение,
 и я должен доложить ___qgiver_.
 когда я убью колдунью.

QBN:

Person _qgiver_ group Questor male
Person _witch_ faction The_Glenmoril_Witches

Place _dung_ remote dungeon7

Clock _2dung_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 14.00:00

Foe _w1_ is Daedra_seducer
Foe _e1_ is Knight
Foe _e2_ is Rogue
Foe _e3_ is Burglar

--	Quest start-up:
  start timer _2dung_
  log 1030 step 0
  place npc _witch_ at _dung_
  pc at _dung_ set _inside_
  reveal _dung_

_day_ task:
  daily from 0:00 to 23:59

_infight_ task:
  when _day_
  change foe _e1_ infighting true
  change foe _e2_ infighting true
  change foe _e3_ infighting true
  clear _day_

_inside_ task:
  start task _trap_
  create foe _e3_ every 30 minutes 1 times with 33% success msg 1014
  start timer _failsafe_
  say 1013

variable _trap_
_tele_ task:
  when _trap_ and not _slain_ and not _inside_
  teleport pc to _dung_
  say 1015
  start task _clearing_

_clearing_ task:
  clear _tele_ _clearing_

_quest_ task:
  when _inside_ and not _slain_
  create foe _e1_ every 15 minutes indefinitely with 4% success
  create foe _e2_ every 20 minutes indefinitely with 5% success
  create foe _e3_ every 25 minutes indefinitely with 6% success

_failsafe_ task:
  hide npc _witch_
  say 1012
  end quest

_talk_ task:
  clicked npc _witch_

_daed_ task:
  when skill Daedric is at least 40

_ww_ task:
  when _talk_ and _daed_
  say 1011
  hide npc _witch_
  place foe _w1_ at _dung_
  restrain foe _w1_

_wf_ task:
  when _talk_ and not _daed_
  say 1010
  hide npc _witch_
  place foe _w1_ at _dung_

_slain_ task:
  killed _w1_
  say 1016

_pcgetsgold_ task:
  when _qgclicked_ and _slain_
  give pc nothing
  legal repute +8
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_2dung_ task:
  say 1003
  end quest