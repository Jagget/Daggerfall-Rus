Quest: JHKN225
DisplayName: Святилище Хаоса
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Отдаленное святилище в %crn подверглось нескольким
<ce> нападениям со стороны группы разбойников. Этому месту
<ce> нужен охранник, но служитель не может позволить себе
<ce> помощь Гильдии Бойцов. Защитите ли вы его?

RefuseQuest:  [1001]
<ce> Чего бы пожелали %t, %pct?

AcceptQuest:  [1002]
<ce> Это место называется ___cult_. 
<ce> Священник описал довольно обычную ситуацию, связанную
<ce> с бандитами и бродягами, а именно: они приходят раз
<ce> в несколько дней, чтобы вытрясти из посетителей золото
<ce> и ценности. Вполне вероятно, что вам придется побродить рядом
<ce> некоторое время, прежде чем они появятся. Как всегда,
<ce> убедитесь, что вы не спите на территории. В конце концов, мы
<ce> говорим о храме. Вам нужно прибыть туда не позже =timer_ дней
<ce> и оставайтесь там до тех пор, пока вопрос не будет решен.
<ce> Счастливого пути, %pct.

QuestFail:  [1003]
<ce> Вы должны были уже защитить ___cult_.

QuestComplete:  [1004]
<ce> Прекрасная работа, %pcf. 
<ce> Я горжусь тем, что сотрудничаю с такими рыцарями, как вы.
<ce> Вы чтите нашу роль хранителей %crn.

Message:  1010
<ce> Служащий подходит и сообщает вам, что количество погибших
<ce> совпадает с тем, что он запомнил о группе. Он выражает вам 
<ce> глубокую сердечную благодарность, прежде чем вернуться
<ce> к своим обязанностям.

Message:  1011
<ce> Это то самое место. Предположительно, вам нужно
<ce> будет подождать поблизости следующую атаку.

Message:  1030
%qdt:
 _qgiver_ попросил меня присмотреть
 за удаленным святилищем под названием ___cult_,
 которое неоднократно становилось мишенью
 для разбойников. Мне нужно прибыть в течение
 =timer_ дней и ждать на территории, пока они
 не появятся и я не смогу воздать им должное.

QBN:
Person _qgiver_ group Questor male

Place _cult_ remote cult

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _wait_ 1:00 18:00

Foe _enemy_ is Thief

--	Quest start-up:
	start timer _timer_
	log 1030 step 0

_there_ task:
	when pc enters cult
	stop timer _timer_
	start timer _wait_
	say 1011
	clear _notthere_

_notthere_ task:
	when pc exits cult
	stop timer _wait_
	start timer _timer_
	clear _there_
-    when not _there_ and not _slain_

variable _wait_
_spawn_ task:
	when _wait_ and _there_ and not _slain_
	create foe _enemy_ every 1 minutes 3 times with 100% success

_infight_ task:
	daily from 0:00 to 23:59
	change foe _enemy_ infighting true
	clear _infight_

_slain_ task:
	killed 3 _enemy_ saying 1010

variable _timer_
_fail_ task:
	when _timer_ and not _slain_
	say 1003
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +4
	end quest