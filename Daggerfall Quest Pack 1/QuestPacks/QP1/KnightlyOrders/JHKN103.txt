Quest: JHKN103
DisplayName: Праздношатающиеся кости
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вы умеете убивать нежить, %pct?
<ce> Мы получаем много тревожных сообщений о злобных скелетах
<ce> за стенами города, которые нападают на невинных 
<ce> путешественников. Если бы вы смогли убить нескольких
<ce> из них, то оказали бы услугу нашему светлому городу.

RefuseQuest:  [1001]
<ce> %oth. Их гораздо легче убить тупым оружием.

AcceptQuest:  [1002]
<ce> В редких случаях они осмеливаются войти в город, так что я 
<ce> полагаю, вы могли бы оставаться поблизости и уничтожать все,
<ce> что пересекает наши границы. Но настоящая опасность находится
<ce> в дикой местности, куда вам и следует отправиться. Победите
<ce> полдюжины или около того из них и представьте, что каждый 
<ce> убитый - это спасенная вами жизнь. Пусть это мотивирует
<ce> вас. Вернитесь ко мне с выполненным заданием, и ваше имя
<ce> обретет почет в этом зале. Неявка в течение одной недели
<ce> будет означать отречение от задания с вашей стороны.

QuestComplete:  [1004]
<ce> Отличная работа, %pct. Это наш ежедневный труд.
<ce> Земля %crn стала немного безопаснее благодаря вам.
<ce> Мы испытываем гордость за то, что вы сделали.

Message:  1010
<ce> С могучим ревом блуждающий скелет бросается на вас.

Message:  1011
<ce> На сегодня скелетов должно быть достаточно.
<ce> Дороги должны стать немного безопаснее.

Message:  1030
%qdt:
 _qgiver_ из __qgiver_
 приказал мне отправиться в пустоши, чтобы
 избавиться от скелетов, которые нападают на прохожих. 
 Если мне удастся уничтожить шесть или около того
 и вернуться в ___qgiver_
 в течение недели, я обрету престиж в ордене.

QBN:
Person _qgiver_ group Questor male

Clock _timer_ 7.00:00

Foe _enemy_ is Skeletal_warrior

Place _dung_ permanent DaggerfallCastle
Place _dung2_ permanent SentinelCastle
Place _dung3_ permanent WayrestCastle

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _dung_ set _inside1_
    pc at _dung2_ set _inside2_
    pc at _dung3_ set _inside3_
    pc at any fighters set _fightered_
    pc at any tavern set _taverned_
    pc at any house set _homed_
    pc at any shop set _shopped_
    pc at any magery set _maged_
    pc at any temple set _templed_
    pc at any palace set _palaced_
    start timer _timer_

variable _inside1_
variable _inside2_
variable _inside3_
variable _fightered_
variable _taverned_
variable _homed_
variable _shopped_
variable _maged_
variable _templed_
variable _palaced_

_donotspawn_ task:
    when _inside1_ or _inside2_ or _inside3_ or _fightered_ or _taverned_ or _homed_ or _shopped_ or _maged_ or _templed_ or _palaced_

_spawner_ task:
    when not _donotspawn_
    create foe _enemy_ every 13 minutes indefinitely with 5% success msg 1010

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ infighting true
    clear _daily_

_slain_ task:
    killed 6 _enemy_
    say 1011

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc nothing
    legal repute +2
    end quest

_timer_ task:
    end quest