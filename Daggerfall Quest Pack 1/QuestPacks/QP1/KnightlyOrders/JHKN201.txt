Quest: JHKN201
DisplayName: Одинокий некромант
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Сторожка в %crn страдает от происков
<ce> некроманта, заклинателя нежити.
<ce> Нам нужен кто-то, кто не боится
<ce> развоплощенных существ
<ce> или ночного времени.

RefuseQuest:  [1001]
<ce> Подобное вторжение встречается довольно часто.
<ce> Вы будете часто видеть такого рода запросы.

AcceptQuest:  [1002]
<ce> Мы не знаем, где прячется злодей, мы
<ce> уже обыскали все дома в
<ce> __tavernhome_. Теперь остается
<ce> только оставаться на улицах по
<ce> ночам, когда появляются привидения,
<ce> и уничтожать каждого появившегося
<ce> духа. Рано или поздно появится
<ce> некромант, и тогда вы должны нанести
<ce> удар. Ради чести сделайте это в
<ce> течение =timer_ дней.

QuestFail:  [1003]
<ce> __qgiver_ поручил вам уничтожить
<ce> некроманта из __tavernhome_ к этому времени.

QuestComplete:  [1004]
<ce> Когда маг повержен, оставшиеся духи исчезают.
<ce> Жители города открывают двери, чтобы выглянуть наружу.
<ce> Опасность исчезла, и они ликуют, радуясь за вас.
<ce> Хотя денежного вознаграждения не предвидится,
<ce> ваше имя здесь не скоро будет забыто.

Message:  1010
<ce> Это правильное место, но здесь слишком много солнца.
<ce> Вам нужно будет находиться на улице, пока не наступит вечер.

Message:  1011
<ce> Пока вы не некроманта, но духи уже начали
<ce> бродить по улицам __tavernhome_.

Message:  1012
<ce> "Вы! Вы испортили мне всю работу!"
<ce>
<ce> Похоже, что вы привлекли внимание некроманта.

Message:  1030
%qdt:
 __qgiver_ из ___qgiver_
 поручил мне избавиться от
 некроманта, который преследует
 __tavernhome_ вызывая духов
 по ночам. Мне нужно уничтожить
 призраков и мага
 в течение =timer_ дней.

QBN:

Place _tavernhome_ remote tavernhome

Person _qgiver_ group Questor

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _spawn_ 4:00 6:00

Foe _vampman_ is Battle_mage
Foe _vamw1_ is Ghost
Foe _vamw2_ is Wraith

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _tavernhome_ set _outside_

_day_ task:
    daily from 6:00 to 18:00

variable _outside_

_dayarrive_ task:
    when _outside_ and _day_
    say 1010

_nightarrive_ task:
    when _outside_ and not _day_
    start timer _spawn_
    stop timer _timer_
    create foe _vamw1_ every 25 minutes indefinitely with 25% success msg 1011
    create foe _vamw2_ every 17 minutes indefinitely with 21% success msg 1011

_stoptimer_ task:
    when not _nightarrive_
    stop timer _spawn_
    start timer _timer_

_infighting_ task:
    daily from 0:00 to 23:59
    change foe _vamw1_ infighting true
    change foe _vamw2_ infighting true
    clear _infighting_

_spawn_ task:
    create foe _vampman_ every 0 minutes 1 times with 100% success msg 1012
    change foe _vampman_ infighting true
    change foe _vampman_ team 13
    cast Feet_of_Notorgo spell on _vampman_
    cast Shield spell on _vampman_
    cast Spell_Resistance spell on _vampman_

_victory_ task:
    killed _vampman_
    remove foe _vamw1_
    remove foe _vamw2_
    give pc nothing
    legal repute +4
    end quest

_timer_ task:
	say 1003
	end quest