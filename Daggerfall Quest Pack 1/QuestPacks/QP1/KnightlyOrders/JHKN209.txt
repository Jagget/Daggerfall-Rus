Quest: JHKN209
DisplayName: Неуклюжие темные искусства
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pct, у меня есть несколько вопросов. Вы ненавидите
<ce> великанов - неуклюжих, жестоких похитителей фермерских
<ce> животных и ничего не подозревающих горожан? А некромантов,
<ce> которые воскрешают наших предков, отправляя их в рабство?
<ce> И стали бы вы ненавидеть группу гигантских некромантов,
<ce> которые могут как раздавить своих врагов, так и поднять
<ce> их дух сражаться за них?

RefuseQuest:  [1001]
<ce> Ответы на эти вопросы должны быть очевидны.
<ce> Какая часть вызывает сомнения, %pcf?

AcceptQuest:  [1002]
<ce> Тогда отправляйтесь в ___dung_.
<ce> Существование таких варваров, обладающих некоторыми
<ce> навыками в магических искусствах, является большой аномалией,
<ce> с которой мы не должны мириться. Гильдия Магов обязана
<ce> очистить %crn от некромантии,
<ce> но она оказалась в состоянии временного бездействия. Гильдия
<ce> Магов сообщила нам, что уничтожение угрозы для горожан -
<ce> наша обязанность. Если вы уничтожите эту банду
<ce> магов-великанов, я сообщу нашей светлости о том,
<ce> что Гильдия Магов не отреагировала. Давайте
<ce> встретимся здесь снова через =timer_ дней, выполнив
<ce> каждый свою работу.

QuestFail:  [1003]
<ce> _qgiver_ нужно было, чтобы вы уничтожили
<ce> гигантских некромантов к этому времени.

QuestComplete:  [1004]
<ce> Блестящая работа. Я внес свой вклад в дипломатическую сторону
<ce> дела. Если Гильдия Магов желает сохранить свой устав
<ce> в %crn, ей нужно будет ускорить темп. Спасибо
<ce> вам за то, что обеспечиваете безопасность наших граждан.

Message:  1020
<ce> Этот великан сразу распознает в вас серьезную угрозу.
<ce> Он начинает читать заклинание, и вы чувствуете
<ce> приближение чего-то темного.

Message:  1021
<ce> Как только этот великан пал, вы сразу почувствовали,
<ce> что темные силы ослабевают. Культ великанов-некромантов
<ce> уничтожен.

Message:  1030
%qdt:
 _qgiver_ поручил мне уничтожить
 культ великанов в __dung_,
 которые каким-то образом
 получили навыки некромантии.
 У меня есть =timer_ дней,
 чтобы вернуться в ___qgiver_.

Message:  1099
<ce> Уничтожение великанов и искоренение некромантии -
<ce> оба достойные деяния. Насколько же более достойным
<ce> является выполнение их одновременно?

QBN:
Person _qgiver_ group Questor male

Place _dung_ remote dungeon13

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Giant
Foe _enemy1_ is Wraith

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _dung_ set _inside_
	reveal _dung_

variable _inside_
_giants_ task:
	when _inside_ and not _slain_
	create foe _enemy_ every 28 minutes indefinitely with 66% success

_injured_ task:
	injured _enemy_
	say 1020

_spawn_ task:
	when _injured_ and _inside_ and not _slain_
	create foe _enemy1_ every 4 minutes indefinitely with 25% success

_infight_ task:
	daily from 0:00 to 23:59
	change foe _enemy_ infighting true
	change foe _enemy1_ infighting true
	change foe _enemy_ team 0
	change foe _enemy1_ team 0
	clear _infight_

_slain_ task:
	killed 7 _enemy_ saying 1021

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	say 1099
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest