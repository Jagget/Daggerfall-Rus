Quest: JHKN237
DisplayName: Дитя льда
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Это дело может вам подойти. Ледяной атронах, существо,
<ce> похожее на голема, нападает на жителей религиозного места
<ce> в %crn. Мы заверили их, что пришлем помощь,
<ce> когда это будет возможно. Я надеюсь, что вы сможете
<ce> выполнить наш долг.

RefuseQuest:  [1001]
<ce> Ваша честь будет отдана гораздо менее способному рыцарю.
<ce> Это задание безумно простое.

AcceptQuest:  [1002]
<ce> Да! Справедливый ответ и правое дело. Место
<ce> называется ___place_. Отправляйтесь туда
<ce> и найдите это существо где-то на улице. Не даруйте ему
<ce> пощады, ибо оно не живое; это не более чем магия
<ce> в ледяной оболочке. Вернитесь сюда в течение
<ce> =timer_ дней, и мы оценим вашу хорошую работу.

QuestFail:  [1003]
<ce> _qgiver_ уже ожидал услышать о том,
<ce> что вы убили атронаха.

QuestComplete:  [1004]
<ce> Прекрасная работа! Вы спасли жизни людей, отправившись
<ce> в это путешествие. Хотя это оружие может быть плохой
<ce> наградой, оно является символом нашего уважения к вам.

Message:  1010
<ce> Где-то по этим улицам бродит атронах.
<ce> Найти его - всего лишь вопрос времени.

Message:  1011
<ce> Резкий крик поблизости предупреждает
<ce> вас о схватке. Люди убегают от кого-то.
<ce> Вам не требуется особых усилий,
<ce> чтобы распознать, что это атронах.

Message:  1012
<ce> Жителям этой общины больше не нужно
<ce> бояться. Откуда взялся атронах, вам
<ce> неизвестно, но вы верите, что на этот
<ce> вопрос ответят соответствующие власти.

Message:  1030
%qdt:
 _qgiver_ из __qgiver_ послал меня
 в религиозную общину под названием
 ___place_, чтобы уничтожить ледяного
 атронаха, нападающего на ее
 жителей. Мне нужно вернуться
 к ___qgiver_ в течение =timer_ дней.

QBN:
Person _qgiver_ group Questor male

Place _place_ remote templehome

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 0:02 0:06

Foe _enemy_ is Ice_atronach

Item _reward_ weapon

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _place_ do _spawn_

_spawn_ task:
    start timer _delay_
    say 1010

_nospawn_ task:
    when not _spawn_
    stop timer _delay_

_delay_ task:
    create foe _enemy_ every 0 minutes 1 times with 100% success
    change foe _enemy_ infighting true
    say 1011

_slain_ task:
    killed _enemy_
    say 1012

_timer_ task:
    say 1003
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    legal repute +4
    end quest