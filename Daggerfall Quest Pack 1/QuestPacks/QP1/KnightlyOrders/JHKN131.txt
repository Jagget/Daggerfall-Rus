Quest: JHKN131
DisplayName: Чумной рыцарь
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Нашествие вредителей охватило
<ce> дворец в %crn, и %t поручили нам
<ce> найти кого-нибудь достаточно
<ce> умного, чтобы покончить с этим.
<ce> Это не потребует боевых действий,
<ce> %pct. Мы хотим отправить вас на
<ce> цивилизованную охоту.

RefuseQuest:  [1001]
<ce> Конечно, мы сможем найти более авантюрное задание
<ce> для человека с вашими навыками.

AcceptQuest:  [1002]
<ce> Это простая задача, добрый рыцарь, по
<ce> крайней мере, в теории. Вам просто нужно
<ce> найти эксперта по уничтожению летучих
<ce> мышей. Не утруждайте себя попытками убить
<ce> их самостоятельно. %t прислали достаточно
<ce> воинов, чтобы доказать, что проблема
<ce> серьезная. Поспрашивайте людей, чтобы найти
<ce> кого-нибудь достаточно квалифицированного,
<ce> и сообщите ему или ей, что работа будет
<ce> оплачена сполна. Уверен, вы сможете решить
<ce> эту проблему. Представьте свой доклад в
<ce> течение =timer_ дней.

QuestFail:  [1003]
<ce> Если вы здесь, почему я продолжаю
<ce> слышать о вредителях, заполонивших
<ce> дворец ___palace_?

QuestComplete:  [1004]
<ce> Как прошла экспедиция, %pct?
<ce> Гордитесь тем, что служили
<ce> своим согражданам. Это великий
<ce> идеал __questgiver_.

Message:  1010
Настоящий позор, не правда ли? %t забирают наши деньги, но не могут содержать дворец в чистоте.
   <--->
Говорят, никто не разберется с летучими мышами лучше, чем _ext_.
   <--->
Если тебе нужно убить летучих мышей, отправляйся в ___exthouse_, рыцарь. Спростие _ext_.
   <--->
Проблемы с летучими мышами? Вам нужен _ext_. Живёт в _exthouse_, ___exthouse_.

Message:  1011
<ce> Что? О, черт возьми. Вам повезло, что %t
<ce> хорошо платят. Я бы лучше намазал вам морду
<ce> болиголовом, если бы это не стоило моего времени.
<ce> Очень хорошо, ведите.

Message:  1012
<ce> Репутация _ext_ вполне заслужена.
<ce> Быстро распознав проблему,
<ce> %g1 начинает вырезать небольшие отверстия вокруг
<ce> дворца и помещает в них ядовитые травы.
<ce> Когда %g1 полностью обходит все
<ce> здание, вы замечаете, как одна летучая мышь
<ce> кусает ядовитые растения и почти сразу же
<ce> перестает дышать. Сильнодействующая смесь, однако.
<ce>
<ce> Когда проект завершен, нарядный слуга
<ce> подходит и хвалит _ext_.
<ce>
<ce> "Какое счастье, что мы вас нашели!
<ce> Здесь было совершенно невыносимо,
<ce> с этими зверями, терроризирующими нас.
<ce> Мы выделили для вас =dummy_ золотых,
<ce> что, как мы надеемся, будет достаточной
<ce> компенсацией за выполнение задания".
<ce>
<ce> _ext_ холодно принимает награду
<ce> и уходит, даже не попрощавшись.
<ce> _questgiver_ будет ждать вашего отчета.

Message:  1013
<ce> Ваш нос и уши без труда
<ce> уловили присутствие летучих мышей
<ce> в этом дворце.

Message:  1014
<ce> Они действительно кажутся бесконечными.
<ce> Прислуга благодарит вас за то, что вы
<ce> облегчили их страдания, пусть даже
<ce> это временно.

Message:  1030
%qdt:
 _questgiver_ попросил меня
 найти уничтожителя летучих мышей, достаточно
 опытного, чтобы очистить дворец
 в ___palace_. Возможно,
 горожане знают
 кого-нибудь подходящего.
 Я должен вернуться в __questgiver_
 в ___questgiver_ до того,
 как пройдет =timer_ дней.

Message:  1031
%qdt:
 Я веду _ext_ во
 дворец в ___palace_, чтобы решить проблему
 с нашествием летучих мышей.

QBN:
Person _questgiver_ group Questor male
Person _ext_ faction The_Thieves_Guild

Place _palace_ remote palace anyInfo 1010
Place _exthouse_ remote random

Item _dummy_ gold range 1400 to 2300

Clock _timer_ 00:00 0 flag 1 range 0 2

Foe _bat_ is Giant_bat

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _palace_ set _inside_
    place npc _ext_ at _exthouse_

variable _inside_
_warn_ task:
    when _inside_ and not _talk_
    say 1013
    create foe _bat_ every 2 minutes indefinitely with 100% success

_killer_ task:
    killed 6 _bat_
    say 1014

_talk_ task:
    clicked npc _ext_
    say 1011
    hide npc _ext_
    add _ext_ face
    log 1031 step 1

_arrival_ task:
    when _inside_ and _talk_
    drop _ext_ face
    say 1012
    start task _victory_

variable _victory_
_qgclicked_ task:
	clicked npc _questgiver_

_timer_ task:
	end quest

_clearclick_ task:
	when _qgclicked_ and not _victory_
  say 1003
	clear _qgclicked_ _clearclick_

_pcgetsnothing_ task:
    when _qgclicked_ and _victory_
    give pc nothing
    legal repute +3
    end quest