Quest: JHKN123
DisplayName: Появление Ворона
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Грозный преступник, известный как Ворон, появился
<ce> в %crn. Нам нужен кто-то хорошо вооруженный,
<ce> чтобы задержать его и предать суду. Вы согласны это сделать?

RefuseQuest:  [1001]
<ce> Что ж, я завербую кого-нибудь более доблестного.

AcceptQuest:  [1002]
<ce> Его убежище - _res_ в ___res_. 
<ce> Этот человек разыскивается в нескольких провинциях
<ce> за неописуемые преступления против цивилизации. Используйте
<ce> любые доступные средства, чтобы пленить его. Мне бы хотелось,
<ce> чтобы вы избежали жертв, но максимальное сопротивление
<ce> требует максимального противодействия. Как бы вы
<ce> ни поступили, вернитесь сюда в течение =timer_ дней
<ce> для суда над ним. %god присмотрит за вами.

QuestFail:  [1003]
<ce> _qgiver_ нужен был ваш отчет о Вороне до суда.

QuestComplete:  [1004]
<ce> Подставное тело? Какая досада. Как правило,
<ce> это его манера уклоняться, но мы надеялись, что именно
<ce> в этот раз нам удастся его схватить. Ваша работа
<ce> будет засчитана как победа, %pct, хотя он
<ce> и ускользнул от нас. Спасибо вам за работу.

Message:  1010
<ce> Ворон нечеловечески ухмыляется вам, его лицо вытягивается,
<ce> меняя свои пропорции. Его голова начинает вытягиваться
<ce> в стороны, а глаза становятся ярко-красными. Как только его
<ce> кожа приобретает другой цвет, становится совершенно очевидно,
<ce> что это не Ворон, а хорошо продуманный отвлекающий маневр.
<ce> Ваша задача, без сомнения, потребует уничтожения этого врага,
<ce> прежде чем сообщить о случившемся _qgiver_.

Message:  1011
<ce> Ворон сбежал, зато он лишился одного из своих убежищ.
<ce> _qgiver_ захочет это знать.

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_
 отдал мне приказ арестовать Ворона,
 известного злодея в ____qgiver_
 и других провинциях, который скрывается
 в _res_, ___res_.
 Мне разрешено использовать любые необходимые средства,
 но нужно вернуться до истечения =timer_ дней.

QBN:
Foe _q1_ is Gargoyle
Foe _q2_ is Ancient_vampire

Person _qgiver_ group Questor male
Person _crow_ named The_Crow

Place _res_ remote house1

Clock _timer_ 00:00 0 flag 1 range 0 2

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _crow_ at _res_

_deliver_ task:
  clicked npc _crow_
  say 1010
  hide npc _crow_
  pick one of _g1_ _g2_

_g1_ task:
  place foe _q1_ at _res_
  change foe _q1_ infighting true

_g2_ task:
  place foe _q2_ at _res_
  change foe _q2_ infighting true

_slain_ task:
  killed _q1_
  killed _q2_
  say 1011

_victory_ task:
  when _qgclicked_ and _slain_
  give pc nothing
  legal repute +4
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _deliver_
  clear _qgclicked_ _clearclick_

_timer_ task:
  say 1003
  end quest