Quest: JHKN133
DisplayName: Преследование добычи
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Стая тигров мигрировала в
<ce> заброшенный дом в %crn. Охотник желает,
<ce> чтобы почетный рыцарь помог убить зверей,
<ce> и когда речь заходит о чести, я тут же думаю о вас.
<ce> Сопроводите ли вы этого следопыта?

RefuseQuest:  [1001]
<ce> Досадно, но, возможно, это не так уж и сложно.

AcceptQuest:  [1002]
<ce> "Ну вот и отлично!"
<ce>
<ce> Шумный рейнджер хлопает вас по плечу,
<ce> и _questgiver_ кивает вам.
<ce>
<ce> "Рад слышать, что у меня есть помощник! Тогда пошли,
<ce> %pcn! Меня зовут _hunter_.
<ce> Мы направляемся в ___poorhome_, чтобы очистить место
<ce> от тигров! Давайте доберемся туда за =timer_ дней.
<ce> Я готов, когда вы будете готовы!"

QuestFail:  [1003]
<ce> Задание провалено.

QuestComplete:  [1004]
<ce> Вы остаетесь в одиночестве.

Message:  1010
<ce> Действительно, вы видите стаю тигров вокруг
<ce> здания. Тот, кто живет внутри, должно быть, в ужасе от
<ce> одной мысли выйти наружу. _hunter_ сжимает вашу руку.
<ce>
<ce> "Ладно, я отвлеку некоторых из них и
<ce> убъю их из лука. Вы убьете тех,
<ce> кто не последует за мной. Готовы? Вперед!"
<ce>
<ce> Прежде чем вы успеваете ответить, следопыт начинает
<ce> кричать и убегать. Три тигра преследуют %g4,
<ce> а остальные выбирают вас.

Message:  1011
<ce> Тигры повержены, вы собираете свои вещи,
<ce> чтобы уйти. _hunter_ кричит в бешенстве:
<ce>
<ce> "Эй! Что ты делаешь? Там еще трое, и мы
<ce> не можем оставить их! Они же вернутся
<ce> и съедят кого-нибудь еще, если мы уйдем!"
<ce>
<ce> Охотник указывает на трех крошечных тигрят,
<ce> тыкающихся в тела павших взрослых.
<ce> Им не больше месяца; они никому не угрожают.
<ce> _hunter_ достает короткий клинок.
<ce>
<ce> "Я сейчас пойду скажу семье, что проблема решена.
<ce> Ты проткни этих троих насквозь,
<ce> а я скоро вернусь."
<ce>
<ce> Убьешь ли ты маленьких тигрят?

Message:  1012
<ce> Вы убиваете тигрят как можно быстрее.
<ce> Их жалобное мяу разрывает воздух.
<ce> Даже если бы они знали, как себя защитить,
<ce> горе от потери родителей омрачает их чувства,
<ce> и они не оказывают сопротивления.
<ce>
<ce> _hunter_ возвращается с широкой улыбкой.
<ce>
<ce> "Каково это — быть героем? Ах, ты, наверное,
<ce> уже привык. Может быть, однажды я присоединюсь
<ce> к вам в __questgiver_
<ce> и мы сможем работать вместе!"

Message:  1013
<ce> _hunter_ приходит в ярость.
<ce>
<ce> "Если ты здесь не для того, чтобы убивать тигров,
<ce> то на что ты годен?! Проваливай и дай мне с этим разобраться!"
<ce>
<ce> Вынув короткий клинок, %g1 направляется к
<ce> детенышам. Вы позволите %g4 убить их?

Message:  1014
<ce> Вы не можете принять в этом участие, но также не можете остановить
<ce> _hunter_. После того как %g1 проткнул детенышей насквозь,
<ce> %g1, разочарованно вздохнув, обращается к вам.
<ce>
<ce> "Что ж, рад, что все закончилось. Не ожидал, что рыцарь
<ce> окажется таким слабаком. Пойду скажу
<ce> что проблема решена. Пока."

Message:  1015
<ce> "Жалкий %ра! Любитель тигров! Я не позволю
<ce> тебе ударить меня ножом в спину!"
<ce>
<ce> _hunter_ отступает назад и готовит лук.

Message:  1016
<ce> Вы сбиваете %g4 с ног, этого достаточно, чтобы
<ce> вселить в н%g4 страх смерти. Затем %g1 начинает
<ce> плакать. Ты даешь понять, что эта битва окончена,
<ce> и охотник начинает уползать прочь. Тигрята,
<ce> с другой стороны, прокладывают путь в дикую
<ce> местность. Вы остаетесь один на поле боя.

Message:  1017
<ce> Вы должны были уже убрать тигров из ___poorhome_.
<ce> Вы вызываете отвращение у _hunter_.

Message:  1030
%qdt:
 Охотник по имени _hunter_
 попросил __questgiver_ о помощи
 в уничтожении прайда тигров в
 ___poorhome_. Мы должны быть
 там в течение =timer_ дней.

QBN:

Person _questgiver_ group Questor male
Person _hunter_ group Resident1

Place _poorhome_ remote poorhome

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _tig_ is 3 Sabretooth_tiger
Foe _hnt_ is Ranger

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _poorhome_ set _inside_
    add _hunter_ face

_inside_ task:
    say 1010
    drop _hunter_ face
    create foe _tig_ every 0 minutes 1 times with 100% success

_killed_ task:
    killed 3 _tig_
    prompt 1011 yes _obey_ no _defy_

_obey_ task:
    say 1012
    give pc nothing
    end quest

_defy_ task:
    prompt 1013 yes _weak_ no _fight_

_weak_ task:
    say 1014
    give pc nothing
    end quest

_fight_ task:
    say 1015
    create foe _hnt_ every 0 minutes 1 times with 100% success

_inj_ task:
    injured _hnt_
    clicked foe _hnt_
    say 1016
    remove foe _hnt_
    give pc nothing
    end quest

_timer_ task:
    say 1017
    end quest