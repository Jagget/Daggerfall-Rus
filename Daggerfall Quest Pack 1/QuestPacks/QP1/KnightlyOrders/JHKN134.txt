Quest: JHKN134
DisplayName: Медовые слова
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Немногие проблемы беспокоят рыцарский орден больше, чем буйство
<ce> знати. Мы в основном избегаем таких проблем, но сегодня это
<ce> касается нас. Один из наших братьев-рыцарей помолвлен с дочерью
<ce> важного аристократа, и только недавно он узнал, что ее прошлое
<ce> не так чисто, как обещали. Он хочет разорвать помолвку, но боится
<ce> королевского гнева. Нам нужен рыцарь, более убедительный, чем
<ce> любой другой. Умеете ли вы достаточно хорошо убеждать, чтобы
<ce> уговорить разгневанного родителя?

RefuseQuest:  [1001]
<ce> Возможно, нам придется нанять какого-нибудь барда.

AcceptQuest:  [1002]
<ce> На этот раз вам не придется уезжать далеко. Встретьтесь с _nob_
<ce> во дворце этого города и представьте убедительные аргументы в
<ce> поддержку нашего брата, _knight_. Соберите все необходимые
<ce> доказательства. Защитите его честь и попробуйте спасти его от
<ce> предстоящего брака. Если слов не достаточно, постарайтесь найти
<ce> другие способы. У вас есть всего пять дней, чтобы обеспечить
<ce> свободу нашему человеку. %god направит ваш путь.

QuestFail:  [1003]
<ce> _knight_ к этому времени уже будет безвозвратно
<ce> помолвлен с этой девушкой.

QuestComplete:  [1004]
<ce> Очень хорошо, я понимаю вашу позицию. Я бы хотел избегать
<ce> дальнейших взаимодействий с этим человеком, но не буду
<ce> считать __qgiver_ ответственными. Прошу вас уходить, и
<ce> будьте более внимательны, чем был этот неотзывчивый человек.
<ce> Ведь в жизни у нас ограниченное количество шансов.

RumorsPostSuccess:  [1007]
Я говорю, что эти археологи - хорошие люди.
   <--->
Археологи и Гильдия Магов всегда ссорятся из-за всякой всячины. %god знает что.

QuestorPostFailure:  [1009]
Кто-то другой вмешался после вашей неудачи с _nob_, славься %god.

QuestLogEntry:  [1010]
%qdt:
 По заданию __qgiver_
 в ___qgiver_ мне нужно
 поговорить с дворянином по имени
 _nob_ в местном дворце,
 чтобы положить конец помолвке
 %g4 дочери и _knight_.
 У меня есть =timer_ дней.

Message:  1011
%qdt:
 _nob_ отклонил мою просьбу
 освободить _knight_ и требует вместо этого
 сумму в размере 2500 золотых монет.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Этикет)
<ce>
<ce> Хмф. Как бы ни было трудно поверить в вашу
<ce> историю, я должен отдать должное __qgiver_.
<ce> Этот скандал обойдется нам всем дороже, чем мы
<ce> рассчитывали... Но альтернатива гораздо хуже.

Message:  1015
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Привлекательность)
<ce>
<ce> Хмф. Как бы ни было трудно поверить в вашу
<ce> историю, я должен отдать должное __qgiver_.
<ce> Этот скандал обойдется нам всем дороже, чем мы
<ce> рассчитывали... Но альтернатива гораздо хуже.

Message:  1016
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА: Этикет)
<ce>
<ce> Возможно, вы переоценили свои ораторские способности.
<ce> _nob_ приходит в ярость.
<ce> Сыплются оскорбления в адрес вас, ваших родителей,
<ce> _knight_ и __qgiver_.
<ce>
<ce> "Возможно, я был дураком, позволив событиям зайти так
<ce> далеко, если _knight_ настолько низок, что выдвигает
<ce> подобные обвинения. Моя дочь не выйдет замуж за негодяя!
<ce> Я требую 2500 золотых, чтобы возместить то, что я вложил
<ce> в эту жалкую помолвку. Затем я отправлю уведомления о
<ce> переводе средств заинтересованным сторонам".
<ce>
<ce> _nob_ продолжает пристально смотреть
<ce> на вас, ожидая денег. __qgiver_,
<ce> вероятно, не смогут вам помочь...

Message:  1017
<ce> Вы роетесь в поисках 2500 золотых, но у вас
<ce> их нет. _nob_ кипит от ярости, хотя %g1 и не
<ce> открывает рта, чтобы снова наорать на вас.

Message:  1018
<ce> Вы незаметно передаете 2500 золотых
<ce> в руки _nob_.

QBN:
Person _qgiver_ group Questor male
Person _nob_ faction Random_Noble
Person _knight_ faction Generic_Knightly_Order male

Place _palace_ local palace

Clock _timer_ 5.00:00

--	Quest start-up:
    start timer _timer_
    log 1010 step 0
    place npc _nob_ at _palace_

_talk_ task:
    clicked npc _nob_

_conv_ task:
    when _talk_ and not _fail_
    start task _begin_

_begin_ task:
    pick one of _go1_ _go2_ _go3_

_check1_ task:
    when skill Etiquette is at least 30

_check2_ task:
    when skill Etiquette is at least 35

_check3_ task:
    when skill Etiquette is at least 40

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
    when _check1_ and _go1_
    start task _victory1_

_c2_ task:
    when _check2_ and _go2_
    start task _victory1_

_c3_ task:
    when _check3_ and _go3_
    start task _victory1_

_d1_ task:
    when not _check1_ and _go1_
    start task _tryspeech_

_d2_ task:
    when not _check2_ and _go2_
    start task _tryspeech_

_d3_ task:
    when not _check3_ and _go3_
    start task _tryspeech_

_tryspeech_ task:
    pick one of _go4_ _go5_ _go6_

_check4_ task:
    when attribute Personality is at least 75

_check5_ task:
    when attribute Personality is at least 80

_check6_ task:
    when attribute Personality is at least 85

variable _go4_
variable _go5_
variable _go6_

_c4_ task:
    when _check4_ and _go4_
    start task _victory2_

_c5_ task:
    when _check5_ and _go5_
    start task _victory2_

_c6_ task:
    when _check6_ and _go6_
    start task _victory2_

_d4_ task:
    when not _check4_ and _go4_
    start task _fail_

_d5_ task:
    when not _check5_ and _go5_
    start task _fail_

_d6_ task:
    when not _check6_ and _go6_
    start task _fail_

_victory1_ task:
    say 1014
    give pc nothing
    end quest

_victory2_ task:
    say 1015
    give pc nothing
    end quest

_fail_ task:
    say 1016
    log 1011 step 1
    clear _talk_

_payup_ task:
    when _talk_ and _fail_
    pay 2500 money do _victory3_ otherwise do _nomoney_

_victory3_ task:
    clear _payup_
    say 1018
    give pc nothing
    end quest

variable _nomoney_
_nomoney1_ task:
    when _nomoney_ and not _victory3_
    say 1017
    clear _talk_ _nomoney_

_timer_ task:
    say 1003
    end quest