Quest: JHKN102
DisplayName: Негостеприимство
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, ==questgiver_ снова нуждается в
<ce> вашей помощи. Всего несколько дней назад
<ce> ___home_ был захвачен бандой головорезов,
<ce> которые занялись грабежами, конфискацией
<ce> имущества и так далее. Это слишком
<ce> далеко, чтобы обычный патруль мог
<ce> справиться с этим, и поэтому на этот крик
<ce> о помощи должны ответить мы. Поможете ли
<ce> вы этим бедным людям?

RefuseQuest:  [1001]
<ce> Полагаю, мне не стоит удивляться.

AcceptQuest:  [1002]
<ce> Поторопитесь. Отбейте головорезам
<ce> интерес убивать горожан. Удостоверьтесь,
<ce> что невинные не пострадают. Прибудьте
<ce> ночью, если нужно. Негодяи, без
<ce> сомнения, держат стражу, так что как
<ce> только вы появитесь на территории, они
<ce> непременно попытаются убить вас. У вас
<ce> есть =timer_ дней. Пусть %god будет с
<ce> вами. Судьба ___home_ в ваших руках.

QuestFail:  [1003]
<ce> _questgiver_

QuestComplete:  [1004]
<ce> Вы должены гордиться, %pcf,
<ce> потому что вы совершили великое дело.
<ce> Такие жертвы не забываются.

RumorsDuringQuest:  [1005]
Вокруг ___questgiver_ появилось много перьев гарпий. Странно, не правда ли?

Message:  1010
<ce> Ваше приближение замечен, и головорезы спешат к вам.

Message:  1011
<ce> Этой банды головорезов больше нет.

Message:  1020
<ce> Как только вы ступаете на территорию,
<ce> молодой человек быстро подбегает и
<ce> шепчет вам на ухо.
<ce>
<ce> "Простите, %ra, но мы не можем позволить вам
<ce> находиться на этой территории. Мы прибегли к
<ce> помощи Темного Братства, и они спасли нас от
<ce> наших врагов. Пожалуйста, уходите, прежде
<ce> чем возникнет недоразумение".
<ce>
<ce> Ясно, что это не то, чему ==questgiver_
<ce> позволил бы произойти. Пытаетесь ли вы
<ce> узнать местоположение этих членов
<ce> Темного Братства?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Этикет, низкая сложность)

Message:  1021
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Ваши ораторские способности оставляют желать
<ce> лучшего. С каждым вопросом юноша выглядит все
<ce> более взволнованным. Наконец допрос заканчивается,
<ce> и вы остаетесь с пустыми руками.

Message:  1022
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Ваши навыки риторики дали о себе знать. Уверенность
<ce> юноши растет, когда вы объясняете, зачем пришли и
<ce> почему Темное Братство больше не будет беспокоить
<ce> эту семью, если они доверятся вам.
<ce>
<ce> "Ладно, но вы сделаете это быстро, верно? Мои
<ce> родители в отчаянии, эти хулиганы берут их запасы
<ce> в качестве оплаты. Я знаю, они остановились
<ce> в близлежащем храме, притворяются паломниками, пока
<ce> занимаются вымогательством. Я не помню, как он
<ce> называется, но это один из тех храмов чего-то там
<ce> или Алтарей к чему-то. Вы знаете, места с маленькими
<ce> куполами. Они не далеко. Я уверен в этом."
<ce>
<ce> Опасаясь быть замеченным кем-то более важным, молодой
<ce> человек желает вам удачи и возвращается в дом.

Message:  1023
<ce> Похоже, вы нашли подходящее святилище.
<ce> Шорох поблизости указывает на то, что кто-то недоволен вашим приходом.

Message:  1027
<ce> Вы решаете больше не вмешиваться.
<ce> Возможно, это именно то, чего хочет семья.
<ce> Вы должны сообщить об этом _questgiver_.

Message:  1028
<ce> Этот вопрос определенно решен.
<ce> Теперь сообщаем _questgiver_, что ___home_ свободен.

Message:  1029
<ce> Два разбойника внезапно появляются рядом, вооруженные до зубов.
<ce>
<ce> "Не следовало бы тебе совать свой нос сюда, в ___home_!
<ce> Тогда бы нам сегодня не пришлось хоронить труп!"

Message:  1030
%qdt:
 Меня послали из
 ___questgiver_ на ферму,
 ___home_, которая была
 захвачена бандой
 преступников. Я должен освободить
 заложников и вернуться к
 _questgiver_ в течение =timer_ дней.

Message:  1031
%qdt:
 Ферма ___home_ была
 освобождена Темным Братством,
 как мне сообщил один из жителей. Мне
 следует вернуться в ___questgiver_
 и доложить об этом.

Message:  1032
%qdt:
 Ферма ___home_ была
 освобождена Темным Братством,
 как сообщил мне один из
 жильцов. Ассасины
 остановились в ближайшем
 храме, если я решу их выследить.

Message:  1033
<ce> У головорезов, похоже, нет никаких документов или
<ce> чего-либо еще, что могло бы привести вас к их логову.
<ce> Поскольку у вас нет зацепок для продолжения, ваш
<ce> единственный выход - вернуться в Гильдию Бойцов.

Message:  1040
<ce> _questgiver_ выглядит глубоко расстроенным.
<ce>
<ce> "Темное Братство? Защищает ___home_? Это
<ce> печальные известия, %pct. Пока мы больше
<ce> ничего не можем сделать, хотя %t должны
<ce> знать. Мы не можем позволить силам тьмы
<ce> еще больше закрепиться на нашей доброй
<ce> земле. Тем не менее, я отдам вам должное
<ce> за то, что вы совершили это путешествие и
<ce> обеспечили безопасность. Спасибо за
<ce> хорошую работу, %pcf".

QBN:
Person _questgiver_ group Questor male
Person _dbdummy_ faction The_Dark_Brotherhood
- Person _nm_ named The_Night_Mother

Place _home_ remote farmhome

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:00
Clock _stalk_ 4:00 1.00:00

Foe _enemy_ is 4 Rogue
Foe _dbenemy1_ is Nightblade
Foe _dbenemy2_ is Bard
Foe _stalker1_ is Rogue
Foe _stalker2_ is Rogue

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _home_ set _housearrive_
	pick one of _norm_ _norm_ _alt_

variable _norm_
variable _alt_
_housearrive_ task:
	start task _spawn_

variable _spawn_

--normal quest section. spawn task is used to ensure no duplicate spawn by stepping on and off property.
_kill_ task:
	when _spawn_ and not _alt_
	create foe _enemy_ every 0 minutes 1 times with 100% success
	say 1010

_slain_ task:
	killed 4 _enemy_ saying 1011

_infight_ task:
	daily from 0:00 to 23:59
	change foe _enemy_ infighting true
	clear _infight_

--end of normal quest section. alternate begins
_altstep_ task:
	when _alt_ and _housearrive_
	start task _altfin_
	log 1031 step 1

_altfin_ task:
	prompt 1020 yes _begin_ no _quit_

_quit_ task:
	say 1027

_begin_ task:
	pick one of _go1_ _go2_ _go3_
	start timer _delay_

_check1_ task:
	when skill Etiquette is at least 20

_check2_ task:
	when skill Etiquette is at least 25

_check3_ task:
	when skill Etiquette is at least 30

variable _go1_
variable _go2_
variable _go3_
_c1_ task:
	when _check1_ and _go1_
	start task _victory_

_c2_ task:
	when _check2_ and _go2_
	start task _victory_

_c3_ task:
	when _check3_ and _go3_
	start task _victory_

_d1_ task:
	when not _check1_ and _go1_

_d2_ task:
	when not _check2_ and _go2_

_d3_ task:
	when not _check3_ and _go3_

_victory_ task:
	say 1022
	log 1032 step 1

variable _delay_
_persuadefail_ task:
	when _begin_ and _delay_ and not _victory_
	say 1021
	start timer _stalk_

_stalk_ task:
	create foe _stalker1_ every 0 minutes 1 times with 100% success
	create foe _stalker2_ every 0 minutes 1 times with 100% success
	say 1029

_killstalker1_ task:
	killed _stalker1_

_killstalker2_ task:
	killed _stalker2_

_killedstalkers_ task:
	when _killstalker1_ and _killstalker2_
	say 1033

_cultent_ task:
	when pc enters cult

_combat_ task:
	when _victory_ and _cultent_
	say 1023
	create foe _dbenemy1_ every 0 minutes 1 times with 100% success
	create foe _dbenemy2_ every 0 minutes 1 times with 100% success
	change foe _dbenemy1_ infighting true
	change foe _dbenemy2_ infighting true

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_ and not _altfin_
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +3
	end quest

_kill1_ task:
	killed _dbenemy1_

_kill2_ task:
	killed _dbenemy2_

_dbend_ task:
	when _kill1_ and _kill2_
	say 1028
	change repute with _dbdummy_ by -20

_altend_ task:
	when _qgclicked_ and _altfin_ and not _dbend_
	say 1040
	change repute with _questgiver_ by +4
	end quest
--+4 reputation intentional

_altvict_ task:
	when _qgclicked_ and _dbend_
	give pc nothing
	legal repute +6
	end quest

_timer_ task:
	say 1003
	end quest