Quest: JHKN126
DisplayName: Похищенное дитя
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Темное Братство снова нанесло удар,
<ce> заманив ребенка в ловушку в отдаленной
<ce> пещере в %crn. Нам нужен герой, чтобы
<ce> спасти юную жертву. Пойдете ли вы?

RefuseQuest:  [1001]
<ce> Уверяю вас, у нас есть много замечательных рыцарей,
<ce> которые ухватятся за эту возможность.

AcceptQuest:  [1002]
<ce> Срок выкупа - через =2dung_ дней.
<ce> Пожалуйста, найдите _child_ к этому
<ce> времени. Ищите %g4 в ___dung_.
<ce> Храбрость и честь, %pct.

QuestFail:  [1003]
<ce> Срок выкупа _child_ истек.

QuestComplete:  [1004]
<ce> Приветствую тебя, _child_! Хочу тебя
<ce> представить твоему спасителю! Имя %pg3 -
<ce> %pcf, и %pg1 - настоящий герой.
<ce> Твоя семья будет здесь в течение часа,
<ce> а эти рыцари обеспечат твою безопасность.
<ce>
<ce> %pct, вы почтили имя нашего ордена
<ce> в этот день. Отличная работа, мой друг.

Message:  1020
<ce> "З-здравствуйте? Кто вы?"
<ce>
<ce> Вы объясняете свою миссию и убеждаете
<ce> _child_ уйти с вами.
<ce> Магическое поле, окружающее %g4, исчезает,
<ce> как только %g1 покидает свой круг. Преследователи
<ce> из Темного Братства теперь узнают,
<ce> что выкуп не получен.
   <--->
<ce> "Мне страшно. Чего вы хотите?"
<ce>
<ce> Вы объясняете свою миссию и убеждаете
<ce> _child_ уйти с вами.
<ce> Магическое поле, окружающее %g4, исчезает,
<ce> как только %g1 покидает свой круг. Преследователи
<ce> из Темного Братства теперь узнают,
<ce> что выкуп не получен.

Message:  1030
%qdt:
 _qgiver_ отправил меня
 в ___dung_, чтобы сорвать попытку выкупа
 Темным Братством.
 Я должен спасти _child_
 в течение =2dung_ дней и вернуть
 %g4 в зал ордена
 в ___qgiver_.

QBN:

Person _qgiver_ group Questor male
Person _child_ faction Children
Person _dummydb_ faction The_Dark_Brotherhood

Place _dung_ remote dungeon6

Clock _2dung_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 30.00:00

Foe _enemy1_ is Bard
Foe _enemy2_ is Rogue

--	Quest start-up:
    start timer _2dung_
    log 1030 step 0
    place npc _child_ at _dung_
    reveal _dung_

_found_ task:
    clicked npc _child_
    say 1020
    hide npc _child_
    create foe _enemy1_ every 35 minutes 3 times with 15% success
    create foe _enemy2_ every 40 minutes 4 times with 35% success
    stop timer _2dung_
    start timer _failsafe_
    add _child_ face

_infight_ task:
    daily from 0:00 to 23:59
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true
    clear _infight_

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _found_
    clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
    when _qgclicked_ and _found_
    give pc nothing
    change repute with _dummydb_ by -10
    legal repute +4
    end quest

_2dung_ task:
    say 1003
    end quest

_failsafe_ task:
    change repute with _qgiver_ by +3
    end quest