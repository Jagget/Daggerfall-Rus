Quest: JHKN108
DisplayName: Стычка с орками
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Недавний рейд на лагерь орков
<ce> в ___dung_ оказался своевременным.
<ce> Нам сообщили, что эти грубияны
<ce> планируют покушение на убийство
<ce> _noble_, знатного человека в регионе
<ce> %crn. Мы должны предотвратить их работу
<ce> по дестабилизации, и у нас слишком мало
<ce> времени, чтобы собрать рыцарей,
<ce> достойных этого дела. Пойдете ли вы и
<ce> выполнять работу армии, %pct?

RefuseQuest:  [1001]
<ce> Если в ближайшие несколько недель вы услышите,
<ce> что _noble_ на встрече с предками, то вы сможете
<ce> сказать своим друзьям, что вы знали об этом заранее.

AcceptQuest:  [1002]
<ce> Тогда двигайтесь быстрее. _noble_ будет находиться
<ce> во дворце в ___palace_. Мы ожидаем, что
<ce> нападение произойдет в ночные часы через
<ce> =2palace_ дней. Расскажите мне эту историю, когда вернетесь.

QuestFail:  [1003]
<ce> Громкие крики доносятся изнутри дворца,
<ce> за которыми следует радостный рев. Вам не нужно
<ce> смотреть, чтобы понять, что произошло.

QuestComplete:  [1004]
<ce> Вооруженные охранники уводят с собой
<ce> _noble_. С вами разговаривает паж.
<ce>
<ce> "%pcn, если все рыцари в %crn будут такими, как
<ce> вы, то нам больше нечего бояться.
<ce> Возможно, в следующий раз они пошлют вас захватить
<ce> самого Гортвога. Ваше имя будет иметь широкую известность
<ce> по всей земле, если вы останетесь верны %t".

Message:  1010
<ce> Двое дворцовых слуг подглядывают за вами из другого конца
<ce> зала и скрываются из виду. Несомненно, у них есть
<ce> какой-нибудь хорошо спланированный маршрут бегства
<ce> на случай, если их опасения оправдаются.

Message:  1011
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Язык орков)
<ce>
<ce> Вы слышите, как орки начинают входить в здание.
<ce> Углубив свой голос, вы рычите на них,
<ce> приказывая прекратить атаку и бежать. Приняв вас за
<ce> одного из своих лидеров, орки отказываются от своего плана
<ce> и разбегаются из ___palace_.
<ce> Находящиеся рядом слуги с удивлением смотрят на ваше
<ce> владение оркским языком.

Message:  1015
<ce> Вы слышите дикий рев изнутри
<ce> дворца. Атака уже началась.

Message:  1018
<ce> Этой ночью орки планируют напасть на
<ce> дворец в ___palace_.

Message:  1020
<ce> Вы входите во дворец, и вас встречает
<ce> испуганный слуга. В воздухе висит напряжение.
<ce>
<ce> "Сообщение было ложным! Нас обманули!
<ce> Они схватили моего господина _noble_!
<ce> Придворный маг говорит, что они направляются
<ce> в ___dung2_. Пожалуйста, помогите %g2!"

Message:  1021
<ce> Пожалуйста! Спасите меня от этих ужасных существ!
<ce> %oth, как я ненавижу орков!
   <--->
<ce> О, %god! Освободите меня от этого места, пожалуйста!

Message:  1022
<ce> _noble_ внезапно наклоняется и тошнит.
<ce> Из уважения вы отворачиваетесь, пока все не закончится.
<ce>
<ce> "%oth, прости меня. В этой дыре мне становится
<ce> хуже с каждой минутой. Пожалуйста, вытащите меня
<ce> отсюда. Я не думаю, что смогу продержаться еще пару
<ce> часов, прежде чем мое состояние станет опасным для жизни".

Message:  1023
<ce> _noble_ внезапно падает на землю.
<ce> Ваши лучшие попытки реанимировать %g4 тщетны.
<ce> Мерзость внутри ___dung2_ одолела %g4.

Message:  1030
%qdt:
 Я должен поспешить в ___palace_.
 чтобы предотвратить нападение орков, которое должно
 произойти через =2palace_ дня. Я должен ждать
 во дворце в ночные часы
 чтобы предотвратить нападение на
 _noble_, который живет там.

Message:  1031
%qdt:
 Слуги дворца в
 ___palace_ сообщили мне, что
 _noble_ был уведен
 в ___dung2_ орками.
 Я должен найти %g4 и вернуть %g4
 во дворец.

Message:  1035
<ce> Отречение от _noble_, когда вы %g2
<ce> так нужны, очень плохо отразится
<ce> на вас и на всем Ордене.

QBN:
Person _noble_ faction Random_Noble

Place _palace_ remote palace

Clock _2palace_ 00:00 0 flag 17 range 0 2
Clock _death_ 00:23
Clock _victory_ 00:01
Clock _failsafe_ 60.00:00
Clock _sickdelay_ 0:02
Clock _sickdeath_ 2:00 4:00

Place _dung_ remote dungeon
Place _dung2_ remote dungeon

Foe _orc1_ is 2 Orc_sergeant
Foe _orc2_ is Orc
Foe _orc3_ is Orc_sergeant
Foe _orc4_ is Orc_warlord
Foe _orc5_ is Orc_shaman

--	Quest start-up:
	start timer _2palace_
	start timer _failsafe_
	log 1030 step 0
	pc at _palace_ set _inside_
	pc at _dung2_ set _orcs_
	pick one of _real_ _false_

_skill_ task:
	when skill Orcish is at least 70

variable _real_
_false_ task:
	pick one of _sick_ _ok_

_night_ task:
	daily from 2:00 to 5:00

variable _inside_
_notice_ task:
	when _inside_ and _real_
	say 1010

_2palace_ task:
	say 1018

_spawn_ task:
	when _timeup_ and _inside_ and _real_ and not _skill_
	create foe _orc1_ every 3 minutes 2 times with 100% success
	start timer _death_
	say 1015

_easywin_ task:
	when _timeup_ and _inside_ and _real_ and _skill_
	say 1011
	start task _victory_

_infight_ task:
	daily from 0:00 to 23:59
	change foe _orc1_ infighting true
	change foe _orc2_ infighting true
	change foe _orc3_ infighting true
	change foe _orc4_ infighting true
	change foe _orc5_ infighting true
	clear _infight_

_final_ task:
	when _2palace_ and _night_
	start task _timeup_

variable _timeup_
_failout_ task:
	when _timeup_ and not _inside_ and not _night_ and _real_
	say 1035
	end quest

_success_ task:
	killed 4 _orc1_
	stop timer _death_
	start timer _victory_

_victory_ task:
	give pc nothing
	legal repute +8
	end quest

variable _death_
_death2_ task:
	when _death_ and _inside_
	say 1003
	end quest

_death3_ task:
	when _death_ and not _inside_
	say 1035
	end quest

_capture_ task:
	when _inside_ and _false_
	stop timer _2palace_
	say 1020
	reveal _dung2_
	place npc _noble_ at _dung2_
	log 1031 step 1

_orcs_ task:
	create foe _orc2_ every 29 minutes indefinitely with 11% success
	create foe _orc3_ every 22 minutes indefinitely with 4% success
	create foe _orc4_ every 45 minutes indefinitely with 3% success
	create foe _orc5_ every 29 minutes indefinitely with 3% success

_rescue_ task:
	clicked npc _noble_
	say 1021
	add _noble_ face
	hide npc _noble_

variable _ok_
variable _sick_
_dis_ task:
	when _rescue_ and _sick_
	start timer _sickdelay_

variable _sickdelay_
_getsick_ task:
	when _orcs_ and _sickdelay_
	say 1022
	start timer _sickdeath_

variable _sickdeath_
_nobledie_ task:
	when _orcs_ and _sickdeath_
	say 1023
	end quest

_win_ task:
	when _inside_ and _rescue_
	start timer _victory_

_failsafe_ task:
	end quest