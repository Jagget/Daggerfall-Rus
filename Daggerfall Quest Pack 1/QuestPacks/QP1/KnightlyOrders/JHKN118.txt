Quest: JHKN118
DisplayName: Бойцы, Хранители

QRC:

QuestorOffer:  [1000]
<ce> Один из членов Гильдии Бойцов стал печально известен в другом
<ce> городе. %rt вмешались и вынесли смертельный приговор.
<ce> Нам нужно, чтобы вы отправились и убили этого негодяя.
<ce> Гильдия Бойцов не похвалит вас за это,
<ce> но вам нужно преодолеть свои страхи.
<ce> Будьте храбры и служите, добрый рыцарь.

RefuseQuest:  [1001]
<ce> Храбрость, кажется, всегда в дефиците.

AcceptQuest:  [1002]
<ce> Отправляйтесь в ___tav_ и найдите место
<ce> под названием _tav_. Известно, что там
<ce> пребывает бесчестный боец. Ищите кого-нибудь хорошо
<ce> вооруженного. Вам предоставляется привилегия первого удара,
<ce> ибо закон на вашей стороне.
<ce> Сообщите мне о смерти преступника в течение =timer_ дней,
<ce> и я подготовлю для вас особую награду.

QuestFail:  [1003]
<ce> _questgiver_ из __questgiver_
<ce> должен был узнать о смерти бойца к этому времени.

QuestComplete:  [1004]
<ce> Очень хорошо. Ваша преданность %rt и нашему Ордену
<ce> не будет подвергнута сомнению.
<ce> Продолжайте хранить верность, мой друг.
<ce>
<ce> Поскольку вы стремитесь защитить %crn,
<ce> у меня есть дар, который в свою очередь защитит вас.
<ce> Пользуйтесь им с честью.

Message:  1011
<ce> "Х-х-а! Я умру, но не сегодня, лакей!"

Message:  1012
<ce> Сегодня верховенство закона в %crn
<ce> соблюдено, ибо этот дурак убит.

Message:  1030
%qdt:
 _questgiver_ из __questgiver_,
 в ___questgiver_, послал меня
 в место под названием ___tav_
 на поиски нарушителя закона,
 члена Гильдии Бойцов,
 находящегося в _tav_.
 Мне предстоит произвести казнь
 и вернуться в течение =timer_ дней.

QBN:

Item _reward_ item class 4 subclass 15

Person _questgiver_ group Questor male
Person _dummyfg_ faction The_Fighters_Guild remote

Place _tav_ remote tavern

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _en1_ is Barbarian

--	Quest start-up:
	start timer _timer_
	place foe _en1_ at _tav_
	restrain foe _en1_
	change foe _en1_ infighting true
	log 1030 step 0

_timer_ task:
	say 1003
	end quest

_inj_ task:
	injured _en1_
	clicked foe _en1_
	say 1011
	change foe _en1_ infighting true

_click_ task:
	clicked foe _en1_
	unrestrain foe _en1_
	say 1011

_slain_ task:
	killed _en1_
	say 1012
	change repute with _dummyfg_ by -15
	start quest JHKNC01

_questorClicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _questorClicked_ and not _slain_
	clear _questorClicked_ _clearclick_

_rewardPlayer_ task:
	when _questorClicked_ and _slain_
	give pc _reward_
	legal repute +3
	end quest