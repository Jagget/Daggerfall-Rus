Quest: JHKN115
DisplayName: Гнев природы
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Малонаселенные районы %crn жалуются
<ce> на спригганов, нападающих на их усадьбы.
<ce> По имеющимся данным, причина может быть
<ce> в ___dung_. Можно ли
<ce> рассчитывать на вас в решении этой проблемы?

RefuseQuest:  [1001]
<ce> То есть вы ищете простую
<ce> проблему для решения, я полагаю?

AcceptQuest:  [1002]
<ce> Так часто происходит, когда какой-нибудь глупец
<ce> позволяет власти завладеть его головой
<ce> и начинает пытаться демонстрировать
<ce> эту власть всему миру. Мы бы хотели, чтобы
<ce> такие люди могли присоединиться к благородному делу
<ce> и направить свое рвение на благие цели,
<ce> но некоторые из них слишком упрямы.
<ce> Сделайте все возможное, чтобы защититься
<ce> от спригганов. Возможно, если вы
<ce> будете говорить на их языке, то добьетесь
<ce> больших успехов, чем другим способом.
<ce> Решите проблему в течение =2dung_ дней,
<ce> а затем вернитесь с отчетом,
<ce> когда освободитесь. Да пребудет
<ce> с вами %god в ___dung_.

QuestFail:  [1003]
<ce> К этому времени вы должны были выяснить
<ce> причину нападения спригганов.

QuestComplete:  [1004]
<ce> Находчивый, опытный и старательный.
<ce> Образец для рыцарей этого ордена.

Message:  1020
<ce> Спригган ковыляет по направлению к вам,
<ce> лишь наполовину осознавая свои собственные
<ce> действия. Вы можете попытаться поговорить
<ce> с ним, чтобы выяснить что-нибудь,
<ce> что могло бы вам помочь.
<ce> Пытаетесь ли вы поговорить (да)
<ce> или попытаетесь избежать этого (нет)?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Язык спригганов, низкая сложность)

Message:  1021
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вы привлекаете внимание этого существа приветствием
<ce> и говорите, что не намерены причинять ему вреда.
<ce> Оно отвечает на ломаном языке, что его вид испытывает
<ce> жгучую агонию, неподвластную контролю. Оно обещает
<ce> сообщить своим сородичам, что вы не представляете опасности.
<ce> Вы надеетесь, что оно сможет выполнить это обещание.
<ce> Вы отпускаете его, и оно уходит в другое место.

Message:  1022
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Всего, что вы знаете о языке спригганов,
<ce> недостаточно. Ваша речь лишь злит существо,
<ce> и оно в ярости убегает.
<ce>
<ce> Если спригганы настолько общительны, как об этом 
<ce> рассказывают, это не предвещает ничего хорошего для вашего
<ce> визита в ___dung_.

Message:  1023
<ce> Этот колдун, благоухающий зелеными травами,
<ce> похоже, и стал причиной безумия спригганов.

Message:  1024
<ce> Бедное существо ковыляет в агонии.
<ce> Мгновение оно смотрит на вас, а затем поворачивается
<ce> и уходит. Возможно, вы могли бы помочь ему,
<ce> но спригганы могут быть капризны.

Message:  1025
<ce> Вы переступаете порог и слышите сердитое ворчание
<ce> спригганов. Вы надеетесь, что они не будут
<ce> вас сильно беспокоить, пока вы будете
<ce> искать источник проблемы.

Message:  1030
%qdt:
 _qgiver_ поручил мне
 устранить некоторое нарушение 
 в ___dung_, из-за которого
 спригганы появляются по всему %crn.
 Враг должен быть уничтожен в течение =2dung_ дней.
 После этого я смогу отчитаться перед ___qgiver_.

QBN:
Person _qgiver_ group Questor male

Place _dung_ remote dungeon6

Clock _2dung_ 00:00 0 flag 1 range 0 2
Clock _spawn_ 1:00 6:00
Clock _failsafe_ 120.00:00

Foe _mage_ is Sorceror
Foe _sprig_ is Spriggan

--	Quest start-up:
  log 1030 step 0
  start timer _spawn_
  start timer _2dung_
  start timer _failsafe_
  reveal _dung_
  place foe _mage_ at _dung_
  pc at _dung_ do _inside_

variable _inside_

_spawn_ task:
  prompt 1020 yes _attempt_ no _flee_

variable _attempt_

_bigwin_ task:
  when _attempt_ and _skilled_
  say 1021

_bigfail_ task:
  when _attempt_ and not _skilled_
  say 1022

_flee_ task:
  say 1024

_smallspawn_ task:
  when _flee_ and _inside_ and not _slain_
  say 1025
  create foe _sprig_ every 15 minutes indefinitely with 5% success

_bigspawn_ task:
  when _bigfail_ and _inside_ and not _slain_
  say 1025
  create foe _sprig_ every 15 minutes indefinitely with 10% success
  create foe _sprig_ every 0 minutes 1 times with 100% success

_skilled_ task:
  when skill Spriggan is at least 20

_infight_ task:
  daily from 0:00 to 23:59
  change foe _sprig_ infighting true
  clear _infight_

_inj_ task:
  injured _mage_
  clicked foe _mage_
  change foe _mage_ infighting true
  change foe _mage_ team 3

_slain_ task:
  killed _mage_
  say 1023
  stop timer _2dung_

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _slain_
  clear _qgclicked_ _clearclick_

_victory_ task:
  when _qgclicked_ and _slain_
  give pc nothing
  legal repute +3
  end quest

_2dung_ task:
  say 1003
  end quest

_failsafe_ task:
  end quest