Quest: JHKN104
DisplayName: Город в осаде
-- Message panels
-- _gem_ было использовано только для названия, переведено как "Янтарь"
QRC:

----todo: add siege sound effects

QuestorOffer:  [1000]
<ce> %pcn, я не знаю, дошли ли до вас эти слухи.
<ce> Позвольте мне объяснить ситуацию. Некая банда
<ce> воинов-отступников, Рыцари Янтаря,
<ce> пытаются добиться легитимности для себя, захватив
<ce> целый город силой, то есть ___questgiver_.
<ce> Силы %crn растоптали несколько
<ce> их лагерей, но они смогли ускользнуть, и теперь пришло
<ce> известие, что скоро на нас начнется полномасштабное
<ce> наступление. Постоите ли вы со мной на страже, чтобы увидеть,
<ce> правдивы ли эти слухи, добрый рыцарь?

RefuseQuest:  [1001]
<ce> Тогда уходите скорее, %pcf. Я не хочу,
<ce> чтобы трус погиб в предстоящем кровопролитии.

AcceptQuest:  [1002]
<ce> Очень хорошо. Ждите здесь,
<ce> пока нам не принесут известие.

QuestFail:  [1003]
<ce> Задание провалено.

QuestComplete:  [1004]
<ce> Паж со знаменем %crn
<ce> приближается, склоняется перед вами
<ce> и начинает зачитывать свиток.
<ce>
<ce> "%pcn, рыцарь - защитник региона %crn,
<ce> мы приветствуем службу, которую вы оказали
<ce> в этот день, быстро верша правосудие над отступниками
<ce> и преступниками, которые подвергли этот город опасности.
<ce> Да пребудет с вами %god в ваших усилиях
<ce> по сохранению справедливости и цивилизации".
<ce>
<ce> Остальные начинают убирать тела.
<ce> Штурм окончен, и жизнь продолжается.

RumorsDuringQuest:  [1005]
Какой-то Темный Эльф брал кредиты в банках и не возвращал их. За это стоит повесить.

RumorsPostFailure:  [1006]
__questgiver_ сумели спасти ___palace_ от вражеских рыцарей.

RumorsPostSuccess:  [1007]
___palace_ снова в безопасности, благодаря %pcn.
   <--->
%pcn из __questgiver_
спасли ___palace_.

Message:  1010
<ce> Паж с гербом региона %crn
<ce> стремительно приближается к _questgiver_
<ce> и вручает %g2 запечатанное письмо, которое
<ce> %g1 поспешно вскрывает, после чего подает вам сигнал.
<ce>
<ce> "Началось! Они вот-вот прибудут!
<ce> Отправляйтесь туда и защищайтесь, как можете!
<ce> Я позову помощь!"

Message:  1011
<ce> Когда первый из разбойников падает,
<ce> вы слышите крик.
<ce>
<ce> "Убейте их всех! В этот день
<ce> мы вернем себе ___palace_!
<ce> За Рыцарей Янтаря!"

Message:  1012
<ce> Приближается заклинатель и выкрикивает боевой клич.
<ce>
<ce> "Вы не один, %pcf! Я здесь, чтобы помочь!"
<ce>
<ce> Вы слышите звуки применения магии по всему городу. Должно
<ce> быть, заклинатели пришли защищать ___palace_.
   <--->
<ce> Приближается заклинатель и выкрикивает боевой клич.
<ce>
<ce> "Мы сделаем это вместе, %pcf! Я помогу вам!"
<ce>
<ce> Вы слышите звуки применения магии по всему городу. Должно
<ce> быть, заклинатели пришли защищать ___palace_.

Message:  1020
<ce> Стражник подает вам сигнал и кричит:
<ce> "Они во Дворце! Вперед, быстро! Остановите их!"
<ce>
<ce> Ваши спутники призывают вас поторопиться.
<ce> Похоже, они наведут здесь порядок.

Message:  1024
<ce> Прятки не помогут ___questgiver_, %pct.

Message:  1030
%qdt:
 Похоже, началась всеобщая война
 между силами
 %crn и какой-то отступнической
 группой рыцарей-притворщиков,
 называющих себя Рыцарями Янтаря,
 в городе ___palace_.
 Я должен защищать город настолько,
 насколько это в моих силах.

Message:  1031
%qdt:
 Стража велела мне поспешить
 к _palace_ в ___palace_,
 так как захватчики, похоже,
 уже там.

Message:  1099
<ce> Защитите ___palace_, %pcf!
<ce> Они уже почти настигли нас!

QBN:
Item _gem_ gem

Person _questgiver_ group Questor male

Place _palace_ local palace

Clock _timer_ 02:00
Clock _delay_ 00:01
Clock _delay2_ 00:02
Clock _delay3_ 00:01
Clock _outdoors_ 00:18

Foe _warr_ is Warrior
Foe _barb_ is Knight
Foe _kni_ is 2 Barbarian
Foe _spells_ is Spellsword

--changed warrior and barbarian around since outdoor spawns need to be on the same team

--	Quest start-up:
	start timer _timer_
	start timer _delay_
	log 1030 step 0
	pc at _palace_ set _inside_
	pc at any fighters set _fightered_
	pc at any tavern set _taverned_
	pc at any house set _homed_
	pc at any shop set _shopped_
	pc at any magery set _maged_
	pc at any temple set _templed_

variable _fightered_
variable _taverned_
variable _homed_
variable _shopped_
variable _maged_
variable _templed_

_day_ task:
	daily from 00:00 to 23:59
	change foe _warr_ infighting true
	change foe _barb_ infighting true
	change foe _kni_ infighting true
	change foe _spells_ infighting true
	change foe _spells_ team 1
	clear _day_

_delay_ task:
	say 1010
	start timer _delay2_

variable _delay2_

_spawnout_ task:
	when _delay2_ and not _slain_ and not _taverned_ and not _fightered_ and not _homed_ and not _shopped_ and not _maged_ and not _templed_
	start timer _outdoors_
	create foe _warr_ every 1 minutes indefinitely with 50% success
	create foe _barb_ every 1 minutes indefinitely with 50% success
	create foe _spells_ every 1 minutes indefinitely with 65% success msg 1012

_msg_ task:
	killed 1 _warr_
	killed 1 _barb_
	say 1011

variable _outdoors_
_slain_ task:
	killed 8 _warr_
	killed 7 _barb_

_finish1_ task:
	when _outdoors_ and _slain_
	say 1020
	log 1031 step 1

variable _inside_
_spawnin_ task:
	when _inside_ and _finish1_
	create foe _kni_ every 0 minutes 1 times with 100% success
	create foe _kni_ every 3 minutes 1 times with 100% success
	create foe _kni_ every 6 minutes 1 times with 100% success

_victory_ task:
	killed 6 _kni_
	start timer _delay3_

_delay3_ task:
	give pc nothing
	legal repute +6
	end quest

_qgclicked_ task:
	clicked npc _questgiver_

_timer_ task:
	say 1024
	end quest

_clearclick_ task:
	when _qgclicked_ and not _slain_
	say 1099
	clear _qgclicked_ _clearclick_
