Quest: JHKN111
DisplayName: История рыцаря
-- Message panels
-- _kni_ = муж.
-- _qgiver_ = муж.
QRC:

QuestorOffer:  [1000]
<ce> Мы не можем найти _kni_, одного из наших
<ce> более опытных участников, вот уже несколько недель.
<ce> Конечно же ему не нужна нянька, но мы знаем, что он
<ce> обычно заглядывает несколько раз в месяц, чтобы посмотреть, есть ли
<ce> проблемы, которые он может решить. Сейчас - тишина. Мы ищем
<ce> какого-нибудь способного служителя, чтобы он нашел его и вернул нам
<ce> спокойствие. Сделаете ли вы это, %pcf?

RefuseQuest:  [1001]
<ce> Вас больше интересует слава и богатство?

AcceptQuest:  [1002]
<ce> Тогда я передаю это дело в ваши умелые руки.
<ce> Ищите его под каждым камнем и в каждой пещере, которые встречаете.
<ce> Найдите его максимум через два месяца. Нам с ним
<ce> есть что обсудить, подискутировать и вспомнить.
<ce> %god да пребудет с вами.

QuestFail:  [1003]
<ce> Прошло два месяца, а _qgiver_ не знает
<ce> ничего о _kni_.

QuestComplete:  [1004]
<ce> _qgiver_ похлопывает вас по плечу.
<ce> Вы считаете свою работу выполненной.

RumorsDuringQuest:  [1005]
_kni_? Ужасно милый парень. Пригнал мой скот, когда тот сломал ограду.
   <--->
_kni_ пропал? %god сжалься над ним. Он замечательный человек.
   <--->
Вы ищете _kni_? Попробуйте обратиться к _family_ в ___famtown_.
   <--->
_kni_ останавливался у какой-то семьи в ___famtown_, я помню.
   <--->
_kni_ - это образец того, какими должны быть все рыцари.
   <--->
_kni_, как я слышал, ходил на охоту в подземелья. Он скоро вернется.
   <--->
Несколько месяцев назад _kni_ убил какого-то военачальника орков. Мы многим ему обязаны.
   <--->
_kni_ изгнал какого-то призрака в ___qgiver_ в прошлом году.
   <--->
_kni_ проводит свои каникулы с _family_ в ___famtown_.

Message:  1012
<ce> Здравствуйте? Да, _kni_ - мой двоюродный брат. Я
<ce> сильно беспокоился последние несколько недель. так долго
<ce> отсутствовать не в его стиле, особенно с учетом того, что мне здесь
<ce> нужна помощь. Если с ним что-нибудь случится, я не переживу.
<ce> Я думаю, что он нашел что-то в ___dung_,
<ce> что заинтересовало его настолько, что заставило остаться. Он очищал
<ce> его несколько раз за последние несколько лет, поэтому я знаю,
<ce> что ему ничего не угрожает. Если _qgiver_
<ce> захочет его искать, я скажу, что сначала нужно пойти туда.

Message:  1013
<ce> Кто вы? Вас послал _qgiver_? %god,
<ce> благодарю тебя. Да, мне нужно уйти, но мои травмы
<ce> очень серьезны. ___dung_ так сильно
<ce> изменился. Пожалуйста, выведите меня.
   <--->
<ce> Да, я _kni_. Славься %god, что
<ce> _qgiver_ послал кого-то на мои поиски.
<ce> Я боялся, что никогда не освобожусь.
<ce> Нам пора. После вас.

Message:  1014
<ce> _kni_, по-видимому, вообще не находится в
<ce> ___dung_. Единственное, что вы находите, это вот этот
<ce> мешок. Вы берете его, чтобы показать _qgiver_.

Message:  1030
%qdt:
 _qgiver_ послал меня
 на поиски _kni_,
 ветерана, который потерялся
 где-то в ____qgiver_.
 У меня есть два месяца, чтобы вернуться
 в ___qgiver_.

Message:  1031
%qdt:
 _family_ сказали мне, что
 _kni_ недавно отправился
 в ___dung_.

Message:  1050
<ce> _kni_, никогда не пугайте меня так снова.
<ce> Вы уже не так молоды, как раньше!
<ce> Моя личная рекомендация - взять напарника,
<ce> даже если речь идет о ___dung_. Но вы
<ce> пришли сюда не для того, чтобы слушать лекции. Добро пожаловать домой.
<ce> И %pcf, у меня нет слов, чтобы поблагодарить вас
<ce> в достаточной степени. И вам добро пожаловать домой.

Message:  1051
<ce> %pcf, какие новости вы принесли? %oth.
<ce> %oth, это не то, чего я ожидал.
<ce> Я сообщу _family_. Это действительно
<ce> трагедия. Я благодарю вас за то, что вы приложили усилия. Мне
<ce> понадобится немного времени, чтобы пережить это. %god благослови нас всех.

QBN:
Person _qgiver_ group Questor male
Person _family_ group Resident1
Person _kni_ faction Generic_Knightly_Order male anyInfo 1005

Place _famtown_ remote random
Place _dung_ remote dungeon0

Clock _timer_ 60.00:00
Clock _death_ 25.00:00 40.00:00

Item _bag_ large_sack

--	Quest start-up:
	start timer _timer_
	start timer _death_
	log 1030 step 0
	place npc _family_ at _famtown_
	place npc _kni_ at _dung_
	add dialog for person _kni_
	pc at _dung_ do _inside_

variable _inside_
_talkfam_ task:
	clicked npc _family_
	say 1012
	log 1031 step 1
	reveal _dung_

_talkkni_ task:
	clicked npc _kni_
	say 1013
	stop timer _death_
	hide npc _kni_
	add _kni_ face
	start quest JHKNC02

_victory1_ task:
	when _qgclicked_ and _talkkni_
	say 1050
	give pc nothing
	legal repute +3
	end quest

_death_ task:
	hide npc _kni_
	place item _bag_ at _dung_

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _talkkni_ and not _grabbag_
	clear _qgclicked_ _clearclick_

_grabbag_ task:
	clicked item _bag_
	say 1014

_victory2_ task:
	when _grabbag_ and _qgclicked_
	say 1051
	change repute with _qgiver_ by -2
	give pc nothing
	end quest

_timer_ task:
	say 1003
	end quest