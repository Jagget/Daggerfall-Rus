Quest: JHKN127
DisplayName: Караван
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Купцы в окрестностях %crn столкнулись с нападениями
<ce> банды воров во время своих путешествий. Они обратились
<ce> к нашему ордену за помощью в искоренении этой угрозы.
<ce> Готовы ли вы помочь им в этом деле?

RefuseQuest:  [1001]
<ce> Этим людям нужен защитник, %pct.

AcceptQuest:  [1002]
<ce> "Отлично. Теперь трудно сказать,
<ce> где банда ударит в следующий раз. Ваша задача -
<ce> сопровождать _guide_ и %g4 караван
<ce> до тех пор, пока воры не нападут. Путешествуйте
<ce> по различным маленьким поселениям, поскольку в отчетах
<ce> говорится, что они стараются избегать больших городов.
<ce> Сровняйте их с землей и доложите мне о вашем
<ce> успехе. Я даю вам один месяц на выполнение этого".
<ce>
<ce> _qgiver_ обращается к _guide_
<ce> и представляет вас, объясняя задачу.
<ce> Купец горячо вас благодарит.
<ce>
<ce> "Это замечательно! Я абсолютно уверен,
<ce> что эта проблема будет решена в кратчайшие сроки.
<ce> Полагаю, нам просто следует продолжать путешествовать,
<ce> пока они нас не найдут!"
   <--->
<ce> "Отлично. Теперь трудно сказать, где
<ce> банда ударит в следующий раз. Ваша задача -
<ce> сопровождать _guide_ и %g4 караван
<ce> до тех пор, пока воры не нападут. Путешествуйте
<ce> по различным маленьким поселениям, поскольку в отчетах
<ce> говорится, что они стараются избегать больших городов.
<ce> Сровняйте их с землей и доложите мне о вашем
<ce> успехе. Я даю вам один месяц на выполнение этого".
<ce>
<ce> _qgiver_ обращается к _guide_
<ce> и представляет вас, объясняя задачу.
<ce> Купец горячо вас благодарит.
<ce>
<ce> "Рыцарь будет защищать меня! %t наконец
<ce> нас услышали! Благодарю вас! Ведите меня. Куда мы пойдем?"

QuestFail:  [1003]
<ce> Во время этой схватки _guide_ пал замертво.
<ce> Это ужасная цена за обнаружение
<ce> этой группы воров.

QuestComplete:  [1004]
<ce> _qgiver_ горячо жмет вам руку за вашу службу.

Message:  1020
<ce> Ваш небольшой караван окружен
<ce> группой воров. Предположительно, это те,
<ce> которых _qgiver_ послал вас найти.

Message:  1021
<ce> _guide_, оставшийся невредимым во время битвы,
<ce> радуется, узнав, что группа воров была убита.
<ce>
<ce> "Вы действительно рыцарь высочайшего порядка!
<ce> Эту историю я буду рассказывать по всему %crn!"
<ce>
<ce> Выразив вам искреннюю благодарность, %g1
<ce> собирает свои вещи для следующего этапа путешествия
<ce> и уходит. Теперь нужно доложить
<ce> _qgiver_ в ___qgiver_.
   <--->
<ce> _guide_, оставшийся невредимым во время битвы,
<ce> радуется, узнав, что группа воров была убита.
<ce>
<ce> "Ого, что это было? Это было невероятно!
<ce> Спасибо вам, спасибо, спасибо!"
<ce>
<ce> Выразив вам искреннюю благодарность, %g1
<ce> собирает свои вещи для следующего этапа путешествия
<ce> и уходит. Теперь нужно доложить
<ce> _qgiver_ в ___qgiver_.

Message:  1022
<ce> Добро пожаловать, %pct. Похоже, _guide_
<ce> не выжил после битвы. Мы в восторге
<ce> от уничтожения этой банды воров, но жаль, что это
<ce> произошло такой ценой. Тем не менее благодарю
<ce> вас за вашу службу. %crn
<ce> в значительной мере выиграет от вашей работы.

Message:  1023
<ce> Добро пожаловать, %pct. Вы уничтожили банду
<ce> воров и сохранили жизнь купца. Воистину
<ce> вы с честью прошли это испытание. Я буду рассказывать
<ce> новобранцам нашего ордена о ваших подвигах
<ce> и о том примере, который они подают. Отличная работа.

Message:  1024
<ce> Хотя _guide_ и пал в бою,
<ce> эта банда все равно разгромлена. Пришло время
<ce> доложить _qgiver_.

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_
 послал меня сопровождать купца
 по имени _guide_ с целью
 искоренить жестокую банду
 воров в сельской местности.
 У меня есть только один месяц,
 чтобы найти их.
 Мне сказали путешествовать
 по маленьким поселениям
 вместо городов.

QBN:

Person _qgiver_ group Questor male
Person _guide_ faction The_Merchants

Clock _goal_ 30.00:00
Clock _battle_ 00:04 00:08
Clock _appear_ 2.00:00 4.00:00

Foe _s1_ is Assassin
Foe _s2_ is Burglar

---- Quest startup
    log 1030 step 0
    add _guide_ face
    start timer _goal_
    pick one of _e1_ _e2_ _e3_ _e4_
    start timer _appear_

variable _e1_
variable _e2_
variable _e3_
variable _e4_
variable _appear_

_f1_ task:
    when pc enters tavernhome

_f2_ task:
    when pc enters village

_f3_ task:
    when pc enters hamlet

_f4_ task:
    when pc enters templehome

_f5_ task:
    when pc exits anywhere
    clear _f5a_

_f5a_ task:
    when pc enters anywhere
    clear _f5_

_t1_ task:
    when _e1_ and _f1_
    start task _spawn_

_t2_ task:
    when _e2_ and _f2_
    start task _spawn_

_t3_ task:
    when _e3_ and _f3_
    start task _spawn_

_t4_ task:
    when _e4_ and _f4_
    start task _spawn_

_t5 task:
    when _appear_ and _f5_
    start task _spawn_

_spawn_ task:
    say 1020
    start timer _battle_
    create foe _s1_ every 0 minutes 1 times with 100% success
    create foe _s2_ every 0 minutes 3 times with 100% success

_infight_ task:
    daily from 0:00 to 23:59
    change foe _s1_ infighting true
    change foe _s2_ infighting true
    clear _infight_

_battle_ task:
    say 1003
    drop _guide_ face

_w1_ task:
    killed _s1_

_w2_ task:
    killed 3 _s2_

_slain_ task:
    when _w1_ and _w2_
    stop timer _battle_

_survive_ task:
    when _slain_ and not _battle_
    say 1021
    drop _guide_ face

_notsurvive_ task:
    when _slain_ and _battle_
    say 1024

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcgetsgold1_ task:
    when _qgclicked_ and _slain_ and _battle_
    say 1022
    change repute with _qgiver_ by -2
    give pc nothing
    legal repute +3
    end quest

_pcgetsgold2_ task:
    when _qgclicked_ and _slain_ and not _battle_
    say 1023
    give pc nothing
    legal repute +3
    end quest

_goal_ task:
    change repute with _qgiver_ by +2
    end quest