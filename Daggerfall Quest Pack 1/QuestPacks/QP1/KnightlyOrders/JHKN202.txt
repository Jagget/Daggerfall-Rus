Quest: JHKN202
DisplayName: Призыватель
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Нам сообщили о каком-то дураке, который спрятался,
<ce> чтобы попрактиковаться в призыве даэдра. Независимо
<ce> от способностей мага, мы должны относиться к этому
<ce> как к реальной угрозе. Нам нужен рыцарь, чтобы
<ce> сразиться с призывателем и предотвратить кризис
<ce> до того, как будут вызваны какие-либо демоны.

RefuseQuest:  [1001]
<ce> Дайте нам знать, если увидите там каких-нибудь героев.

AcceptQuest:  [1002]
<ce> Я отмечу ___dung_ на Вашей карте. Где-то внутри
<ce> работает маг-изгой. Находясь внутри, вы, скорее
<ce> всего, столкнетесь с бесами, поскольку они были
<ce> вызваны в рамках предыдущего эксперимента. Принесите
<ce> мне весть о смерти демонолога в течение =timer_ дней,
<ce> чтобы мы могли обрести покой.

QuestFail:  [1003]
<ce> _qgiver_ уже ожидал вашего
<ce> отчета о призывателе даэдра.

QuestComplete:  [1004]
<ce> Отличная работа, %pct. Для нас гораздо
<ce> лучше упреждать такие случаи - никому
<ce> не нравится сражаться с даэдра.

QuestorPostSuccess:   [1008]
Продолжайте практиковать свои навыки, и даже самые суровые даэдра падут перед вами.

Message:  1010
<ce> В воздухе витает слабая магическая энергия.
<ce> Вы надеетесь, что еще не слишком поздно
<ce> остановить этого призывателя даэдра.

Message:  1011
<ce> "Нет! Вы не остановите меня, не тогда, когда я так близко!"

Message:  1012
<ce> Вызыватель повержен, ритуал прерван.

Message:  1030
%qdt:
 __qgiver_
 из ___qgiver_ ожидает от меня
 уничтожения вызывателя даэдра
 где-то в ___dung_. Мне нужно
 сделать это и вернуться с отчетом
 за =timer_ дней.

QBN:

Person _qgiver_ group Questor male

Place _dung_ remote dungeon3

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _mage_ is Mage
Foe _enemy1_ is Imp

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	place foe _mage_ at _dung_
	pc at _dung_ set _inside_
	reveal _dung_

_inside_ task:
    say 1010

_quest_ task:
	when _inside_ and not _slain_
	create foe _enemy1_ every 13 minutes indefinitely with 15% success

_daily_ task:
	daily from 0:00 to 23:59
	change foe _enemy1_ infighting true
	clear _daily_

_inj_ task:
    injured _mage_
	clicked foe _mage_
    say 1011
    change foe _mage_ infighting true
    change foe _mage_ team 16

_slain_ task:
	killed 1 _mage_ saying 1012

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	legal repute +3
	end quest

_qgclicked_ task:
	clicked npc _qgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_timer_ task:
    end quest