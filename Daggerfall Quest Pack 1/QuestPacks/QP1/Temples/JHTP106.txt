Quest: JHTP106
DisplayName: Брачная консультация
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Среди множества обязанностей, которые мы выполняем,
<ce> таких как лечение больных и упокоение живых мертвецов,
<ce> у нас есть некоторые обязанности по отношению к более
<ce> мирским делам. Скорее всего, мне следует спросить,
<ce> насколько красноречива ваша речь, прежде чем давать вам
<ce> это задание. Сегодня к нам прибыл некий _husb_, который
<ce> просит помощи в спасении его брака. Для этого
<ce> потребуется лингвист, а не военачальник, и сделать это
<ce> нужно сегодня. Способны ли вы на это?

RefuseQuest:  [1001]
<ce> Лучше быть честным.

AcceptQuest:  [1002]
<ce> "Ну тогда я сейчас пойду и позову его. Просто
<ce> скажите им все, что поможет им оставаться вместе
<ce> как пара и сохранить брак, договорились? Неважно,
<ce> что вы об этом думаете, работа прежде всего".
<ce>
<ce> _qgiver_ зовет _husb_.
<ce> Через мгновение тот входит.
<ce>
<ce> "О, спасибо вам, что уделяете нам
<ce> время. Не знаю, чем вообще тут можно
<ce> помочь, но может у вас что-нибудь
<ce> получится. Мы живем в_res_,
<ce> в городе. Отправимся же туда
<ce> и поговорим с моей женой, _wife_".

QuestFail:  [1003]
<ce> _husb_ оглядывается по сторонам.
<ce>
<ce> "Где, черт возьми, мы находимся? Мне нужно вернуться
<ce> домой, чтобы она снова не обвинила меня в пьянстве".
<ce>
<ce> После этого он начинает свой путь в одиночку.

QuestComplete:  [1004]
<ce> Некоторое время вы продолжаете в том же духе,
<ce> пытаясь показать им, что их порочный круг
<ce> не приведет ни к чему хорошему. Постепенно они
<ce> оба начинают добросовестно слушать. Когда вы
<ce> заканчиваете, они обмениваются взглядами,
<ce> и один из них говорит.
<ce>
<ce> "%ra, мы не можем выразить, как мы вам
<ce> благодарны. Нам еще предстоит пройти долгий
<ce> путь, но теперь мы видим все немного лучше.
<ce> Передайте _qgiver_ наши пожелания. Возьмите
<ce> плату, которую мы обещали. Да благословит вас %god
<ce> за вашу доброту".

Message:  1028
<ce> _husb_ слабо улыбается своей жене.
<ce>
<ce> "Здравствуй, милая, я привел служителя храма, который
<ce> попытается помочь нам меньше ссориться".
<ce>
<ce> Ее глаза вспыхивают, а обе руки сжимаются в кулаки.
<ce>
<ce> "Не 'здравствуймилуй' тут мне! Вести себя прилично
<ce> перед совершенно незнакомым человеком - это как
<ce> раз в твоем духе! Почему бы тебе не спросить
<ce> себя, что храм делает с людьми, которые изменяют
<ce> своей супруге с кем попало в _tavern_?!"
<ce>
<ce> Это явно не будет простым делом.
   <--->
<ce> _husb_ слабо улыбается своей жене.
<ce>
<ce> "Здравствуй, милая, я привел служителя храма, который
<ce> попытается помочь нам меньше ссориться".
<ce>
<ce> Ее глаза вспыхивают, а обе руки сжимаются в кулаки.
<ce>
<ce> "Не 'здравствуймилуй' тут мне! Вести себя
<ce> прилично перед совершенно незнакомым человеком -
<ce> это как раз в твоем духе! Почему бы тебе
<ce> не спросить, что храм думает о людях, которые
<ce> выманивают у родственников жены их состояние,
<ce> а потом сбегают в дальний уголок %crn?!"
<ce>
<ce> Это явно не будет простым делом.

Message:  1029
<ce> Проходит несколько минут в препирательствах, прежде
<ce> чем вы успеваете произнести хоть слово. Как только вы
<ce> открыли рот, _wife_ повернулась к вам и спросила,
<ce>
<ce> "Что ж, почему бы нам тогда не устроить дискуссию?
<ce> Если мы тратим ваше драгоценное время на свои
<ce> проблемы, то вы должны хотя бы поучаствовать.
<ce> Скажите мне, как вы думаете, мужчина, который
<ce> проводит пять ночей в неделю в таверне,
<ce> выполняет свои мужские обязанности?"

Message:  1031
<ce> _husb_ смотрит на
<ce> вас двоих с некоторым унынием.
<ce>
<ce> "Дорогая, никто не утверждает обратного.
<ce> Я знаю, что не справляюсь. Просто я чувствую себя
<ce> несчастным, возвращаясь домой каждый вечер
<ce> к еще большим ссорам. Поэтому я и ухожу".
<ce>
<ce> _wife_ поворачивается к вам.
<ce>
<ce> "Ну, служитель храма, теперь что скажете?
<ce> Нужно ли мне "успокоиться" вместо того, чтобы
<ce> показать ему, почему он не прав?"

Message:  1034
<ce> _wife_ выглядит обеспокоенной
<ce> вашим ответом. Затем она задает другой вопрос.
<ce>
<ce> "Если мужчина приходит домой пьяный и затевает ссору,
<ce> разве он не должен заплатить за это хоть чем-то?"

Message:  1035
<ce> _wife_ выглядит обеспокоенной
<ce> вашим ответом. Затем она задает другой вопрос.
<ce>
<ce> "Он не справился со своими супружескими обязанностями.
<ce> Он женился на мне из-за моих денег, а теперь тратит их
<ce> все. Разве я не имею права обижаться на него за это?

Message:  1037
<ce> Вмешивается _husb_, теперь уже мрачный.
<ce>
<ce> "Дорогая, ты знала это обо мне еще до того, как мы
<ce> поженились. Я просил тебя быть терпеливой со мной.
<ce> Почему ты не можешь дать мне больше времени?".
<ce>
<ce> Его жена смотрит на него с отвращением.
<ce>
<ce> "Прошло уже =int_ лет! =int_ лет! А ты все
<ce> такой же, как всегда! Мужчина должен
<ce> выполнять свои обещания, не так ли?"

Message:  1038
<ce> Вмешивается _husb_, теперь уже мрачный.
<ce>
<ce> "Дорогая, ты знала это обо мне еще до того, как мы
<ce> поженились. Я просил тебя быть терпеливой со мной.
<ce> Почему ты не можешь дать мне больше времени?".
<ce>
<ce> Его жена смотрит на него с отвращением.
<ce>
<ce> "И что мне дало терпение? Уже прошло =int_ лет!
<ce> Ты не имеешь права на большее
<ce> терпение! Разве это не так, %ra?"

Message:  1040
<ce> Вы пытаетесь объяснить, что какими бы ни были их
<ce> трудности, они будут счастливее, если будут пытаться
<ce> совершенствоваться индивидуально, а не зацикливаться на
<ce> недостатках другого. Они оба задумчиво слушают, но как
<ce> только их взгляды встречаются, они возвращаются к более
<ce> низменным мыслям. Спор продолжается несколько минут.
<ce>
<ce> _husb_ говорит: "Если я пообещаю сегодня
<ce> отказаться от медовухи и вернуться к отцовским
<ce> обязанностям, ты дашь мне хотя бы шанс?"
<ce>
<ce> _wife_ закатывает глаза.
<ce>
<ce> "Есть какая-то причина, по которой ты делаешь это
<ce> предложение в присутствии незнакомого человека, но
<ce> не тогда, когда мы с тобой вдвоем? Где ты был в прошлом
<ce> году или =int_ лет назад?"
<ce>
<ce> _husb_ серьезно смотрит на вас.
<ce>
<ce> "Тогда пусть %ra будет свидетелем моего обещания. Если
<ce> я потерплю неудачу, вы можете пойти и рассказать об этом
<ce> случае _qgiver_ в качестве свидетельства, а я приму
<ce> любые последствия, которые вы решите применить. Разве
<ce> это не справедливое предложение, друг?"

Message:  1043
<ce> _wife_ выглядит спокойной, но не совсем довольной.
<ce>
<ce> "Что ж, вот еще вопрос. Разве это нормально,
<ce> когда пьяный мужчина бьет свою жену?
<ce> Это убого, я вам скажу!"
<ce>
<ce> _husb_ стыдливо опускает глаза.

Message:  1044
<ce> _wife_ ухмыляется.
<ce>
<ce> "Ну что ж, тогда вот этот свидетель пусть запомнит это
<ce> так же хорошо, как и твое обещание. И как только я найду
<ce> способ отплатить тебе за все те страдания, которые ты
<ce> мне причинил, тогда мы сможем поговорить о том, чтобы
<ce> вместе найти мир и покой. Не так ли?"

Message:  1045
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА: Этикет)

Message:  1080
%qdt:
 _qgiver_ попросил меня
 отправиться в _res_ вместе
 с _husb_, чтобы разрешить
 некоторые трудности в браке.
 Это должно быть сделано сегодня.

Message:  1099
<ce> Вместо того чтобы помочь им разрешить
<ce> конфликт, вы мутите воду и подрываете свой
<ce> авторитет. Ссора длится еще некоторое время, и обе
<ce> стороны расходятся на ночь. Вы остаетесь
<ce> один, не получив даже "пока".

QBN:

Person _qgiver_ group Questor
Person _husb_ group Local_3.1 male
Person _wife_ group Local_4.0 female

Place _res_ local house
Place _tavern_ local tavern

Clock _timer_ 1.00:00

Item _int_ gold range 3 to 7
Item _reward_ gold

--	Quest start-up:
  start timer _timer_
  log 1080 step 0
  place npc _wife_ at _res_
  add _husb_ face

_begin_ task:
  clicked npc _wife_
  say 1028
  prompt 1029 yes _fail_ no _Q1_

_Q1_ task:
  prompt 1031 yes _junction1_ no _fail_

variable _junction1_
_pick1_ task:
  when _junction1_ and not _skill1_
  pick one of _Q2A_ _Q2B_

_shortcut1_ task:
  when _junction1_ and _skill1_
  say 1045
  start task _success_

_Q2A_ task:
  prompt 1034 yes _junction2_ no _fail_

_Q2B_ task:
  prompt 1035 yes _fail_ no _junction2_

variable _junction2_
_pick2_ task:
  when _junction2_ and not _skill2_
  pick one of _Q3A_ _Q3B_

_shortcut2_ task:
  when _junction2_ and _skill2_
  say 1045
  start task _success_

_Q3A_ task:
  prompt 1037 yes _Q4_ no _fail_

_Q3B_ task:
  prompt 1038 yes _fail_ no _Q4_

_Q4_ task:
  prompt 1040 yes _junction3_ no _fail_

variable _junction3_
_pick3_ task:
  when _junction3_ and not _skill3_
  pick one of _Q5A_ _Q5B_

_Q5A_ task:
  prompt 1043 yes _success_ no _fail_

_Q5B_ task:
  prompt 1044 yes _fail_ no _success_

_shortcut3_ task:
  when _junction3_ and _skill3_
  say 1045
  start task _success_

_skill1_ task:
  when skill Etiquette is at least 60

_skill2_ task:
  when skill Etiquette is at least 40

_skill3_ task:
  when skill Etiquette is at least 20

_fail_ task:
  say 1099
  hide npc _wife_
  drop _husb_ face
  end quest

_success_ task:
  give pc _reward_
  hide npc _wife_
  drop _husb_ face
  end quest

_timer_ task:
  say 1003
  end quest