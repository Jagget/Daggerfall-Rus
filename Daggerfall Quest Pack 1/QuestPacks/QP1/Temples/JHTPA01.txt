Quest: JHTPA01
DisplayName: Отмена проклятия
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Наш долг перед Стендарром должен быть превыше
<ce> всего. Он, Владыка Милосердия, дал нам указ спасти
<ce> одного человека от не-смерти - вампиризма.
<ce> Печальной душе требуется охрана, ибо ходят слухи,
<ce> что противоборствующие силы хотят вмешаться и
<ce> истребить ночного сталкера как "ошибку природы".
<ce> Докажи что твоя преданность храму превыше всего
<ce> остального, помогая в этом деле.

RefuseQuest:  [1001]
<ce> Тогда пусть Стендарр смилостивится над нами, если мы оплошаем.

AcceptQuest:  [1002]
<ce> Очень хорошо. Ты найдешь своего подопечного,
<ce> _prsn_, в __prsn_ здесь, в ___prsn_. Сопровождай
<ce> клиента до %g4 места назначения. Возможно, ты не
<ce> увидишь сильных признаков вампиризма, ведь
<ce> заражение произошло еще недавно, но %g4 природу
<ce> отрицать нельзя. Не предавай Стендарра и
<ce> используй любые средства, чтобы доставить %g4 к
<ce> месту излечения. Если тебе не удастся сделать
<ce> это в течение =timer_ дней, на смену тебе, к
<ce> твоему стыду, будут посланы другие.

QuestFail:  [1003]
<ce> Соратники из __qgvr_ подходят к тебе и _prsn_. Настаивая на
<ce> том, чтобы поторопиться с выполнением задания, они берут у
<ce> тебя %g4 и отправляются в сторону ___goalplace_.

QuestComplete:  [1004]
<ce> _prsn_ выпивает зелье и ждет. Через несколько
<ce> мгновений %g4 кожа возвращается к своему обычному
<ce> цвету. Дыхание и сердцебиение становятся
<ce> нормальными, и вскоре от несмерти не осталось и
<ce> следа. С ликованием %g1 крепко и благодарно
<ce> обнимает тебя и покидает _goalplace_.
<ce>
<ce> Ты веришь, что войска Аркея отступятся, как только
<ce> увидят %g4 в нормальном состоянии.

Message:  1010
<ce> Стендарр? Ты от Стендарра?
<ce>
<ce> %oth, ты заставил меня поволноваться. Агенты другого храма
<ce> рыщут по всему ___prsn_ из-за слухов о моём существовании.
<ce> Безжалостные, они не успокоятся, пока я не стану прахом в
<ce> урне. Я молю, веди меня к ___goalplace_.
<ce>
<ce> Я продал свой дом и почти все, что у меня есть,
<ce> за сведения о лекарстве, которое продает
<ce> _apoc_, посещающий _goalplace_ в ___goalplace_.
<ce> Я уверен, что лекарство будет готово, если мы
<ce> прибудем в течение =timer_ дней. Пусть Стендарр
<ce> благоскловит наше путешествие, %ra.

Message:  1011
<ce> Вы заметили поблизости трех человек с
<ce> оружием в руках.
<ce>
<ce> "Стоять! Мы не хотим проливать кровь, кроме той
<ce> твари за твоей спиной. Если ты немедленно отойдёшь
<ce> в сторону, тебе не будет причинен никакой вред. Мы
<ce> клянемся в этом Аркею!"
<ce>
<ce> Ты знаешь, что Храм Аркея услышит об этом, если ты
<ce> откажешься от их требований. Но ты должен сделать
<ce> выбор. Будешь ли ты защищать вампира от них?

Message:  1013
<ce> "Тогда Аркей благослови наши клинки!!!"

Message:  1014
<ce> _prsn_ удивляется, когда ты бросаешь %g4 на
<ce> землю перед собой. Последователи Аркея
<ce> быстры и беспощадны. Крик едва успевает
<ce> прозвучать в воздухе, как они уже пробивают
<ce> %g4 насквозь несколькими ударами.
<ce> Убедившись, что _prsn_ больше не встанет,
<ce> они склоняются перед тобой.
<ce>
<ce> "Большое спасибо тебе за то, что ты помог нам уничтожить этого
<ce> вампира! Пусть никто не перейдет тебе дорогу безнаказанно!"
<ce>
<ce> Они уносят труп как доказательство. Один из них
<ce> дарит тебе подарок, священный фолиант, в знак
<ce> сотрудничества между храмами.

Message:  1015
<ce> _prsn_ испытывает глубокое облегчение, обнаружив здесь
<ce> _apoc_, как и было обещано. Однако алхимик, похоже, не
<ce> разделяет озабоченности по поводу судьбы вампира.
<ce>
<ce> "Привет, добро пожаловать. Да, у меня здесь есть твое зелье.
<ce> Всего за 300 золотых ты можешь получить его".
<ce>
<ce> В припадке безумия _prsn_ без колебаний вытаскивает из
<ce> твоих вещей монеты, бросает их в _apoc_, и получает
<ce> долгожданное лекарство.

Message:  1016
<ce> _prsn_ испытывает глубокое облегчение, обнаружив здесь
<ce> _apoc_, как и было обещано. Однако алхимик, похоже, не
<ce> разделяет озабоченности по поводу судьбы вампира.
<ce>
<ce> "Привет, добро пожаловать. Да, у меня здесь есть твое зелье.
<ce> Всего за 300 золотых ты можешь получить его".
<ce>
<ce> _prsn_ кажется озадаченным тем, что,
<ce> продав всё %g4 имущество, %g1 должен
<ce> заплатить ещё, чтобы получить лекарство.
<ce> Не имея больше денег, %g1 умоляет тебя
<ce> покрыть расходы. Ты роешься в сумке, но
<ce> не находишь требуемых 300 золотых.
<ce>
<ce> Конечно, есть и другая альтернатива.
<ce> Менее щепетильный человек может просто
<ce> забрать зелье с трупа. Применишь ли ты
<ce> насилие, чтобы добиться своего?

Message:  1017
<ce> _apoc_ вскрикивает и хватается за оружие.

Message:  1018
<ce> _prsn_ переживает, что пришлось отнять
<ce> жизнь, но %g4 мораль сейчас спит. Вдвоем вы
<ce> находите лекарство, и %g1 выпивает его.

Message:  1019
<ce> Ты считаешь, что это ужасно нелогично, когда агент Бога
<ce> Милосердия убивает когда удобно. Ты останавливешь свою руку.
<ce> Тебе требуется 300 золотых на покупку зелья.

Message:  1030
%qdt:
__qgvr_ из ___qgvr_ требует,
чтобы я охранял незадачливого
вампира, который должен найти
лекарство от смерти. Я должен
встретиться с вампиром, которого
зовут _prsn_, здесь, в __prsn_.

Message:  1031
%qdt:
_prsn_ должен отправиться в
___goalplace_, чтобы найти алхимика
по имени _apoc_ в _goalplace_. Если
мы уложимся в =timer_ дней, то
получим лекарство от вампиризма

Message:  1032
%qdt:
_apoc_ вымогает у нас ещё
300 золотых за лекарство
для _prsn_. Мы должны
собрать деньги, иначе %g1
останется вампиром.

QBN:
Person _qgvr_ group Questor
Person _prsn_ faction The_Vraseth local
Person _apoc_ faction Apothecaries_of_Stendarr
Person _arkay_ faction Arkay

Foe _knight_ is Knight
Foe _archer_ is Archer
Foe _healer_ is Healer
Foe _apok_ is Healer

Place _goalplace_ remote apothecary

Item _gift_ item class 10 subclass 12

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _battle_ 6:00 1.00:00

--	Quest start-up:
    log 1030 step 0
    start timer _timer_
    place npc _apoc_ at _goalplace_
    hide npc _apoc_

_clicked_ task:
    clicked npc _prsn_
    say 1010
    hide npc _prsn_
    add _prsn_ face
    start timer _battle_
    restore npc _apoc_
    log 1031 step 1

_battle_ task:
    prompt 1011 yes _defend_ no _surrender_

_angryarkay_ task:
    change repute with _arkay_ by -20

_defend_ task:
    say 1013
    create foe _knight_ every 0 minutes 1 times with 100% success
    create foe _archer_ every 0 minutes 1 times with 100% success
    create foe _healer_ every 0 minutes 1 times with 100% success
    change foe _knight_ infighting true
    change foe _archer_ infighting true
    change foe _healer_ infighting true
    start task _angryarkay_

_surrender_ task:
    drop _prsn_ face
    say 1014
    get item _gift_
    make _gift_ permanent
    change repute with _qgvr_ by -8
    end quest

_ending_ task:
    clicked _apoc_ and at least 300 gold otherwise do _nogold_
    say 1015
    give pc nothing
    start quest JHTPA01a
    end quest

_nogold_ task:
    prompt 1016 yes _murder_ no _peace_

_murder_ task:
    say 1017
    hide npc _apoc_
    place foe _apok_ at _goalplace_
    change foe _apok_ infighting true

_killed_ task:
    killed _apok_
    say 1018
    legal repute -10
    give pc nothing
    start quest JHTPA01a
    end quest

_peace_ task:
    say 1019
    log 1032 step 2
    clear _nogold_ _ending_

_timer_ task:
    say 1003
    end quest