Quest: JHTP103
DisplayName: Мобильная аптека
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, один из наших добрых членов в ___dst_ страдает от
<ce> какой-то жестокой болезни. Обычно мы нанимаем целителей для
<ce> своих, но эта болезнь явно не опасна для жизни. Мы лишь
<ce> хотели бы сократить период выздоровления с нескольких
<ce> недель до нескольких дней. С помощью этой пачки _hrb_,
<ce> _npc_ вскоре выздоровеет самым замечательным образом. Можем
<ce> ли мы рассчитывать на то, что ты отвезешь это %g2?

RefuseQuest:  [1001]
<ce> Нагрузка в этом храме увеличится и без н%g3.
<ce> Ты уверен, что это твое решение?

AcceptQuest:  [1002]
<ce> Наша самая искренняя благодарность тебе. _npc_ отдыхает в _dst_,
<ce> который находится в ___dst_. Доставка должна быть достаточной, я
<ce> полагаю, так как %g1 знает, как этим пользоваться. Мы не можем
<ce> позволить себе платить тебе за это много, но такова наша работа.
<ce> Я увижу тебя через =qtmr_ дней.

QuestFail:  [1003]
<ce> Ожидалось, что ты уже вернешься в ___qgvr_.

QuestComplete:  [1004]
<ce> _qgvr_ вручает тебе =rwrd_ золотых монет и еще
<ce> раз благодарит за преданность делу.

Message:  1020
<ce> Кто ты? %oth, ты приехал из ___qgvr_? Я бы
<ce> не хотел беспокоить их так. Но, полагаю, работы
<ce> хватает. Я возьму _hrb_. Спасибо. Скажи им, что я
<ce> отправлюсь в путь к концу следующей недели.

Message:  1021
<ce> Как выглядел _npc_? Это то, на что %g1 жалуется каждые несколько
<ce> месяцев. Кто-то сказал мне, что мы должны просто
<ce> вложить деньги в какой-нибудь дом, где %g1 сможет жить здесь,
<ce> но это та же самая проблема с деньгами. Кстати, о деньгах: их
<ce> не так уж много, но это то, что мы можем предложить.
<ce> Пожалуйста, пойми, что даже мелочи доставляют божеству
<ce> удовольствие. Убить даэдра по заданию и доставить травы по
<ce> заданию - все это одинаково значимо для богов наверху.

Message:  1025
<ce> Что это такое? Тебя прислал храм? О, дорогой. Боюсь, что твое
<ce> путешествие было немного просчитано. За последние пару дней я
<ce> самым звездным образом преодолел болезнь и даже сейчас чувствую
<ce> себя готовым возобновить свои службы. Но раз уж ты здесь,
<ce> почему бы не поехать вместе? Позволь мне только собрать свои
<ce> вещи, и я с большим удовольствием возьму тебя в проводники.

Message:  1026
<ce> Ах, как я рад тебя видеть, _npc_! Жаль, что нам пришлось
<ce> послать за тобой нашего доброго друга %pcf, но все хорошо, что
<ce> хорошо кончается. Как хорошо, что вы смогли путешествовать
<ce> вместе для безопасности. Вот твоя награда, %pcf. Я заберу
<ce> _hrb_ обратно, если он у тебя ещё есть.

Message:  1030
%qdt:
 Я должен доставить
 некоторое количество _hrb_
 _npc_, который болен в
 _dst_ на ___dst_. Затем я
 должен вернуться в ___qgvr_
 через =qtmr_ дней.

Message:  1031
%qdt:
 _npc_ чувствует себя достаточно хорошо,
 чтобы сопровождать меня в ___qgvr_.

QBN:
Item _hrb_ small_plant
Item _rwrd_ gold range 50 to 80

Person _qgvr_ group Questor
Person _npc_ group Cleric

Place _dst_ remote house

Clock _qtmr_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
  start timer _qtmr_
  log 1030 step 0
  get item _hrb_
  place npc _npc_ at _dst_

_receive_ task:
  clicked npc _npc_
  pick one of _sick_ _okay_

_sick_ task:
  say 1020
  take _hrb_ from pc

_sickend_ task:
  when _qgclicked_ and _sick_
  say 1021
  give pc _rwrd_
  end quest

_okay_ task:
  add _npc_ face
  say 1025
  hide npc _npc_
  log 1031 step 1

_okayend_ task:
  when _qgclicked_ and _okay_
  drop _npc_ face
  say 1026
  give pc _rwrd_
  end quest

_qgclicked_ task:
  clicked npc _qgvr_

_clearclick_ task:
  when _qgclicked_ and not _receive_
  clear _qgclicked_ _clearclick_

_qtmr_ task:
  say 1003
  end quest