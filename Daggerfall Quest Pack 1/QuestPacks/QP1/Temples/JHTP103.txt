Quest: JHTP103
DisplayName: Передвижная аптека
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, один из наших добрых членов в ___dest_ страдает
<ce> от какой-то жестокой болезни. Обычно мы нанимаем
<ce> целителей для своих, но эта болезнь явно не опасна
<ce> для жизни. Мы лишь хотели бы сократить период
<ce> выздоровления с нескольких недель до нескольких дней.
<ce> С помощью этой пачки _herb_ _npc_ вскоре выздоровеет
<ce> самым замечательным образом. Можем ли мы рассчитывать
<ce> на то, что вы отвезете это %g2?

RefuseQuest:  [1001]
<ce> Нагрузка в этом храме увеличится и без н%g4.
<ce> Вы уверены, что это ваше решение?

AcceptQuest:  [1002]
<ce> Наша самая искренняя благодарность вам. _npc_ отдыхает
<ce> в _dest_, который находится в ___dest_. Доставки должно
<ce> быть достаточно, я полагаю, так как %g1 знает, как этим
<ce> пользоваться. Мы не можем позволить себе платить вам
<ce> за это много, но такова наша работа. Я увижу вас
<ce> через =timer_ дней.

QuestFail:  [1003]
<ce> Ожидалось, что к этому моменту вы уже вернетесь в ___qgiver_.

QuestComplete:  [1004]
<ce> _qgiver_ вручает вам =reward_ золотых и еще
<ce> раз благодарит за преданность делу.

Message:  1020
<ce> Кто вы? %oth, вы приехали из ___qgiver_? Я бы
<ce> не хотел беспокоить их так. Но, полагаю, работы
<ce> хватает. Я возьму _herb_. Спасибо. Скажите им, что я
<ce> отправлюсь в путь к концу следующей недели.

Message:  1021
<ce> Как выглядел _npc_? Это то, на что %g1 жалуется каждые
<ce> несколько месяцев. Кто-то сказал мне, что мы должны
<ce> просто вложить деньги в какой-нибудь дом, где %g1 сможет
<ce> жить, но это все та же проблема с деньгами. Кстати,
<ce> о деньгах, их не так уж много, но это то, что мы можем
<ce> предложить. Пожалуйста, поймите, что даже мелочи
<ce> доставляют божеству удовольствие. Убить даэдра
<ce> по заданию и доставить травы по заданию - все это
<ce> одинаково значимо для богов наверху.

Message:  1025
<ce> Что это такое? Вас прислал храм? О, дорогой. Боюсь, что
<ce> ваше путешествие было небольшим просчетом. За последние
<ce> пару дней я блестяще справился с болезнью и чувствую
<ce> себя готовым возобновить свои услуги. Но раз уж вы здесь,
<ce> почему бы не поехать вместе? Позвольте мне только собрать
<ce> свои вещи, и я с большим удовольствием возьму вас
<ce> в проводники.

Message:  1026
<ce> Ах, как я рад вас видеть, _npc_! Жаль, что нам пришлось
<ce> послать за вами нашего доброго друга %pcf, но все хорошо,
<ce> что хорошо кончается. Как хорошо, что вы смогли
<ce> путешествовать вместе для безопасности. Вот ваша награда,
<ce> %pcf. Я заберу _herb_ обратно, если он все еще у вас.

Message:  1030
%qdt:
 Я должен доставить
 некоторое количество _herb_
 _npc_, который болен
 в _dest_ на ___dest_. Затем я
 должен вернуться в ___qgiver_
 через =timer_ дней.

Message:  1031
%qdt:
 _npc_ чувствует себя достаточно хорошо,
 чтобы сопровождать меня в ___qgiver_.

QBN:
Item _herb_ small_plant
Item _reward_ gold range 50 to 80

Person _qgiver_ group Questor
Person _npc_ group Cleric

Place _dest_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  get item _herb_
  place npc _npc_ at _dest_

_receive_ task:
  clicked npc _npc_
  pick one of _sick_ _okay_

_sick_ task:
  say 1020
  take _herb_ from pc

_sickend_ task:
  when _qgclicked_ and _sick_
  say 1021
  give pc _reward_
  end quest

_okay_ task:
  add _npc_ face
  say 1025
  hide npc _npc_
  log 1031 step 1

_okayend_ task:
  when _qgclicked_ and _okay_
  drop _npc_ face
  say 1026
  give pc _reward_
  end quest

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _receive_
  clear _qgclicked_ _clearclick_

_timer_ task:
  say 1003
  end quest