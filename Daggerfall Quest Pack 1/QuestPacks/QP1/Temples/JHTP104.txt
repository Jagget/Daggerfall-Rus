Quest: JHTP104
DisplayName: Бюро находок
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, мы беспокоимся о самочувствии нашего дорогого
<ce> друга _rsc_, который несколько недель назад отправился
<ce> в паломничество в ___dng_. Хотя наша общая практика
<ce> заключается в том, чтобы дать паломникам возможность
<ce> уединиться, есть некоторые исключительные случаи,
<ce> которые требуют проверки. Мы просим вас сходить туда
<ce> и вывести %g4 на свет дневной еще раз.

RefuseQuest:  [1001]
<ce> Удручающий ответ, %pcf. Что же мы тогда будем делать
<ce> с %g2?
   <--->
<ce> Полагаю, мы могли бы послать вас за трупом
<ce> через несколько недель. Вы этого хотите?

AcceptQuest:  [1002]
<ce> Замечательно, мой друг. Спасибо. Надеюсь, мы
<ce> все-таки признаем %g4 предсказание ложным, ведь
<ce> %g1 бормотал что-то о смертельном проклятии и так
<ce> далее. Видите ли, мы сразу же удовлетворили
<ce> просьбу об отпуске. В любом случае, как насчет
<ce> того, чтобы увидеть вас здесь через =qtimer_ дней?
<ce> Я считаю, что это достаточный срок для
<ce> данного задания. Прощайте.

QuestComplete:  [1004]
<ce> _qgvr_ глубоко вздыхает,
<ce> и вы считаете вопрос исчерпанным.

RumorsDuringQuest:  [1005]
Они сказали мне, что ___qgvr_ - хорошее место для жизни.
А вы как думаете?
   <--->
Вы недавно были в ___qgvr_? Мне нравится.

QuestorPostsuccess:  [1008]
Добро пожаловать, %pcf. Скоро ___dng_ будет бояться вашего
имени.
   <--->
Ваше мастерство и репутация приветствуются здесь, %pcf.

QuestLogEntry:  [1010]
%qdt:
 _qgvr_ в ___qgvr_
 послал меня найти _rsc_
 в ___dng_. У меня есть
 =qtimer_ дней, чтобы вернуться.

Message:  1011
<ce> Громкий крик почти пугает вас.
<ce>
<ce> "Вы там! Вас послал _qgvr_, не %g1 ли?
<ce> Заберите меня отсюда! Мне нужно попасть в ___qgvr_!"
<ce>
<ce> _rsc_ радостно присоединяется к вам.
   <--->
<ce> Кто-то появляется в поле зрения, испуганно глядя на вас.
<ce>
<ce> "Извините! Мне нужно вернуться в ___qgvr_.
<ce> Можете ли вы мне помочь? Меня зовут _rsc_".
<ce>
<ce> Вы говорите %g2, чтобы он следовал за вами.

Message:  1012
<ce> Ах, _rsc_, какое счастье видеть вас снова.
<ce> Паломничество того стоило? Большинство людей
<ce> после такого опыта никогда больше не осмеливаются
<ce> спускаться под землю, поэтому мы и удовлетворили
<ce> вашу просьбу. А сейчас у нас для вас много
<ce> работы. Нет времени на кошмары. А вы уже
<ce> поблагодарили %pcn? Если бы не %g1, вы были бы уже
<ce> мертвы. Еще раз искренне благодарим, добрый %pct.

Message:  1020
<ce> Неподалеку слышны  шаркающие шаги.
<ce> Голос хрипло произносит:
<ce>
<ce> "Король Червей дал мне высшую форму.
<ce> Он хочет наделить этим даром и вас".
<ce>
<ce> Вы замечаете, что рот существа
<ce> не двигается. Очевидно, что здесь действует
<ce> мощная магия. Вы готовитесь избавить _rsc_
<ce> от страданий.
   <--->
<ce> Вы слышите, как рядом затихли чьи-то шаги, а затем
<ce> голос произнес:
<ce>
<ce> "Мои наилучшие пожелания _qgvr_. Скоро я увижу %g4".
<ce>
<ce> Похоже, проклятие было не просто выдумкой.
<ce> Единственное спасение для _rsc_ сейчас - это смерть.

Message:  1021
<ce> Вы хотели бы, чтоб в этом не было необходимости, но это
<ce> было все, что вы могли сделать. Пора сообщить _qgvr_.

Message:  1022
<ce> Расскажите мне, что случилось? Где _rsc_?
<ce>
<ce> О, кажется, меня сейчас стошнит. Только некроманты могли
<ce> сделать такое. О, это отвратительно. Мог ли я что-то
<ce> изменить, если бы не удовлетворил %g4 просьбу? Могли бы
<ce> мы вылечить %g4... О, боги. Нет, я не могу больше думать
<ce> об этом сегодня. Не сегодня. Завтра, возможно. Но не
<ce> больше. Спасибо вам за то, что выполнили свой долг.
<ce> Вот и все.
   <--->
<ce> С возвращением, %pcf, но где же _rsc_?
<ce>
<ce> Это ужасно. Теперь мне интересно, навлек ли %g1
<ce> проклятие на себя собственными действиями или оно было
<ce> непроизвольным. О, если бы мы только могли это знать.
<ce> Будем надеяться, что никогда больше не услышим ничего
<ce> подобного. Спасибо вам, %pcf. Вы выполнили свой долг.

Message:  1050
<ce> _qgvr_ сказал вам, что вы уже должны быть в ___qgvr_.

QBN:

Person _qgvr_ group Questor
Person _rsc_ group Resident1

Place _dng_ remote dungeon4

Clock _qtimer_ 00:00 0 flag 17 range 0 2
Clock _begint_ 1:00 1.12:00

Foe _enemy_ is Zombie

--	Quest start-up:
	start timer _qtimer_
	reveal _dng_
	log 1010 step 0
-	place npc _rsc_ at _dng_
	pc at _dng_ set _inside_
	pick one of _E.01_ _E.02_

variable _E.01_
variable _E.02_
_inside_ task:
	start timer _begint_
	stop timer _qtimer_

_begint_ task:
	start timer _qtimer_

_qtimer_ task:
	say 1050
	end quest

_S.01_ task:
	when _E.01_ and _begint_ and _inside_
	start task _S.01b_

_S.01b_ task:
	say 1011
	add _rsc_ face

_reward1_ task:
	when _qgclicked_ and _S.01b_
	say 1012
	give pc nothing
	end quest

_S.02_ task:
	when _E.02_ and _begint_ and _inside_
	say 1020
	create foe _enemy_ every 0 minutes 1 times with 100% success

_step2_ task:
	killed _enemy_
	say 1021

_reward2_ task:
	when _qgclicked_ and _step2_
	say 1022
	give pc nothing
	end quest

_qgclicked_ task:
	clicked npc _qgvr_

_S.04_ task:
	when _qgclicked_ and not _begint_
	clear _qgclicked_ _S.04_
