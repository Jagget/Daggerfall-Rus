Quest: JHTP102
DisplayName: Поездка за пожертвованиями
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> А, хорошо, что ты здесь. Нам нужно послать
<ce> кого-нибудь надёжного в___rsd_, чтобы он
<ce> получил пожертвование и принёс его сюда.
<ce> Сделаешь ли ты это для нас?

RefuseQuest:  [1001]
<ce> Да всё нормально. Не обращай на меня внимания. Я
<ce> просто буду голодать здесь со всеми остальными.

AcceptQuest:  [1002]
<ce> Очень хорошо. Я предупреждаю тебя, чтобы ты
<ce> быстро возвращался сюда. Несколько наших
<ce> курьеров за эти годы рассказывали истории о
<ce> вооруженном нападении, как только деньги
<ce> оказываются в их руках. Беги или защищайся, как
<ce> хочешь. Найди нашего спонсора,
<ce> _dnr_, в _rsd_.
<ce> Сообщи %g2 эту кодовую фразу: "_pw_". Деньги
<ce> понадобятся нам здесь через =timer_ дней.

QuestFail:  [1003]
<ce> В храме ожидали, что ты уже
<ce> вернешься в ___qstgvr_.

QuestComplete:  [1004]
<ce> Ни соблазн украсть у _dnr_ или продать
<ce> его взнос не смогли тебя поколебать. У
<ce> тебя есть будущее среди нас, %pcf.
<ce> Возьми это на расходы.

QuestorPostFailure:  [1009]
Мы доверили тебе простое дело - пожертвование, а ты его провалил.

Message:  1020
<ce> Да, "_pw_", очень хорошо. Я вложил
<ce> пожертвование в этот пакет. Передай
<ce> привет _qstgvr_. Мы дружим
<ce> уже несколько лет. Надо будет
<ce> встретиться в следующий раз,
<ce> когда буду в ___qstgvr_. Будь
<ce> осторожен там.

Message:  1025
<ce> Остановись, %ra. Мы знаем, что эти деньги
<ce> пойдут жадным жрецам в богато украшенном
<ce> храме. Отдай их нам, чтобы мы могли отнести
<ce> их тем, кто действительно в них нуждается.

Message:  1026
<ce> Как бы то ни было, деньги нам всё равно нужны.
<ce> Сложи свое оружие, чтобы никто не пострадал.

Message:  1027
<ce> Спасибо. Уверяю тебя, мы представляем
<ce> безголосых и беззащитных %crn. Если мы еще
<ce> раз встретимся, то это будет большой
<ce> честью для нас. Прощай.

Message:  1028
<ce> Пакет не запечатан каким-либо значимым образом. Ты
<ce> можешь легко развязать узел и забрать деньги себе.
<ce> Перенаправишь ли ты пожертвование _dnr_
<ce> в свой собственный денежный мешочек?

Message:  1029
<ce> Внутри ты находишь =stl_ золотых.

Message:  1030
%qdt:
 _qstgvr_ отправил меня в ___rsd_,
 чтобы забрать пожертвование от
 _dnr_, который должен находиться в
 _rsd_. Я должен надежно охранять
 его и вернуться к ___qstgvr_ через
 =timer_ дней.

Message:  1031
<ce> __qstgvr_ должен очень доверять тебе, чтобы позволить
<ce> тебе обращаться с такими деньгами без охраны. Ты
<ce> закрываешь сумку и больше не думаешь об этом.

QBN:
Item _mny_ large_sack
Item _pw_ small_plant
Item _rwrd_ gold range 40 to 60
Item _stl_ gold range 600 to 800

Person _qstgvr_ group Questor
Person _dnr_ group Noble

Place _rsd_ remote random

Foe _enemy1_ is 2 Thief

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _delay_ 00:05 00:09

--	Quest start-up:
  start timer _timer_
  log 1030 step 0
  place npc _dnr_ at _rsd_

_receive_ task:
  clicked npc _dnr_
  say 1020
  get item _mny_
  start timer _delay_
  _mny_ used do _question_

_question_ task:
  prompt 1028 yes _thief_ no _dud_

_thief_ task:
  say 1029
  get item _stl_
  make _stl_ permanent
  take _mny_ from pc
  change repute with _qstgvr_ by -3
  end quest

_dud_ task:
  say 1031

_delay_ task:
  create foe _enemy1_ every 300 minutes 3 times with 25% success

_choice_ task:
  injured _enemy1_
  clicked foe _enemy1_
  prompt 1025 yes _altend_ no _refuse_

_altend_ task:
  say 1027
  restrain foe _enemy1_
  remove foe _enemy1_
  take _mny_ from pc
-  start quest ????
  end quest

_refuse_ task:
  say 1026

_victory_ task:
  toting _mny_ and _qstgvr_ clicked
  take _mny_ from pc
  give pc _rwrd_
  end quest

_timer_ task:
  say 1003
  end quest