Quest: JHTP108
DisplayName: Заряд батареи
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Не все болезни одинаковы. Большинству из них требуется
<ce> несколько серьезных заклинаний по излечению, чтобы
<ce> избавиться от них, но мы должны быть готовы
<ce> к некоторым особым случаям, которые иногда находят нас.
<ce> Нам нужно, чтобы кто-то подзарядил одну из реликвий,
<ce> которые мы используем для таких редких болезней.
<ce> Вы сможете это сделать?

RefuseQuest:  [1001]
<ce> Хм... Это не особенно сложно.

AcceptQuest:  [1002]
<ce> Тогда вот что. Он бесполезен для лечения обычных
<ce> болезней Залива Илиак, так что не ждите чудес, если
<ce> вам приспичит. Ваша задача - спустить его
<ce> в ___dung_ примерно на два дня. Вы должны оставаться
<ce> под землей столько, сколько потребуется для
<ce> подзарядки его энергии. Вы узнаете, когда он будет
<ce> готов. В течение нескольких минут он испустит
<ce> отчетливое свечение, после чего затихнет, и это
<ce> все чего вам надо дождаться. Вернитесь не позже чем
<ce> через месяц и вы обеспечите жителям ___qgiver_
<ce> свободу от экзотических болезней Тамриэля.
<ce> Безопасного спуска в глубины, %pct!

QuestFail:  [1003]
<ce> Прошел уже месяц с тех пор, как вы получили
<ce> _tri_, и не вернулись.

QuestComplete:  [1004]
<ce> Ах, вот оно. Может быть, вам не понятно,
<ce> но я вижу разницу. Спасибо, что зарядили
<ce> _tri_. Теперь возьмите это. Возможно, эта одежда
<ce> придется по вкусу вам или вашим родным.

Message:  1010
<ce> Похоже, вы вольны бродить по ___dung_, пока
<ce> _tri_ не восстановит свою магическую энергию.

Message:  1011
<ce> Вы осознаете, что среди ваших вещей быстро накапливается
<ce> свет. _tri_ начинает интенсивно светиться и продолжает
<ce> сиять около минуты. Затем _tri_ затихает и возвращается
<ce> к своему обычному виду. Готово.

Message:  1012
<ce> Встречный путешественник издалека приветствует вас,
<ce> а затем собирается продолжить путь. Однако _tri_
<ce> в вашем рюкзаке выглядит достаточно необычно,
<ce> чтобы привлечь внимание.
<ce>
<ce> "Эй! Это одна из тех вещей, которые они используют
<ce> в храмах, верно? От тяжелых болезней? Да это же стоит
<ce> кучу денег! У меня есть для вас =pay_ золотых, если вы
<ce> отдадите его мне прямо сейчас!"

Message:  1013
<ce> "Как хотите".
<ce>
<ce> Вы остаетесь в одиночестве.

Message:  1014
<ce> "О, отлично! Спасибо, %ra. Не знаю, как вы достали эту
<ce> штуку, но дома это произведет фурор. Что это будет
<ce> за подарок!"
<ce>
<ce> Ваш новый знакомый передает вам деньги
<ce> и забирает _tri_. Вы видите хорошую возможность
<ce> забрать его обратно силой, если захотите.

Message:  1015
<ce> (ПРОВЕРКА АТРИБУТА ПРОВАЛЕНА: Сила)
<ce>
<ce> Из ближайших теней появляется фигура в капюшоне
<ce> и отбирает у вас _tri_. Вы смотрите, как вор
<ce> спешит дальше по темным переходам.

Message:  1016
<ce> "Эй, извини! Эти штуки действительно ценные, вот и все!
<ce> Вот, возьми обратно. Просто оставь меня в живых".

Message:  1017
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Сила)
<ce>
<ce> Из ближайших теней появляется фигура в капюшоне, чтобы
<ce> украсть у вас _tri_. Враг недооценивает вашу силу
<ce> и неловко поскальзывается, пытаясь вырвать у вас _tri_.
<ce> Вместо того чтобы повторить попытку, потенциальный вор
<ce> убегает от возможного возмездия.

Message:  1018
<ce> _tri_ источает магическую силу.
<ce> Какую бы болезнь это ни лечило, у вас ее больше нет.

Message:  1019
<ce> "Эй, что ты так смотришь на меня?"

Message:  1020
<ce> Вы забираете _tri_ с трупа незадачливого клиента.

Message:  1021
<ce> Ваш клиент убегает и уносит с собой _tri_.

Message:  1030
%qdt:
 _qgiver_ из __qgiver_
 отправил меня в ___dung_
 для подзарядки магического _tri_,
 способного лечить редкие
 болезни. Я должен вернуть его
 ___qgiver_ в течение месяца.

QBN:
Item _tri_ trinket
Item _pay_ gold range 1100 to 1700
Item _mclo_ mens_clothing
Item _fclo_ womens_robe
Item _ring_ ring

Person _qgiver_ group Questor

Foe _thief_ is Thief
Foe _cust_ is Knight

Place _dung_ remote dungeon2

Clock _timer_ 30.00:00
Clock _recharge_ 1.16:00 1.23:00
Clock _event_ 8:00 1.00:00
Clock _escape_ 0:05

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    get item _tri_
    pc at _dung_ set _inside_
    have _tri_ set _holding_
    reveal _dung_

_inside_ task:
    say 1010
    start timer _recharge_
    start timer _event_

_stop_ task:
    when not _inside_
    stop timer _recharge_
    stop timer _event_

variable _recharge_
variable _holding_
_strength_ task:
    when attribute Strength is at least 80

_event_ task:
    pick one of _e1_ _e2_ _e3_

_e1_ task:
    prompt 1012 yes _sell_ no _refuse_

_sell_ task:
    prompt 1014 yes _betray_ no _escape_
    get item _pay_
    take _tri_ from pc

_betray_ task:
    say 1019
    create foe _cust_ every 0 minutes 1 times with 100% success
    restrain foe _cust_
    start timer _escape_

_escape_ task:
    say 1021
    remove foe _cust_
    end quest

_murder_ task:
    killed _cust_
    say 1020
    get item _tri_

_refuse_ task:
    say 1013

variable _e2_
_lose_ task:
    when _e2_ and not _strength_
    say 1015
    take _tri_ from pc
    place foe _thief_ at _dung_
    clear _holding_

_inj_ task:
    injured _thief_
     clicked foe _thief_
    say 1016
    get item _tri_
    restrain foe _thief_
    change foe _thief_ infighting true

_defend_ task:
    when _e2_ and _strength_
    say 1017

variable _e3_
_finish_ task:
    when _recharge_ and _holding_
    say 1011
    _tri_ used do _use_

_use_ task:
    say 1018

_victory_ task:
    when _qgclicked_ and _finish_
    give pc _mclo_
    give pc _fclo_
    give pc _ring_
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _finish_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest