Quest: JHTPA02
DisplayName: Промывка мозгов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Наша давняя вражда с Гильдией Магов не должна
<ce> никого удивлять. Они неоднократно крали нашу
<ce> работу и выдавали ее за свою, и это лишь самое
<ce> малое из их преступлений против нас. Но нам
<ce> сообщили, что один из наших соратников был
<ce> украден и вынужден работать в зале Гильдии
<ce> Магов далеко от ___qgiver_. Джулианос требует
<ce> вернуть нашего друга. Вы пойдете?

RefuseQuest:  [1001]
<ce> Помните, что если бы это случилось с вами,
<ce> то один из нас вас бы спас. Помните об этом.

AcceptQuest:  [1002]
<ce> Джулианос улыбается вам, %pct. Вы отправитесь
<ce> в ___magery_, чтобы найти =apoc_ по имени
<ce> _apoc_ в зале магов. Вам следует ожидать
<ce> сопротивления, как только ваши намерения станут
<ce> известны, но никто не обвинит вас, если вы
<ce> будете защищаться. Гильдии Магов вообще проще
<ce> убить, чтобы %rt не узнали, чем они там на
<ce> самом деле занимаются. Каким бы ни был ваш
<ce> метод, вернитесь сюда через =timer_ дней с _apoc_
<ce> в целости и сохранности. Если это время
<ce> истечет, на ваше место будут посланы другие,
<ce> а вы будете признаны неудачником.

QuestFail:  [1003]
<ce> К этому времени вы должны были вернуться
<ce> в ___qgiver_ с _apoc_.

QuestComplete:  [1004]
<ce> _apoc_ дико заметался по комнате, когда %g1 узнал
<ce> знакомые лица приверженцев Джулианоса. Целители
<ce> приближаются и начинают долгую, тяжелую работу
<ce> по развеиванию чар Гильдии Магов, оттаскивая %g4
<ce> в другую комнату. _qgiver_ говорит:
<ce>
<ce> "Это некрасиво, но необходимо. Гильдия
<ce> Магов заплатит за свои преступления, даже
<ce> если будет гореть вся Империя. Они познают
<ce> гнев Джулианоса! Что касается вас, то пока
<ce> вас не было, мне удалось заполучить в свои
<ce> руки один довольно уникальный предмет.
<ce> Примете ли вы его в качестве награды?"

Message:  1010
<ce> В Гильдии Магов царит напряженная атмосфера. Люди
<ce> здесь, похоже, понимают, что что-то не так, но им
<ce> страшно что-то предпринять.
<ce>
<ce> Вы надеетесь, что никто из них не встанет у вас на пути,
<ce> когда вы найдете _apoc_.

Message:  1011
<ce> Этот =apoc_ соответствует описанию, которое
<ce> _qgiver_ дал вам. Однако %g4 глаза смотрят вдаль,
<ce> и %g1, похоже, ничего не соображает. Вы хватаете
<ce> %g4 за запястье, и хотя %g1 не оказывает
<ce> сопротивления, все же кричит: "П-помогите! Враги! Они
<ce> идут, чтобы забрать меня!!!"

Message:  1012
<ce> Вас нашла серьезная на вид группа
<ce> из храма Джулианоса. Они забирают _apoc_
<ce> из-под вашей опеки и ведут %g4 в ___qgiver_.
<ce> Ваша награда теряет силу.

Message:  1030
%qdt:
 __qgiver_ из ___qgiver_
 попросил меня отправиться
 в ___magery_, чтобы найти =apoc_
 по имени _apoc_ внутри Гильдии
 Магов. Я должен вернуться
 в течение =timer_ дней.

QBN:
Person _qgiver_ group Questor
Person _apoc_ faction Apothecaries_of_Stendarr
Person _mages_ faction The_Mages_Guild

Foe _mage_ is Mage
Foe _battl_ is Battle_mage

Place _magery_ remote magery

Item _item_ item class 4 subclass 1

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
    log 1030 step 0
    start timer _timer_
    hide npc _apoc_
    pc at _magery_ do _inside_

_inside_ task:
    start task _place_

_place_ task:
    say 1010
    place npc _apoc_ at _magery_
    restore _apoc_

_clicked_ task:
    clicked npc _apoc_
    say 1011
    hide npc _apoc_
    add _apoc_ face
    change repute with _mages_ by -10
    create foe _mage_ every 0 minutes 1 times with 100% success
    create foe _battl_ every 0 minutes 1 times with 100% success
    change foe _mage_ infighting true
    change foe _battl_ infighting true

_timer_ task:
    say 1003
    end quest

_timerpeople_ task:
    when _timer_ and _clicked_
    say 1012

_victory_ task:
    when _qgclicked_ and _clicked_
    give pc _item_
    change repute with _mages_ by -10
    start quest JHTPA02a
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _clicked_
    clear _qgclicked_ _clearclick_