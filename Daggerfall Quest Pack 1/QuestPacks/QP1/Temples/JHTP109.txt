Quest: JHTP109
DisplayName: Очищение
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Есть ли у тебя способность изгонять нежить, %pct?
<ce> Подойдет любой вариант заклинания. Просто требуется,
<ce> чтобы ты сам его произнес. Есть небольшая проблема с
<ce> гнусными некромантами, которую нас попросили решить.

RefuseQuest:  [1001]
<ce> Возможно, мне придется послать за этим в Гильдию Магов.

AcceptQuest:  [1002]
<ce> Я поверю тебе на слово. Мы были весьма удивлены, когда
<ce> %t прислал нам сообщение о том, что одна местная пара
<ce> предоставила свой дом некромантам для проведения нескольких
<ce> темных ритуалов. С тех пор они изменили свое мнение и
<ce> попросили нас помочь убрать нежить из их обители.
<ce>
<ce> Чтобы ты не думал, что это будет обычная поездка с
<ce> целью убить мумию, предупреждаю тебя, что это будет
<ce> не так. Найди какое-нибудь удобное место в доме,
<ce> чтобы наложить эффект рассеивания, а затем
<ce> оставайся в здании. Если ты уйдешь, все станет
<ce> намного хуже. Твоя жизненная сила будет
<ce> поддерживать божественное влияние, пока ты
<ce> остаешься там. Я подозреваю, что для завершения
<ce> очищения потребуется около суток. Такова сила
<ce> злобных некромантов. Я ожидаю, что ты прибудешь
<ce> завтра к этому времени. Ищи _res_ здесь неподалеку
<ce> в ___res_. Я буду ждать твоего отчета об успехе.

QuestFail:  [1003]
<ce> _questgiver_ ожидал, что к этому времени ты уже
<ce> устранишь темное влияние _res_.

QuestComplete:  [1004]
<ce> Разница в ___res_ ощутима даже отсюда. Мне не нужны
<ce> дальнейшие доказательства. Темнота немного отступила в
<ce> этот день. Отличная работа, %pcf. Вот, возьми _tri_.

Message:  1010
<ce> Это явно правильное здание. Жители должны
<ce> испытывать облегчение при виде тебя, но их
<ce> беспокоит влияние зла.

Message:  1011
<ce> "Нет... Нет! Нет!"
<ce>
<ce> Некроманты связали здесь дух, чтобы поддерживать
<ce> своё влияние. Это будет действительно долгий день.

Message:  1012
<ce> Дух, который здесь обитает, получил силу
<ce> материализоваться несколько раз. С силой
<ce> некромантов нельзя шутить.

Message:  1013
<ce> "Сгинь... Сгинь... Сгинь... Сгинь... Сгинь..."

Message:  1014
<ce> "Смерть приходит за тобой, %ra..."

Message:  1015
<ce> "Как долго я буду терпеть эту боль?"

Message:  1016
<ce> "Пожалуйста, остановись, остановись, остановись, остановись..."

Message:  1017
<ce> "Ты мой пропавший ребенок? Приди ко мне, милый..."

Message:  1018
<ce> "Прекрати... это... заклинание... для... меня... боль...".

Message:  1019
<ce> "Освободи меня от нежизни! Освободи меня!"

Message:  1020
<ce> "Ты пожалеешь, что бросил вызов некромантам!"

Message:  1021
<ce> "Тьма всегда будет пожирать свет, смертный".

Message:  1022
<ce> "Скорее, покинь это место... Я сдамся..."

Message:  1023
<ce> "Я почти свободен! Пожалуйста, покинь здание и
<ce> произнеси заклинание заново!"

Message:  1024
<ce> "Сдайся и стань таким, как мы!"

Message:  1025
<ce> Эффект закрепился навсегда. Дух, привязанный к этому дому,
<ce> рассеивается, не имея больше возможности существовать в
<ce> этом мире. _questgiver_ будет ждать твоего доклада.

Message:  1030
%qdt:
 _questgiver_ послал меня очистить
 _res_, местный дом,
 от влияния некромантов. Я должен
 произнести любое заклинание, чтобы развеять
 нежить внутри, а затем оставаться внутри в
 течение двадцати четырёх часов. Я должен
 приступить к работе до того, как пройдут сутки.

Message:  1031
<ce> Ты выходишь из дома и вдруг слышишь громкий
<ce> свистящий звук, доносящийся изнутри, а затем сверху
<ce> здания. Вредоносный дух, который ты отлавливал и
<ce> изводил, вырвался из пут и устремился на волю.
<ce> Вскоре он вновь окажется под контролем некромантов
<ce> для осуществления очередного злодейского замысла.

QBN:
Item _tri_ trinket

Person _questgiver_ group Questor male
Person _dummynec_ faction The_Necromancers

Foe _ghost_ is Ghost
Foe _wraith_ is Wraith

Place _res_ local house

Clock _timer_ 1.00:00
Clock _t1_ 0:56 1:26
Clock _t2_ 0:56 1:26
Clock _t3_ 0:56 1:26
Clock _t4_ 0:56 1:26
Clock _t5_ 0:56 1:26
Clock _t6_ 0:56 1:26
Clock _t7_ 0:56 1:26
Clock _t8_ 0:56 1:26
Clock _t9_ 0:56 1:26
Clock _t10_ 0:56 1:26
Clock _t11_ 0:56 1:26
Clock _t12_ 0:56 1:26
Clock _t13_ 0:56 1:26
Clock _t14_ 0:56 1:26
Clock _t15_ 0:56 1:26
Clock _t16_ 0:56 1:26
Clock _t17_ 0:56 1:26
Clock _t18_ 0:56 1:26
Clock _t19_ 0:56 1:26
Clock _victory_ 1.00:00

--	Quest start-up:
    log 1030 step 0
    pc at _res_ set _inside_
    start timer _timer_

_inside_ task:
    say 1010
    cast Dispel-Undead effect do _cast_

_sound1_ task:
    when _inside_ and not _cast_
    play sound Ghost_1 every 2 minutes 100 times

_cast_ task:
    say 1011
    stop timer _timer_
    start timer _victory_
    start timer _t1_

_fail_ task:
    when _cast_ and not _inside_ and not _victory_
    say 1031
    end quest

_sound2_ task:
    when _cast_ and _inside_ and not _victory_
    play sound whisting_wind_4 every 2 minutes 100 times

_spawn_ task:
    when _cast_ and not _victory_
    create foe _ghost_ every 23 minutes indefinitely with 8% success
    create foe _wraith_ every 41 minutes indefinitely with 11% success

_slain_ task:
    killed _ghost_
    killed _wraith_
    say 1012

_t1_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t2_

_t2_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t3_

_t3_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t4_

_t4_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t5_

_t5_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t6_

_t6_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t7_

_t7_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t8_

_t8_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t9_

_t9_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t10_

_t10_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t11_

_t11_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t12_

_t12_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t13_

_t13_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t14_

_t14_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t15_

_t15_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t16_

_t16_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t17_

_t17_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t18_

_t18_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_
    start timer _t19_

_t19_ task:
    pick one of _e1_ _e2_ _e3_ _e4_ _e5_ _e6_ _e7_ _e8_ _e9_ _e10_ _e11_ _e12_

_e1_ task:
    say 1013

_e2_ task:
    say 1014

_e3_ task:
    say 1015

_e4_ task:
    say 1016

_e5_ task:
    say 1017

_e6_ task:
    say 1018

_e7_ task:
    say 1019

_e8_ task:
    say 1020

_e9_ task:
    say 1021

_e10_ task:
    say 1022

_e11_ task:
    say 1023

_e12_ task:
    say 1024

_victory_ task:
    say 1025
    stop timer _t12_
    stop timer _t13_
    stop timer _t14_
    stop timer _t15_
    stop timer _t16_
    stop timer _t17_
    stop timer _t18_
    stop timer _t19_
--    change repute with _dummynec_ by -5

_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _tri_
    end quest

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    clear _qgclicked_ _clearclick_

_timer_ task:
    say 1003
    end quest