-- Cartographers Quest
Quest: JHCG029
DisplayName: Орсиниум
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG029

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG029

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG029

QuestFail:  [1003]
<ce> Картографы запросили ваш отчет
<ce> относительно Орсиниума к этому времени.

QuestComplete:  [1004]
<ce> Вы серьезно? Дайте-ка мне посмотреть карту.
<ce> %oth! Вы единственный
<ce> за весь год, кто действительно сделал карту
<ce> Орсиниума. Это заслуживает особой похвалы,
<ce> настолько, насколько это в моих силах.
<ce> Примите свой гонорар и мое восхищение. Вы
<ce> сделали то, к чему очень немногие относятся серьезно.

Message:  1010
<ce> Пожилой паж, улыбаясь, протягивает вам
<ce> развернутый свиток.
   <--->
<ce> "Письмо от картографов, %ra".
<ce> Паж протягивает вам свиток, который вы
<ce> кладете в свой рюкзак.

Message:  1011
  Следопыт,

  настала ваша очередь составить карту от города Орсиниум
  досюда. Мы установили лимит времени =timer_ дней. Обратитесь
  ко мне, если вам нужны указания, как добраться.

                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Значит, вы действительно собираетесь попробовать, а,
<ce> %pcf? Это достойно восхищения. Большинство людей, увидев
<ce> фразу "город Орсиниум", просто сжигают письмо. Я знаю
<ce> это не понаслышке. Я скажу вам прямо сейчас, что легкого
<ce> пути туда нет. Ни одного. Долгий, но проверенный путь
<ce> начинается с Лачуги Старой Эвелории в регионе Орсиниум,
<ce> откуда вы идете на север Обливион знает сколько
<ce> километров. Также нужно будет сместиться на восток,
<ce> так что будьте начеку. Если люди начнут говорить вам,
<ce> что вы в Вротгарианских горах, повернитесь и посмотрите
<ce> внимательнее. Старожилы говорят, что проще начать из
<ce> Усадьбы Букингинг и идти на восток-юго-восток, но по мне
<ce> так это полная ерунда. Нет никаких свидетельств, что
<ce> нужно идти в ту сторону. Просто помните, что это почти
<ce> прямо на север от Лачуги Старой Эвелории и немного ниже
<ce> по широте, чем Мидпат Хилл, и не совсем в Вротгарианских
<ce> горах. Если все остальное не получится, просто помните,
<ce> что это небольшая цивилизованная на вид деревня, так
<ce> что заберитесь на холм и используйте телескоп.
<ce> Рано или поздно вы увидите здания, если просто оглянетесь
<ce> вокруг. %god поможет вам найти его, %pcf. Похоже, больше
<ce> никому это не удастся. Я и ваш гонорар ждем вас здесь.

Message:  1013
<ce> После очень долгого похода вы видите перед собой Орсиниум.
   <--->
<ce> Перед вами лежит легендарный затерянный город Орсиниум.
<ce> По крайней мере, вам так кажется.

Message:  1030
%qdt:
 Картографы поручили мне
 отправиться в город Орсиниум
 и составить карту до ___place_
 в ____place_. _qg_
 также может дать мне указания.

Message:  1031
%qdt:
 Согласно _qg_,
 я влип по уши. Город Орсиниум
 находится далеко на севере и немного
 к востоку от Лачуги Старой Эвелории.


Message:  1099
<ce> Вы храбрая душа, если решили попробовать составить
<ce> карту Орсиниума. Если хотите, просто подождите,
<ce> пока время задания не истечет, а затем
<ce> вернитесь к нам. Никто не будет винить
<ce> вас за то, что вы так поступили.

QBN:
Item _reward_ gold range 700 to 799
Item _letter_ letter used 1011

Place _dung_ permanent OrsiniumCity
Place _place_ remote tavern

Clock _timer_ 65.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest