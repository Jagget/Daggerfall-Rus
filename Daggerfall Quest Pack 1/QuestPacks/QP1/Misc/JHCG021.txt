-- Cartographers Quest
Quest: JHCG021
DisplayName: Гробница Неизвестного...
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG021

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG021

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG021

QuestFail:  [1003]
<ce> Срок вашего задания на разведку
<ce> безымянной гробницы истек.

QuestComplete:  [1004]
<ce> Не то, что вы ожидали, правда, Следопыт?
<ce> Мы, картографы, делаем разные карты для разных целей. 
<ce> То место, куда мы только что отправили вас,
<ce> не появится на обычных картах, это я могу сказать точно.
<ce> Получите свою плату и посмотрите, поверит ли кто-нибудь
<ce> в то, чему вы были свидетелем.

Message:  1010
<ce> Вы понимаете, что сообщение попало 
<ce> в ваши вещи незаметно для вас.
   <--->
<ce> В вашей сумке лежит письмо, на котором стоит
<ce> печать компаса Картографов.

Message:  1011
  Следопыт,
 
  У нас есть задание, которое потребует от вас разведать  
  место, которое я сейчас не могу назвать. Пожалуйста, обратитесь 
  ко мне за указаниями, как туда добраться. Все, что я могу 
  сказать, это гробница, которая имеет большое значение 
  для жителей Хай Рока. На это задание у вас 
  есть =timer_ дней.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Следопыт, приятно вас встретить. Мы отправляем вас
<ce> в Меневию. Начните с деревни Весткасл.
<ce> От Дома Дибеллы идите прямо на юг.
<ce> Ваш пункт назначения будет виден после второго холма, 
<ce> который вы пересечете. Это небольшое здание из кирпича
<ce> со странной на вид крышей, которая простирается над ним,
<ce> как навес. Загляните внутрь, если хотите, 
<ce> любопытства ради, но я призываю к осторожности. Мы уже
<ce> потеряли достаточно людей в этой пещере. Я хочу
<ce> получить от вас весточку до истечения =timer_ дней.

Message:  1013
<ce> Должно быть, это и есть та самая таинственная гробница.
   <--->
<ce> Вы видите перед собой гробницу. Теперь нужно
<ce> вернуться к _qg_.

Message:  1030
%qdt:
 Картографы послали меня
 разведать безымянную гробницу. 
 Я должен поговорить с _qg_ 
 в ___place_ в ____place_, 
 чтобы узнать, где она находится.
 У меня есть =timer_ дней, 
 чтобы увидеть ее и вернуться.

Message:  1031
%qdt:
 Картографы проинструктировали меня,
 как найти гробницу. Мне следует
 начать в деревне Весткасл 
 в Меневии. Начиная от Дома
 Дибеллы, я должен пройти на юг через 
 два холма. Передо мной должна быть гробница - 
 небольшое кирпичное здание.

Message:  1099
<ce> Это место должно быть довольно легко найти.
<ce> Не знаю точно, но там не более нескольких сотен
<ce> шагов до выхода. Следуйте указаниям 
<ce> и вернитесь сюда, когда найдете его.

QBN:
Item _reward_ gold range 700 to 799
Item _letter_ letter used 1011

Place _dung_ permanent LysandusTomb1
Place _place_ remote house2

Clock _timer_ 70.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest