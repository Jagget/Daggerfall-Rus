-- Perpetual Quest
Quest: JHPQB01
DisplayName: Забег к Даэдра
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHPQB01

RefuseQuest:  [1001]
<ce> ERROR 1001 JHPQB01

AcceptQuest:  [1002]
<ce> ERROR 1002 JHPQB01

QuestFail:  [1003]
<ce> ERROR 1003 JHPQB01

QuestComplete:  [1004]
<ce> Даэдра убита, Охма Инфиниум падает на землю.
<ce> Теперь его сила принадлежит вам.

Message:  1010
<ce> Паж спокойно предлагает вам письмо и уходит.

Message:  1011
%pcn, %ra на службе Уриэля Септима VII
<ce>
  Прошу простить мою бестактность. Император говорил со мной
 о вас, и у меня есть некоторая информация, которую я могу
предложить в качестве помощи. Если то, что здесь изложено,
не принесет вам пользы, пожалуйста, не обращайте внимания
и забудьте, что я вас побеспокоил.
<ce>
  В области ____dung_ в глуши лежат некоторые руины
по имени ___dung_. Это место было использовано в качестве
логова одним порождений Обливиона, даэдра-лордом
по имени =enemy_. Само по себе это не заслуживает столь
пристального внимания, если бы не тот факт, что другой
агент в этом регионе сообщил нам, что враг обладает
артефактом огромной силы под названием Охма Инфиниум.
Из короткого разговора выяснилось, что враг проведет
в пещере еще три месяца, а когда выйдет, намерен забрать
с собой Охма Инфиниум.
<ce>
  Мое участие в этом деле заключается в том, чтобы
проинформировать вас о существовании этого предмета и убедить
использовать все возможные средства для его извлечения.
Несомненно, это будет большим подспорьем для вас.
<ce>
  Это письмо испарится через неделю после доставки, так что
я прошу вас записать всю полезную информацию. Карта
к ___dung_ прилагается на обороте этой страницы.
<ce>
                Самые теплые приветствия от Императора,
                Великий Рыцарь

Message:  1012
<ce> По словам Великого Рыцаря, в этой пещере
<ce> обитает =enemy_. Охма Инфиниум ждет меня.

Message:  1013
<ce> =enemy_ просто смеется над вашим вторжением.
<ce> Есть только один способ забрать Охма Инфиниум.

QBN:
Item _reward_ artifact Oghma_Infinium
Item _letter_ letter used 1011

Place _dung_ remote dungeon

Clock _timer_ 7.00:00
Clock _fail_ 90.00:00
Clock _offer_ 9.00:00 22.00:00

Foe _enemy_ is Daedra_lord

--	Quest start-up:
  start timer _offer_

_offer_ task:
  give pc _letter_ notify 1010
  have _letter_ set _read_
  pc at _dung_ set _inside_

_read_ task:
  start timer _timer_
  start timer _fail_
  place foe _enemy_ at _dung_
  reveal _dung_

_timer_ task:
  take _letter_ from pc

_fail_ task:
  end quest

_inside_ task:
  say 1012

_inj_ task:
  injured _enemy_
  clicked foe _enemy_
  say 1013
  change foe _enemy_ infighting true

_kill_ task:
  killed _enemy_
  give pc _reward_
  make _reward_ permanent
  end quest