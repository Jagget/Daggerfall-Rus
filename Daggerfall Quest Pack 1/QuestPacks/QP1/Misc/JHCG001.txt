-- Cartographers Quest
Quest: JHCG001
DisplayName: Все кладбища проверены
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG001

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG001

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG001

QuestFail:  [1003]
<ce> На составление карты ___grav_ для Картографов
<ce> вам было дано =timer_ дней, которые уже
<ce> закончились.

QuestComplete:  [1004]
<ce> А, %pcf. Позвольте мне взглянуть на вашу
<ce> карту. Ну, в конце концов, мы были не так
<ce> уж далеко! Благодарю вас за эту
<ce> информацию. Мы выделили для вас скромную
<ce> награду. Продолжайте, рекрут. В будущем
<ce> нам будет что предложить вам. Пока мы
<ce> недостаточно вам доверяем.

Message:  1010
<ce> Паж обращается к вам. "Эй! %pcf, верно?
<ce> У Картографов есть для вас сообщение!"
<ce> Он вручает вам письмо и уходит.
   <--->
<ce> Паж обращается к вам. "Эй! %pcf, верно?
<ce> У Картографов есть для вас сообщение!"
<ce> Она вручает вам письмо и уходит.

Message:  1011
_place_
___place_, ____place_
<ce>
  %pcn,
<ce>
  Мы нуждаемся в ваших услугах. Мы должны нанести на карту
  расстояние от __grav_ до ___place_. Я могу отметить его
  примерное расположение на вашей карте, если вы встретитесь
  со мной. Нам нужно, чтобы это было сделано за =timer_ дней.
<ce>
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов

Message:  1012
<ce> %pcf! Как приятно вас видеть. Кажется,
<ce> перед нами стоит еще одна простая
<ce> задача. Мы заплатим вам, как всегда.
<ce> Наберитесь терпения, и вы сможете
<ce> посетить более экзотические места.
<ce>
<ce> Как уже было сказано, __grav_ — это место, которым
<ce> мы пренебрегали дольше, чем следовало бы.
<ce> Я покажу его примерное местоположение на вашей
<ce> карте. Нам нужно, чтобы вы предоставили нам
<ce> точные координаты и расстояние в пути.
<ce> Встретимся здесь, как только составите карту.

Message:  1013
<ce> Похоже, это __grav_. Невероятно,
<ce> что кладбище могло быть забыто, но ведь
<ce> Картографы существуют не просто так.
   <--->
<ce> Насколько вы можете судить, это кладбище,
<ce> которое _qg_ велели вам найти. Теперь нужно
<ce> сообщить %g2 о местонахождении кладбища.

Message:  1030
%qdt:
 Картографы дали мне задание
 составить карту __grav_
 в ____place_. Я должен доставить свой
 отчет _qg_ за =timer_ дней или
 меньше. Я также могу обратиться к н%g2
 в ___place_, чтобы узнать дорогу.

Message:  1099
<ce> __grav_ — это затерянная локация
<ce> здесь, в %crn. Мы доверяем вам
<ce> посетить его и сообщить нам
<ce> подробности о его местонахождении.

QBN:
Item _reward_ gold range 150 to 200
Item _letter_ letter used 1011

Place _grav_ remote graveyard
Place _place_ remote tavern

Clock _timer_ 24.00:00 38.00:00

Person _qg_ group Librarian

--	Quest start-up:
          start timer _timer_
          give pc _letter_ notify 1010
          place npc _qg_ at _place_
          pc at _grav_ set _inside_
          have _letter_ set _log_

_log_ task:
    log 1030 step 0

_qgclicked_ task:
	clicked npc _qg_

_begin_ task:
     when _qgclicked_ and not _talked_ and not _found_
     say 1012
     reveal _grav_
     clear _qgclicked_
     start task _talked_

variable _talked_
_inside_ task:
     start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT1
    give pc _reward_
    end quest

_timer_ task:
         say 1003
         start quest JHCGFAIL
         end quest