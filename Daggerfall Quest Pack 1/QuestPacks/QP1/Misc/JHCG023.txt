-- Cartographers Quest
Quest: JHCG023
DisplayName: Башня Диренни
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG023

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG023

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG023

QuestFail:  [1003]
<ce> Вам уже нужно было отправиться в Башню Диренни.

QuestComplete:  [1004]
<ce> Говорят, что Башня Диренни - одно из самых старых
<ce> зданий во всем Тамриэле. Ну, так
<ce> говорят. Кто знает, насколько это правда. Надеюсь, что
<ce> вам понравилось живописное путешествие, Следопыт.
<ce> Держите свой гонорар за задание.

Message:  1010
<ce> Курьер вручает вам свиток.
<ce> "От Картографов", - говорит он,
<ce> перед тем как отправиться по делам.
   <--->
<ce> Курьер вручает вам свиток.
<ce> "От Картографов", - говорит она,
<ce> перед тем как отправиться по делам.

Message:  1011
  Следопыт,
<ce>
  Нам нужна карта от Башни Диренни до этого места. Это
  находится на Острове Балфиера. Это довольно простое
  путешествие, если вы знаете, куда ехать. Если нет,
  приходите ко мне и я сориентирую вас, как смогу. Мы даем
  вам =timer_ дней.
<ce>
                _place_
                ___place_, ____place_
                _qg_
                Ассистент Мастера
                Ассоциация Картографов

Message:  1012
<ce> Очень хорошо, Следопыт. Покажите мне свою карту.
<ce> Посмотрите сначала на Остров Балфиера. Там есть один
<ce> город в центре самого большого массива суши, Сингбери.
<ce> Начните оттуда и двигайтесь на восток. Дальше это будет
<ce> просто дикая местность. Увидеть саму башню должно быть
<ce> не так уж трудно. Вся остальная растительность,
<ce> как правило, находится довольно низко к земле. Приходите
<ce> сюда после этого и представьте свой отчет. Желаю удачи.

Message:  1013
<ce>Это, должно быть, Башня Диренни.
   <--->
<ce> Птицы кружат вокруг этой огромной каменной башни.
<ce> Должно быть, это то, за чем вас послали картографы.

Message:  1030
%qdt:
 _qg_, из ___place_
 в ____place_, отправил меня в Башню
 Диренни, чтобы нарисовать карту. У меня есть
 =timer_ дней, чтобы сделать это.

Message:  1031
%qdt:
 Чтобы добраться до Башни Диренни, я должен начать
 в Сингбери и пойти на восток. Нужно просто
 долго идти, пока я не увижу ее.

Message:  1099
<ce> Это огромная каменная башня,
<ce> сильно контрастирующая с окружающей
<ce> местностью. Она должна выделяться довольно четко.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent DirenniTowerEntry
Place _place_ remote house2

Clock _timer_ 60.00:00 65.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest