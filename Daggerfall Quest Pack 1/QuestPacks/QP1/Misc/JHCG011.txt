-- Cartographers Quest
Quest: JHCG011
DisplayName: Курган
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG011

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG011

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG011

QuestFail:  [1003]
<ce> Картографы ожидали, что к этому времени
<ce> вы уже разведаете дорогу к Кургану Скург.

QuestComplete:  [1004]
<ce> Большинство жителей ____place_ считают
<ce> некромантов всего лишь выдумкой. Но Курган
<ce> Скург существует, и вы - живое тому доказательство.
<ce> Мало-помалу наша работа приносит правду в Тамриэль.
<ce> Получите эту награду в обмен на свой отчет.

Message:  1010
<ce> Паж называет ваше имя и заталкивает письмо
<ce> в ваш рюкзак, после чего называет другое имя
<ce> и заталкивает письмо в рюкзак этого человека.
   <--->
<ce> В ваш рюкзак попало письмо.
<ce> На нем печать Картографов - компас.

Message:  1011
  %pcf,
<ce>
  Мы иногда требуем, чтобы наши скауты отправлялись в самые
  отдаленные уголки Тамриэля для выполнения заданий. Сегодня
  как раз один из таких дней. Нам нужно, чтобы вы отправились
  к Кургану Скург, а затем в ___place_. Если вам нужны указания,
  загляните сначала ко мне. На это у нас есть =timer_ дней.
<ce>
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов
                ___place_, ____place_

Message:  1012
<ce> %pcf, я рад вас видеть. Вы найдете Курган Скург
<ce> в горах Драконьего Хвоста. Это легендарное место благодаря
<ce> сказкам, которые рассказывают о некромантах, живущих там.
<ce>
<ce> Если вы не сможете найти его сами, начните с Амбара Маагтам.
<ce> Отойдите от него точно на юго-юго-восток и продолжайте
<ce> движение. Это направление должно привести вас прямо
<ce> к Кургану. Внимательно смотрите вокруг. Если вам нужен
<ce> дополнительный ориентир, то это прямо к югу от Кровоточащей
<ce> Души Аркея, хотя и гораздо дальше к северу. Не ходите на юг
<ce> дальше Македы. Курган будет выглядеть как черный куб,
<ce> увенчанный шпилем с небольшой башней на каждом углу.
<ce> Как только вы его найдете, возвращайтесь.
<ce> И помните: =timer_ дней! Я буду ждать вас здесь.

Message:  1013
<ce> Вы видите перед собой Курган Скург.
<ce> Теперь предстоит отправиться в ___place_.
   <--->
<ce> Перед вами раскинулся Курган Скург.
<ce> _qg_ ждет вашего доклада.

Message:  1030
%qdt:
 _qg_ говорят, что мне нужно найти
 в Курган Скург, чтобы нанести на карту
 расстояние до ___place_, что
 в ____place_. У меня есть =timer_ дней,
 чтобы поехать туда, а затем
 доложить %g2.

Message:  1031
%qdt:
 От _qg_ мне даны следующие
 инструкции, как добраться до Кургана Скург.
 Начиная с Амбара Маагтам,
 я должен идти точно на юго-юго-восток,
 что в конечном итоге должно привести меня туда.
 Курган находится непосредственно к югу от храма
 под названием Кровоточащая Душа Аркея. Мне также
 не следует идти дальше на юг, чем Македа.

Message:  1099
<ce> Курган Скург ждет.
<ce> Не беспокойтесь, вам ничего не грозит,
<ce> если вы не решитесь войти внутрь.

QBN:
Item _reward_ gold range 1000 to 1099
Item _letter_ letter used 1011

Place _dung_ permanent ScourgBarrowEntry
Place _place_ remote tavern

Clock _timer_ 80.00:00 85.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT3
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL2
    end quest