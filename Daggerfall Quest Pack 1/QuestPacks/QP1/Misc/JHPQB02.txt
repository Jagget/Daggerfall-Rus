-- Perpetual Quest
Quest: JHPQB02
DisplayName: Забег к вампиру
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHPQB02

RefuseQuest:  [1001]
<ce> ERROR 1001 JHPQB02

AcceptQuest:  [1002]
<ce> ERROR 1002 JHPQB02

QuestFail:  [1003]
<ce> ERROR 1003 JHPQB02

QuestComplete:  [1004]
<ce> Вампир убит, Амулет Некроманта падает на землю.
<ce> Теперь его сила принадлежит вам.

Message:  1010
<ce> Паж спокойно предлагает вам письмо и уходит.

Message:  1011
%pcn, %ra на службе Уриэля Септима VII

  Прошу простить мою бестактность. Император говорил со мной
о вас, и у меня есть некоторая информация, которую я могу
предложить в качестве помощи. Если то, что здесь изложено,
не принесет вам пользы, пожалуйста, не обращайте внимания
и забудьте, что я вас побеспокоил.

  В регионе ____dung_ в глуши лежат некие руины
под названием ___dung_. Это место стало логовом для одного
из ошибок природы, Древнего вампира по имени =enemy_.
Само по себе это не заслуживает столь пристального
внимания, если бы не тот факт, что враг обладает
артефактом огромной силы под названием Амулет
Некроманта. Наша информация позволяет предположить,
что враг проведет в пещере еще три месяца, а когда
уйдет, то намерен забрать амулет с собой.

  Мое участие в этом деле заключается в том,
чтобы проинформировать вас о существовании этого объекта
 и убедить использовать все возможные средства для
 его извлечения. Несомненно, он окажет вам большую помощь.

  Это письмо испарится через неделю после доставки, так что
я прошу вас записать всю полезную информацию. Карта
к ___dung_ прилагается на обороте этой страницы.

                Самые теплые приветствия от Императора,
                Великий Рыцарь

Message:  1012
<ce> По словам Великого Рыцаря, в этой пещере
<ce> обитает =enemy_. Амулет Некроманта ждет меня.

Message:  1013
<ce> =enemy_ просто смеется над вашим вторжением.
<ce> Есть только один способ забрать Амулет Некроманта.

QBN:
Item _reward_ artifact Necromancers_Amulet
Item _letter_ letter used 1011

Place _dung_ remote dungeon

Clock _timer_ 7.00:00
Clock _fail_ 90.00:00
Clock _offer_ 9.00:00 22.00:00

Foe _enemy_ is Ancient_vampire

--	Quest start-up:
  start timer _offer_

_offer_ task:
  give pc _letter_ notify 1010
  have _letter_ set _read_
  pc at _dung_ set _inside_

_read_ task:
  start timer _timer_
  start timer _fail_
  place foe _enemy_ at _dung_
  reveal _dung_

_timer_ task:
  take _letter_ from pc

_fail_ task:
  end quest

_inside_ task:
  say 1012

_inj_ task:
  injured _enemy_
  clicked foe _enemy_
  say 1013
  change foe _enemy_ infighting true

_kill_ task:
  killed _enemy_
  give pc _reward_
  make _reward_ permanent
  end quest