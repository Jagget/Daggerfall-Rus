-- Cartographers Quest
Quest: JHCG025
DisplayName: Шабаш Врот
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG025

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG025

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG025

QuestFail:  [1003]
<ce> Картографы поручили вам
<ce> составить карту Шабаша Врот к этому времени.

QuestComplete:  [1004]
<ce> Молодец, %pcf. Поначалу найти такие вещи
<ce> всегда непросто, но как только вы это сделаете,
<ce> то сможете продолжать выполнять задание с полной
<ce> уверенностью. Возможно, у нас никогда не закончатся
<ce> задания по составлению карт, даже если вы будете
<ce> отправляться в одни и те же места снова и снова.

Message:  1010
<ce> Мужчина, носящий на одежде герб
<ce> Картографов, вручает вам письмо
<ce> и стремительно уходит.
   <--->
<ce> Женщина, носящая на одежде герб
<ce> Картографов, вручает вам письмо
<ce> и стремительно уходит.

Message:  1011
  Следопыт,

  Вы нам нужны, чтобы составить карту Шабаша Врот,
  в Вротгарианских горах. Я нахожусь
  в ___place_ на случай, если вам понадобятся инструкции.
  На выполнение этого задания у вас есть =timer_ дней.

                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Конечно, я помогу вам, %pcf.
<ce> Начните с деревни Дирвилль во Вротгарианских горах.
<ce> А именно, встаньте рядом с магазином
<ce> под названием Дирвилль: Наилучший Универмаг
<ce> и идите на восток. Если вы будете точно
<ce> следовать этому направлению, то пройдете прямо
<ce> над каменным кругом Шабаша Врот. Поблизости нет никаких
<ce> ориентиров или холмов, так что, к сожалению,
<ce> это все указания, что я могу вам дать.
<ce> Добавлю, что это не дальше, чем в двух километрах отсюда.
<ce> Не стоит идти до Скайрима в поисках его.
<ce> Мы дали вам =timer_ дней
<ce> на выполнение этого задания.

Message:  1013
<ce> Это, должно быть, Шабаш Врот.
   <--->
<ce> Вы нашли Шабаш Врот. Теперь самое
<ce> время отправиться к _qg_.

Message:  1030
%qdt:
 Картографы отправили меня
 в Шабаш Врот во Вротгарианских Горах.
 У меня есть =timer_ дней, чтобы найти
 его и прибыть в ___place_
 в ____place_ за своей наградой.

Message:  1031
%qdt:
 _qg_ сказал, что я должен поехать в Дирвилль,
 магазин "Дирвилль: Наилучший Универмаг", а затем
 просто идти на восток. Шабаш - это каменный круг
 посреди зелени не далее
 чем в двух километрах от деревни.

Message:  1099
<ce> Его не всегда легко найти, так как там
<ce> нет никаких ориентиров, кроме Дирвилля.
<ce> Следите за зазубренными камнями в Шабаше.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent WrothCoven
Place _place_ remote tavern

Clock _timer_ 70.00:00 80.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest