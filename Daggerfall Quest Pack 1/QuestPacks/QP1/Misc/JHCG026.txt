-- Cartographers Quest
Quest: JHCG026
DisplayName: Замок Некромоган
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG026

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG026

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG026

QuestFail:  [1003]
<ce> Ваше задание разведать Замок
<ce> Некромоган уже закончилось.

QuestComplete:  [1004]
<ce> Очень хорошо, Следопыт. Вы заглянули внутрь? Другие
<ce> рассказывали мне об ужасных стонах, которые они слышат
<ce> из его глубин. Я с удовольствием поручу другим работать
<ce> над этой тайной, даже если это будет оплачиваться из
<ce> моей собственной зарплаты. Кстати, о зарплате, вот она.

Message:  1010
<ce> "Письмо, %pcf! Картографы!" Курьер
<ce> радостно вкладывает вам в руки письмо, после чего
<ce> поворачивается и уходит.
   <--->
<ce> Курьер машет вам рукой. "Привет, %ra! Вы ведь
<ce> тот Следопыт с Картографами, верно?
<ce> Это для вас!" Вы получаете письмо.

Message:  1011
  Следопыт,
<ce>
  У нас есть задание, которое требует соблюдения некоторого
  этикета. Пожалуйста, особо не разглашайте условия
  этого задания. Нам нужно отправить вас в Замок
  Некромоган в Даггерфолле. Если вы не понимаете значения
  этого, не спрашивайте. У вас есть =timer_ дней,
  чтобы принести свой отчет. Спросите меня, если вам нужны
  указания. Не забудьте в этом случае заглянуть внутрь,
  чтобы предоставить нам максимально точную карту.
<ce>
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Прежде чем мы начнем, %pcf, я должен еще раз подчеркнуть
<ce> глубокую важность того, чтобы никто не знал,
<ce> что вы собираетесь посетить это место. Местоположение
<ce> Замка Некромоган не предназначено для широкой публики.
<ce> Мы нанесли его на некоторые карты, но не на все.
<ce>
<ce> Чтобы туда добраться, я рекомендую начать с Поющего
<ce> Провидца Кинарет в районе Даггерфолла. Прижмитесь
<ce> к самому северо-западному дому в тамошней маленькой
<ce> общине. Затем идите прямо на север. Это небольшой поход,
<ce> но он должен быть тайным. Ищите курган в земле с ветхой
<ce> дверью, лежащий перед небольшим прудом. Никакого замка
<ce> там нет. Моя теория заключается в том, что такое название
<ce> призвано заставить людей пойти искать настоящий большой
<ce> замок и оставить это место в покое. Но в любом случае,
<ce> этих инструкций будет достаточно. В качестве альтернативы
<ce> вы можете начать со Старой Лачуги Йеомсли и идти на
<ce> восток или с Благословенного Вознесения Кинарет и идти на
<ce> запад. Замок Некромоган находится там, где встречаются
<ce> все эти точки. И помните, что мы дали вам =timer_ дней.

Message:  1013
<ce> Вы нашли Замок Некромоган. Это название гораздо
<ce> более оправдано под землей, чем над ней.
   <--->
<ce> Как ни странно, это, должно быть, Замок Некромоган.

Message:  1030
%qdt:
 Меня послали составить карту
 для Картографов от Замка
 Некромоган до ___place_
 в ____place_. Я могу пойти туда или попросить
 у _qg_ подсказки. Мне
 нужно войти в замок, чтобы
 составить полноценную карту.

Message:  1031
%qdt:
 Рекомендуется начать
 с Поющего Провидца Кинарет
 в регионе Даггерфолл, у дома,
 расположенного дальше всего на северо-западе, и идти
 прямо на север. Замок Некромоган
 будет представлять собой небольшой курган в земле.
 Он также находится там, где встречаются линии
 от Поющего Провидца Кинарет, Старой Лачуги
 Йеомсли и Благословенного Вознесения Кинарет.

Message:  1099
<ce> Замок Некромоган должен
<ce> быть труднодоступным. Посмотрите, где сходятся эти
<ce> три точки. Вы заметите.

QBN:
Item _reward_ gold range 600 to 699
Item _letter_ letter used 1011

Place _dung_ permanent Castle_Necromoghan
Place _place_ remote tavern

Clock _timer_ 65.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest