Quest: treefellercycle
DisplayName: Работа дровосеком
-- Message panels
QRC:

Message:  1011
<ce> Вы хотите нарубить немного дров (ПРИНЯТЬ),
<ce> выкинуть топор (ИСКЛ.)
<ce> или отменить это предложение (ОТМЕНА)?

Message:  1012
<ce> Вы убираете топор.

Message:  1013
<ce> Вы выбрасываете топор.
<ce> Возможно, кто-то другой будет
<ce> зарабатывать с его помощью.

Message:  1014
 Я ношу с собой топор дровосека.
 С его помощью я могу рубить дрова за деньги.

Message:  1015
<ce> Если вы надеетесь заготовить дрова, то должны
<ce> хотя бы находиться на свежем воздухе.

Message:  1016
<ce> По той или иной причине топора дровосека
<ce> у вас больше нет. Вам придется найти
<ce> какую-нибудь другую работу.

Message:  1017
<ce> В пустыне мало древесины. Плохое владение
<ce> топором и отсутствие поблизости деревьев
<ce> замедляют работу. Когда вы нашли клиента,
<ce> вы продали то немногое, что смогли
<ce> заготовить всего за _desertreward1_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1018
<ce> В пустыне мало древесины. Вы
<ce> достаточно хорошо владеете топором,
<ce> но здесь не так много запасов.
<ce> Найдя клиента, вы продали
<ce> заготовленные дрова за _desertreward2_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1019
<ce> В пустыне мало древесины. Вы умело
<ce> владеете топором и быстро справляетесь
<ce> с ограниченным запасом. Когда вы нашли
<ce> клиента, вы продали заготовленные
<ce> дрова за _desertreward3_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1020
<ce> В пустыне мало древесины. Вы очень умело обращаетесь
<ce> с топором и быстро рубите те немногие деревья,
<ce> которые можете найти. Вы продали заготовленные
<ce> дрова первому встречному клиенту за _desertreward4_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1021
<ce> Поблизости много деревьев, которые можно
<ce> заготовить. Ваше плохое владение топором
<ce> замедляет прогресс. Когда вы нашли клиента,
<ce> вы продали заготовленные дрова за _reward1_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1022
<ce> Поблизости много деревьев, которые можно заготовить.
<ce> Вы достаточно хорошо владеете топором и добиваетесь
<ce> хороших успехов. Когда вы нашли клиента, желающего
<ce> купить заготовленные дрова, вы продали их за _reward2_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1023
<ce> Поблизости много деревьев, которые
<ce> можно заготовить. Вы неплохо владеете
<ce> топором и быстро собираете внушительную
<ce> кучу дров. Найдя клиента, вы продали
<ce> заготовленные дрова за _reward3_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

Message:  1024
<ce> Поблизости есть много деревьев, которые
<ce> можно заготовить. Вы прекрасно владеете
<ce> топором и быстро создаете гору поваленной
<ce> древесины. Когда вы нашли клиента, вы
<ce> продали заготовленные дрова за _reward4_ золотых.
<ce>
<ce> Между попытками рубить дрова должно пройти три часа.

QBN:

Item _axe_ item class 3 subclass 14
Item _desertreward1_ gold range 9 to 20
Item _desertreward2_ gold range 15 to 25
Item _desertreward3_ gold range 20 to 30
Item _desertreward4_ gold range 30 to 40
Item _reward1_ gold range 15 to 25
Item _reward2_ gold range 20 to 35
Item _reward3_ gold range 40 to 55
Item _reward4_ gold range 65 to 75

Place _dung_ permanent DaggerfallCastle
Place _dung2_ permanent SentinelCastle
Place _dung3_ permanent WayrestCastle

Clock _wait_ 3:00

--	Quest start-up:
    log 1014 step 0
    get item _axe_
    pc at _dung_ set _inside1_
    pc at _dung2_ set _inside2_
    pc at _dung3_ set _inside3_
    pc at any fighters set _fightered_
    pc at any tavern set _taverned_
    pc at any house set _homed_
    pc at any shop set _shopped_
    pc at any magery set _maged_
    pc at any temple set _templed_
    pc at any palace set _palaced_
    _axe_ used do _trigger_
    have _axe_ set _possessaxe_

_trigger_ task:
    promptmulti 1011 0:cutwood _attempt_ 9:giveup _endquest_ 2:cancel _cancel_

variable _attempt_

_cancel_ task:
    say 1012
    start task _renewquest_

_endquest_ task:
    say 1013
    end quest

_noeffect_ task:
    when _inside1_ or _inside2_ or _inside3_ or _fightered_ or _taverned_ or _homed_ or _shopped_ or _maged_ or _templed_ or _palaced_

_notoutdoors_ task:
    when _noeffect_ and _attempt_ and not _sold_
    say 1015
    start task _renewquest_

_begincut_ task:
    when _attempt_ and not _noeffect_

_renewquest_ task:
    start quest treefellercycle
    end quest

variable _sold_

_desertoutcome1_ task:
    when _desert_ and _begincut_ and not _skill1_
    say 1017
    get item _desertreward1_
    start timer _wait_
    start task _sold_

_desertoutcome2_ task:
    when _desert_ and _begincut_ and _skill1_ and not _skill2_
    say 1018
    get item _desertreward2_
    start timer _wait_
    start task _sold_

_desertoutcome3_ task:
    when _desert_ and _begincut_ and _skill2_ and not _skill3_
    say 1019
    get item _desertreward3_
    start timer _wait_
    start task _sold_

_desertoutcome4_ task:
    when _desert_ and _begincut_ and _skill3_
    say 1020
    get item _desertreward4_
    start timer _wait_
    start task _sold_

_normaloutcome1_ task:
    when not _desert_ and _begincut_ and not _skill1_
    say 1021
    get item _reward1_
    start timer _wait_
    start task _sold_

_normaloutcome2_ task:
    when not _desert_ and _begincut_ and _skill1_ and not _skill2_
    say 1022
    get item _reward2_
    start timer _wait_
    start task _sold_

_normaloutcome3_ task:
    when not _desert_ and _begincut_ and _skill2_ and not _skill3_
    say 1023
    get item _reward3_
    start timer _wait_
    start task _sold_

_normaloutcome4_ task:
    when not _desert_ and _begincut_ and _skill3_
    say 1024
    get item _reward4_
    start timer _wait_
    start task _sold_

_wait_ task:
    start task _renewquest_

_skill1_ task:
    when skill Axe is at least 15

_skill2_ task:
    when skill Axe is at least 30

_skill3_ task:
    when skill Axe is at least 45

_desert_ task:
    climate base desert

variable _possessaxe_

_noaxe_ task:
    when not _possessaxe_
    say 1016
    end quest

variable _inside1_
variable _inside2_
variable _inside3_
variable _fightered_
variable _taverned_
variable _homed_
variable _shopped_
variable _maged_
variable _templed_
variable _palaced_

