Quest: locationaldaedra
DisplayName: Даэдра (Квест Пак 1)
-- Message panels
QRC:

RumorsDuringQuest:   [1005]
В ___city_ сейчас проблемы с даэдра.
<--->
%god сжалься над ___city_, страдающий от нападения даэдра.
<--->
В ___city_ проблемы с даэдра.
<--->
___city_ наполнен даэдра.

RumorsPostFailure:   [1006]
%rt вычистил даэдра из ___city_.
<--->
%rt пришлось нанять армию, чтобы выгнать даэдра из ___city_.

Message:  1010
<ce> Вы видите предупреждающий знак на столбе по мере приближения к ___city_.
<ce>
<ce> "Путешественники в ___city_,
<ce> новичок-призыватель даэдра открыл портал в Обливион,
<ce> и с тех пор в городе были замечены демоны. Угрозу,
<ce> которую они представляют, невозможно переоценить.
<ce> По возможности оставайтесь дома.
<ce> Я даже сейчас собираю силы, чтобы уничтожить их.
<ce> С уважением,
<ce> %rt, %rn".
<--->
<ce> Кто-то вывесил предупреждение за пределами ___city_.
<ce>
<ce> "Маг-изгой призвал даэдра против этого города. Они нападут на
<ce> любого, кто попадется им на глаза. Бегите или защищайтесь,
<ce> как вам больше подходит. %rt скоро пришлет помощь.
<ce> Оставайтесь дома, пока не поступят дальнейшие известия".

QBN:

Place _city_ remote city

Clock _timer_ 5.00:00 6.00:00

Foe _enemy_ is Fire_daedra
Foe _enemy2_ is Daedroth
Foe _enemy3_ is Frost_daedra

--	Quest start-up:
	start timer _timer_
    pc at _city_ do _inside_

_inside_ task:
    say 1010
    send _enemy_ every 13 minutes with 7% success
    send _enemy2_ every 17 minutes with 9% success
    send _enemy3_ every 11 minutes with 5% success

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ infighting true
    change foe _enemy2_ infighting true
    change foe _enemy3_ infighting true
    clear _daily_

_timer_ task:
    end quest