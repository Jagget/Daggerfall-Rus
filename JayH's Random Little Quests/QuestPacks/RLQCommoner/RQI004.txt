Quest: RQI004
DisplayName: Трюк с удвоением денег
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Приветствую вас, %ra. Есть время для игры?
<ce> Все очень просто. Вы платите 100 золотых, не подлежащих
<ce> возврату, и мы бросаем монету. Если орел, я плачу вам 200.
<ce> Если решка, то оставляю ваши деньги себе. Вы можете
<ce> продолжать удваивать до тех пор, пока выпадает
<ce> орел, но если когда-нибудь выпадет решка, я заберу
<ce> все. Хотите поставить сотню?

RefuseQuest:  [1001]
<ce> Возможно, это мудрый выбор, друг.

QuestComplete:  [1004]
<ce> Тогда ладно. Вот ваши деньги.

QuestorPostSuccess:  [1008]
Вы неплохо разбираетесь в математике!

Message:  1011
<ce> Вы отсчитываете 100 золотых монет и отдаете их.
<ce>
<ce> "Итак, результат... орел. У вас 200. Хотите продолжить?"

Message:  1012
<ce> Вы снова наблюдаете за подбрасыванием монеты. Выпадает орел.
<ce>
<ce> "У вас 400. Продолжаем?"

Message:  1013
<ce> Монета подбрасывается еще раз. Выпадает орел.
<ce>
<ce> "Теперь у вас 800. Продолжать?"

Message:  1014
<ce> Вы наблюдаете за очередным подбрасыванием монеты. 
<ce> Снова выпадает орел.
<ce>
<ce> "%oth, я начинаю жалеть об этом. 1600 в банке. 
<ce> Продолжаем?"

Message:  1015
<ce> Монету подбрасывают еще раз. Выпадает орел.
<ce>
<ce> "Вы знаете, какова вероятность этого? Уже 3200 золотых. 
<ce> Еще раз?"

Message:  1016
<ce> Вы наблюдаете за очередным подбрасыванием монеты. 
<ce> Выпадает орел.
<ce>
<ce> "Я не верю в это. Правда, не верю.
<ce> 6400 золотых для вас. Рискнете всем еще раз?"

Message:  1017
<ce> Монета подбрасывается еще раз. Она падает в вашу пользу.
<ce>
<ce> "Вы собираетесь обчистить меня, %ra.
<ce> Если только вы не проиграете при следующем подбрасывании. 
<ce> Попробуем?"
<ce> В настоящее время у вас на кону 12800 золотых.

Message:  1018
<ce> Еще раз взлетает монета, и еще раз выпадает орел.
<ce>
<ce> "Я в это не верю. Забирайте свои деньги и уходите.
<ce> Каким бы колдовством вы ни занимались, я не хочу здесь 
<ce> находиться. Больше вы не получите ни одного броска.
<ce> Уходите сейчас же".
<ce>
<ce> Вам дают 25600 золотых.
<ce> Очевидно, вам здесь больше делать нечего.

Message:  1019
<ce> (ПРОВЕРКА АТРИБУТА ПРОЙДЕНА: Удача)
<ce>
<ce> Вы уверены, что никто не ожидал участия в игре такого 
<ce> удачливого человека, как вы. Вы выигрываете при первом броске
<ce> монеты, а затем еще и еще.
<ce>
<ce> "Эй, здесь что-то не так. Я не знаю, что вы затеяли,
<ce> но мне это не нравится. Забирайте свои 800 монет 
<ce> и убирайтесь отсюда".

Message:  1020
<ce> Монета подбрасывается, и выпадает решка.
<ce>
<ce> "Извините, %ra, но выигрыш мой, честный и справедливый".
<ce>
<ce> У вас забрали деньги.

Message:  1021
<ce> Вы обыскиваете свои карманы в поисках 100 монет,
<ce> но не находите достаточного количества.
<ce> На этот раз вам придется пропустить.

QBN:

Item _200_ gold range 200 to 200
Item _400_ gold range 400 to 400
Item _800_ gold range 800 to 800
Item _1600_ gold range 1600 to 1600
Item _3200_ gold range 3200 to 3200
Item _6400_ gold range 6400 to 6400
Item _12800_ gold range 12800 to 12800
Item _25600_ gold range 25600 to 25600

--	Quest start-up:
    pay 100 money do _begin_ otherwise do _nomoney_

_lucky_ task:
    when attribute Luck is at least 90

variable _begin_

_beginner_ task:
    when _begin_ and not _lucky_
    start task _choice2_

_instantwin_ task:
    when _begin_ and _lucky_
    say 1019
    get item _800_
    make _800_ permanent
    end quest

_nomoney_ task:
    say 1021
    end quest

_fail_ task:
    say 1020
    end quest

_choice2_ task:
    prompt 1011 yes _branch2_ no _quit1_

_quit1_ task:
    give pc _200_
    end quest

_branch2_ task:
    pick one of _choice3_ _fail_

_choice3_ task:
    prompt 1012 yes _branch3_ no _quit2_

_quit2_ task:
    give pc _400_
    end quest

_branch3_ task:
    pick one of _choice4_ _fail_

_choice4_ task:
    prompt 1013 yes _branch4_ no _quit3_

_quit3_ task:
    give pc _800_
    end quest

_branch4_ task:
    pick one of _choice5_ _fail_

_choice5_ task:
    prompt 1014 yes _branch5_ no _quit4_

_quit4_ task:
    give pc _1600_
    end quest

_branch5_ task:
    pick one of _choice6_ _fail_

_choice6_ task:
    prompt 1015 yes _branch6_ no _quit5_

_quit5_ task:
    give pc _3200_
    end quest

_branch6_ task:
    pick one of _choice7_ _fail_

_choice7_ task:
    prompt 1016 yes _branch7_ no _quit6_

_quit6_ task:
    give pc _6400_
    end quest

_branch7_ task:
    pick one of _choice8_ _fail_

_choice8_ task:
    prompt 1017 yes _branch8_ no _quit7_

_quit7_ task:
    give pc _12800_
    end quest

_branch8_ task:
    pick one of _victory_ _fail_

_victory_ task:
    say 1018
    get item _25600_
    make _25600_ permanent
    end quest