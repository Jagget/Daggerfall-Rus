Quest: RLQguardweapon
DisplayName: СМК: Оружие городской стражи
-- Message panels
QRC:

QuestComplete:  [1004]
<ce> Убедившись, что никто не смотрит, вы берете
<ce> топор. Его крепление хлипкое и легко ломается.
<ce> Так как это ничем не примечательный топор,
<ce> продать его будет довольно просто.

Message:  1010
<ce> Вы видите прикрепленный к стене топор
<ce> и предупреждающую надпись над ним:
<ce>
<ce> "СОБСТВЕННОСТЬ ГОРОДСКОЙ СТРАЖИ. НЕ ТРОГАТЬ".
<ce>
<ce> Хотя он выглядит довольно обычным, вы можете взять
<ce> его, если считаете себя искусным карманником.
<ce> Вы не думаете, что за вами сейчас кто-то наблюдает,
<ce> а туманный воздух служит естественным прикрытием.
<ce> Решитесь ли вы на попытку?

Message:  1011
<ce> Вам не нужен гнев городской стражи.
<ce> Вы игнорируете топор.

Message:  1012
<ce> Вы кладете руки на топор и пытаетесь потянуть.
<ce> Ваши навыки карманника не оправдывают себя,
<ce> поскольку ваши движения очевидны и привлекают
<ce> внимание. Городская стража направляется к вам,
<ce> требуя остановиться.

QBN:

Item _reward_ item class 3 subclass 14

Clock _delay_ 00:10

--	Quest start-up:
    prompt 1010 yes _accept_ no _reject_

_reject_ task:
    say 1011
    end quest

_accept_ task:
    pick one of _go1_ _go2_

_skill1_ task:
    when skill Pickpocket is at least 15

_skill2_ task:
    when skill Pickpocket is at least 20

variable _go1_
variable _go2_

_c1_ task:
    when _skill1_ and _go1_
    start task _success_

_c2_ task:
    when _skill2_ and _go2_
    start task _success_

_d1_ task:
    when not _skill1_ and _go1_
    start task _fail_

_d2_ task:
    when not _skill2_ and _go2_
    start task _fail_

variable _success_
variable _fail_

_finalwin_ task:
    when _accept_ and _success_
    give pc _reward_
    end quest

_finalfail_ task:
    when _accept_ and not _success_
    say 1012
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_

_delay_ task:
    end quest