Quest: RLQimmortal
DisplayName: СМК: Бессмертный
-- Message panels
QRC:

QuestComplete:   [1004]
<ce> Варвар повержен. Никто не живет вечно.

Message:  1010
<ce> Вы слышите чей-то рев разносящийся по округе.
<ce> Варвар кричит людям:
<ce>
<ce> "Хахаха! Я - аватар богов! Никто не сможет победить меня!"
<ce>
<ce> Затем воин начинает безрассудно размахивать топором, пытаясь произвести
<ce> впечатление на жителей. Люди опасаются, что это может обернуться
<ce> трагедией. Попытаетесь ли вы остановить варвара?

Message:  1011
<ce> Вы призываете безумного бойца остановиться,
<ce> но в ответ получаете лишь гнев. Набросившись на вас,
<ce> берсерк кричит: "Надо было оставить меня в покое,
<ce> %ra! Меня не победить!!!"

Message:  1012
<ce> Вы остаетесь в стороне и наблюдаете за тем, как члены городской стражи
<ce> совместными усилиями пытаются усмирить буйного. Потребовалось много
<ce> стражников и поддержка нескольких заклинателей, но им удалось
<ce> арестовать его. Этот парень явно был силен.

QBN:

Item _reward_ gold range 3000 to 5000

Foe _angrybat_ is Barbarian

Clock _ending_ 3:00

--	Quest start-up:

_level_ task:
    level 9 completed
    prompt 1010 yes _pick_ no _ignore_

_pick_ task:
    say 1011
    create foe _angrybat_ every 0 minutes 1 times with 100% success
    change foe _angrybat_ infighting true
    give item _reward_ to _angrybat_
    start timer _ending_

_heal_ task:
    daily from 0:00 to 23:59
    cast Heal spell on _angrybat_
    clear _heal_

_ignore_ task:
    say 1012
    end quest

_notlevel_ task:
    when not _level_
    end quest

_slain_ task:
    killed _angrybat_
    give pc nothing

_ending_ task:
    remove foe _angrybat_
    end quest