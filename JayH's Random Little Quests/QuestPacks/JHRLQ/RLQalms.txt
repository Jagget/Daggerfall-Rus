Quest: RLQalms
DisplayName: СМК: Милостыня для бедных
-- Message panels
QRC:

Message:  1010
%qdt:
 Со слов попрошайки, которому я дал немного денег,
 сегодня ночью в Гильдии Магов будет храниться
 ценный зачарованный _item_.

Message:  1011
<ce> Нищий умоляет прохожих дать ему немного денег.
<ce>
<ce> "Милостыню? Милостыня для бедных? 20 золотых, кто-нибудь?"
<ce>
<ce> Вы предложите ему деньги?
<--->
<ce> Вы видите поблизости нищего,
<ce> выпрашивающего у прохожих деньги.
<ce>
<ce> "Можете ли вы выделить 20 золотых для беднейшего из людей?
<ce> Пожалуйста, мне нужно сегодня поесть".
<ce>
<ce> Вы предложите ему деньги?

Message:  1012
<ce> Вы избегаете нищенствующих.

Message:  1013
<ce> Вы подходите и кладете в руки нищего 20 золотых
<ce> монет. Он восклицает от радости и многократно
<ce> кланяется вам, а затем уходит в
<ce> ближайшую таверну.
<--->
<ce> Вы подходите и кладете в руки нищего 20 золотых
<ce> монет. Он восклицает от радости и многократно
<ce> кланяется вам, а затем уходит в сторону
<ce> близлежащего здания.
<--->
<ce> Вы подходите и кладете в руки нищего 20 золотых
<ce> монет. Он восклицает от радости и многократно
<ce> кланяется вам, а затем продолжает
<ce> просить милостыню у других людей.

Message:  1014
<ce> Несмотря на ваши лучшие намерения, вы не находите
<ce> 20 золотых монет среди своих вещей. На этот раз
<ce> тебе придется отказаться от подачи милостыни.

Message:  1015
<ce> Нищий радуется, а затем наклоняется и шепчет:
<ce>
<ce> "Такому доброму, как вы, следует иметь побольше
<ce> денег. Люди из Гильдии Магов сегодня вечером
<ce> после закрытия спрячут в своем зале какой-то
<ce> зачарованный _item_. Заберите его себе. Не
<ce> думаю, что им он нужен больше, чем вам".
<ce>
<ce> После этого нищий отодвигается и начинает
<ce> просить деньги у других прохожих.

QBN:

Place _magery_ local magery

Item _item_ magic_item

Clock _timer_ 12:00

--	Quest start-up:
    prompt 1011 yes _pay_ no _reject_

_reject_ task:
    say 1012
    end quest

variable _pay_

_payhim_ task:
    when _pay_ and not _given_
    pay 20 money do _given_ otherwise do _nomoney_

_nomoney_ task:
    say 1014
    end quest

_given_ task:
    pick one of _tip_ _end_ _end_ _end_ _end_

_end_ task:
    say 1013
    end quest

_daytime_ task:
    daily from 11:00 to 23:00

_tip_ task:
    say 1015
    log 1010 step 0

_plant_ task:
    when _tip_ and not _daytime_
    place item _item_ at _magery_
    start timer _timer_
    have _item_ set _grab_

_grab_ task:
    make _item_ permanent
    end quest

_timer_ task:
    end quest