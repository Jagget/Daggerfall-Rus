Quest: RLQdungeoncaptive
DisplayName: СМК: Спасти из подземелья
-- Message panels
QRC:

QuestFail:   [1003]
<ce> Несмотря на все ваши усилия,
<ce> _captive_, без сомнения, уже никогда
<ce> не покинет ___dung_.

QuestComplete:  [1004]
<ce> _captive_ с облегчением вдыхает свежий воздух.
<ce>
<ce> "Спасибо, спасибо, спасибо! Эти лабиринты просто ужасны!
<ce> Не знаю, как кто-то из них выбирается! Я позабочусь о том,
<ce> чтобы все узнали, что вы для меня сделали! Все!
<ce> И простой хозяин таверны, и %rt! 
<ce> Да благословит вас %god!"
<ce>
<ce> Зная дальнейший путь отсюда, %g1 отправляется в путь.

Message:  1010
<ce> У ваших ног валяется лист бумаги, на котором написано,
<ce> что разыскивается _captive_, что %g1
<ce> отсутствует уже несколько дней и что последний раз %g4
<ce> видели по пути в подземелье. Поднимете ли вы объявление?

Message:  1011
<ce> Вы берете объявление и внимательно читаете.
<ce> ---
Имя: _captive_
Описание: =captive_
Последнее известное местонахождение: ___dung_.
Слава и доброе имя тому, кто найдет %g4
и спасет из подземелья.
<ce> ---
<ce> Похоже, что у вас на это есть всего лишь =timer_ дней.

Message:  1012
<ce> Судя по описанию, это _captive_.
<ce> Увидев вас, %g1 плачет от радости.
<ce>
<ce> "Наконец-то вы меня нашли!
<ce> Пожалуйста, вытащите меня отсюда!"

Message:  1013
<ce> Наверняка кто-нибудь другой возьмется за это дело.

Message:  1030
%qdt:
 Мне попалось на глаза объявление о поиске человека
 по имени _captive_. Это =captive_. 
 Нужно поискать в ___dung_.
 У меня осталось =timer_ дней.

QBN:

Person _captive_ faction Dancers

Place _dung_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2

--	Quest start-up:
    prompt 1010 yes _accept_ no _quit_

_quit_ task:
    say 1013
    end quest

_accept_ task:
    say 1011
    start timer _timer_
    log 1030 step 0
    place npc _captive_ at _dung_
    pc at _dung_ do _inside_
    reveal _dung_

variable _inside_
_begin_ task:
    clicked npc _captive_
    say 1012
    hide npc _captive_
    add _captive_ face

_transform_ task:
    when _begin_ and not _inside_
    give pc nothing
    legal repute +10
    end quest

_timer_ task:
    say 1003
    end quest