Quest: RLQdefendtown
DisplayName: СМК: Защита города
-- Message panels
QRC:

QuestComplete:   [1004]

Message:  1010
<ce> Вы замечаете три фигуры в капюшонах, разгуливающие
<ce> по окрестностям и замышляющие какой-то беспредел
<ce> под прикрытием тумана. Прежде чем они успевают уйти далеко,
<ce> к ним приближается егерь.
<ce>
<ce> "Стойте! Никаких беспорядков! Сдайте оружие
<ce> или я буду вынужден лишить вас жизни!"
<ce>
<ce> Они не повинуются и готовят оружие к бою. Вмешаетесь
<ce> ли вы, чтобы помочь егерю, или останетесь в стороне?

Message:  1011
<ce> Вы готовы к бою и подаете сигнал егерю,
<ce> что поможете ему. "Спасибо, друг!" - кричит он,
<ce> и битва начинается.

Message:  1012
<ce> Трое преступников повержены. Городские стражники наконец
<ce> находят путь сквозь туман. Они благодарят вас за вашу
<ce> помощь в обеспечении безопасности города.

Message:  1013
<ce> Воспользовавшись туманом, вы скрываетесь
<ce> из виду. Начинается тяжелая битва, и егерь
<ce> падает. Боясь быть пойманными, преступники
<ce> спешат покинуть город.
   <--->
<ce> Вы пользуетесь туманом, чтобы оставаться вне поля
<ce> зрения. Начинается тяжелая битва, и преступники
<ce> падают. После этого прибывают члены городской стражи,
<ce> чтобы поблагодарить егеря за его самоотверженность
<ce> в обеспечении безопасности города.

Message:  1014
<ce> Трое преступников повержены. Городские стражники наконец
<ce> находят путь сквозь туман. Они благодарят вас обоих
<ce> за помощь в обеспечении безопасности города.
<ce> Егерь также выражает вам огромную благодарность.
<ce>
<ce> "Как хорошо, что есть люди, готовые пресечь
<ce> беду в самом ее источнике! Я молюсь, чтобы ваш
<ce> праведный гнев никогда не утихал!"
<ce>
<ce> Закончив дело, егерь исчезает в тумане.
<--->
<ce> Трое преступников повержены. Городские
<ce> стражники наконец находят путь сквозь туман.
<ce> Они благодарят вас обоих за помощь
<ce> в обеспечении безопасности города. Егерь
<ce> тоже благодарит вас и исчезает в тумане.


QBN:

Foe _monster_ is Ranger
Foe _rat1_ is Thief
Foe _rat2_ is Thief
Foe _rat3_ is Thief

Clock _timer_ 1:00

--	Quest start-up:
    prompt 1010 yes _fight_ no _avoid_

_fight_ task:
    say 1011
    create foe _monster_ every 0 minutes 1 times with 100% success
    change foe _monster_ infighting true
    change foe _monster_ team 1
    create foe _rat1_ every 0 minutes 1 times with 100% success
    change foe _rat1_ infighting true
    create foe _rat2_ every 0 minutes 1 times with 100% success
    change foe _rat2_ infighting true
    create foe _rat3_ every 0 minutes 1 times with 100% success
    change foe _rat3_ infighting true
    start timer _timer_
    cast Orc_Strength spell on _monster_
    cast Feet_of_Notorgo spell on _monster_
    cast Shield spell on _monster_

_kill1_ task:
    killed _rat1_

_kill2_ task:
    killed _rat2_

_kill3_ task:
    killed _rat3_

_killall_ task:
    when _kill1_ and _kill2_ and _kill3_

_rangerdead_ task:
    killed _monster_

_goodend_ task:
    when _killall_ and not _rangerdead_
    say 1014
    remove foe _monster_
    legal repute +2

_badend_ task:
    when _killall_ and _rangerdead_
    say 1012
    legal repute +2

_timer_ task:
    end quest

_avoid_ task:
    say 1013
    end quest