Quest: RLQvampiregold
DisplayName: СМК: Вампир и золото
-- Message panels
QRC:

QuestComplete:  [1004]
<ce> Женщина в капюшоне дает вам деньги и убегает,
<ce> пока ее никто не заметил.

Message:  1010
<ce> Вы заметили, что за вами наблюдает женщина в капюшоне.
<ce> Она говорит низким голосом:
<ce>
<ce> "Приветствую вас, незнакомец. У меня есть золото, а у вас -
<ce> кровь. Мы должны обменяться, я настаиваю. Пожалуйста".
<ce>
<ce> Она обнажает клыки и просит покормить ее. За 25 золотых
<ce> вы позволите ей выпить кровь? Или, если хотите, вы
<ce> можете попытаться убить этого вампира.
<ce>
<ce> Что вы сделаете: позволите ей питаться (ПРИНЯТЬ),
<ce> откажетесь (ОТКАЗ) или нападете на вампира (ПРОТИВ)?

Message:  1011
<ce> Она молча отворачивается.

Message:  1012
<ce> С большой осторожностью и быстротой она впивается
<ce> зубами в вашу шею. Вы не чувствуете особой боли,
<ce> пока она пьет из вас кровь.
<ce>
<ce> "Еще? У меня есть еще золото", - говорит она.
<ce>
<ce> За 25 золотых вы позволите ей выпить еще крови?

Message:  1013
<ce> Она снова кормится. На этот раз вы чувствуете
<ce> больше боли, но вам удается выстоять.
<ce>
<ce> Она предлагает еще 25 золотых, если вы снова позволите.

Message:  1014
<ce> Она высасывает еще больше крови, торопясь получить все, что можно.
<ce> На этот раз гораздо больнее, но вы выживаете.
<ce>
<ce> "Почти готово. В последний раз?" - спрашивает она, показывая еще 25 золотых.

Message:  1015
<ce> Боль уже невыносима, но она наконец-то удовлетворена.

Message:  1016
<ce> Вампирша шипит и защищается.

QBN:

Item _reward1_ gold range 25 to 25
Item _reward2_ gold range 50 to 50
Item _reward3_ gold range 75 to 75
Item _reward4_ gold range 100 to 100

Foe _vamp_ is Vampire

Clock _delay_ 1:00

--	Quest start-up:
    promptmulti 1010 0:accept _accept1_ 1:reject _reject_ 11:counter _attack_

_attack_ task:
    say 1016
    create foe _vamp_ every 0 minutes 1 times with 100% success
    change foe _vamp_ infighting true
    start timer _delay_

_delay_ task:
    end quest

_kill_ task:
    killed _vamp_
    legal repute +2

_reject_ task:
    say 1011
    end quest

_accept1_ task:
    reduce player health by 8
    prompt 1012 yes _accept2_ no _reject_

_accept2_ task:
    reduce player health by 12
    prompt 1013 yes _accept3_ no _reject_

_accept3_ task:
    reduce player health by 25
    prompt 1014 yes _acceptfinal_ no _reject_

_pay1_ task:
    when _accept1_ and _reject_ and not _accept2_
    give pc _reward1_
    end quest

_pay2_ task:
    when _accept2_ and _reject_ and not _accept3_
    give pc _reward2_
    end quest

_pay3_ task:
    when _accept3_ and _reject_ and not _acceptfinal_
    give pc _reward3_
    end quest

_acceptfinal_ task:
    reduce player health by 35
    say 1015
    give pc _reward4_
    end quest