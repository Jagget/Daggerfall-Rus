Quest: RLQhousehunt
DisplayName: СМК: Охота в доме
-- Message panels
QRC:

QuestFail:   [1003]
<ce> Тридцать минут истекли, а крыса не умерла.

QuestComplete:  [1004]
<ce> Вы обнаруживаете, что шут ждет вас снаружи.
<ce>
<ce> "Какая радость! Разве это не было чертовски весело?
<ce> Вот, возьмите свои деньги! Это того стоило!"

RumorsDuringQuest:   [1005]
Какой-то идиот спрятал крысу в _house_.
<--->
Да, какая-то крыса забралась в _house_. Я живу рядом.

Message:  1011
<ce> К вам, весело ухмыляясь, неторопливо подходит шут.
<ce>
<ce> "Привет! Ищете развлечений? Я спрятал мерзкую, злобную крысу
<ce> в одном из зданий города. Не скажу где, но если вы сможете
<ce> убить ее за 30 минут, то сказочно заработаете -
<ce>  =reward_ золотых! Хотите попробовать?"

Message:  1012
<ce> Лицо шута вытягивается.
<ce>
<ce> "Фууу. Вы портите мне представление!
<ce> Тогда занимайтесь своими делами!"

Message:  1013
<ce> Он улюлюкает, поднимая обе руки вверх.
<ce>
<ce> "Отлично! Ну, тогда идите, ищите! 30 минут - это все,
<ce> что у вас есть, начиная с этого момента! Может быть,
<ce> поспрашиваете местных, слышал ли кто-нибудь
<ce> какие-нибудь новости? Удачи!"

Message:  1014
<ce> Вам кажется, что внутри этого здания шуршит крыса.

Message:  1016
%qdt:
 Шут из ___house_ бросил мне вызов:
 найти крысу в одном из зданий
 города и убить ее менее чем 
 за 30 минут. Если мне это удастся,
 я получу =reward_ золотых.
 Мне посоветовали узнать новости
 у местных, вдруг они видели крысу.

QBN:

Foe _monster_ is Giant_rat

Clock _timer_ 0:30

Place _house_ local tavern

Item _reward_ gold range 101 to 125

--	Quest start-up:
    prompt 1011 yes _accept_ no _reject_
    start timer _timer_

_reject_ task:
    say 1012
    end quest

_accept_ task:
    say 1013
    log 1016 step 0
    pc at _house_ set _inside_
    place foe _monster_ at _house_

_inside_ task:
    say 1014

_killed_ task:
    killed _monster_
    start task _victory_
    stop timer _timer_

variable _victory_

_getgold_ task:
    when _victory_ and not _inside_
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    end quest