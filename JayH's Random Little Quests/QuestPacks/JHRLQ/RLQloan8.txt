Quest: RLQloan8
DisplayName: СМК: Кредит 8
-- Message panels
QRC:

QuestComplete:   [1004]
<ce> "Э-ге-гей! %pcf! Подождите!"
<ce>
<ce> К вам бодро приближается гонец.
<ce>
<ce> "_merchant_ шлет вам привет и безмерно
<ce> благодарит за вашу доброту. Получите
<ce> обещанные вам деньги".

QuestLogEntry:  [1010]
%qdt:
 У меня взяли в долг 164 золотых -
 _merchant_, что работает в _store_.
 Мне обещали вернуть двойную сумму
 в течение 12 часов.

Message:  1011
<ce> К вам обращается очень нервный человек.
<ce>
<ce> "Прошу прощения, здравствуйте, %ra?
<ce> Меня зовут _merchant_, мне принадлежит
<ce> _store_. Очень приятно. Простите, что
<ce> отнимаю у вас время, но мне необходимо
<ce> спросить. Не могли бы вы временно
<ce> выделить 164 золотых? Мой магазин едва
<ce> не прогорел, а сегодня нужно внести
<ce> очень важный платеж. Я верну вам двойную
<ce> сумму в течение 12 часов, клянусь!"
<ce>
<ce> Вы одолжите 164 золотых?

Message:  1012
<ce> _merchant_ удрученно отворачивается.
<ce>
<ce> "Я не виню вас, %ra, ведь сейчас
<ce> трудные времена. Не волнуйтесь,
<ce> я %g2self разберусь с этим".

Message:  1013
<ce> "О да! Да, да, да! Спасибо, спасибо! Вы меня
<ce> очень выручили. Где вы планируете быть через
<ce> 12 часов? Я вам верну все, что обещал, %god
<ce> покарай меня, если это не так! Я этого не забуду!"

Message:  1014
<ce> Вы роетесь в своих вещах, но не находите
<ce> под рукой 164 золотых. _merchant_
<ce> стонет в отчаянии и благодарит вас за
<ce> потраченное время.

Message:  1015
<ce> 12 часов истекли, а денег вы так и не дождались.
<ce> Похоже, вас обманули.

QBN:

Item _reward_ gold range 328 to 328

Person _merchant_ faction The_Merchants

Place _store_ local generalstore

Clock _timer_ 12:00

--	Quest start-up:
    prompt 1011 yes _pay_ no _reject_

_reject_ task:
    say 1012
    end quest

variable _pay_

_payhim_ task:
    when _pay_ and not _given_
    pay 164 money do _given_ otherwise do _nomoney_

_nomoney_ task:
    say 1014
    end quest

_given_ task:
    pick one of _success_ _success_ _failure_
    start timer _timer_
    say 1013
    change repute with _merchant_ by +2

variable _timer_
variable _success_
variable _failure_

_finalwin_ task:
    when _timer_ and _success_
    give pc _reward_
    end quest

_finalfail_ task:
    when _timer_ and _failure_
    say 1015
    end quest