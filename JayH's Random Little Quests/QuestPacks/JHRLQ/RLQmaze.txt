Quest: RLQmaze
DisplayName: СМК: Maze
-- Message panels
QRC:

QuestComplete:   [1004]
<ce> Мужчина, судя по всему, доволен и удаляется по своим делам.

Message:  1010
<ce> Некий хвастун расхаживает по округе,
<ce> демонстрируя прохожим бумажный листок.
<ce> В конце концов, он подходит к вам.
<ce>
<ce> "Здравствуйте, %ra! У меня для вас есть
<ce> любопытное предложение. Я разработал чрезвычайно
<ce> запутанный лабиринт, который под силу пройти
<ce> за отведенное время только самым одаренным людям.
<ce> Чем быстрее вы справитесь, тем больше получите.
<ce> Но если вам потребуется больше минуты, я заберу
<ce> у вас 100 золотых. Не желаете ли испытать свои силы?"

Message:  1011
<ce> Вы игнорируете его предложение и продолжаете
<ce> свой путь. Мужчина пристает к другим людям.

Message:  1012
<ce> Вы выхватываете у него бумажку и молниеносно демонстрируете,
<ce> как пройти лабиринт от входа до выхода. Вам потребовалось
<ce> менее чем =number_ секунд. Он ошеломлен. Будто бы подозревая
<ce> вас в обмане, он все же извлекает некоторую сумму денег
<ce> в качестве награды, помимо тех 100 золотых,
<ce> которые вы ему дали.

Message:  1013
<ce> Вы берете бумажку и аккуратно прослеживаете путь
<ce> с конца к началу. Он внимательно следит за вами,
<ce> чтобы вы не сжульничали. Вам требуется около двадцати секунд,
<ce> чтобы найти верный путь и выполнить его условие.
<ce> Он соглашается, что вы справились отлично, и предлагает
<ce> вам небольшую сумму денег вдобавок к тем
<ce> 100 золотым, которые вы ему дали.

Message:  1014
<ce> Возможно, вы несколько переоценили свои интеллектуальные
<ce> способности, потому что вблизи лабиринт оказался куда сложнее.
<ce> Вы пробуете разные пути, но постоянно натыкаетесь на тупики.
<ce> Тем не менее, вам удается пройти его менее чем за 40 секунд.
<ce> Он вежливо аплодирует вам и предлагает вам небольшую награду,
<ce> помимо тех 100 золотых, которые вы ему дали.

Message:  1015
<ce> Вы, похоже, оказались в затруднительном положении,
<ce> потому что этот лабиринт не похож ни на один из тех,
<ce> что вы видели раньше. Вы натыкаетесь на тупики один за другим.
<ce> Раздражение растет, потому что вы не понимаете, как его вообще
<ce> можно пройти. Наконец, раздается звонок, оповещающий о том,
<ce> что время истекло. Мужчина с радостью забирает 100 золотых,
<ce> как и было договорено.

Message:  1016
<ce> У вас недостаточно 100 золотых, чтобы попробовать
<ce> пройти лабиринт. Мужчина хмурится на вас.
<ce>
<ce> "Вы думали обмануть меня? Не в этот раз."
<ce>
<ce> Он сердито уходит.

QBN:

Item _reward1_ gold range 151 to 175
Item _reward2_ gold range 125 to 150
Item _reward3_ gold range 101 to 124
Item _number_ gold range 6 to 10

--	Quest start-up:
    prompt 1010 yes _pay_ no _reject_

_reject_ task:
    say 1011
    end quest

variable _pay_

_payhim_ task:
    when _pay_ and not _given_
    pay 100 money do _given_ otherwise do _nomoney_

_nomoney_ task:
    say 1016
    end quest

variable _given_
_int90_ task:
    when attribute Intelligence is at least 90

_int70_ task:
    when attribute Intelligence is at least 70

_int50_ task:
    when attribute Intelligence is at least 50

_bigwin_ task:
    when _given_ and _int90_
    say 1012
    give pc _reward1_
    end quest

_medwin_ task:
    when _given_ and _int70_ and not _int90_
    say 1013
    give pc _reward2_
    end quest

_smallwin_ task:
    when _given_ and _int50_ and not _int70_
    say 1014
    give pc _reward3_
    end quest

_fail_ task:
    when _given_ and not _int50_
    say 1015
    end quest