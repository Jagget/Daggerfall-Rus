Quest: RLQyousteal
DisplayName: СМК: Вы своруете?
-- Message panels
QRC:

QuestComplete:   [1004]
<ce> Ваша цель ничего не замечает. Вы без единого шороха
<ce> хватаете драгоценный камень и прикарманиваете его,
<ce> пока никто не видит.

Message:  1010
<ce> Беспечный крестьянин идет по своим делам и не
<ce> замечает, что у него из кармана торчит
<ce> блестящий драгоценный камень. Если бы вы были
<ce> хороши в воровстве, вы могли бы попытаться
<ce> присвоить его себе. Вы попробуете?
<--->
<ce> Беспечная крестьянка идет по своим делам и не
<ce> замечает, что у нее из кармана торчит
<ce> блестящий драгоценный камень. Если бы вы были
<ce> хороши в воровстве, вы могли бы попытаться
<ce> присвоить его себе. Вы попробуете?

Message:  1011
<ce> Вы игнорируете драгоценный камень и продолжаете свой путь.

Message:  1012
<ce> Вы спокойно шагаете вперед и пытаетесь дотянуться до драгоценного
<ce> каменя, но оказываетесь слишком неуклюжи, чтобы вытащить его.
<ce> Ваша цель поворачивается, а затем зовет на помощь.

QBN:

Item _reward_ gem

Clock _delay_ 00:10

--	Quest start-up:
	prompt 1010 yes _accept_ no _reject_

_reject_ task:
	say 1011
	end quest

_accept_ task:
	pick one of _go1_ _go2_ _go3_

_check1_ task:
	when skill Pickpocket is at least 20

_check2_ task:
	when skill Pickpocket is at least 30

_check3_ task:
	when skill Pickpocket is at least 40

variable _go1_
variable _go2_
variable _go3_

_c1_ task:
	when _check1_ and _go1_
	start task _success_

_c2_ task:
	when _check2_ and _go2_
	start task _success_

_c3_ task:
	when _check3_ and _go3_
	start task _success_

_d1_ task:
	when not _check1_ and _go1_
	start task _fail_

_d2_ task:
	when not _check2_ and _go2_
	start task _fail_

_d3_ task:
	when not _check3_ and _go3_
	start task _fail_

variable _success_
variable _fail_

_finalwin_ task:
	when _accept_ and _success_
	give pc _reward_
	end quest

_finalfail_ task:
	when _accept_ and not _success_
	say 1012
	setplayercrime Theft
	spawncityguards immediate
	start timer _delay_

_delay_ task:
	end quest