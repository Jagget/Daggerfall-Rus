Quest: RLQdrugdeliver
DisplayName: СМК: Доставка наркотиков
-- Message panels
QRC:

QuestComplete:   [1004]
<ce> ДА! Дай мне это! Отойди! Убирайся вон!
<ce> Да, вот, деньги, бери!

Message:  1010
<ce> Фигура в капюшоне останавливает вас
<ce> и говорит тихим голосом.
<ce>
<ce> "Простите покорнейше, но мне нужно кое-что
<ce> доставить в пределах города. Заплачу
<ce> =reward_ золотых, если вы знаете, как это
<ce> сделать незаметно".

Message:  1011
<ce> "Хорошо. Место называется _res_, это на %di
<ce> отсюда. Городская стража скоро узнает об этом
<ce> и будет обыскивать район. Возможно, стоит
<ce> поспешить, если вы не хотите объясняться со
<ce> стражниками. У вас есть шесть часов, иначе
<ce> ваша награда аннулируется. И да - _person_.
<ce> Так зовут получателя".

Message:  1012
<ce> Прошло шесть часов, а вы так и не доставили
<ce> _ingred_ в _res_.

Message:  1013
<ce> Фигура молча уходит.

Message:  1014
<ce> К вам приближается городской стражник
<ce> и не дает вам прохода. Если бы вы не были
<ce> так хороши, он мог бы найти _ingred_,
<ce> что вам дали. Но, поскольку вы были
<ce> хорошо подготовлены, вы отделались лишь
<ce> небольшим безрезультатным обыском.

Message:  1015
<ce> К вам приближается городской стражник и не дает вам
<ce> прохода. Вы думали, что хорошо спрятали _ingred_,
<ce> но это не так. Он забирает конрабанду и убегает,
<ce> крича, чтобы вас арестовали. Очевидно, что вы не
<ce> подходили для этой работы.

Message:  1016
%qdt:
 Мне поручили кое-что доставить человеку по имени
 _person_ в _res_, ___res_.
 У меня есть шесть часов,
 и мне заплатят =reward_ золотых.

QBN:

Clock _timer_ 6:00
Clock _smalldelay_ 0:10 0:30

Item _reward_ gold range 92 to 125
Item _ingred_ drug

Person _person_ faction Dancers

Place _res_ local house

--	Quest start-up:
    prompt 1010 yes _accept_ no _reject_

_reject_ task:
    say 1013
    end quest

_accept_ task:
    say 1011
    get item _ingred_
    place npc _person_ at _res_
    start timer _timer_
    start timer _smalldelay_
    log 1016 step 0

_finalwin_ task:
    toting _ingred_ and _person_ clicked
    give pc _reward_
    end quest

variable _smalldelay_
_pickpocket_ task:
    when skill Pickpocket is at least 30
    start task _qualified_

_stealth_ task:
    when skill Stealth is at least 50
    start task _qualified_

variable _qualified_
_caught_ task:
    when _smalldelay_ and not _qualified_
    setplayercrime Smuggling
    spawncityguards immediate
    take _ingred_ from pc
    hide npc _person_
    say 1015
    end quest

_scammed_ task:
    when _smalldelay_ and _qualified_
    say 1014

_timer_ task:
    say 1012
    make _ingred_ permanent
    end quest