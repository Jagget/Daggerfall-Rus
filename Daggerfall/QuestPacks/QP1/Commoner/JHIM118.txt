Quest: JHIM118
DisplayName: Мои маленькие пони
-- Thanks to Metalhead324 for the concept
QRC:

QuestorOffer:  [1000]
<ce> Приветствую тебя, друг, меня зовут _qgiver_. У 
<ce> меня для тебя деловое предложение, мой хороший 
<ce> %ra. Знаете ли вы, что кентавры в Тамриэле 
<ce> чрезвычайно редки? На их ловле и продаже можно 
<ce> заработать большие деньги. Если хочешь, я знаю 
<ce> идеальное место, где можно найти парочку. Я 
<ce> могу заплатить вам =reward_ золото.

RefuseQuest:  [1001]
<ce> Ха. Знаете, закона против этого нет.

AcceptQuest:  [1002]
<ce> Хорошо, тогда давайте перейдем к делу. Место 
<ce> называется ____dung_. Я покажу тебе на 
<ce> карте. Ты обеспечиваешь мне безопасность и 
<ce> позволяешь мне делать там свою работу. Нам 
<ce> нужно всего лишь несколько хороших жеребят, 
<ce> приятель! Вы не пожалеете об этом, совсем! 
<ce> Но сможем ли мы закончить к =timer_ дням? Я 
<ce> занятой человек, ты знаешь.

QuestFail:  [1003]
<ce> "Вы потратили слишком много времени, и я не заплачу вам! 
<ce> Я найду помощь в приручении кентавров в другом месте!"

QuestComplete:  [1004]
<ce> Освободившись от опасности нападения кентавра, 
<ce> _qgiver_ поворачивается и кланяется.
<ce>
<ce> "Вы очень хороши в этом! Возможно, я поищу вас 
<ce> снова когда-нибудь. Если вы надежны, %ra, можно 
<ce> заработать хорошие деньги. Возьмите свое золото и 
<ce> мои наилучшие пожелания. Ну-ка, пойдемте, твари!"
<ce>
<ce> Затем %g1 уводит жеребят.

QuestorPostSuccess:   [1008]
Эти кентавры продавались довольно хорошо. Мы могли бы сделать из этого хороший бизнес.

Message:  1010
<ce> _qgiver_ шепчет: "Хорошо, вот как это будет работать. 
<ce> Я буду звать этих существ, а ты будешь убивать 
<ce> взрослых, которые попытаются нам помешать. Как только 
<ce> я найду несколько хороших жеребят, я надену на них 
<ce> лассо, и мы уйдем отсюда. Смотри в оба!"

Message:  1011
<ce> Не зная о том, что ты не знаешь, _qgiver_ загнал в 
<ce> угол пару молодых кентавров и закрепил на их шеях 
<ce> верёвки. Процесс гораздо более безобидный, чем 
<ce> кажется. Используя %g3 зов кентавра, %g1 успокаивает 
<ce> их достаточно, чтобы заставить следовать за собой.
<ce>
<ce> "Ну что ж, тогда это выглядит вполне позитивно.
<ce> Давай убежим, пока не привлекли к себе еще больше внимания.."

Message:  1012
<ce> "Нет! О, нет!"
<ce>
<ce> _qgiver_ отделился от тебя на небольшое расстояние, 
<ce> которого оказалось достаточно, чтобы подпустить к себе 
<ce> разъяренного кентавра. Существо прекращает %g3 жизнь и 
<ce> поворачивается к тебе, разъярённое твоим вторжением.

Message:  1013
<ce> Когда кентавр умер, ты осматриваешь труп _qgiver_. 
<ce> Ты находишь деньги, которыми %g1 намеревался 
<ce> расплатиться с тобой, и немногое другое. Ты 
<ce> возвращаешь их себе. Без %g3 зова здешние кентавры 
<ce> должны быть гораздо более мирными.

Message:  1030
%qdt:
 Некто по имени _qgiver_ 
 нанял меня для поимки 
 молодых кентавров в 
 ___dung_ из ____dung_. Мне 
 заплатят =reward_ золотых, 
 если мы сможем выполнить 
 задание за =timer_ дней.

QBN:
Item _reward_ gold range 800 to 1200

Person _qgiver_ group Questor male

Place _dung_ remote dungeon14

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _indung_ 0:20 0:40

Foe _enemy_ is Centaur
Foe _enemy1_ is Centaur

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _dung_ set _inside_
    reveal _dung_
    pick one of _f1_ _f1_ _f2_
    add _qgiver_ face
    hide npc _qgiver_

variable _f1_
variable _f2_

_inside_ task:
    start timer _indung_
    stop timer _timer_
    say 1010

_notinside_ task:
    when not _inside_
    stop timer _indung_
    start timer _timer_

_active_ task:
    when _inside_ and not _indung_
    create foe _enemy_ every 2 minutes indefinitely with 75% success

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ infighting true
    change foe _enemy1_ infighting true
    clear _daily_

variable _indung_

_notdead_ task:
    when _indung_ and _f1_
    say 1011
    start task _escape_

variable _escape_

_fullvictory_ task:
    when _escape_ and not _inside_
    give pc _reward_
    end quest

_dead_ task:
    when _indung_ and _f2_
    drop _qgiver_ face
    say 1012
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    destroy _qgiver_

_retribute_ task:
    killed _enemy1_
    say 1013
    get item _reward_
    end quest

_timer_ task:
    say 1003
    end quest