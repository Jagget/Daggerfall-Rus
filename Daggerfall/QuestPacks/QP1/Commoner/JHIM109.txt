Quest: JHIM109
DisplayName: Сюрприз!
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Эй, некоторые из моих друзей и я устраиваем своего рода
<ce> празднование дня рождения для нашего товарища,
<ce> и нам нужен кто-то со стороны.
<ce> Мы заплатим вам =reward_ золота за помощь.
<ce> Без драки или опасности, обещаем.

RefuseQuest:  [1001]
<ce> %oth. Ну что ж, удачи вам.

AcceptQuest:  [1002]
<ce> Хорошо, вот план. Мы делаем что-то вроде
<ce> пародийного похищения. Вы будете
<ce> притворяться, что похищаете его и везете его весь путь до
<ce> ___dung_, где мы
<ce> встретимся с вами и устроим ему сюрприз на день рождения.
<ce> У нас есть кто-то у его дома, чтобы сообщить нам,
<ce> когда вы приедете и уйдете, так что мы не будем слишком
<ce> далеко. Мы также дали знать городской страже,
<ce> и они пока не против. Имя нашего друга
<ce> _friend_. Он будет
<ce> в _res_ сегодня после 8.
<ce> Обязательно заберите его сегодня.
<ce> Накройте его голову этим мешком и заставьте
<ce> его двигаться. Увидимся с другой стороны.

QuestFail:  [1003]
<ce> Вы затянули с временем. _questgiver_,
<ce> который следил за всем издалека вместе с друзьями,
<ce> забирает _friend_ и портит
<ce> сюрприз, опасаясь, что вы пошли слишком далеко.
<ce> Вы не получаете оплаты.

QuestComplete:  [1004]
<ce> _friend_ стонет от страха.
<ce> Вы задаетесь вопросом, как долго продлится этот спектакль.
<ce>
<ce> Затем вы слышите множество голосов, кричащих: "СЮРПРИЗ!"
<ce>
<ce> Толпа его друзей окружает его, снимает
<ce> его узы и смеется над забавным временем, которое они
<ce> провели, планируя это. Вас обсыпают комплиментами за
<ce> ваше исполнение в этом акте. Когда все выходят
<ce> наружу, _questgiver_ дает вам
<ce> обещанное вознаграждение и благодарит вас.

QuestorPostSuccess:   [1008]
Вы умеете хранить секреты. Это хорошо.

Message:  1010
<ce> Да, я _friend_.
<ce> Чем могу вам помочь?

Message:  1011
<ce> Вы накрываете голову _friend_ 
<ce> мешком и начинаете тащить его.
<ce> Он кричит о помощи, но никто не вмешивается.
<ce> Либо все в курсе, либо все действительно 
<ce> боятся вас, это еще предстоит выяснить.

Message:  1030
%qdt:
 _questgiver_ из
 ___res_ попросил меня помочь
 притвориться, что я похищаю
 его друга как шутку на день рождения.
 Я могу найти _friend_
 в _res_ после 20:00.
 Мне дали мешок, чтобы закрыть
 ему голову. Я отведу его в
 ___dung_ за =reward_ золотых монет.

QBN:
Person _questgiver_ group Questor
Person _friend_ group Resident1 male

Place _dung_ remote dungeon11
Place _res_ local house

Clock _oneday_ 1.00:00
Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _find_ 0:00 0:03

Item _reward_ gold
Item _sack_ small_sack

--	Quest start-up:
	start timer _oneday_
	log 1030 step 0
    hide npc _questgiver_
	place npc _friend_ at _res_
	pc at _dung_ set _idun_
    pc at _res_ set _ires_
	reveal _dung_
    get item _sack_

_oneday_ task:
    end quest

_day_ task:
    daily from 5:00 to 20:00
    hide npc _friend_

_night_ task:
    when not _day_
    restore _friend_

_ires_ task:
    _sack_ used do _taken_

_talk_ task:
    clicked npc _friend_
    say 1010

variable _taken_
_moveit_ task:
    when _taken_ and _ires_ and _night_
    start task _moveit2_

_moveit2_ task:
    say 1011
    hide npc _friend_
    add _friend_ face
    stop timer _oneday_
    start timer _timer_
    take _sack_ from pc
    start task _moving_

variable _moving_
variable _idun_
_winner_ task:
    when _idun_ and _moving_
    start timer _find_

_nowinner_ task:
    when not _winner_
    stop timer _find_

_find_ task:
    give pc _reward_
    end quest

_rescue_ task:
    when _timer_ and _moveit2_
    say 1003

_timer_ task:
	end quest