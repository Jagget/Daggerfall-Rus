Quest: JHIM122
DisplayName: Проклясть Гильдию Воров
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Эй, вы! У меня плохое настроение 
<ce> и мне нужно отомстить Гильдии Воров. 
<ce> Вчера вечером они совершили налёт 
<ce> на мой дом и теперь собираются получить 
<ce> свое. Я заплачу вам =reward_ золотых 
<ce> за небольшую помощь в этом.

RefuseQuest:  [1001]
<ce> О. Тогда, о %god, не рассказывайте им обо мне.

AcceptQuest:  [1002]
<ce> Ладно, возьмите этот _trink_. Я зачаровал его 
<ce> на нечто особенное. Он заставит одного 
<ce> из этих огненных атронахов появиться там, 
<ce> где вы его используете. Если повезет, то здание 
<ce> сгорит дотла или что-то в этом роде! Просто 
<ce> войдите внутрь _res_ в %di, там 
<ce> останавливаются некоторые из Гильдии Воров. 
<ce> Используйте этот _trink_ и выбирайтесь оттуда. 
<ce> Я заплачу вам, когда все будет сделано. 
<ce> Но не затягивайте больше чем на день, ладно? 
<ce> Не хочу, чтобы они пронюхали обо мне.

QuestFail:  [1003]
<ce> Вам потребовалось больше суток на размещение 
<ce> атронаха в _res_, и теперь _questgiver_ 
<ce> откажется вам платить.

QuestComplete:  [1004]
<ce> Ха-ха! Мило, не правда ли? Кучка грязных грабителей 
<ce> получает по заслугам. Они это заслужили, не жалейте 
<ce> их. У меня здесь ваши =reward_ золотых.

RumorsPostSuccess:   [1007]
Какой-то идиот учинил акт вандализма в против Гильдии Воров.

QuestorPostFailure:   [1009]
Даже не говорите со мной. Вы запорете даже самую простую работу.

Message:  1010
<ce> Должно быть, это тот самый дом.

Message:  1011
<ce> _trink_ должен быть использован внутри _res_.

Message:  1012
<ce> Вы активируете _trink_. 
<ce> Порыв ветра предшествует образованию 
<ce> атронаха поблизости.

Message:  1013
<ce> Вы необъяснимым образом убили атронаха, которого вызвали.
<ce> Некоторые умы действительно не предназначены для преступлений.

Message:  1030
%qdt:
 _questgiver_ заплатит мне =reward_ золотых, 
 чтобы я вызвал атронаха внутри _res_ из ___res_, 
 используя зачарованный _trink_. Я должен сделать 
 это и вернуться в течение одного дня.

QBN:
Person _questgiver_ group Questor
Person _tg_ faction The_Thieves_Guild remote

Place _res_ local house

Clock _timer_ 1.00:00

Item _reward_ gold range 101 to 249
Item _trink_ trinket

Foe _flfoe_ is Fire_atronach

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _res_ do _inside_
    _trink_ used do _activate_
    get item _trink_

variable _activate_
_notyet_ task:
    when _activate_ and not _inside_ and not _victory_
    say 1011
    clear _activate_ _notyet_

_inside_ task:
    say 1010

_summon_ task:
    when _activate_ and _inside_
    say 1012
    take _trink_ from pc
    create foe _flfoe_ every 0 minutes 1 times with 100% success
    change foe _flfoe_ infighting true
    start task _victory_
    change repute with _tg_ by -10

variable _victory_

_attack_ task:
    killed _flfoe_
    say 1013
    end quest

_timer_ task:
    say QuestFail
    end quest

_clickqgiver_ task:
    clicked npc _questgiver_

_noreward_ task:
    when _clickqgiver_ and not _victory_
    clear _noreward_ _clickqgiver_

_reward_ task:
    when _clickqgiver_ and _victory_
    give pc _reward_
    end quest