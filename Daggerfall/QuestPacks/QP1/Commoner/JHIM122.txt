Quest: JHIM122
DisplayName: Проклясть гильдию воров
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Эй, ты! У меня плохое настроение, и 
<ce> мне нужно отомстить Гильдии Воров. 
<ce> Вчера вечером они совершили налёт на 
<ce> мой дом и теперь собираются получить 
<ce> своё. Я заплачу тебе =reward_ золотых 
<ce> за небольшую помощь в этом.

RefuseQuest:  [1001]
<ce> Ох. Тогда, о %god, не рассказывай им обо мне.

AcceptQuest:  [1002]
<ce> Ладно, возьми вот _trink_. Я зачаровал его 
<ce> на нечто особенное. Он заставит одного из 
<ce> этих огненных атронахов появиться там, где 
<ce> ты его используешь. Если повезет, то здание 
<ce> сгорит дотла или что-то в этом роде! Просто 
<ce> войди внутрь _res_ в %di, там 
<ce> останавливаются некоторые из Гильдии Воров. 
<ce> Используй этот _trink_ и выбирайся оттуда. Я 
<ce> заплачу тебе, когда все будет сделано. Но не 
<ce> затягивай больше чем на день, ладно? Не 
<ce> хочу, чтобы они пронюхали обо мне.

QuestFail:  [1003]
<ce> Вы потратили больше суток на размещение 
<ce> атронаха в _res_, и _questgiver_ 
<ce> откажется платить вам.

QuestComplete:  [1004]
<ce> Ха-ха! Мило, не правда ли? Кучка грязных грабителей 
<ce> получает по заслугам. Они это заслужили, не жалей 
<ce> их. У меня здесь твое =reward_ золотых.

RumorsPostSuccess:   [1007]
Какой-то идиот учинил вандализм в против Гильдии Воров.

QuestorPostFailure:   [1009]
Даже не разговаривай со мной. Ты запорешь даже самую простую работу.

Message:  1010
<ce> Наверное, это тот самый дом.

Message:  1011
<ce> При этом _trink_ должен быть использован внутри _res_.

Message:  1012
<ce> Ты активируешь _trink_. Порыв 
<ce> ветра предшествует образованию 
<ce> атронаха поблизости.

Message:  1013
<ce> Ты необъяснимым образом убил атронаха, которого вызвал.
<ce> Некоторые мозги действительно не предназначены для преступлений.

Message:  1030
%qdt:
 _questgiver_ заплатит мне =reward_ золотых, 
 чтобы я вызвал атронаха внутри _res_ из ___res_, 
 используя зачарованный _trink_. Я должен сделать 
 это и вернуться в течение одного дня.

QBN:
Person _questgiver_ group Questor
Person _tg_ faction The_Thieves_Guild remote

Place _res_ local house

Clock _timer_ 1.00:00

Item _reward_ gold range 101 to 249
Item _trink_ trinket

Foe _flfoe_ is Fire_atronach

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _res_ do _inside_
    _trink_ used do _activate_
    get item _trink_

variable _activate_
_notyet_ task:
    when _activate_ and not _inside_ and not _victory_
    say 1011
    clear _activate_ _notyet_

_inside_ task:
    say 1010

_summon_ task:
    when _activate_ and _inside_
    say 1012
    take _trink_ from pc
    create foe _flfoe_ every 0 minutes 1 times with 100% success
    change foe _flfoe_ infighting true
    start task _victory_
    change repute with _tg_ by -10

variable _victory_

_attack_ task:
    killed _flfoe_
    say 1013
    end quest

_timer_ task:
    say QuestFail
    end quest

_clickqgiver_ task:
    clicked npc _questgiver_

_noreward_ task:
    when _clickqgiver_ and not _victory_
    clear _noreward_ _clickqgiver_

_reward_ task:
    when _clickqgiver_ and _victory_
    give pc _reward_
    end quest