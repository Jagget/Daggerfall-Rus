Quest: JHIM111
DisplayName: Как спасти жизнь
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Мой дорогой %ra, я разозлил не того врага
<ce> и чувствую, что моя жизнь в опасности. Мне нужен 
<ce> телохранитель, прямо сейчас. Станешь ли ты моим спасителем?

RefuseQuest:  [1001]
<ce> И я умру, и никто не будет скорбеть по мне.

AcceptQuest:  [1002]
<ce> Тогда я нанимаю вас с этой самой секунды. Мои вещи со мной,
<ce> и это заведение ничего не значит, пока мой преследователь 
<ce> покушается на мою жизнь. Я точно знаю, что это один разъяренный 
<ce> человек, какой-то колдун. Мне повезло, что я не привлек к себе  
<ce> еще больше внимания. Я останусь с вами, пока не буду уверен, что 
<ce> все уладилось само собой, так или иначе. Не обращайте на меня 
<ce> внимания. Я сам присмотрю за собой, пока буду с вами. 
<ce> Занимайтесь своими обычными делами.

QuestFail:  [1003]
<ce> ERROR 1003 JHIM111

QuestComplete:  [1004]
<ce> Теперь _qgiver_ смущенно смотрит на вас.
<ce>
<ce> "Что ж, с этим делом покончено, не так ли, %pcf? Моя вечная
<ce> благодарность тебе за то, что ты оберегал меня. Возьми эти 
<ce> =reward_ золотых. Мне было бы стыдно теперь иметь их у себя."

QuestorPostSuccess:   [1008]
Я благодарен вам за защиту. Мне следует тщательнее выбирать своих врагов.

Message:  1017
<ce> Вы чувствуете, как что-то движется.

Message:  1018
<ce> _qgiver_ хватается за голову и задыхается от страха.
<ce>
<ce> "Не могу поверить, что кто-то пытался меня убить. Куда
<ce> катится этот мир? О, это ужасно.
<ce> Хоть я и благодарен, что мне не пришлось проливать кровь,
<ce> моя репутация всё равно пострадает. Возможно,
<ce> ___qgiver_ действительно не для меня."

Message:  1019
<ce> Курьер торопливо подходит к вам, кладет письмо
<ce> в вашу сумку и убегает по более важным делам.
<ce>
<ce> Вы тут же открываете его.

Message:  1020
_qgiver_ из ___qgiver_
 
Это мое последнее предупреждение. Либо заплатите сумму
в =fakegold_ золотых к этому числу следующего месяца,
либо я заберу ваших животных. Пожалуйста, сделайте все
возможное, чтобы предотвратить это. Я не хочу видеть,
как вы теряете средства к существованию только из-за каких-то
крайне глупых долгов, что вы накопили.
 
                      С уважением,
                      _human_
                      ___dummy_

Message:  1021
<ce> _qgiver_ читает письмо с широко раскрытыми глазами.
<ce>
<ce> "О боже. Я думаю, что ошибся во всем этом деле.
<ce> Тогда, думаю, мы оба можем согласиться, что 
<ce> никакой убийца не направляется ко мне, верно?
<ce> По крайней мере, не по этому вопросу. О боже. 
<ce> Дайте мне перевести дух. Это потрясающая радость."

Message:  1030
%qdt:
 _qgiver_, житель
 ___qgiver_, попросился
 сопровождать меня в путешествиях, пока
 какой-то убийца ищет %g3.
 Мне просто нужно быть начеку,
 пока не придет время.

QBN:
Item _reward_ gold
Item _fakegold_ gold range 3001 to 6999
Item _letter_ letter

Person _qgiver_ group Questor male
Person _human_ group Local_3.0

Place _dummy_ remote house1

Clock _timer_ 12:00 6.12:00
Clock _failsafe_ 180.00:00

Foe _enemy_ is Nightblade

--	Quest start-up:
    start timer _timer_
    start timer _failsafe_
    log 1030 step 0
    pick one of _E.01_
    hide npc _qgiver_
    add _qgiver_ face

variable _timer_
variable _E.01_
variable _E.02_

_danger_ task:
    when _E.01_ and _timer_
    create foe _enemy_ every 1200 minutes indefinitely with 100% success msg 1017

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy_ infighting true
    clear _daily_

_victory1_ task:
    killed _enemy_
    say 1018
    give pc _reward_
    end quest

_safe_ task:
    when _E.02_ and _timer_
    give pc _letter_ notify 1019
    have _letter_ set _victory2_

_victory2_ task:
    say 1020
    say 1021
    give pc _reward_
    end quest

_failsafe_ task:
    end quest