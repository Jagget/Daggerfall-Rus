Quest: JHIM121
DisplayName: О любви и справедливости
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Я понес тяжкую обиду, и закон не дает мне 
<ce> справедливости. Примкни ко мне и исправь причиненное 
<ce> мне зло. За свою помощь ты получишь вознаграждение.

RefuseQuest:  [1001]
<ce> Сгинь.

AcceptQuest:  [1002]
<ce> Тогда пойдем вершить правосудие. 
<ce> Мы найдем этого человека, _fool_, 
<ce> в _res_ в этом городе. Веди!

QuestFail:  [1003]
<ce> "Я сам свершу правосудие!
<ce> Ты делаешь это слишком долго."
<ce>
<ce> _questgiver_ спешит уйти.

QuestComplete:  [1004]
<ce> Что сделано, то сделано.

Message:  1010
<ce> _fool_ трусит при виде вас двоих. 
<ce> Нет никаких сомнений, за чем вы 
<ce> пришли. _questgiver_ рычит.
<ce>
<ce> " =num_ лет женат, и никто никогда не смел приблизиться 
<ce> к моей возлюбленной! А теперь ты опорочил ее! Там, где 
<ce> закон не справляется, мы добьемся своего!"
<ce>
<ce> _fool_ кричит: "Я осознаю свою неправоту! 
<ce> Пожалуйста, давай что-нибудь придумаем! Я 
<ce> исправился! Просто сохрани мне жизнь!"

Message:  1011
<ce> _questgiver_ застыл в холодном гневе.
<ce>
<ce> "То, что ты украл, никто никогда не сможет вернуть..
<ce> Ты умрешь!"
<ce>
<ce> _fool_ начинает рыдать, хватаясь за оружие 
<ce> в попытке самообороны. Поможешь ли ты 
<ce> _questgiver_ убить _fool_?

Message:  1012
<ce> _questgiver_ ревет в ярости, 
<ce> и начинается битва.

Message:  1013
<ce> _questgiver_ благодарит тебя за помощь, 
<ce> но выглядит не лучше, чем раньше.
<ce>
<ce> "Я думал, что это меня удовлетворит. Но это не 
<ce> так. Я не успокоюсь, пока не обрету покой."
<ce>
<ce> Передав вам обещанный платеж, %g1 уходит.

Message:  1014
<ce> _questgiver_ ревет в ярости, не 
<ce> желая слушать. Ты стал %g3 врагом, 
<ce> как и _fool_. Ты и он готовитесь 
<ce> защищаться от _questgiver_.

Message:  1015
<ce> _fool_ ужасно плачет при виде 
<ce> мертвого тела _questgiver_. Этот 
<ce> опыт ужасно потряс всех.
<ce>
<ce> "Я знал %g3 отдаленно, как еще одного жителя здесь, в 
<ce> ___questgiver_, но никогда не думал, что %g1 может дойти до 
<ce> такого. Как можно быть настолько искореженным ненавистью?"
<ce>
<ce> _fool_ требуется еще некоторое время, чтобы выплакаться. 
<ce> Вскоре он открывает лежащую рядом коробочку и вручает тебе 
<ce> кольцо, не в силах выразить свою благодарность словами. Он 
<ce> отмахивается от тебя, чтобы поскорбеть в одиночестве.

Message:  1016
<ce> Ты решаешь не принимать никакого участия в этой вражде. 
<ce> Каким бы ни был исход, он не будет касаться тебя.

Message:  1017
<ce> Похоже, ты единственный, кто остался в живых 
<ce> после этого безумия. Ты находишь деньги, 
<ce> которые тебе обещал _questgiver_.

Message:  1030
%qdt:
 _questgiver_ заплатит мне =reward_ золотых за 
 то, что я помогу %g2 отомстить кому-то по имени 
 _fool_ в ___questgiver_. Напряжение, витающее в 
 воздухе, подсказывает мне, что на это у нас 
 должно уйти не больше дня.

QBN:
Person _questgiver_ group Questor
Person _fool_ group Local_3.0 male

Place _res_ local house

Clock _timer_ 1.00:00
Clock _failsafe_ 1:00

Item _reward_ gold range 101 to 249
Item _num_ gold range 3 to 11
Item _ring_ ring

Foe _flfoe_ is Nightblade
Foe _brfoe_ is Barbarian

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    hide npc _questgiver_
    add _questgiver_ face
    pc at _res_ do _inside_
    place npc _fool_ at _res_

variable _inside_
_talk_ task:
    clicked npc _fool_
    drop _questgiver_ face
    say 1010
    prompt 1011 yes _attack_ no _mercy_
    start timer _failsafe_

_attack_ task:
    say 1012
    hide npc _fool_
    place foe _flfoe_ at _res_
    create foe _brfoe_ every 0 minutes 1 times with 100% success
    start task _allybr_

_allybr_ task:
    change foe _flfoe_ team 0
    change foe _brfoe_ team 1
    change foe _brfoe_ infighting true
    change foe _flfoe_ infighting true

_attackvic_ task:
    when _attack_ and not _killedbr_ and _killedfl_
    say 1013
    remove foe _brfoe_
    give pc _reward_
    end quest

_mercy_ task:
    say 1014
    hide npc _fool_
    place foe _flfoe_ at _res_
    create foe _brfoe_ every 0 minutes 1 times with 100% success
    start task _allyfl_

_allyfl_ task:
    change foe _flfoe_ team 1
    change foe _brfoe_ team 0
    change foe _brfoe_ infighting true
    change foe _flfoe_ infighting true

_killedfl_ task:
    killed _flfoe_

_killedbr_ task:
    killed _brfoe_

_mercyvic_ task:
    when _mercy_ and _killedbr_ and not _killedfl_
    say 1015
    remove foe _flfoe_
    give pc _ring_
    destroy _questgiver_
    end quest

_wipeout_ task:
    when _talk_ and _killedbr_ and _killedfl_
    say 1017
    give pc _reward_
    destroy _questgiver_
    end quest

_timer_ task:
    say 1003
    end quest

_flee_ task:
    when _talk_ and not _inside_
    say 1016
    give pc nothing
    destroy _questgiver_
    end quest

_failsafe_ task:
    end quest