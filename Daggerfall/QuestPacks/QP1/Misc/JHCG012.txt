-- Cartographers Quest
Quest: JHCG012
DisplayName: В Шедунгент
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG012

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG012

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG012

QuestFail:  [1003]
<ce> Ваше задание по предоставлению карты
<ce> из Шедунгента истекло.

QuestComplete:  [1004]
<ce> Как я уже мог упомянуть, мы, вероятно, будем
<ce> отправлять тебя обратно в Шедунгент снова и снова.
<ce> Королева Аубк-и проявляет особый интерес к
<ce> обители своей свекрови. Но как только
<ce> ты узнаешь, где оно находится, это легкие деньги.

Message:  1010
<ce> Юноша, находящийся на службе у придворного, вручает
<ce> тебе письмо. "Ты, должно быть, %pcf.
<ce> Это тебе". Затем он идет дальше.
   <--->
<ce> Девушка, находящийся на службе у придворного, вручает
<ce> тебе письмо. "Ты, должно быть, %pcf.
<ce> Это тебе". Затем она идет дальше.

Message:  1011
  %pcf,
 
  Я не помню, отправляли ли мы тебя
  В Шедунгент, во Вротгарианские горы.
  Горы. Если да, то, пожалуйста, прости нас за
  это задание. Тебе будет выплачено вознаграждение, как и
  во всех остальных случаях. Нам нужно, чтобы ты составил карту
  расстояний от туда до сюда. Поговори со мной,
  если тебе нужны указания как добраться до этого места. Это нужно
  сделать в течение =timer_ дней.
 
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов
                ___place_, ____place_

Message:  1012
<ce> Всегда рад, %pcf. Шедунгент находится довольно далеко от нас,
<ce> что делает его важной целью, которую нужно раз за разом наносить на карту.
<ce> Бывшая королева-мать, Нульфага, также находится там. Мы стараемся
<ce> не беспокоить ее без уважительной причины.
 
<ce> Если ты не знаешь, где он находится, то предупрежу, что найти его нелегко.
<ce> Он находится к востоку от Театр Киран и к югу от Гробницы Хартхаус.
<ce> Отправляйся в Лонгвелл в Вротгарианских горах. Иди на
<ce> юго-юго-восток, пока не окажешься прямо между Плантацией Гринсли
<ce> и Усадьбой Гринсли. Оттуда направляйся прямо на юг.
<ce> Шедунгент находится в низине, поэтому холмы - твои помощники.
 
<ce> В качестве альтернативы ты можешь начать с Плантации Гринсли и
<ce> идти на восток-юго-восток, но так ты быстро заблудишься.
<ce> Просто продолжай смотреть по сторонам. Уверяю тебя, он где-то там.
<ce> Это необычное здание, полностью черное, с лестницей на одной стороне и
<ce> соломенной крышей. Возвращайся, как найдешь его, не позже чем через =timer_ дней.

Message:  1013
<ce> Ты узнаешь Шедунгент.
<ce> Пришло время вернуться к ___qg_.
   <--->
<ce> Теперь, когда ты разведал Шедунгент,
<ce> пришло время предоставить свой отчет _qg_.

Message:  1030
%qdt:
 Мне поручили составить карту
 расстояний от Шедунгента
 до ___place_ в
 ____place_. У меня есть =timer_
 дней, чтобы добраться туда и
 отчитаться перед _qg_.

Message:  1031
%qdt:
 _qg_ дал мне эти шаги
 чтобы найти Шедунгент. Из Лонгвелла
 я могу идти на юго-юго-восток, пока не 
 окажусь между Плантацией Гринсли и
 Усадьбой Гринсли, а затем на юг.
 Я также могу начать с Плантации Гринсли.
 и двигаться на восток-юго-восток, но 
 %g1 предупредил меня, что это более трудный маршрут.
 Я также отметил, что он находится к востоку от Театра Киран
 и к югу от Гробницы Хартхаус.

Message:  1099
<ce> Я предупреждаю тебя, что не стоит входить в Шедунгент.
<ce> За эти годы я потерял несколько человек
<ce> таким образом. Просто составь карту и возвращайся.

QBN:
Item _reward_ gold range 1000 to 1099
Item _letter_ letter used 1011

Place _dung_ permanent ShedungentEntry
Place _place_ remote house2

Clock _timer_ 76.00:00 82.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT3
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL2
    end quest