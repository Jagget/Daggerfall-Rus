-- Cartographers Quest
Quest: JHCG026
DisplayName: Замок Некромоган
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG026

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG026

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG026

QuestFail:  [1003]
<ce> Твое задание разведать Замок
<ce> Некромоган уже закончилось.

QuestComplete:  [1004]
<ce> Очень хорошо, Следопыт. Ты заглянул
<ce> внутрь? Другие рассказывали мне об ужасных стонах
<ce> которые они слышат из его глубин. Я с удовольствием дам
<ce> другим работать над этой тайной, даже если будет оплачено
<ce> из моей собственной зарплаты. Кстати, о зарплате, вот твоя.

Message:  1010
<ce> "Письмо, %pcf! Картографы!" Курьер
<ce> радостно вкладывает тебе в руки письмо, после чего
<ce> поворачивается и уходит.
   <--->
<ce> Курьер машет тебе рукой. "Привет, %ra! Ты ведь
<ce> тот Следопыт с Картографами, верно?
<ce> Это для тебя!" Ты получаешь письмо.

Message:  1011
  Следопыт,
 
  У нас есть задание, которое требует соблюдения некоторого
  этикета. Пожалуйста, не разглашайте широко условия
  этого задания. Нам нужно отправить тебя в Замок
  Некромоган в Даггерфолле. Если ты не понимаешь
  значение этого, не спрашивай. У тебя есть =timer_
  дней, чтобы принести свой отчет. Спроси меня, если тебе нужны указания.
  Не забудь в этом случае заглянуть внутрь,
  чтобы предоставить нам максимально точную карту.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Прежде чем мы начнем, %pcf, я должен подчеркнуть
<ce> еще раз глубокую важность того, чтобы никто не знал.
<ce> что ты собираешься посетить это место. Местоположение
<ce> Замка Некромоган не предназначено для широкой публики.
<ce> Мы нанесли его на некоторые карты, но не на все.
 
<ce> Чтобы туда добраться, я рекомендую начать с Поющего Провидеца
<ce> Кинарет в районе Даггерфолла. Прижмись к
<ce> самому северо-западному дому в тамошней маленькой общине.
<ce> Затем иди прямо на север. Это небольшой поход, но он должен быть
<ce> быть тайным, в конце концов. Ищи курган в земле с
<ce> ветхой дверью, лежащий перед небольшим прудом. Никакого замка там нет.
<ce> Моя теория заключается в том, что такое название 
<ce> призвано заставить людей пойти искать настоящий большой
<ce> замок и оставить это место в покое. Но в любом случае,
<ce> этих инструкций будет достаточно. В качестве альтернативы ты можешь
<ce> начать со Старой Лачуги Йеомсли и идти на восток или Благословенного Вознесения
<ce> Кинарет и иди на запад. Замок Некромоган находится там, где
<ce> встречаются все эти точки. И помни, что мы дали тебе =timer_ дней.

Message:  1013
<ce> Ты нашел Замок Некромоган. Это название гораздо
<ce> более оправдано под землей, чем над ней.
   <--->
<ce> Как ни странно, это должен быть Замок Некромоган.

Message:  1030
%qdt:
 Меня послали составить карту для
 Картографов от Замка
 Некромоган до ___place_ в
 ____place_. Я могу пойти туда, или спросить
 _qg_ о подсказах. Мне
 нужно войти в замок, чтобы
 составить полноценую карту.

Message:  1031
%qdt:
 Рекомендуется начать с
 Поющего Провидеца Кинарет в
 регионе Даггерфолл, у дома
 расположенном дальше всего на северо-западе, и идти
 прямо на север. Замок Некромоган
 будет представлять собой небольшой курган в земле.
 Он так же находится там, где встречаются линии от
 Поющего Провидеца Кинарет, Старой Лачуги
 Йеомсли, и Благословенного Вознесения Кинарет.

Message:  1099
<ce> Замок Некромоган должен
<ce> быть труднодоступным. Посмотри, где сходятся эти
<ce> три точки сходятся. Ты заметишь.

QBN:
Item _reward_ gold range 600 to 699
Item _letter_ letter used 1011

Place _dung_ permanent Castle_Necromoghan
Place _place_ remote tavern

Clock _timer_ 65.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest