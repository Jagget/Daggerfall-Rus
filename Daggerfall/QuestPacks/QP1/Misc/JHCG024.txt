-- Cartographers Quest
Quest: JHCG024
DisplayName: Шабаш Даста
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG024

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG024

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG024

QuestFail:  [1003]
<ce> Срок действия вашего задания на составление карты 
<ce> Шабаша Даста истек.

QuestComplete:  [1004]
<ce> Надеюсь, его было легко найти. Вы когда-нибудь
<ce> общались с ведьмами? Я слышал много неприятных слухов
<ce> о них, но они вели себя вполне вежливо, когда
<ce> я составлял карты. Ну, в общем, вот ваш гонорар.

Message:  1010
<ce> Молодой курьер достает письмо
<ce> из своей сумки. "Это для вас, %pcf".
<ce> Затем она уходит.
   <--->
<ce> Молодой курьер достает письмо
<ce> из своей сумки. "Это для вас, %pcf".
<ce> Затем он уходит.

Message:  1011
  Следопыт,
 
  Мы отправляем вас в Шабаш Даста, 
  в Антифилосе. Встретимся в ___place_, если вам
  нужны указания, чтобы найти его. Мы даем
  вам =timer_ дней, чтобы принести свой отчет.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Хорошо, %pcf. Начнем с вашей карты. Видите этот храм,
<ce> Вечный Восход Мары в Антифилосе? Начните оттуда и идите
<ce> на северо-восток. Пройдет пара минут, прежде чем вы
<ce> сможете увидеть шабаш вдалеке, но близко не подходите.
<ce> Это каменный купол, покрытый сверху песком.
<ce> На это у вас будет =timer_ дней.

Message:  1013
<ce> Вы нашли Шабаш Даста.
   <--->
<ce> Перед вами лежит Шабаш Даста.

Message:  1030
%qdt:
 Картографы поручили мне
 составить карту до Шабаша Даста 
 в Антифилосе. Я должен сделать это 
 в течение =timer_ дней, а затем
 прибыть в ___place_ в ____place_
 за своим вознаграждением.

Message:  1031
%qdt:
 _qg_ посоветовал мне начать 
 с Вечного Восхода Мары в Антифилосе
 и идти прямо на северо-восток. Шабаш,
 каменный купол с песчаным покрытием,
 должен быть в паре минут ходьбы.

Message:  1099
<ce> Растительность там такая засушливая,
<ce> что найти Шабаш будет легко.
<ce> Вам не придется идти очень далеко, чтобы увидеть его.

QBN:
Item _reward_ gold range 500 to 599
Item _letter_ letter used 1011

Place _dung_ permanent Coven_of_Dust
Place _place_ remote house2

Clock _timer_ 55.00:00 65.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest