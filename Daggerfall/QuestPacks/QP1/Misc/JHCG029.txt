-- Cartographers Quest
Quest: JHCG029
DisplayName: Орсиниум
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG029

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG029

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG029

QuestFail:  [1003]
<ce> Картографы запросили ваш отчет
<ce> относительно Орсиниума к этому времени.

QuestComplete:  [1004]
<ce> Вы серьезно? Дайте-ка мне посмотреть карту.
<ce> %oth! Вы единственный 
<ce> за весь год, кто действительно сделал карту
<ce> Орсиниума. Это заслуживает особой похвалы,
<ce> настолько, насколько это в моих силах.
<ce> Примите свой гонорар и мое восхищение. Вы
<ce> сделали то, к чему очень немногие относятся серьезно.

Message:  1010
<ce> Пожилой паж, улыбаясь, протягивает вам
<ce> развернутый свиток.
   <--->
<ce> "Письмо от картографов, %ra".
<ce> Паж протягивает тебе свиток, который ты
<ce> кладёшь в свой рюкзак.

Message:  1011
  Следопыт,
 
  Настала твоя очередь составить карту от города Орсиниум до сюда.
  Мы установили лимит времени =timer_ дней. Обратись
  ко мне, если тебе нужны указания, как добраться.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Значит, ты действительно собираешься попробовать, а %pcf?
<ce> Это достойно восхищения. Большинство людей, увидев фразу "город Орсиниум",
<ce> просто сжигают письмо. Я знаю это не понаслышке. Я скажу тебе
<ce> прямо сейчас, что легкого пути туда нет. Ни одного. Длинный
<ce> но проверенный - начиная с Лачуги Старой Эвелории в регионе Орсиниум,
<ce> откуда ты отправляешься на север за Обливион знает сколько
<ce> много километров. Так же нужно будет сместиться на восток,
<ce> так что будь начеку. Если люди начнут говорить тебе, что ты в
<ce> Вротгарианских горах, повернись и посмотри внимательнее.
<ce> Старожилы говорят, что проще начать из Усадьбы Букингинг
<ce> и идти на восток-юго-восток, но по мне так это полная ерунда.
<ce> Нет никаких свидетельств, что нужно идти в ту сторону.
<ce> Просто помни, что это почти прямо на север от
<ce> Лачуги Старой Эвелории и немного ниже по широте, чем Мидпат
<ce> Хилл, и не совсем в Вротгарианских горах. Если все остальное
<ce> не получится, просто помни, что это небольшая цивилизованная на вид деревня,
<ce> так что заберись на холм и используй телескоп. Ты увидишь здания.
<ce> рано или поздно, если ты просто посмотришь вокруг. %god поможет тебе
<ce> найти его, %pcf. Похоже, больше никому это не удастся. Я и
<ce> твой гонорар ждём тебя здесь.

Message:  1013
<ce> После очень долгого похода ты видишь перед собой Орсиниум.
   <--->
<ce> Перед тобой лежит легендарный затерянный город Орсиниум.
<ce> По крайней мере, тебе так кажется.

Message:  1030
%qdt:
 Картографы поручили мне
 отправиться в город Орсиниум и
 составить карту до ___place_
 в ____place_. _qg_
 также может дать мне указания.

Message:  1031
%qdt:
 Согласно _qg_,
 я влип по уши. Город Орсиниум
 находится далеко на севере и немного
 к востоку от Лачуги Старой Эвелории.
 

Message:  1099
<ce> Ты храбрая душа, если решил попробовать составить
<ce> карту Орсиниума. Если хочешь, просто подожди
<ce> пока время задания не истечёт, а затем
<ce> вернись к нам. Никто не будет винить
<ce> тебя за то, что ты так поступил.

QBN:
Item _reward_ gold range 700 to 799
Item _letter_ letter used 1011

Place _dung_ permanent OrsiniumCity
Place _place_ remote tavern

Clock _timer_ 65.00:00 75.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest