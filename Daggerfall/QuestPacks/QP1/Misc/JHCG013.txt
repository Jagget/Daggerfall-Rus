-- Cartographers Quest
Quest: JHCG013
DisplayName: Охота на ведьм
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG013

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG013

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG013

QuestFail:  [1003]
<ce> К этому моменту ты уже должен был сообщить Картографам
<ce> где находятся Гленморильские Ведьмы.

QuestComplete:  [1004]
<ce> С возвращением. Довольно странные люди, верно?
<ce> Ты волен вернуться и посмотреть, что они
<ce> могут предложить, если захочешь. Только не забудь,
<ce> не нужно в это впутывать меня и остальных членов 
<ce> Ассоциации. Если мы обнаружим, что ты пьешь кровь или
<ce> танцуешь в грозу или что-то в этом роде,
<ce> мы тебя не знаем.

Message:  1010
<ce> Юноша, находящийся на службе у придворного, вручает
<ce> тебе письмо. "Ты, должно быть, %pcf.
<ce> Это тебе". Затем он идет дальше.
   <--->
<ce> Девушка, находящийся на службе у придворного, вручает
<ce> тебе письмо. "Ты, должно быть, %pcf.
<ce> Это тебе". Затем она идет дальше.

Message:  1011
  %pcf,
 
  Мы хотим поручить тебе составить карту пути от
  Шабаша Гленморил в Илессанских Холмах до моего текущего местоположения.
  Тебе предоставляется =timer_ дней от настоящего момента. Спроси
  меня, если тебе нужны указания, как его найти.
 
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов
                ___place_, ____place_

Message:  1012
<ce> Добро пожаловать, скаут. Маршрут который тебе предстоит, не
<ce> особенно прост. Один друг сказал мне, что ты можешь запросто 
<ce> добраться до Шабаша Гленморил.
 
<ce> Для начала скачи в Жилище Старой Христаусы.
<ce> в Илессанские Холмы. Прижми лошадь
<ce> к западной стене дома, а затем
<ce> скачите в течение от восьмидесяти до восьмидесяти пяти
<ce> секунд прямо на запад. После этого,
<ce> продолжай двигаться на север, пока не увидишь его. Оно выглядит как
<ce> кирпичный купол с травой на вершине. Если ты обнаружишь
<ce> себя на той же широте, что и
<ce> Кладбище Хартхарт, то ты зашел слишком
<ce> далеко на север. Это к востоку от Кроссфорта и
<ce> севернее Жилища Старой Ивелоны, если это тебе поможет.
<ce> Я не знаю, впервые ли ты встречаешься с
<ce> ведьмами, но если это так, то убедись, что у тебя
<ce> крепкий желудок, прежде чем делать им одолжение.

Message:  1013
<ce> Купол перед тобой, должно быть и есть
<ce> шабаш Гленморильских Ведьм
<ce> Ты можешь сообщить об этом _qg_.
   <--->
<ce> Ты видишь Гленморильские Ведьмы. Теперь
<ce> ты можешь отправиться в ___place_, чтобы отчитаться.

Message:  1030
%qdt:
 _qg_ послал меня, чтобы
 найти шабаш
 Гленморильских Ведьм в
 Илессанских Холмах. У меня есть =timer_
 дней, чтобы показать свою карту
 в ___place_.

Message:  1031
%qdt:
 Чтобы найти Шабаш Гленморил,
 я могу заседлать лошадь и отправиться в
 Жилище Старой Христаусы в Илессанские Холмы.
 Начав возле дома, я могу проехать
 на запад в течение восьмидесяти-восьмидесяти пяти сек,
 а затем поехать прямо на север. Если я достигну
 той же широты, что и Кладбище Хартхарт,
 мне нужно ехать дальше на юг.

Message:  1099
<ce> Некоторые люди пытаются найти ведьм, чтобы добиться благосклонности
<ce> от них. Я не лезу в дела таких людей.
<ce> Очень многое может пойти не так, если работать с ними.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent GlenmorilCoven
Place _place_ remote house2

Clock _timer_ 70.00:00 76.00:00

Person _qg_ group Questor male

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT3
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL2
    end quest