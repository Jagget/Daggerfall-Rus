-- Cartographers Quest
Quest: JHCG013
DisplayName: Охота на ведьм
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG013

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG013

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG013

QuestFail:  [1003]
<ce> К этому моменту вы уже должны были сообщить Картографам,
<ce> где находятся Гленморильские Ведьмы.

QuestComplete:  [1004]
<ce> С возвращением. Довольно странные люди, верно?
<ce> Вы вольны вернуться и посмотреть, что они
<ce> могут предложить, если захотите. Только не забудьте,
<ce> не нужно в это впутывать меня и остальных членов 
<ce> Ассоциации. Если мы обнаружим, что вы пьете кровь 
<ce> и танцуете в грозу или что-то в этом роде,
<ce> мы вас не знаем.

Message:  1010
<ce> Юноша, находящийся на службе у придворного, вручает
<ce> вам письмо. "Вы, должно быть, %pcf.
<ce> Это вам". Затем он идет дальше.
   <--->
<ce> Девушка, находящаяся на службе у придворного, вручает
<ce> вам письмо. "Вы, должно быть, %pcf.
<ce> Это вам". Затем она идет дальше.

Message:  1011
  %pcf,
 
  Мы хотим поручить вам составить карту пути 
  от Шабаша Гленморил в Илессанских Холмах до моего текущего 
  местоположения. Вам предоставляется =timer_ дней от настоящего 
  момента. Спросите меня, если вам нужны указания, как его найти.
 
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов
                ___place_, ____place_

Message:  1012
<ce> Добро пожаловать, скаут. Маршрут который вам предстоит, 
<ce> не особенно прост. Один друг сказал мне, что вы можете 
<ce> запросто добраться до Шабаша Гленморил.
 
<ce> Для начала скачите в Жилище Старой Христаусы
<ce> в Илессанские Холмы. Прижмите лошадь к западной  
<ce> стене дома, а затем скачите в течение восьмидесяти - 
<ce> восьмидесяти пяти секунд прямо на запад.
<ce> После этого продолжайте двигаться на север,
<ce> пока не увидите его. Он выглядит как
<ce> кирпичный купол с травой на вершине. Если вы обнаружите
<ce> себя на той же широте, что и Кладбище Хартхарт,
<ce> то вы зашли слишком далеко на север.
<ce> Это к востоку от Кроссфорта и севернее Жилища Старой Ивелоны,
<ce> если это вам поможет.
<ce> Я не знаю, впервые ли вы встречаетесь 
<ce> с ведьмами, но если это так, то убедитесь, что у вас
<ce> крепкий желудок, прежде чем делать им одолжение.

Message:  1013
<ce> Купол перед вами, должно быть, и есть
<ce> шабаш Гленморильских Ведьм
<ce> Вы можете сообщить об этом _qg_.
   <--->
<ce> Вы видите Гленморильских Ведьм. Теперь
<ce> вы можете отправиться в ___place_, чтобы отчитаться.

Message:  1030
%qdt:
 _qg_ послал меня, 
 чтобы найти шабаш
 Гленморильских Ведьм 
 в Илессанских Холмах. У меня есть =timer_ дней, 
 чтобы показать свою карту
 в ___place_.

Message:  1031
%qdt:
 Чтобы найти Шабаш Гленморил,
 я могу сесть на лошадь и отправиться 
 в Жилище Старой Христаусы в Илессанские Холмы.
 Начиная от дома, я могу проехать
 на запад в течение восьмидесяти - восьмидесяти пяти секунд,
 а затем поехать прямо на север. Если я достигну
 той же широты, что и Кладбище Хартхарт,
 мне нужно ехать дальше на юг.

Message:  1099
<ce> Некоторые люди пытаются найти ведьм, чтобы добиться 
<ce> благосклонности от них. Я не лезу в дела таких людей.
<ce> Очень многое может пойти не так, если работать с ними.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent GlenmorilCoven
Place _place_ remote house2

Clock _timer_ 70.00:00 76.00:00

Person _qg_ group Questor male

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT3
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL2
    end quest