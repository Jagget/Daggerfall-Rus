-- Cartographers Quest
Quest: JHCG023
DisplayName: Башня Диренни
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG023

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG023

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG023

QuestFail:  [1003]
<ce> Тебе уже нужно было отправиться в Башню Диренни.

QuestComplete:  [1004]
<ce> Говорят, что Башня Диренни - одно из самых старых
<ce> зданий во всем Тамриэле. Ну, это то, что
<ce> говорят. Кто знает, насколько это правда. Надеюсь, что
<ce> тебе понравилось живописное путешествие, Следопыт.
<ce> Держи свой гонорар за задание.

Message:  1010
<ce> Курьер вручает тебе свиток.
<ce> "От Картографов", - говорит он
<ce> перед тем, как отправиться по делам.
   <--->
<ce> Курьер вручает тебе свиток.
<ce> "От Картографов", - говорит она
<ce> перед тем, как отправиться по делам.

Message:  1011
  Следопыт,
 
  Нам нужна карта от Башни Диренни до сюда.
  Это находится на Острове Балфиера. Это довольно простое путешествие
  если ты знаешь, куда ехать. Если нет, приходи ко мне и
  я сориентирую тебя, как смогу. Мы даем тебе =timer_ дней.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Очень хорошо, Следопыт. Покажи мне свою карту. Посмотри
<ce> сначала на Остров Балфиера. Там есть один город в
<ce> центре самого большого массива суши, Сингбери.
<ce> Начни оттуда и двигайся на восток. Дальше это будет просто
<ce> дикая местность. Саму башню не должно быть слишком
<ce> трудно заметить. Вся остальная растительность
<ce> как правило, находится довольно низко к земле. Приходи сюда
<ce> после этого и представь свой отчет. Желаю удачи.

Message:  1013
<ce>Это, должно быть, Башня Диренни.
   <--->
<ce> Птицы кружат вокруг этой огромной каменной башни. Это
<ce> должно быть то, за чем тебя послали Картографы.

Message:  1030
%qdt:
 _qg_, из ___place_ в
 ____place_, отправил меня в Башню
 Диренни, чтобы нарисовать карту. У меня есть
 =timer_ дней, чтобы сделать это.

Message:  1031
%qdt:
 Чтобы добраться до Башни Диренни, я должен начать
 в Сингбери и пойти на восток. Нужно просто
 долго идти, пока я не увижу её.

Message:  1099
<ce> Это огромная каменная башня,
<ce> сильно контрастирующая с окружающей
<ce> местностью. Она должна выделяться довольно четко.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent DirenniTowerEntry
Place _place_ remote house2

Clock _timer_ 60.00:00 65.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest