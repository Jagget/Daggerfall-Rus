-- Cartographers Quest
Quest: JHCG003
DisplayName: Менеджеры среднего звена
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG003

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG003

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG003

QuestFail:  [1003]
<ce> _qg_ неловко вздыхает.
<ce> "Я верю, что у тебя были самые лучшие намерения, но
<ce> это была реальная задача, которую нам нужно было выполнить. Я должен
<ce> вернуться в ___place_. Мы отправим тебе
<ce> письмо, когда сможем, о том, что делать дальше".

QuestComplete:  [1004]
<ce> Да, действительно, это правильное место. Хорошая работа,
<ce> %pcf. Позволь мне вручить тебе твою оплату. Возможно
<ce> это и была тренировка, но мы не хотим, чтобы
<ce> чтобы ты чувствовал, что мы зря потратили твое время.
<ce> Я обещаю тебе, что как только ты поднимешься в ранге,
<ce> мы больше не будем вмешиваться в твои дела.
<ce> А сейчас я возвращаюсь в ___place_. Береги себя.

Message:  1010
<ce> Курьер громко кричит: "Эй! Ты, %ra!
<ce> Мне нужно отдать это тебе! Возьми!"
<ce> После этого он быстро уходит.
   <--->
<ce> Курьер громко кричит: "Эй! Ты, %ra!
<ce> Мне нужно отдать это тебе! Возьми!"
<ce> После этого она быстро уходит.

Message:  1011
  %pcf,
 
  Картографы поручили мне сопровождать вас
  во время поиска пути в ____place_.
  Давайте встретимся в ___place_, чтобы мы могли пойти
  вместе. Пунктом назначения будет __home_,
  и упражнение должно быть выполнено не дольше
  чем через =timer_ дней, чтобы
  поддержать вашу репутацию в ассоциации.
 
                _qg_
                Исследователь, полевой офицер
                Ассоциация Картографов
                ___place_, ____place_

Message:  1012
<ce> А, %pcf, рад тебя видеть.
<ce> Нам нужно идти в __home_. Это
<ce> хорошая прогулка, так что у меня будет много
<ce> времени, чтобы понаблюдать за твоей техникой разведки.
<ce> Я готов идти, как только ты будешь готов.

Message:  1020
<ce> Ваши тренировки с Картографами
<ce> нужно было закончить к сегодняшнему дню.

Message:  1030
%qdt:
 _qg_ из ___place_ нужно
 понаблюдать за мной во время разведывательного
 упражнении для Картографов.
 Мы должны вместе добраться до
 __home_ раньше, чем за =timer_ дней.

QBN:
Item _reward_ gold range 150 to 200
Item _letter_ letter used 1011

Place _home_ remote farmhome
Place _place_ remote house2

Clock _timer_ 24.00:00 38.00:00

Person _qg_ group Chemist

--	Quest start-up:
          start timer _timer_
          give pc _letter_ notify 1010
          place npc _qg_ at _place_
          pc at _home_ set _inside_
          have _letter_ set _log_

_log_ task:
    log 1030 step 0

_qgclicked_ task:
     clicked npc _qg_ 
     say 1012
     hide npc _qg_
     add _qg_ face

variable _inside_
_victory_ task:
    when _qgclicked_ and _inside_
    start quest JHCGENT1
    give pc _reward_
    end quest

variable _timer_
_failwithout_ task:
        when _timer_ and not _qgclicked_
         say 1020
         start quest JHCGFAIL
         end quest                 

_failwith_ task:
         when _timer_ and _qgclicked_
         say 1003
         start quest JHCGFAIL
         end quest