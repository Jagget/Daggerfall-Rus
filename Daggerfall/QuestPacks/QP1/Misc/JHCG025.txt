-- Cartographers Quest
Quest: JHCG025
DisplayName: Шабаш Врот
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHCG025

RefuseQuest:  [1001]
<ce> ERROR 1001 JHCG025

AcceptQuest:  [1002]
<ce> ERROR 1002 JHCG025

QuestFail:  [1003]
<ce> Картографы поручили тебе
<ce> составить карту Шабаш Врот к этому времени.

QuestComplete:  [1004]
<ce> Молодец, %pcf. Такие вещи
<ce> всегда нелегко найти вначале, но как только
<ce> ты это сделал, то можешь продолжать выполнять задание
<ce> с полной уверенностью. Возможно, у нас никогда
<ce> не закончится задания по составлению карт, даже если ты будешь
<ce> отправляться в одни и те же места снова и снова.

Message:  1010
<ce> Мужчина, носящий на одежде герб
<ce> Картографов, вручает тебе письмо.
<ce> и стремительно уходит.
   <--->
<ce> Женщина, носящая на одежде герб
<ce> Картографов, вручает тебе письмо.
<ce> и стремительно уходит.

Message:  1011
  Следопыт,
 
  Ты нам нужен, чтобы составить карту Шабаш Врот,
  в Вротгарианских горах. Я нахожусь в
  ___place_ на случай, если тебе понадобятся инструкции.
  На выполнение этого задания у тебя есть =timer_ дней.
 
                    _qg_
                    Ассистент Мастера
                    Ассоциация Картографов
                    ___place_, ____place_

Message:  1012
<ce> Конечно, я помогу тебе, %pcf.
<ce> Начни с деревни Дирвиль в
<ce> Вротгарианских горах. А именно,
<ce> встань рядом с магазином под названием Дирвиль: Наилучший
<ce> Универмаг, и иди на восток. Если ты будешь следовать
<ce> этому направлению в точности, то ты должен пройти прямо
<ce> над каменным кругом Шабаш Врот.
<ce> Поблизости нет никаких ориентиров или холмов, так что
<ce> к сожалению, это все указания, какие я могу дать
<ce> тебе. Добавлю, что это не дальше
<ce> двух километров. Не стоит идти до
<ce> Скайрима в поисках его. Мы дали тебе =timer_
<ce> дней на выполнение этого задания.

Message:  1013
<ce> Это, должно быть, Шабаш Врот.
   <--->
<ce> Ты нашел Шабаш Врот. Теперь самое
<ce> вермя отправиться к _qg_.

Message:  1030
%qdt:
 Картографы отправили меня в
 Шабаш Врот в Вротгарианских
 Горах. У меня есть =timer_ дней, чтобы найти
 его и прибыть в ___place_
 в ____place_ за своей наградой.

Message:  1031
%qdt:
 _qg_ сказал, что я должен поехать в Дирвилль, в
 Дирвиль: Наилучший Универмаг, а затем
 просто идти на восток. Шабаш - это каменный круг
 посреди зелени, не далее
 чем в двух километрах от деревни.

Message:  1099
<ce> Его не всегда легко найти, так как там
<ce> нет никаких ориентиров, кроме Дирвилля.
<ce> Следи за зазубренными камнями в Шабаше.

QBN:
Item _reward_ gold range 800 to 899
Item _letter_ letter used 1011

Place _dung_ permanent WrothCoven
Place _place_ remote tavern

Clock _timer_ 70.00:00 80.00:00

Person _qg_ group Chemist

--	Quest start-up:
    give pc _letter_ notify 1010
    place npc _qg_ at _place_
    have _letter_ set _log_

_log_ task:
    log 1030 step 0
    start timer _timer_
    pc at _dung_ set _inside_

_qgclicked_ task:
    clicked npc _qg_

_begin_ task:
    when _qgclicked_ and not _talked_ and not _found_
    say 1012
    clear _qgclicked_
    log 1031 step 1
    start task _talked_

variable _talked_
_inside_ task:
    start task _found_

_found_ task:
    say 1013

_reminder_ task:
    when _qgclicked_ and _talked_ and not _found_
    say 1099
    clear _qgclicked_ _reminder_

_victory_ task:
    when _qgclicked_ and _found_
    start quest JHCGENT4
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    start quest JHCGFAL3
    end quest