Quest: JHTG123
DisplayName: Возвращение заначки
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас очередная ситуация с залогом.
<ce> Один из идиотов здесь, в ___qgiver_.
<ce> хранит какие-то _drug_ в пещере,
<ce> и стражники пронюхали об этом.
<ce> Они выставили охрану внутри,
<ce> и нам нужен кто-то, кто пойдет и возьмет тайник.

RefuseQuest:  [1001]
<ce> Не хочешь подставить шею?
<ce> В гильдии одни трусы.

AcceptQuest:  [1002]
<ce> Поспеши в ___dung_, так это называется.
<ce> Я отмечу его на твоей карте, если ты не знаешь это место.
<ce> Ты ищешь _drug_. Маленькая штука, вызывающая привыкание,
<ce> это разновидность наркотика, если ты не знаешь.
<ce> Так вот, я говорил тебе, что у входа стоит охранник,
<ce> и ты не можешь его убить.
<ce> По крайней мере, не должен. Никто не собирается
<ce> ломать тебе шею за то, что ты это сделал,
<ce> но тебе не нужна плохая репутация. Точно не в %crn.
<ce> Принеси препарат обратно сюда через =timer_ дней,
<ce> потому что у нас есть договоренность о том, чтобы 
<ce> в скором времени отправили его дальше.

QuestFail:  [1003]
<ce> _qgiver_ хотел, чтобы ты уже
<ce> доставить _drug_ к этому времени.

QuestComplete:  [1004]
<ce> Вот твои деньги, %ra.

Message:  1011
<ce> Вот и _drug_. Я это заберу.
<ce> Уже пошли слухи, что
<ce> ты убил охранника в ___dung_.
<ce> Не лучший образ для тебя и гильдии, но мы справимся.
<ce> Просто будь осторожнее в таких делах, хорошо?

Message:  1012
<ce> А, _drug_. Хорошая работа.
<ce> Охранник, которого они выставили в ___dung_,
<ce> прислал сообщение, что это место было ограблено
<ce> "неизвестным нападавшим" и его нужно покинуть.
<ce> Знаешь, как мы это называем? Успех!

Message:  1013
<ce> Это _drug_, который тебя послали найти.

Message:  1014
<ce> Часовой замечает как ты проник в ___dung_.
<ce>
<ce> "Стоять! Здесь проходит следствие!
<ce> Немедленно покинь это место или прими последствия!"

Message:  1015
<ce> Убив этого охранника, ты сможешь спокойно
<ce> исследовать это место.

Message:  1030
%qdt:
 _qgiver_ из ___qgiver_ послал меня, чтобы я
 забрал какой-то забытый _drug_
 в месте, которое называется ___dung_, ____dung_.
 Мне нужно вернуться
 в течение =timer_ дней.
 У входа выставлен охранник
 и мне не следует убивать его.

QBN:

Person _qgiver_ group Questor male

Item _reward_ gold range 350 to 400
Item _drug_ drug

Foe _enemy_ is Knight

Place _dung_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2

-- Quest start-up:
    start timer _timer_
    log 1030 step 0
    place item _drug_ at _dung_
    reveal _dung_
    pc at _dung_ set _spawner_

_spawner_ task:
    start task _spawn_

_slain_ task:
    killed _enemy_
    say 1015
    legal repute -10

_spawn_ task:
    say 1014
    create foe _enemy_ every 0 minutes 1 times with 100% success

_got_ task:
    clicked item _drug_
    say 1013

_victory_ task:
    toting _drug_ and _qgiver_ clicked

_badend_ task:
    when _victory_ and _slain_
    say 1011
    give pc _reward_
    end quest
 
_goodend_ task:
    when _victory_ and not _slain_
    say 1012
    give pc _reward_
    end quest

_timer_ task:
    say 1003
    make _drug_ permanent
    end quest