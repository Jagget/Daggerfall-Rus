Quest: JHTG104
DisplayName: Исповедь
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас на руках сложное дело.
<ce> Они схватили _dummy_. Ты встречался 
<ce> с ним до того, как его поймали? Не важно. Он сидит в тюрьме и
<ce> ожидает суда, за огромный грабёж. Вот почему
<ce> суд откладывается. Они собирают улики и
<ce> заперли его в ___pal_. Нам нужен кто-то, кто
<ce> сможет украсть признание этого идиота до начала суда,
<ce> что через =2pal_ дней. Помоги нам.

RefuseQuest:  [1001]
<ce> Тогда я больше не буду давать за тебя взятки. Ни в коем случае.
   <--->
<ce> Ты действительно считаешь, что он этого заслуживает?

AcceptQuest:  [1002]
<ce> План такой. Он находится во дворце в ___pal_.
<ce> Мы знаем, что это место серьезно охраняется.
<ce> Но это не заметно, пока не поднимется тревога.
<ce> Как только признание у тебя, сразу же используй _trinket_.
<ce> Он вытащит тебя. Это довольно сложный план
<ce> отхода, но они спустят с тебя шкуру, если схватят.
<ce> После этого ты можешь не торопиться с возвращением,
<ce> но ты должен забрать его позже чем в =2pal_ дней, иначе
<ce> суд состоится. Нельзя терять время. До встречи.

QuestFail:  [1003]
<ce> ERROR 1003 JHTG104

QuestComplete:  [1004]
<ce> Почти не чувствуется, что ты уходил, не так ли %pcf?
<ce> На что мы только не идём ради брата. Что ж, ты доказал
<ce> свою силу на этот раз. Возьми свою награду. Хорошая работа.

Message:  1010
<ce> Ты активируешь _trinket_.

Message: 1011
<ce> Охранник замечает тебя и начинает бить тревогу.
<ce>
<ce> "Стой! Воришка! Все идите сюда скорее!"
   <--->
<ce> Затаившийся охранник начинает бить тревогу.
<ce>
<ce> "Эй! Стой! Кто-то крадет улики!"

Message:  1015
<ce> _trinket_ распался на части под действием
<ce> заклинания. Здесь тебя никто не выследит, но тебе придется
<ce> самому искать выход.
<ce>
<ce> (Подсказка: ты можешь оказаться в месте, из которого невозможно выбраться)
<ce> (например, в закрытой клетке или на дне желоба. Если это так, то)
<ce> (сохранись и попробуй консольную команду "teleport", чтобы выбраться)

Message:  1020
<ce> Вы не смогли украсть доказательства для суда над _dummy_.

Message:  1030
%qdt:
 _qgiver_ из Гильдии Воров
 нуждается в том, чтобы я добыл признание и
 показания одного из членов
 гильдии во дворце в ___pal_.
 У меня есть план отхода - телепортироваться
 используя _trinket_, который они мне подарили. Я должен
 украсть его в течение =2pal_ дней, но
 после этого я могу не торопиться возвращаться
 к ___qgiver_.

Message: 1050
<ce> Это, наверное, и есть письмо с признанием.

QBN:
Item _item_ letter
Item _trinket_ trinket
Item _reward_ gold range 500 to 800

Place _dung_ remote dungeon
Place _pal_ remote palace

Clock _2pal_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00
Clock _danger_ 00:01

Person _qgiver_ group Questor male
Person _dummy_ group Local_4.0 male
Person _dummytg_ faction The_Thieves_Guild

Foe _knight_ is 2 Knight

--	Quest start-up:
 start timer _2pal_
 start timer _failsafe_
 log 1030 step 0
 place item _item_ at _pal_
 get item _trinket_
 pc at _pal_ do _inside_

_begin_ task:
 clicked item _item_
 _trinket_ used do _send_
 start timer _danger_
 say 1050

_danger_ task:
  say 1011
  change repute with _dummytg_ by -15

variable _inside_
_spawn_ task:
  when _danger_ and not _send_
  create foe _knight_ every 1 minutes indefinitely with 100% success
  play sound halt every 0 minutes 1 times
  setplayercrime Theft
  spawncityguards immediate

_send_ task:
 stop timer _2pal_
 stop timer _danger_
 say 1010
 teleport pc to _dung_
 say 1015
 take _trinket_ from pc

_pcgetsgold_ task:
  toting _item_ and _qgiver_ clicked
  give pc _reward_
  end quest

_2pal_ task:
  say 1020
  end quest

_failsafe_ task:
  end quest