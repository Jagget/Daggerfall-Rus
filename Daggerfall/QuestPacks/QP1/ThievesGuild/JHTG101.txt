Quest: JHTG101
DisplayName: Тест на скорость
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Иногда в гильдии мы любим позабавиться спортом.
<ce> У меня есть приглашение, к которому ты, возможно, захочешь прислушаться.
<ce> В одном из склепов неподалеку, который кишит летучими мышами,
<ce> есть сундук с сокровищами. Если ты сможешь найти сундук
<ce> и принести его сюда, не убив ни одной летучей мыши,
<ce> то получишь нечто зачарованное. Хочешь поучаствовать?

RefuseQuest:  [1001]
<ce> Посмотрим, может быть, ты передумаешь.

AcceptQuest:  [1002]
<ce> "Хорошо, мы должны сделать кое-что, прежде чем
<ce> мы скажем тебе, где он находится".
<ce>
<ce> Входит закутанная в робы фигура, накладывает на тебя два коротких
<ce> заклинания, и уходит. Ты не чувствуешь никакой разницы.
<ce>
<ce> "Вот. Теперь мы сможем тебя видеть, а летучие мыши
<ce> будут роиться за тобой. Не убей ни одной из них в
<ce> склепе. Место называется ___mondung_.
<ce> Заклинание не будет длиться вечно, так что возвращайся обратно
<ce> сюда через =timer_ дней с сундуком в руках.
<ce> Ты получишь зачарованное _reward_,
<ce> если сделаешь это. Береги себя".

QuestFail:  [1003]
<ce> Тебе нужно было доставить сундук в
<ce> Гильдию Воров к этому времени.

QuestComplete:  [1004]
<ce> Это было захватывающее зрелище. Многие собрались
<ce> посмотреть и выпить медовухи, даже делали ставки.
<ce> Хотелось бы повторять это почаще. Вот
<ce> твоя _reward_. Надеюсь, это хорошо послужит тебе.

QuestLogEntry:  [1010]
%qdt:
 Гильдия Воров поставила передо мной задачу
 найти определенный сундук в ___mondung_
 и доставить его им в ___questgiver_
 в течение =timer_ дней. При этом я не могу убить ни одной
 летучей мышей внутри этого места.

Message:  1011
<ce> Как только твоя рука касается этого сундука, перед тобой
<ce> появляется магический знак отличия. Должно быть, это тот сундук.
<ce> Ты разочарован, увидев, что он пуст, но сундук
<ce> все же нужно доставить.

Message:  1012
<ce> Как только летучая мышь падает, перед твоими глазами появляется
<ce> видение черепа. Гильдия Воров знает, что ты потерпел неудачу.

Message:  1013
<ce> Ты уже слышишь слабое стрекотание летучих мышей.

QBN:
Item _reward_ magic_item
Item _chest_ item class 1 subclass 10

Person _questgiver_ group Questor male

Place _mondung_ remote dungeon0

Clock _timer_ 00:00 0 flag 17 range 1 2

Foe _bat_ is Giant_bat

--	Quest start-up:
  start timer _timer_
  log 1010 step 0
  pc at _mondung_ set _inside_
  place item _chest_ at _mondung_
  reveal _mondung_

_inside_ task:
  say 1013
  create foe _bat_ every 5 minutes indefinitely with 33% success

_find_ task:
  clicked item _chest_
  say 1011

_slain_ task:
  killed _bat_
  say 1012
  make _chest_ permanent
  end quest

_timer_ task:
  say 1003
  make _chest_ permanent
  end quest

_victory_ task:
  toting _chest_ and _questgiver_ clicked
  give pc _reward_
  end quest