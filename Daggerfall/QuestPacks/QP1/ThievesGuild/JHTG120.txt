Quest: JHTG120
DisplayName: Замок аристократа
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> У нас в городе есть член гильдии, которому удалось
<ce> сорвать большой куш, совершив взлом. Украл ящик с
<ce> с очень ценными вещами внутри,
<ce> но нужен слесарь, чтобы открыть его.
<ce> Это не будет свершением мирового масштаба,
<ce> но тебе лучше знать, как взламывать сложные замки,
<ce> если ты собираешься взяться за это дело.

RefuseQuest:  [1001]
<ce> Хорошо. Не притворяйся тем, кем ты не являешься.

AcceptQuest:  [1002]
<ce> Отправляйся в _tav_ и ищи _thief_.
<ce> Мы сказали %g2 не приносить сюда ящик,
<ce> на всякий случай, вдруг что-то пойдет не так.
<ce> Нам здесь не нужны городские стражники.
<ce> Если ты откроешь его, твоя репутация в гильдии повысится,
<ce> как и за любую другую работу. _thief_
<ce> заплатит гильдийский взнос, так что всё, что %g1
<ce> даст тебе, будет твоим. Не жди больше
<ce> чем 1 день, чтобы добраться туда, %g1 не может ждать вечно.

QuestFail:  [1003]
<ce> _thief_ отказался ждать тебя в _tav_.

QuestComplete:  [1004]
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Ты был создан для таких моментов.
<ce> Ты садишься перед коробкой и приступаешь к работе.
<ce> Есть несколько сюрпризов, которые тебя обеспокоят,
<ce> но ты справляешься с ними и открываетшь засов.
<ce> _thief_ громко выдыхает, когда ящик открывается.
<ce>
<ce> "Очень мило. Позвольте мне взглянуть...
<ce> Пачки денег, это хорошо.
<ce> Позволь мне взять долю гильдии, и
<ce> ты получишь =gold_ золотых. А это что такое?
<ce> Пара зачарованных предметов.
<ce> Я забираю свой, а тебе достанется второй.
<ce> Приятно было познакомиться".
<ce>
<ce> _thief_ быстро собирает в кучу свою долю
<ce> и поспешно выходит за дверь.

Message:  1010
<ce> "Наконец-то они прислали помощь. Эта штука меня пугает,
<ce> так что тебе лучше знать, что ты делаешь. Я отойду
<ce> и понаблюдаю издалека, на всякий случай".
<ce>
<ce> Замок сделан хорошо и витиевато, но ничего необычного.
<ce> Судя по всему, его легко взломает вор среднего уровня.
<ce> Ты всё ещё можешь отступить, если не уверен.
<ce> Пытаешься ли ты открыть шкатулку?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Взлом, сложность средняя)

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Ты садишься перед коробкой и приступаешь к работе.
<ce> Ты удивлен тем, что находишь внутри замка.
<ce> Вещи находятся не на своих обычных местах, и ты
<ce> находишь трудности и секреты там, где не ожидаешь.
<ce> У тебя возникает плохое предчувствие, и как раз в тот момент, когда ты собираешься
<ce> вынуть отмычку, ты слышишь громкий треск, доносящийся изнутри коробки.
<ce> Из верхней части начинает вырываться пар, и громкий свистящий звук
<ce> разносится по таверне. Ты не можешь его выключить.
<ce>
<ce> "ИДИОТ! СТРАЖНИКИ ЭТО УСЛЫШАТ! БЕГИ!!!"
<ce>
<ce> _thief_ хватает коробку и убегает.
<ce> Теперь тебе придется разбираться с последствиями.

Message:  1013
<ce> "Правда? Ну что ж. Я рад, что ты достаточно умен,
<ce> чтобы отступить, когда понимаешь, что это выше твоих сил.
<ce> Некоторые из этих придурков в гильдии так бы и продолжали
<ce> бить по этой коробке, пока не сломают запястье.
<ce> Я скажу _qgiver_, чтобы он прислал кого-нибудь другого. Пока!".
<ce>
<ce> _thief_ отнес ящик в другой угол
<ce> таверны, чтобы не вызывать подозрений.

Message:  1030
%qdt:
 Я подписался на то, чтобы
 прийти в _tav_, ___tav_
 и открыть запертый ящик
 который держит _thief_.
 Я должен прибыть туда в течение
 24 часов, чтобы открыть его.

QBN:
Item _gold_ gold range 500 to 700
Item _reward_ magic_item

Place _tav_ local tavern

Person _thief_ faction The_Thieves_Guild
Person _qgiver_ group Questor

Clock _timer_ 1.00:00
Clock _delay_ 0:03

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    place npc _thief_ at _tav_

_found_ task:
    clicked npc _thief_
    prompt 1010 yes _attempt_ no _flee_

variable _attempt_
_flee_ task:
    say 1013
    hide npc _thief_
    end quest

_skilled_ task:
    when skill Lockpicking is at least 40

_victory_ task:
    when _attempt_ and _skilled_
    get item _gold_
    make _gold_ permanent
    give pc _reward_
    hide npc _thief_
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    hide npc _thief_
    remove log step 0
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_
    stop timer _timer_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest