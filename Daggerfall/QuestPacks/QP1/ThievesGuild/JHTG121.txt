Quest: JHTG121
DisplayName: Мелкое воровство
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Ты знаешь, как чистить карманы, %pct?
<ce> У нас есть кое-кто в таверне в городе.
<ce> И этот кто-то держит в руках то, что не должен.
<ce> Нам нужно, чтобы это было у нас к концу дня.

RefuseQuest:  [1001]
<ce> Не самое популярный навый. Позор нам, как ворам.

AcceptQuest:  [1002]
<ce> Хорошо, цель в _tav_,
<ce> это на %di, в ___tav_.
<ce> Ищи парня со шрамом на подбородке,
<ce> рыжие волосы. В одном из карманов у него лежит
<ce> листок бумаги, на котором написана очень важная информация.
<ce> Что это такое, тебе знать не обязательно.
<ce> Принеси её сюда, пока не истёк один день.

QuestFail:  [1003]
<ce> Гильдия Воров хотела, чтобы к этому времени ты уже 
<ce> украл и доставил бумагу от рыжего человека.

QuestComplete:  [1004]
<ce> Вот оно. Я не слышу, чтобы кто-то кричал "стоп",
<ce> так что буду считать, что работа была выполнена чисто. Отличная работа.
<ce> Нам бы не помешало побольше таких карманников, как ты.

QuestorPostSuccess:   [1008]
Как поживают пальцы? Украл что-нибудь хорошее в последнее время?

QuestorPostFailure:   [1009]
Ааа, наш так называемый карманник. Такими темпами гильдия превратится в посмешище.

Message:  1010
<ce> Так совпало что твоя цель вот-вот выйдет из таверны.
<ce> Он соответствует описанию. Если ты собираешься обокрасть его,
<ce> то это твой шанс. Воспользуешься ли ты им?
<ce>
<ce> (ПРОВЕРКА НАВЫКА: ДА: Карманные кражи, сложность низкая)

Message:  1011
<ce> Слова на этой бумаге постоянно смещаются.
<ce> Чтобы прочитать их, тебе понадобится заклинание.

Message:  1012
<ce> (ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> Ты ждешь, пока он пройдет мимо, прежде чем потянуться в задний карман
<ce> его одежды. Карман оказался пуст, и цель замечает твою попытку.
<ce> Он наносит тебе быстрый удар наотмашь, после чего зовет охранников
<ce> и скрывается в городе.

Message:  1013
<ce> У тебя могут возникнуть серьезные проблемы, если ты сделаешь что-то подобное.
<ce> Ты отходишь в сторону и позволяешь мужчине покинуть таверну.
<ce> _qgiver_ может быть недоволен, но так ты чувствуешь себя лучше.

Message:  1014
<ce> (ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Ты знаешь, как с этим справиться.
<ce> Ты нацеливаешься на карман мужчины и как бы случайно натыкаешься на него.
<ce> Он злится из-за и ворчит из-за этой стычки, но не замечает
<ce> твоей кражи. Ты извиняешься, и он уходит, ворча под нос.

Message:  1030
%qdt:
 Какой-то мужчина с рыжими волосами и шрамом на подбородке
 находится в _tav_, имея на руках бумагу, представляющую ценность
 для Гильдии Воров. Мне нужно стащщить этот документ
 и доставить _qgiver_ сюда
 в ___tav_ до того, как истечёт день.

QBN:
Item _gold_ gold range 150 to 200
Item _letter_ letter used 1011

Place _tav_ local tavern

Person _qgiver_ group Questor

Clock _timer_ 1.00:00
Clock _delay_ 0:02

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _tav_ do _inside_

_skilled_ task:
    when skill Pickpocket is at least 25

_inside_ task:
    prompt 1010 yes _attempt_ no _flee_

_flee_ task:
    say 1013
    end quest

variable _attempt_

_theft_ task:
    when _attempt_ and _skilled_
    get item _letter_
    say 1014

_victory_ task:
    toting _letter_ and _qgiver_ clicked
    give pc _gold_
    end quest

_bigfail_ task:
    when _attempt_ and not _skilled_
    say 1012
    remove log step 0
    stop timer _timer_
    reduce player health by 5

_outside_ task:
    when _bigfail_ and not _inside_
    setplayercrime Theft
    spawncityguards immediate
    start timer _delay_

_delay_ task:
    end quest

_timer_ task:
    say 1003
    end quest
