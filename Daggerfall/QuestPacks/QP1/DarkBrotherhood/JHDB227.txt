Quest: JHDB227
DisplayName: Воры запрещены
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Идиотизм в Тамриэле, похоже, не знает 
<ce> границ. Представьте себе глупость 
<ce> человека, который входит в святилище 
<ce> Темного Братства без предупреждения и 
<ce> приглашения и начинает подбирать ценные 
<ce> предметы с земли. Таково качество членов 
<ce> Гильдии Воров в наши дни. Если вы убьете 
<ce> этого вора, то будете вознаграждены 
<ce> предметом, который даст вам возможность 
<ce> плавать, как самая быстрая из рыб.

RefuseQuest:  [1001]
<ce> Члены Гильдии Воров прискорбно слабы.
<ce> Для кого-то другого эта работа станет воскресной прогулкой.

AcceptQuest:  [1002]
<ce> Я молюсь, чтобы Гильдия Воров научилась держать 
<ce> своих членов подальше от наших залов, и как 
<ce> можно скорее. Этот дурак недавно уехал из 
<ce> города и теперь живет в _tav_ в ___tav_. Так 
<ce> говорит Начальник шпионажа. Вернитесь с 
<ce> известием о смерти вора в течение =timer_ дней.

QuestFail:  [1003]
<ce> Мастер шпионажа не ошибается, %pcf.
<ce> Найдите и убейте вора в _tav_ в ___tav_.

QuestComplete:  [1004]
<ce> Честно говоря, я надеялся, что вы примете этот контракт. 
<ce> Ожидается, что наши члены будут ставить пожелания Матери Ночи 
<ce> превыше всех других соображений. Потеря Гильдии Воров - это ваша 
<ce> выгода, и ваш новый _reward_ доказывает это.

Message:  1010
<ce> Где-то в этой таверне находится вор, 
<ce> которого вы должны убить.

Message:  1011
<ce> "О, да? Я крепче тебя!"

Message:  1012
<ce> Вор мертв.

Message:  1030
%qdt:
 Темное Братство ___questgiver_
 даст мне предмет, наделяющий
 способностью быстро плавать, если я убью члена
 Гильдии Воров, который украл у нас.
 Вор остановился в _tav_ в ___tav_.
 Я должен сообщить о своем успехе в течение =timer_ дней.

QBN:
Person _questgiver_ group Questor male
Person _fgperson_ faction The_Thieves_Guild remote

Place _tav_ remote tavern

Clock _timer_ 00:00 0 flag 1 range 0 2

Item _reward_ item class 4 subclass 29

Foe _enemy_ is Thief

--	Quest start-up:
    start timer _timer_
    place foe _enemy_ at _tav_
    change foe _enemy_ infighting true
    log 1030 step 0
    pc at _tav_ do _inside_
    hide npc _fgperson_

_inside_ task:
    say 1010

_inj_ task:
    injured _enemy_

_talk_ task:
    when _inj_ and not _victory_
    say 1011
    cast Feet_of_Notorgo spell on _enemy_
    cast Nimbleness spell on _enemy_
    cast Shield spell on _enemy_

_victory_ task:
    killed _enemy_
    say 1012

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _victory_
    say 1003
    clear _qgclicked_ _clearclick_
    
_pcgetsgold_ task:
    when _qgclicked_ and _victory_
    give pc _reward_
    start quest JHDBC02
    end quest

_timer_ task:
    end quest