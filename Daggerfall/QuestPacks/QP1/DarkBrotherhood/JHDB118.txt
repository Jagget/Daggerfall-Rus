Quest: JHDB118
DisplayName: Сборщик долгов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Посредственное существо, живущее в %crn, 
<ce> накопило долг, превышающий шестизначную цифру, 
<ce> что обязывает кредиторов обратиться к нам. 
<ce> Смерть теперь - единственное будущее дурака. 
<ce> Вы можете заняться этим на досуге.

RefuseQuest:  [1001]
<ce> Вместо этого ищете работу курьера?

AcceptQuest:  [1002]
<ce> Найдите владельца _res_ в ___res_. Кто бы 
<ce> это ни был, вероятно, он вооружен, поскольку 
<ce> это не первый раз, когда кто-то пытается 
<ce> уладить это дело. Расскажете мне историю о 
<ce> расчленении этой цели через =timer_ дней.

QuestFail:  [1003]
<ce> Темному Братству к настоящему времени требовался ваш 
<ce> отчет о нерадивом домовладельце в ___res_

QuestComplete:  [1004]
<ce> Отличная работа. Как было продемонстрировано, мы играем 
<ce> важную роль. Пусть никто не мошенничает безнаказанно, 
<ce> пока у нас есть агенты для выполнения нашей работы.

Message:  1010
<ce> Это, должно быть, хорошо вооруженный должник.

Message:  1011
<ce> "Нет! Подожди! Пожалуйста, не надо! Я умоляю тебя! Я дам тебе все, что 
<ce> угодно! Вот! В этой коробке последние деньги, которые у меня есть, 
<ce>  =reward_ золотых! Возьми это и, пожалуйста, просто оставь меня в живых!"

Message:  1012
<ce> "Спасибо вам! О, пожалуйста, просто дайте мне секунду. Позвольте мне 
<ce> ввести комбинацию. Это займет совсем немного времени. Вы можете 
<ce> наблюдать за мной, видите? Я никуда не собираюсь уходить."
<ce>
<ce> Должник поворачивает несколько циферблатов на сложной коробке неподалеку.
<ce> Вскоре щелчок эхом разносится по комнате
<ce>
<ce> "Ладно! Дело сделано. Он откроется через шестьдесят секунд, хорошо?
<ce> Я ухожу отсюда! Спасибо! До свидания!"
<ce>
<ce> Вы остаетесь один. Проходит целая минута, и дверца 
<ce> ящика распахивается, открывая вашу взятку.

Message:  1013
<ce> Надеюсь, преступники в %crn научатся действовать 
<ce> лучше, чем этот дурак. Заказ завершен.

Message:  1030
%qdt:
 Темное Братство ___qgiver_
 отправило меня в ___res_, чтобы погасить
 просроченный долг в _res_.
 Мне нужно сообщить о своем успехе через =timer_ дней.

QBN:
Person _qgiver_ group Questor male
Person _debtor_ faction The_Merchants

Place _res_ remote house

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _m1_ is Nightblade
Foe _m2_ is Vampire
Foe _m3_ is Werewolf

Item _reward_ gold range 2501 to 2799

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pick one of _e1_ _e1_ _e1_ _e2_ _e3_
    pick one of _t1_ _t2_

variable _t1_
variable _t2_
_e1_ task:
    place npc _debtor_ at _res_

_e2_ task:
    place foe _m2_ at _res_
    change foe _m2_ infighting true

_e3_ task:
    place foe _m3_ at _res_
    change foe _m3_ infighting true

_ipr_ task:
    injured _m1_
    injured _m2_
    injured _m3_
    say 1010

_click_ task:
    clicked npc _debtor_

_norm_ task:
    when _click_ and _t1_
    say 1010
    hide npc _debtor_
    place foe _m1_ at _res_
    change foe _m1_ infighting true

_choice_ task:
    when _click_ and _t2_
    prompt 1011 yes _bribe_ no _dud_

_bribe_ task:
    say 1012
    hide npc _debtor_
    get item _reward_
    end quest

_dud_ task:
    place foe _m1_ at _res_
    change foe _m1_ infighting true

_slain_ task:
    killed _m1_
    killed _m2_
    killed _m3_
    say 1013

_timer_ task:
    end quest

_qgclicked_ task:
    clicked npc _qgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc nothing
    end quest