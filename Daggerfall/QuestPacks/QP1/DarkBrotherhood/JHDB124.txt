Quest: JHDB124
DisplayName: В самых темных глубинах
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Нам нужна помощь человека, опытного в бою, быстро 
<ce> соображающего и действующего. Один из наших старших 
<ce> членов проводит ритуал обретения темных сил и скоро 
<ce> проснется слабым как никогда. Кто-то должен обеспечить 
<ce> защиту, когда это произойдет. Ты пойдешь?

RefuseQuest:  [1001]
<ce> Тогда убирайся с дороги. Эта задача должна быть выполнена.

AcceptQuest:  [1002]
<ce> Где-то в ___dung_ наш друг _dbf_ нашел убежище, 
<ce> выполняя длительный ритуал превращения в лича. 
<ce> Мастер шпионажа получил информацию о том, что племя 
<ce> варваров нападет на н%g3 сразу после завершения 
<ce> превращения, пока %g1 слабее всего. Оставайтесь с 
<ce> _dbf_ до тех пор, пока все враги не будут убиты. В 
<ce> качестве награды у меня будет волшебный _item_. 
<ce> Поторопись, потому что я сомневаюсь, что %g1 сможет 
<ce> отложить завершение ритуала дольше, чем на =2dung_ 
<ce> дней. Покажите %g2 этот старый _gem_, который 
<ce> принадлежит %g2, и верните его мне с %g3 силой в нем.

QuestFail:  [1003]
<ce> _dbf_ был убит.

QuestComplete:  [1004]
<ce> А, _dbf_ зарядил своей энергией _gem_.
<ce> Достаточное доказательство. В будущем нам предстоит многое 
<ce> совместных дел. Ваша преданность Братству была отмечена.

Message:  1010
<ce> Похоже, что _dbf_ едва ли находится в сознании.
<ce> Огромная магическая сила исходит от тела.
<ce>
<ce> Почувствовав _gem_, %g1, кажется, становится более 
<ce> разумным. Теперь, когда вы здесь, можно приступать к 
<ce> заключительному этапу ритуала.

Message:  1011
<ce> (ЯЗЫК ОРКОВ, ПРОВЕРКА НАВЫКА ПРОВАЛЕНА)
<ce>
<ce> До вас доносятся крики орков. Вы не 
<ce> знаете, что они имеют в виду, но они 
<ce> явно враждебны. Они направляются к 
<ce> вам двоим.

Message:  1012
<ce> (ЯЗЫК ОРКОВ, ПРОВЕРКА НАВЫКА ПРОЙДЕНА)
<ce>
<ce> Вас приводят в чувство крики орков. Вы 
<ce> достаточно хорошо знаете их речь, чтобы понять.
<ce>
<ce> "Еще один лич! Убить его! Бретонцы больше не 
<ce> получат власти!"
<ce>
<ce> Они направляются к вам двоим.

Message:  1013
<ce> Ритуал заканчивается, и _dbf_ хихикает.
<ce>
<ce> "Наконец-то несокрушимая сила принадлежит мне! Пусть
<ce> эти орки станут первыми свидетелями моего гнева!"
<ce>
<ce> Затем %g1 приглашает вас продолжить бой.

Message:  1014
<ce> Ритуал, который проводил _dbf_, к настоящему времени
<ce> завершен, и вас не было рядом, чтобы %g3 защитить.

Message:  1015
<ce> Орки мертвы. _dbf_ восхищенно улыбается.
<ce>
<ce> "Молодец, %ra. Да встретимся мы снова во тьме".
<ce>
<ce> Вы объясняете, что вам нужны доказательства вашего
<ce> поступка, и %g1 накладывает заклинание на _gem_.
<ce> Затем %g1 покидает вас.

Message:  1030
%qdt:
 Темное Братство ___questgiver_ 
 отправило меня в ___dung_, чтобы 
 защитить _dbf_, члена, проходящего 
 ритуал, чтобы стать личом. Я должен 
 предъявить, а затем вернуть именную 
 драгоценность в качестве доказательства 
 содеянного. Ритуал, скорее всего, 
 закончится в течение =2dung_ дней.

QBN:

Person _questgiver_ group Questor male
Person _dbf_ faction The_Necromancers
Person _darkb_ faction The_Dark_Brotherhood

Place _dung_ remote dungeon9

Item _gem_ lodestone
Item _gem2_ lodestone
Item _item_ magic_item

Clock _2dung_ 00:00 0 flag 1 range 0 17
Clock _awaken_ 00:01
Clock _failsafe_ 180.00:00

Foe _orcn_ is Orc_shaman
Foe _orcw_ is Orc_warlord
Foe _ally_ is Lich

--	Quest start-up:
    start timer _2dung_
    log 1030 step 0
    get item _gem_
    reveal _dung_
    place npc _dbf_ at _dung_

_orc_ task:
    when skill Orcish is at least 40

_click_ task:
    toting _gem_ and _dbf_ clicked
    say 1010
    start task _spawn_
    stop timer _2dung_
    start timer _failsafe_

_spawn_ task:
    create foe _orcn_ every 0 minutes 2 times with 100% success
    create foe _orcw_ every 0 minutes 2 times with 100% success
    start timer _awaken_

_noun_ task:
    when _spawn_ and not _orc_
    say 1011

_undt_ task:
    when _spawn_ and _orc_
    say 1012

_awaken_ task:
    hide npc _dbf_
    say 1013
    place foe _ally_ at _dung_
    change foe _ally_ team 1
    change foe _ally_ infighting true

_day_ task:
    daily from 00:00 to 23:59
    change foe _orcn_ infighting true
    change foe _orcw_ infighting true
    clear _day_

_kill1_ task:
    killed 2 _orcn_

_kill2_ task:
    killed 2 _orcw_

_victory_ task:
    when _kill1_ and _kill2_
    say 1015
    get item _gem2_
    remove foe _ally_

_casualty_ task:
    killed 1 _ally_ saying 1003
    change repute with _darkb_ by -8
    end quest

_pcgetsgold_ task:
    toting _gem2_ and _questgiver_ clicked
    give pc _item_
    end quest

_2dung_ task:
    say 1014
    change repute with _darkb_ by -8
    end quest

_failsafe_ task:
    end quest