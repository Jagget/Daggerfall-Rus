Quest: JHDB111
DisplayName: Вмешиваться в дела магов
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Вы когда-нибудь слышали об Архимагистре, %pct?
<ce> Это самая известная государственная должность Гильдии Магов.
<ce> Существует ли один Архимагистр или их много, не
<ce> моя забота; у нас есть задание убрать его,
<ce> с мощным зачарованным _reward_ в качестве награды.
<ce> Хотели бы вы получить такую награду?

RefuseQuest:  [1001]
<ce> Мы должны проверить наших посвященных на членство в Гильдии Магов.

AcceptQuest:  [1002]
<ce> Этот Архимагистр, кем бы он ни был, проводит
<ce> визит зала гильдии в ___mg_.
<ce> Ищите кого-нибудь под псевдонимом "_archm_".
<ce> Для этого контракта не нужна утончённость.
<ce> Смело идите во имя Мефалы и убейте его.
<ce> Тот, кто стоит у вас на пути, пусть пеняет на себя.
<ce> Ваша лицензия на убийство абсолютна.
<ce>
<ce> Возвращайтесь сюда в течение =timer_ дней,
<ce> и ваш _reward_ будет ждать вас.

QuestFail:  [1003]
<ce> Ваше время требовать награду за смерть Архимагистра прошло.

QuestComplete:  [1004]
<ce> Должно быть, это была настоящая битва,
<ce> судя по слухам, которые доходили до Начальника разведки.
<ce> Тем больше славы Матери Ночи.
<ce> Никто не застрахован от визита Братства.
<ce> Никто.
<ce>
<ce> Я надеюсь, вам это понравилось.

RumorsPostSuccess:   [1007]
На Архимагистра напали в ___mg_... но они говорят, что это было подставное тело.
   <--->
Темное братство пыталось убить Архимагистра в ___mg_, но это была подделка.
   <--->
Темное братство пытается убить Архимагистра. В конце концов, никто не в безопасности.

Message:  1011
<ce> "Да? Могу я вам чем-нибудь помочь?"
<ce>
<ce> Тонкость сделала бы это намного проще,
<ce> но сегодня у вас нет такой возможности.
<ce> Вы сообщаете ему, что это
<ce> день его смерти.
<ce> Он кричит громко и испуганно.
<ce>
<ce> "Темное братство в наших залах!
<ce> Все, защищайтесь!!"

Message:  1012
<ce> "Этого... этого не может быть!! Нет!"
<ce>
<ce> Архимагистр умирает.

Message:  1030
%qdt:
 "Один" Архимагистр, или "тот самый" Архимагистр
 Гильдии Магов посещает
 их зал в ___mg_.
 Братство требует, чтобы я убил его
 и вернулся к ___questgiver_
 в течение =timer_ дней за
 могущественным зачарованным _reward_.

QBN:
Person _questgiver_ group Questor male
Person _dummymg_ faction The_Mages_Guild
Person _archm_ faction The_Archmagister male

Place _mg_ remote magery

Item _reward_ item class 4 subclass 33

Clock _timer_ 00:00 0 flag 1 range 0 2

Foe _archmag_ is Mage
Foe _enemy1_ is Battle_mage
Foe _enemy2_ is Spellsword
Foe _enemy3_ is Iron_atronach
Foe _enemy4_ is Flesh_atronach

--	Quest start-up:
    start timer _timer_
    place npc _archm_ at _mg_
    log 1030 step 0
    pc at _mg_ set _inside_

variable _inside_

_ith_ task:
    clicked npc _archm_
    say 1011
    hide npc _archm_
    place foe _archmag_ at _mg_
    change foe _archmag_ infighting true
    cast Shield spell on _archmag_
    cast Troll's_Blood spell on _archmag_
    cast Shalidor's_Mirror spell on _archmag_
    cast Spell_Shield spell on _archmag_
    cast Spell_Absorption spell on _archmag_
    create foe _enemy3_ every 0 minutes 1 times with 100% success
    create foe _enemy4_ every 0 minutes 1 times with 100% success
    change foe _enemy3_ infighting true
    change foe _enemy4_ infighting true
    change foe _enemy3_ team 19
    change foe _enemy4_ team 19

_daily_ task:
    daily from 0:00 to 23:59
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true
    clear _daily_

_spawn_ task:
    when _ith_ and _inside_
    create foe _enemy1_ every 1 minutes indefinitely with 33% success
    create foe _enemy2_ every 2 minutes indefinitely with 66% success

_slain_ task:
    killed 1 _archmag_ saying 1012
    change repute with _dummymg_ by -70

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    start quest JHDBC03
    end quest

_timer_ task:
    say 1003
    end quest