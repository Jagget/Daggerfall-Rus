-- Quest: C:\QUESTS\WORKING\N0B11Y18.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: mage
-- Repute: 11
-- QuestId: 18
-- Edited for Daggerfall Unity by Jay_H
Messages: 64
Quest: N0B11Y18
DisplayName: Бывший студент Часть I
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pct, похоже вы действительно так преданны
<ce> Гильдии Магов... или мне это только кажется?
<ce> У меня есть очень важное задание, и я хочу,
<ce> чтобы им занялись именно вы.

RefuseQuest:  [1001]
<ce> Мне действительно показалось.

AcceptQuest:  [1002]
<ce> Отлично. Теперь к делу. Один маг по имени
<ce> _rebel_ поднимает мятеж против нас, и, пока
<ce> это все еще в стадии начала, нам необходимо
<ce> убрать %g4. Мой добрый ученик показывал большие
<ce> способности, но древние законы Гильдии оказались
<ce> %g2 не по плечу. К сожалению именно они есть
<ce> основа всей жизни Гильдии и без них нельзя обойтись.
<ce> Нам удалось узнать, что %g сейчас проводит какие-то
<ce> исследования, суть которых нам очень не нравится.
<ce> Через =qtime_ дней они будут закончены.
<ce> лаборатория называется __rebelhouse_. Ступайте туда
<ce> и постарайтесь устранить эту угрозу.

QuestFail:  [1003]
<ce> Идите, %pcn.

QuestComplete:  [1004]
<ce> Возьмите этот подарок, _reward_, как
<ce> знак моей признательности за ваши дела.

Message:  1020
%qdt:
<ce> Мне пришлось взять задание по
<ce> поиску и уничтожению мятежного мага по
<ce> имени _rebel_ который находится в лаборатории
<ce> __rebelhouse_.
<ce> Как только задание будет выполнено, нужно
<ce> будет сообщить об этом магу _questgiver_ из
<ce> Гильдии Магов города ___questgiver_.
<ce> Мне дали =qtime_ дней.

Message:  1021
%qdt:
<ce> Я думаю, решение пощадить мага _rebel_ было
<ce> правильным. Теперь мне надо сообщить магу
<ce> _questgiver_ о том, что в лаборатории
<ce> __rebelhouse_ следы мага _rebel_ найти
<ce> не удалось, и оставить это письмо.

Message:  1030
<ce> Итак, _questgiver_ еще раз отправляет
<ce> одного из своих лакеев чтобы уколоть меня.
<ce> У стариков, которые безвылазно сидят в Гильдии
<ce> Магов, нет тяги к приключениям, нет чувства
<ce> опасности, нет никаких забот и они ничему так
<ce> и не научатся. Я даже не подумаю ничего делать
<ce> против них, кроме того, чтоб уйти из под их
<ce> контроля. И только лишь из-за того, что я просто
<ce> не могу жить по их правилам, Гильдия хочет меня
<ce> уничтожить! Почему вы так смиренно повинуетесь
<ce> им, убивая по заказу, как наемный убийца?
<ce> Идемте со мной, бросим им вызов! Всем!

Message:  1031
<ce> Чудесно! Всего лишь скажите старому магу
<ce> _questgiver_, что меня здесь не было.
<ce> Но оставьте %g2 это письмо.
<ce> Мне нужно будет исчезнуть, но я найду
<ce> вас очень быстро, и нас будут ждать
<ce> великие дела!

Message:  1032
<ce> Что ж, вам будет очень тяжко.
<ce> Защищайтесь!

Message:  1040
<ce> Дорогой друг _questgiver_,
<ce>
<ce> Прошу прощения, что так все получилось. Я
<ce> с ностальгией вспоминаю те дни, проведенные.
<ce> с вами. Помните ли вы те дни, когда мне, послушно
<ce> склонившись, приходилось слушать вас так, как будто
<ce> вам было чему научить меня, кроме повторения стухших
<ce> баллад о прошлом величии? =mage1_, архимаг
<ce> =mage3_ -- в их дни Гильдия Магов была
<ce> чем-то, заслуживающим уважения.
<ce> Довольно! Я не могу предложить свою верность
<ce> Гильдии, пока она не прекратит подавлять
<ce> все инакомыслия.
<ce>
<ce> Если вы пришлете ко мне еще кого-нибудь из
<ce> своих подхалимов, мне придется защищаться. Считайте
<ce> это предупреждением.
<ce>
<ce> Ваш покорный ученик,
<ce>
<ce> _rebel_

Message:  1041
<ce> Что...какая дерзость! У меня нет
<ce> слов! Теперь вы видите, что мага
<ce> _rebel_ необходимо остановить
<ce> любой ценой, до того как %g подорвет
<ce> все то, на чем основывается Гильдия.
<ce> Очень неприятно, что вы упустили эту
<ce> жалкую мошку, но я не буду жалеть %g4
<ce> жалкую жизнь.

Message:  1042
<ce> Очень хорошо, %pct. Гильдия Магов не
<ce> сможет выжить, если мы не будем
<ce> исполнять свои собственные правила,
<ce> какими бы жестокими они не казались.

Message:  1050
<ce> Бросьте эту безнадежную драку и
<ce> идите со мной!

Message:  1051
<ce> Вижу, вы готовы признать мою точку
<ce> зрения. Повеселимся вместе!

Message:  1052
<ce> _questgiver_, кажется, проверяет
<ce> вашу преданность. Но нужно ли быть рабом
<ce> правил и уставов?
<ce> Присоединяйтесь к моей веселой стае!

Message:  1053
<ce> Вы испытываете мое терпение, но я
<ce> даю вам последний шанс.
<ce> Остановитесь, и я забуду все,
<ce> что было.

Message:  1060
<ce> Кажется, нам лучше было бы делать это снаружи.

Message:  1061
<ce> Ха ха, не здесь!

Message:  1062
<ce> Маг _questgiver_ забывает всем сказать о
<ce> моих магических знаниях, когда %g проповедует
<ce> о моей непокорности.

Message:  1063
<ce> Снова не получилось, мой дорогой друг. Почему
<ce> ваши наставники не показали вам эти трюки?


-- Symbols used in the QRC file:
--
--                %g occurs 5 times.
--               %g2 occurs 3 times.
--               %g4 occurs 2 times.
--              %pcn occurs 2 times.
--              %pct occurs 2 times.
--              %qdt occurs 2 times.
--           =mage1_ occurs 1 time.
--           =mage3_ occurs 1 time.
--           =qtime_ occurs 4 times.
--    ___questgiver_ occurs 1 time.
--     __rebelhouse_ occurs 3 times.
--      _questgiver_ occurs 8 times.
--           _rebel_ occurs 6 times.
--          _reward_ occurs 1 time.

QBN:
Item _letter_ letter used 1040
Item _reward_ magic_item

Person _rebel_ named Baltham_Greyman
Person _questgiver_ group Questor

Place aide remote dungeon
Place _rebelhouse_ remote house1

Clock _qtime_ 00:00 0 flag 17 range 0 2
Clock _S.04_ 00:01 00:05
Clock _S.05_ 00:01 00:10
Clock _S.06_ 00:01 00:14
Clock _healer_ 00:02 00:18

Foe _mage1_ is Mage
Foe _mage2_ is Mage
Foe _mage3_ is Mage
Foe _mage4_ is Mage
Foe _F.04_ is Mage

--	Quest start-up:
	start timer _qtime_
	log 1020 step 0
	place npc _rebel_ at _rebelhouse_

_qtime_ task:
	end quest

_S.01_ task:
	clicked npc _rebel_
	prompt 1030 yes _S.02_ no _S.03_
	hide npc _rebel_
	--changed "place npc _rebel_ at aide" to "hide" action. Classic would transfer the NPC away, hiding is far easier

_qgclicked_ task:
  clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _S.01_
	say 1003
	clear _qgclicked_ _clearclick_

_S.02_ task:
	start quest 502 502
	log 1021 step 1
	get item _letter_
	say 1031
	remove foe _mage2_
	remove foe _F.04_
	remove foe _mage1_
	remove foe _mage4_
	remove foe _mage3_
	--changed "place foe _mage2_ at aide" etc to "remove foe," same as with hide npc reason

_S.03_ task:
	start timer _S.04_
	--S.14 timer was nonexistent. changed to S.04
	say 1032
    hide npc _rebel_
	place foe _mage1_ at _rebelhouse_
	cast Spell_Reflection spell on _mage1_

_S.04_ task:
	pc at _rebelhouse_ set _S.19_
	remove foe _mage1_
	say 1060
--"place foe" not known to support "say ###." moved to its own line.
--changed "place foe at false dungeon" to "remove foe"

_S.05_ task:
	create foe _mage3_ every 1 minutes 1 times with 100% success
	say 1061
	start timer _S.06_
	remove foe _mage2_
--"create foe" not known to support "say ###." moved to its own line.
--changed "place foe at false dungeon" to "remove foe"

_S.06_ task:
	create foe _mage4_ every 1 minutes 1 times with 100% success
	say 1062
	start timer _healer_
	remove foe _mage3_
	--"create foe" not known to support "say ###." moved to its own line.
	--changed "place foe at false dungeon" to "remove foe"

_healer_ task:
	create foe _F.04_ every 1 minutes 1 times with 100% success
	say 1063
	remove foe _mage4_
	--"create foe" not known to support "say ###." moved to its own line.
	--changed "place foe at false dungeon" to "remove foe"

_S.08_ task:
	killed 1 _mage2_
	killed 1 _F.04_
	killed 1 _mage4_
	killed 1 _mage3_
	killed 1 _mage1_

_S.09_ task:
	when _S.08_ or _S.02_
	stop timer _healer_
	stop timer _S.06_
	stop timer _S.05_
	stop timer _S.04_

_S.10_ task:
	injured _mage1_
	cast Heal spell on _mage1_

_S.11_ task:
	injured _mage2_
	prompt 1050 yes _S.02_ no _S.03_
	cast Heal spell on _mage2_

_S.12_ task:
	injured _mage3_
	prompt 1051 yes _S.02_ no _S.03_
	cast Heal spell on _mage3_

_S.13_ task:
--boar task renamed to _S.13_. Variables should have underscores
	injured _mage4_
	prompt 1052 yes _S.02_ no _S.03_


_S.14_ task:
	injured _F.04_
	prompt 1053 yes _S.02_ no _S.03_

_S.15_ task:
	toting _letter_ and _questgiver_ clicked
	say 1041

_success_ task:
	when _qgclicked_ and _S.08_
	say 1042

_pcgetsgold_ task:
	when _S.15_ or _success_
	give pc _reward_
	end quest

variable _S.19_
_S.20_ task:
	when _S.04_ and not _S.19_ and not _S.08_
	create foe _mage2_ every 1 minutes 1 times with 100% success
	start timer _S.05_
	cast Spell_Reflection spell on _mage2_
