-- Quest: C:\QUESTS\WORKING\L0B50Y11.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: Brotherhood
-- Repute: 50
-- QuestId: 11
-- Edited for Daggerfall Unity by Jay_H
Messages: 32
Quest: L0B50Y11
DisplayName: Борьба с гильдией воров
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  Есть интересная работа,
<ce>  %pcn. Высокопоставленный член
<ce>  Гильдии Воров. Это будет
<ce>  не просто. Беретесь?

RefuseQuest:  [1001]
<ce>  Это - не для слабохарактерных!
                                     <--->
<ce>  Понятно. Возвращайтесь, когда будете
<ce>  заинтересованы настоящим делом.

AcceptQuest:  [1002]
<ce>  А вы круты! Имя
<ce>  цели - _marknpc_, вот здесь, в
<ce>  ___questgiver_. %g шляется
<ce>  по округе, но обычно
<ce>  прикрыт телохранителями. Будь осторожнее!
<ce>  У тебя семь дней. Сделаешь -
<ce>  возвращайся сюда, я заплачу _gold _
<ce>  золотых. Спроси _questgiver_.

QuestComplete:  [1004]
<ce>  Превосходная работа %pcn.
<ce>  Надеюсь, что это не было слишком
<ce>  трудно для вас. Вот награда,
<ce>  Гильдия щедро платит за услуги!

RumorsDuringQuest:  [1005]
_marknpc_ в конце концов настигнут неприятности, подстроенные его врагами!
                                     <--->
_marknpc_ все равно ожидает кровавый конец, помяни мое слово!

RumorsPostfailure:  [1006]
_marknpc_ покинул %reg, но %g вернется.

RumorsPostsuccess:  [1007]
_marknpc_ допустил слишком много ошибок -- %g давно уже обречен.
                                     <--->
Никого не удивит что _marknpc_ мертв -- %g был нарушителем спокойствия.

QuestorPostsuccess:  [1008]
Я не забыл то, что вы сделали с _marknpc _, %pcf. Не волнуйтесь об этом. 

QuestorPostfailure:  [1009]
<ce>  _marknpc_ все еще жив, %pcf.
<ce>  Это в любом случае не облагораживает 
<ce>  ситуацию, и я весьма недоволен!

QuestLogEntry:  [1010]
%qdt:
<ce>  Принят заказ; нужно убить
<ce>  _marknpc_ из ___questgiver_ для
<ce>  Темного Братства. Мой агент
<ce>  _questgiver_ доложил,
<ce>  что цель много перемещается по округе.
<ce>  У меня семь дней, чтобы закончить работу.

Message:  1015
<ce>  _marknpc_ все еще жив.
<ce>  Не играйте со мной, %pcn.
<ce>  У Темного Братства нет
<ce>  чувства юмора. Я только слышал 
<ce>  что _fakeplace_ - это ложное
<ce>  место. Не торчите там понапрасну.
<ce>  Поглядите, может раскопаете другие зацепки
<ce>  на то, где %g проводит время.

Message:  1020
<ce>  _marknpc_ наслаждается случайными
<ce>  рандеву со мной. %g никогда не
<ce>  остается надолго. %g только использует
<ce>  меня для своих удовольствий.
<ce>  За 50 золотых я с удовольствием
<ce>  сдам %g3 с потрохами.

Message:  1021
<ce>  На помощь! _marknpc _!
<ce>   Убийца в доме!

Message:  1022
<ce>  Вы убили _marknpc_! Сохраните
<ce>  мне жизнь, и я клянусь, что никому
<ce>  не скажу, кто сделал это.

Message:  1023
<ce>  _marknpc_ прибывает сюда ежедневно,
<ce>  около 6 часов. %g отсылает своих
<ce>  телохранителей прочь. %g проводит здесь
<ce>  приблизительно час или около того.

Message:  1030
<ce> _marknpc_ - вор и негодяй. 
                                     <--->
<ce> Все думают, что _marknpc_ болтается в _fakeplace_, но %g этого не делает.
                                     <--->
<ce> _marknpc_ обычно проводит свои вечера в _inn_.
                                     <--->
<ce> _marknpc_ проводит свое утро, отдыхая дома.
                                     <--->
<ce> _marknpc_ живет в _house_, но не проводит там много времени.
                                     <--->
<ce> К полудню _marknpc_ можно найти в _guildhall_.
                                     <--->
<ce> Я слышал, что _guildhall_ - притон Гильдии Воров. Попытайся застать его там.
                                     <--->
<ce>                       У _marknpc_ есть госпожа.
                                     <--->
<ce> У _marknpc_ есть госпожа, живущая в _mistresshome_.
                                     <--->
<ce> У _marknpc_ много телохранителей.
                                     <--->
<ce> Когда _marknpc_ посещает свою госпожу, то отсылает телохранителей прочь.
                                     <--->
<ce> Я бы не стал ссориться с телохранителями _marknpc_.
                                     <--->
<ce> Они говорят, что _marknpc_ защищен заклятьями. 

Message:  1031
<ce> _marknpc_ замертов упал на землю.
<ce>  Братство может гордиться!


-- Symbols used in the QRC file:
--
--                %g occurs 23 times.
--               %g2 occurs 6 times.
--               %g3 occurs 10 times.
--              %pcf occurs 43 times.
--              %pcn occurs 3 times.
--              %qdt occurs 1 time.
--              %reg occurs 2 times.
--    ___questgiver_ occurs 2 times.
--       _fakeplace_ occurs 3 times.
--            _gold_ occurs 4 times.
--       _guildhall_ occurs 4 times.
--           _house_ occurs 2 times.
--             _inn_ occurs 2 times.
--         _marknpc_ occurs 35 times.
--    _mistresshome_ occurs 2 times.
--      _questgiver_ occurs 3 times.

QBN:
Item _gold_ gold
Item _bribe3_ gold range 50 to 50

Person _mistress_ face 3 group Group_7.2 female local
Person _questgiver_ face 1 group Questor
Person _marknpc_ face 5 faction The_Thieves_Guild male local anyInfo 1030

Place _house_ local house2
Place _inn_ local tavern
Place _guildhall_ local random
Place _mistresshome_ local house2
Place _fakeplace_ local random

Clock _S.00_ 7.00:00 0 flag 1 range 0 1

Foe _home_ is Thief
Foe _F.01_ is Sorceror
Foe _F.02_ is Knight
Foe _F.03_ is Rogue

--	Quest start-up:
	dialog link for location _mistresshome_ person _mistress_ 
	dialog link for location _house_ 
	dialog link for location _guildhall_ 
	dialog link for location _fakeplace_ 
	start timer _S.00_ 
	log 1010 step 0 
	pc at _fakeplace_ set _S.11_ 
	place npc _mistress_ at _mistresshome_ 
	place npc _marknpc_ at _fakeplace_ 

_S.00_ task:
	end quest 

_S.01_ task:
	daily from 00:00 to 10:00 
	pc at _house_ set _S.05_ 
	cast Shield spell on _home_ 
	place foe _home_ at _house_ 

_S.02_ task:
	daily from 19:01 to 23:59 
	pc at _inn_ set _S.05_ 
	cast Shield spell on _home_ 
	place foe _home_ at _inn_ 

_S.03_ task:
	daily from 10:01 to 18:00 
	pc at _guildhall_ set _S.05_ 
	cast Shield spell on _home_ 
	place foe _home_ at _guildhall_ 

_S.04_ task:
	daily from 18:01 to 19:00 
	pc at _mistresshome_ set _S.06_ 
	place foe _home_ at _mistresshome_ 

_S.05_ task:
	create foe _F.03_ every 2 minutes 5 times with 50% success 
	create foe _F.02_ every 4 minutes 5 times with 50% success 
	create foe _F.01_ every 3 minutes 5 times with 50% success 

variable _S.06_
_S.07_ task:
	killed 1 _home_ saying 1031 

_S.08_ task:
	clicked npc _mistress_ 

_clearclick_ task:
	when not _S.07_ and _S.21_ 
	say 1015 
	clear _S.21_ _clearclick_ 

_vamp3_ task:
	when _S.07_ and _S.21_ 
	give pc _gold_ 
	end quest 

_S.11_ task:
	hide npc _marknpc_ 

until _S.11_ performed:
	restore _marknpc_ 

_S.12_ task:
	when _S.04_ and _S.08_ and not _S.07_ 
	say 1021 

_S.13_ task:
	when _S.04_ and _S.08_ and _S.07_ 
	say 1022 

_S.14_ task:
	when not _S.04_ and _S.08_ and not _S.07_ 
	prompt 1020 yes _yes_ no _no_ 

variable _yes_
variable _no_
_S.17_ task:
	clicked _mistress_ and at least 50 gold otherwise do _S.18_ 

variable _S.18_
_S.19_ task:
	when _S.17_ and _no_ 
	get item _bribe3_ 

_S.20_ task:
	when _S.17_ and _yes_ 
	say 1023 
	unset _S.08_ 

_S.21_ task:
	clicked npc _questgiver_ 
